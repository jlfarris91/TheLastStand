package HumanPlayers
import PlayerExtensions
import HashList
import ClosureEvents
import ProjectConstants

public HashList<int> g_HumanPlayerSlotIds

public force g_HumanPlayers

public force g_PlayingHumanPlayers
public int g_PlayingHumanPlayerCount
public int g_PlayingHumanPlayerUserCount

public force g_HumanPlayersInGame
public int g_HumanPlayerInGameCount

// ============================================================================
public function updateHumanPlayersForce()
  g_PlayingHumanPlayers.clear()
  g_PlayingHumanPlayerCount = 0
  g_HumanPlayersInGame.clear()
  g_HumanPlayerInGameCount = 0
  for p in g_HumanPlayers
    if (p.isPlaying())
      g_PlayingHumanPlayers.addPlayer(p)
      g_PlayingHumanPlayerCount++
      if (p.isControlledByUser())
        g_PlayingHumanPlayerUserCount++
      Log.debug("Player {0} {1} is playing".format(p.getId().toString(), p.getName()))
    if (p.isIngame())
      g_HumanPlayersInGame.addPlayer(p)
      g_HumanPlayerInGameCount++

// ============================================================================
function initializeHumanPlayersForce()
  for id in g_HumanPlayerSlotIds
    let p = players[id]
    if (p.isHumanPlayer() or p.isBotPlayer())
      g_HumanPlayers.addPlayer(p)

// ============================================================================
public function player.isHumanPlayer() returns bool
  return g_HumanPlayerSlotIds.has(this.getId())

// ============================================================================
public function player.isPlayingHumanPlayer() returns bool
  return this.isPlaying() and this.isHumanPlayer() and (DEV_ENVIRONMENT or this.isControlledByUser())

// ============================================================================
public function player.isBotPlayer() returns bool
  return this.isHumanPlayer() and this.isControlledByComputer()

// ============================================================================
function onPlayerLeftGame()
  updateHumanPlayersForce()

// ============================================================================
init
  g_HumanPlayers = CreateForce()
  g_PlayingHumanPlayers = CreateForce()
  g_HumanPlayersInGame = CreateForce()
  
  // This needs to match the human slots in the wurst.build file
  g_HumanPlayerSlotIds = new HashList<int>()
  ..add(0)
  ..add(1)
  ..add(2)
  ..add(3)
  ..add(4)
  ..add(5)
  ..add(6)
  ..add(9)

  initializeHumanPlayersForce()
  updateHumanPlayersForce()

  EventListener.add(EVENT_PLAYER_LEAVE, () -> onPlayerLeftGame())

  Log.debug("Number of human players: " + I2S(g_HumanPlayers.count()) + " playing: " + I2S(g_PlayingHumanPlayers.count()))