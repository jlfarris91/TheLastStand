package OnPlayerHeroDied
import RegisterEvents
import PlayerMetadata
import GameSettings
import DisplayTextToPlayer
import Camp
import GameConstants

function givePlayerUnitsToVillagers(player p)
  for u in p.getCamp().structures
    u.setOwner(PLAYER_VILLAGERS, true)
  for u in p.getCamp().survivors
    u.setOwner(PLAYER_VILLAGERS, true)

function disableFogOfWarForPlayer(player p)
  CreateFogModifierRectBJ(true, p, FOG_OF_WAR_VISIBLE, GetPlayableMapRect())

function onPlayerHeroDied(player p)
  p.setDisplayName("-Deceased-")
  p.setIsDead(true)

  if (GameSettings.allowRespawn)
    displayTextToPlayer(p, "You will be respawned at the start of the next day")
  else
    disableFogOfWarForPlayer(p)

  p.resetPlayerMetadata()
  p.getCamp().reset()
  // TODO: disable trackables for player

function onPlayerUnitDied()
  var dyingUnit = GetDyingUnit()
  var owner = dyingUnit.getOwner()

  if (dyingUnit == owner.getHeroUnit())
    onPlayerHeroDied(owner)

init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onPlayerUnitDied)