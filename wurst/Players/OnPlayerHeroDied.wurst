package OnPlayerHeroDied
import RegisterEvents
import GameSettings
import DisplayTextToPlayer
import GameConstants
import Camp
import HumanPlayerMetadata
import UnitMetadata

// ============================================================================
function givePlayerUnitsToVillagers(player p)
  let playerMetadata = p.getHumanMetadataRequired()
  let camp = playerMetadata.getCamp()
  let campUnits = camp.getUnits()
  for u in campUnits
    if (u != playerMetadata.getHero())
      u.setOwner(PLAYER_VILLAGERS, true)

// ============================================================================
function disableFogOfWarForPlayer(player p)
  CreateFogModifierRectBJ(true, p, FOG_OF_WAR_VISIBLE, GetPlayableMapRect())

// ============================================================================
function onPlayerHeroDied(HumanPlayerMetadata playerMetadata)
  playerMetadata.setDisplayName("-Deceased-")

  if (GameSettings.allowRespawn)
    displayMessageToPlayer(playerMetadata.getPlayer(), "You will be respawned at the start of the next day")
  else
    disableFogOfWarForPlayer(playerMetadata.getPlayer())

  givePlayerUnitsToVillagers(playerMetadata.getPlayer())

  playerMetadata.reset()
  playerMetadata.getCamp().reset()

// ============================================================================
function onPlayerUnitDied()
  let dyingUnit = GetDyingUnit()
  let owner = dyingUnit.getOwner()
  let playerMetadata = owner.getMetadata() castTo HumanPlayerMetadata

  if (playerMetadata == null)
    return

  if (dyingUnit == playerMetadata.getHero().getUnit())
    onPlayerHeroDied(playerMetadata)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onPlayerUnitDied)