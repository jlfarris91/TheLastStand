package HumanPlayerMetadata
import public PlayerMetadata
import UnitMetadata
import HumanPlayers
import ErrorHandling
import initlater Camp
import GameConstants
import ClosureEvents

// ============================================================================
public class HumanPlayerMetadata extends PlayerMetadata
  private int _kills
  private IUnitMetadata _hero
  private ICamp _camp

  // --------------------------------------------------------------------------
  construct(player p)
    super(p)
    _camp = new Camp(this)

  // --------------------------------------------------------------------------
  function getCamp() returns ICamp
    return _camp

  // --------------------------------------------------------------------------
  function getKills() returns int
    return _kills

  // --------------------------------------------------------------------------
  function incrementKills()
    _kills++

  // --------------------------------------------------------------------------
  function getMaterials() returns int
    return GetPlayerState(getPlayer(), PLAYER_STATE_RESOURCE_LUMBER)

  // --------------------------------------------------------------------------
  function setMaterials(int value)
    SetPlayerState(getPlayer(), PLAYER_STATE_RESOURCE_LUMBER, value)
  
  // --------------------------------------------------------------------------
  function giveMaterials(int value)
    setMaterials(this.getMaterials() + value)

  // --------------------------------------------------------------------------
  function removeMaterials(int value)
    setMaterials(IMaxBJ(this.getMaterials() - value, 0))

  // --------------------------------------------------------------------------
  function getGold() returns int
    return GetPlayerState(getPlayer(), PLAYER_STATE_RESOURCE_GOLD)

  // --------------------------------------------------------------------------
  function setGold(int value)
    SetPlayerState(getPlayer(), PLAYER_STATE_RESOURCE_GOLD, value)
  
  // --------------------------------------------------------------------------
  function giveGold(int value)
    setGold(getGold() + value)

  // --------------------------------------------------------------------------
  function removeGold(int value)
    setMaterials(IMaxBJ(this.getGold() - value, 0))

  // --------------------------------------------------------------------------
  function getIsDead() returns bool
    if (_hero == null)
      return true
    return _hero.isAlive() == false

  // --------------------------------------------------------------------------
  function getHero() returns IUnitMetadata
    return _hero

  // --------------------------------------------------------------------------
  function setHero(IUnitMetadata hero)
    _hero = hero

// ============================================================================
public function player.getHumanMetadata() returns HumanPlayerMetadata
  return this.getMetadata() castTo HumanPlayerMetadata

// ============================================================================
public function player.getHumanMetadataRequired() returns HumanPlayerMetadata
  let metadata = this.getMetadata() castTo HumanPlayerMetadata
  if (metadata == null)
    error("Player \"{0}\" does not have required metadata".format(this.getName()))
  return metadata

// ============================================================================
public function unit.getOwnerHumanMetadata() returns HumanPlayerMetadata
  return this.getOwnerMetadata() castTo HumanPlayerMetadata

// ============================================================================
function initializePlayerMetadata(player p)
  let metadata = new HumanPlayerMetadata(p)
  registerPlayerMetadata(metadata)

// ============================================================================
function onPlayerLeftGame()
  let leavingPlayer = GetTriggerPlayer()
  let metadata = leavingPlayer.getHumanMetadata()
  if (metadata == null)
    return

  g_PlayingHumanPlayers.remove(leavingPlayer)

  let hero = metadata.getHero()
  if (hero != null)
    hero.remove()

  let camp = metadata.getCamp()
  if (camp != null)
    let campTent = camp.getCampTent()
    if (campTent != null)
      campTent.kill()
    camp.giveAllUnitsToPlayer(PLAYER_VILLAGERS)

// ============================================================================
init
  for p in g_PlayingHumanPlayers
    initializePlayerMetadata(p)

  initializePlayerMetadata(PLAYER_VILLAGERS)

  EventListener.add(EVENT_PLAYER_LEAVE, () -> onPlayerLeftGame())