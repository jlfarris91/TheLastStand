package PlayerMetadata
import HashMap
import Composition
import Type
import StringExtensions
import public initlater PlayerMetadataExtensions

HashMap<player, IPlayerMetadata> g_PlayerMetadata

// ============================================================================
public interface IPlayerMetadata
  function getPlayer() returns player
  function getDisplayName() returns string
  function setDisplayName(string displayName)
  function reset()

// ============================================================================
public abstract class BasePlayerMetadata implements IComposite, IPlayerMetadata
  protected CompositionManager _compositionManager

  protected player _player
  private string _displayName

  // --------------------------------------------------------------------------
  construct(player p)
    this._player = p
    this.setDisplayName(p.getName())
    this.reset()

  // --------------------------------------------------------------------------
  override function reset()
    skip

  // --------------------------------------------------------------------------
  ondestroy
    if (_compositionManager != null)
      destroy _compositionManager
      _compositionManager = null

  // --------------------------------------------------------------------------
  override function getPlayer() returns player
    return _player

  // --------------------------------------------------------------------------
  override function getDisplayName() returns string
    return _displayName

  // --------------------------------------------------------------------------
  override function setDisplayName(string displayName)
    _displayName = displayName

  // --------------------------------------------------------------------------
  function resetDisplayName()
    setDisplayName(_player.getName())

  // --------------------------------------------------------------------------
  @inline
  override function addComponent(IComponent component) returns IComponent
    if (_compositionManager == null)
      _compositionManager = new CompositionManager()
    return _compositionManager.addComponent(component)
   
  // --------------------------------------------------------------------------
  @inline
  override function removeComponent(IComponent component) returns bool
    if (_compositionManager == null)
      return false
    return _compositionManager.removeComponent(component)

  // --------------------------------------------------------------------------
  @inline
  override function getComponent(Type componentType) returns IComponent
    return _compositionManager != null ? _compositionManager.getComponent(componentType) : null

// ============================================================================
public function player.getDisplayName() returns string
  return this.getMetadata().getDisplayName()

// ============================================================================
public function player.setDisplayName(string displayName)
  this.getMetadata().setDisplayName(displayName)

// ============================================================================
public function player.getDisplayNameColored() returns string
  return this.getDisplayName().colorize(this.getColor().toColor())

// ============================================================================
public function player.resetPlayerMetadata()
  this.getMetadata().reset()

// ============================================================================
/**
*   Gets the PlayerMetadata instance registered for player p or creates a new PlayerMetadata instance if
*   one has not been registered yet.
*/
@inline
public function player.getMetadata() returns IPlayerMetadata
  return g_PlayerMetadata.get(this)

// ============================================================================
@inline
public function unit.getOwnerMetadata() returns IPlayerMetadata
  return g_PlayerMetadata.get(this.getOwner())

// ============================================================================
public function registerPlayerMetadata(IPlayerMetadata playerMetadata)
  g_PlayerMetadata.put(playerMetadata.getPlayer(), playerMetadata)

// ============================================================================
init
  g_PlayerMetadata = new HashMap<player, IPlayerMetadata>()