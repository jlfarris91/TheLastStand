package Waygates
import GroupUtils
import TlsUnitIds
import ErrorHandling

constant string WAYGATE_MINIMAP_ICON_PATH = "UI\\MiniMap\\MinimapIcon\\MinimapIconNeutralBuilding.blp"
unit array[4] g_waygates

enum Quadrant
  NE
  SE
  SW
  NW

// ============================================================================
public function vec2.getQuadrant() returns Quadrant
  if (this.x >= 0 and this.y >= 0)
    return Quadrant.NE
  if (this.x >= 0 and this.y < 0)
    return Quadrant.SE
  if (this.x < 0 and this.y < 0)
    return Quadrant.SW
  if (this.x < 0 and this.y >= 0)
    return Quadrant.NW
  error("invalid pos")
  return Quadrant.NE

// ============================================================================
public function unit.getQuadrant() returns Quadrant
  return this.getPos().getQuadrant()

// ============================================================================
function initializeWaygates()
  let temp = getGroup()
  temp.enumUnitsOfType(TlsUnitIds.waygate, null)
  for _unit in temp
    let quadrant = _unit.getPos().getQuadrant()
    g_waygates[quadrant castTo int] = _unit
  temp.release()

  let waygateNE = g_waygates[Quadrant.NE castTo int]
  let waygateSE = g_waygates[Quadrant.SE castTo int]
  let waygateSW = g_waygates[Quadrant.SW castTo int]
  let waygateNW = g_waygates[Quadrant.NW castTo int]

  WaygateSetDestination(waygateNE, waygateSE.getX(), waygateSE.getY())
  WaygateActivate(waygateNE, true)
  CreateMinimapIconOnUnit(waygateNE, 255, 255, 255, WAYGATE_MINIMAP_ICON_PATH, FOG_OF_WAR_MASKED)

  WaygateSetDestination(waygateSE, waygateSW.getX(), waygateSW.getY())
  WaygateActivate(waygateSE, true)
  CreateMinimapIconOnUnit(waygateSE, 255, 255, 255, WAYGATE_MINIMAP_ICON_PATH, FOG_OF_WAR_MASKED)

  WaygateSetDestination(waygateSW, waygateNW.getX(), waygateNW.getY())
  WaygateActivate(waygateSW, true)
  CreateMinimapIconOnUnit(waygateSW, 255, 255, 255, WAYGATE_MINIMAP_ICON_PATH, FOG_OF_WAR_MASKED)
  
  WaygateSetDestination(waygateNW, waygateNE.getX(), waygateNE.getY())
  WaygateActivate(waygateNW, true)
  CreateMinimapIconOnUnit(waygateNW, 255, 255, 255, WAYGATE_MINIMAP_ICON_PATH, FOG_OF_WAR_MASKED)

// ============================================================================
init
  initializeWaygates()