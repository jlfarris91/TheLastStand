package WeatherManager
import MapBounds
import ClosureTimers
import Math
import Gradient
import ColorUtility
import Range
import GameConstants

weathereffect g_lordaeronRainLight
weathereffect g_lordaeronRainHeavy
weathereffect g_windHeavy
CallbackSingle g_currentWeatherTimer

real g_fogFadeDT = 0.1
real g_fogFadeT = 0.0
real g_fogFadeDir = -1.0
real g_fogFadeDuration = 5.0
constant real NO_FOG_Z = 3500

colorA g_fogColor = colorA(0, 77, 77, 255)
rangeReal g_fogOnParams = rangeReal(1300.0, 2100.0)

GradientColorList g_weatherFogColorGradient

bool g_updateFog = true
GradientColorList g_dncFogColorGradient
GradientList<GradientList<real, real>, real> g_camZoomDncFogZStartGradient
GradientList<GradientList<real, real>, real> g_camZoomDncFogZEndGradient

GradientList<real, real> g_camZoomWeatherFogZStartGradient
GradientList<real, real> g_camZoomWeatherFogZEndGradient

public tuple fogParams(real zstart, real zend, real density)

// ============================================================================
public enum WeatherEffectType
  NONE
  RAIN_LIGHT
  RAIN_HEAVY
  FOG

// ============================================================================
public class Weather
  private static WeatherEffectType m_weatherEffectType

  // --------------------------------------------------------------------------
  static function getWeatherEffect() returns WeatherEffectType
    return m_weatherEffectType

  // --------------------------------------------------------------------------
  static function setWeatherEffect(WeatherEffectType weatherEffectType)
    cancelWeatherEffectTimer()
    
    if (weatherEffectType == WeatherEffectType.RAIN_LIGHT)
      startLightRain()
    else
      stopLightRain()
    
    if (weatherEffectType == WeatherEffectType.RAIN_HEAVY)
      startHeavyRain()
    else
      stopHeavyRain()

    if (weatherEffectType == WeatherEffectType.FOG)
      startFog()
    else
      stopFog()

    m_weatherEffectType = weatherEffectType

  // --------------------------------------------------------------------------
  static function startWeatherEffect(WeatherEffectType weatherEffectType, real duration)
    setWeatherEffect(weatherEffectType)
    g_currentWeatherTimer = doAfter(duration, () -> clearWeatherEffects())

  // --------------------------------------------------------------------------
  static function clearWeatherEffects()
    setWeatherEffect(WeatherEffectType.NONE)

  // --------------------------------------------------------------------------
  private static function cancelWeatherEffectTimer()
    if (g_currentWeatherTimer != null)
      destroy g_currentWeatherTimer
      g_currentWeatherTimer = null

  // --------------------------------------------------------------------------
  private static function startLightRain()
    if (g_lordaeronRainLight == null)
      g_lordaeronRainLight = AddWeatherEffect(boundRect, WeatherEffects.lordaeronRainLight)
    g_lordaeronRainLight.enable()

  // --------------------------------------------------------------------------
  private static function stopLightRain()
    if (g_lordaeronRainLight != null)
      g_lordaeronRainLight.disable()

  // --------------------------------------------------------------------------
  private static function startHeavyRain()
    if (g_lordaeronRainHeavy == null)
      g_lordaeronRainHeavy = AddWeatherEffect(boundRect, WeatherEffects.lordaeronRainHeavy)
    g_lordaeronRainHeavy.enable()

  // --------------------------------------------------------------------------
  private static function stopHeavyRain()
    if (g_lordaeronRainHeavy != null)
      g_lordaeronRainHeavy.disable()

  // --------------------------------------------------------------------------
  private static function startFog()
    g_fogFadeDir = 1.0

  // --------------------------------------------------------------------------
  private static function stopFog()
    g_fogFadeDir = -1.0

  // --------------------------------------------------------------------------
  static function toggleWeatherUpdate()
    g_updateFog = not g_updateFog

  // --------------------------------------------------------------------------
  static function isUpdatingFog() returns bool
    return g_updateFog

  // --------------------------------------------------------------------------
  static function updateFog(real t)

    let timeOfDay = GetTimeOfDay()
    let dayNightT = timeOfDay / 24.0

    let cameraZoom = GetCameraField(CAMERA_FIELD_TARGET_DISTANCE)
    let zoomT = parameterize01(CAMERA_DISTANCE_MIN, CAMERA_DISTANCE_MAX, cameraZoom)
    
    let zstart_dayNight = g_camZoomDncFogZStartGradient.sample(zoomT)
    let zend_dayNight = g_camZoomDncFogZEndGradient.sample(zoomT)
    let color_dayNight = g_dncFogColorGradient.sample(dayNightT)
    let color_weatherFog = g_weatherFogColorGradient.sample(dayNightT)

    let zstart_weatherFog = g_camZoomWeatherFogZStartGradient.sample(zoomT)
    let zend_weatherFog = g_camZoomWeatherFogZEndGradient.sample(zoomT)

    let zstart = lerp(zstart_dayNight, zstart_weatherFog, t)
    let zend = lerp(zend_dayNight, zend_weatherFog, t)
    let color = color_dayNight.mix(color_weatherFog, t)
    SetTerrainFogEx(0, zstart, zend, 0.0, color.getRed01(), color.getGreen01(), color.getBlue01())

// ============================================================================
init

  let defaultCameraZoomT = parameterize01(CAMERA_DISTANCE_MIN, CAMERA_DISTANCE_MAX, CAMERA_DISTANCE_DEFAULT)

  // Represents the fog color for the full day
  g_dncFogColorGradient = new GradientColorList((colorA a, colorA b, real t) -> a.mix(b, t), COLOR_BLACK)
  ..addEntry(0.0, colorA(26, 38, 64, 255))
  ..addEntry(4.0 / 24.0, colorA(26, 46, 56, 255))
  ..addEntry(8.0 / 24.0, colorA(77, 77, 77, 255))
  ..addEntry(0.5, colorA(100, 100, 77, 255))
  ..addEntry(16.0 / 24.0, colorA(77, 77, 77, 255))
  ..addEntry(20.0 / 24.0, colorA(26, 46, 56, 255))
  ..addEntry(1.0, colorA(26, 38, 64, 255))

  let dnc_FogZStart_MinZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 500.0)
  ..addEntry(6.0 / 24.0, 500.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 500.0)
  ..addEntry(1.0, 500.0)

  let dnc_FogZStart_DefZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 1600.0)
  ..addEntry(6.0 / 24.0, 1600.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 1600.0)
  ..addEntry(1.0, 1600.0)

  let dnc_FogZStart_MaxZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 1600.0)
  ..addEntry(6.0 / 24.0, 1600.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 1600.0)
  ..addEntry(1.0, 1600.0)

  g_camZoomDncFogZStartGradient = new GradientList<GradientList<real, real>, real>(0.0) (a, b, zoomT) -> 
    let timeOfDay = GetTimeOfDay()
    let dayNightT = timeOfDay / 24.0
    return lerp(a.sample(dayNightT), b.sample(dayNightT), zoomT)
  g_camZoomDncFogZStartGradient.addEntry(0.0, dnc_FogZStart_MinZoom_Gradient)
  g_camZoomDncFogZStartGradient.addEntry(defaultCameraZoomT, dnc_FogZStart_DefZoom_Gradient)
  g_camZoomDncFogZStartGradient.addEntry(1.0, dnc_FogZStart_MaxZoom_Gradient)

  let dnc_FogZEnd_MinZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 900.0)
  ..addEntry(4.0 / 24.0, 900.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 900.0)
  ..addEntry(1.0, 900.0)

  let dnc_FogZEnd_DefZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 2300.0)
  ..addEntry(4.0 / 24.0, 2300.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 2300.0)
  ..addEntry(1.0, 2300.0)

  let dnc_FogZEnd_MaxZoom_Gradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 3000.0)
  ..addEntry(4.0 / 24.0, 3000.0)
  ..addEntry(8.0 / 24.0, NO_FOG_Z)
  ..addEntry(16.0 / 24.0, NO_FOG_Z)
  ..addEntry(18.0 / 24.0, 3000.0)
  ..addEntry(1.0, 3000.0)

  g_camZoomDncFogZEndGradient = new GradientList<GradientList<real, real>, real>(0.0) (a, b, zoomT) ->
    let timeOfDay = GetTimeOfDay()
    let dayNightT = timeOfDay / 24.0
    return lerp(a.sample(dayNightT), b.sample(dayNightT), zoomT)
  g_camZoomDncFogZEndGradient.addEntry(0.0, dnc_FogZEnd_MinZoom_Gradient)
  g_camZoomDncFogZEndGradient.addEntry(defaultCameraZoomT, dnc_FogZEnd_DefZoom_Gradient)
  g_camZoomDncFogZEndGradient.addEntry(1.0, dnc_FogZEnd_MaxZoom_Gradient)

  g_weatherFogColorGradient = new GradientColorList((colorA a, colorA b, real t) -> a.mix(b, t), COLOR_BLACK)
  ..addEntry(0.0, colorA(50, 30, 90, 255))
  ..addEntry(4.0 / 24.0, colorA(10, 60, 60, 255))
  ..addEntry(8.0 / 24.0, colorA(77, 77, 77, 255))
  ..addEntry(12.0 / 24.0, colorA(95, 73, 40, 255))
  ..addEntry(16.0 / 24.0, colorA(77, 77, 77, 255))
  ..addEntry(20.0 / 24.0, colorA(10, 60, 60, 255))
  ..addEntry(1.0, colorA(50, 30, 90, 255))

  g_camZoomWeatherFogZStartGradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 500)
  ..addEntry(defaultCameraZoomT, 1300)
  ..addEntry(1.0, 1600)

  g_camZoomWeatherFogZEndGradient = new GradientList<real, real>(0.0, (real a, real b, real t) -> lerp(a, b, t))
  ..addEntry(0.0, 1000)
  ..addEntry(defaultCameraZoomT, 2100)
  ..addEntry(1.0, 2700)

  doPeriodically(g_fogFadeDT) (CallbackPeriodic cb) ->
    g_fogFadeT = clamp01(g_fogFadeT + (g_fogFadeDT / g_fogFadeDuration) * g_fogFadeDir)
    if (g_updateFog)
      Weather.updateFog(g_fogFadeT)