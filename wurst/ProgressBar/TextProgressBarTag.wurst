package TextProgressBarTag
import TextProgressBar

constant real PROGRESS_BAR_CHAR_SIZE = 7.0

// ============================================================================
public class ProgressBarTag
  private TextProgressBar m_progressBar
  private texttag m_texttag
  private vec3 m_pos
  private real m_size

  // --------------------------------------------------------------------------
  construct(TextProgressBar progressBar, vec3 pos, real size)
    m_progressBar = progressBar
    m_pos = pos
    m_size = size
    
    m_texttag = CreateTextTag()
    m_texttag.setPermanent(true)
    m_texttag.setVelocity(0, 0)

    update(0)
    hide()

  // --------------------------------------------------------------------------
  ondestroy
    m_texttag.destr()

  // --------------------------------------------------------------------------
  function show()
    m_texttag.setVisibility(true)

  // --------------------------------------------------------------------------
  function showForPlayer(player p)
    if (localPlayer == p)
      m_texttag.setVisibility(true)

  // --------------------------------------------------------------------------
  function hide()
    m_texttag.setVisibility(false)

  // --------------------------------------------------------------------------
  function update(real progress)
    let text = m_progressBar.sample(progress)
    
    // Try to center the progress bar
    var offsetPos = m_pos
    offsetPos.x -= I2R(m_progressBar.getLength()) * 0.5 * PROGRESS_BAR_CHAR_SIZE

    m_texttag.setPos(offsetPos)
    m_texttag.setText(text, m_size)
