package ProgressBarTag
import ProgressBar

constant real PROGRESS_BAR_CHAR_SIZE = 7.0

public class ProgressBarTag
  private ProgressBar _progressBar
  private texttag _texttag
  private real _size

  construct(ProgressBar progressBar, vec3 pos, real size)
    this._progressBar = progressBar
    this._size = size
    this._texttag = CreateTextTag()
    this._texttag.setPermanent(true)
    this.setPos(pos)
    this.update(0)
    this.hide()

  ondestroy
    this._texttag.destr()

  function show()
    this._texttag.setVisibility(true)

  function showForPlayer(player p)
    if (localPlayer == p)
      this._texttag.setVisibility(true)

  function hide()
    this._texttag.setVisibility(false)

  function setPos(vec3 pos)
    var offsetPos = pos
    // Try to center the progress bar
    offsetPos.x -= I2R(_progressBar.getLength()) * 0.5 * PROGRESS_BAR_CHAR_SIZE
    this._texttag.setPos(offsetPos)

  function update(real progress)
    this._texttag.setText(_progressBar.sample(progress), this._size)
