package TimeOfDay
import GameConstants

trigger g_OnDayTrigger
trigger g_OnNightTrigger

// ============================================================================
public function getTimeOfDayElapsed() returns real
  let currentTime = GetTimeOfDay()
  if (currentTime > TIME_OF_DAY and currentTime <= TIME_OF_NIGHT)
    return currentTime - TIME_OF_DAY
  return 0

// ============================================================================
public function getTimeOfNightElapsed() returns real
  let currentTime = GetTimeOfDay()
  if (currentTime > TIME_OF_NIGHT)
    return currentTime - TIME_OF_NIGHT
  if (currentTime < TIME_OF_DAY)
    return (24.0 - TIME_OF_NIGHT) + currentTime
  return 0

// ============================================================================
public function getTimeOfDayPercentage() returns real
  return getTimeOfDayElapsed() / TIME_OF_DAY_LENGTH

// ============================================================================
public function getTimeOfNightPercentage() returns real
  return getTimeOfNightElapsed() / TIME_OF_NIGHT_LENGTH

// ============================================================================
public function registerDayEvent(code callback)
  g_OnDayTrigger.addCondition(Filter(callback))

// ============================================================================
public function registerNightEvent(code callback)
  g_OnNightTrigger.addCondition(Filter(callback))

// ============================================================================
init

  g_OnDayTrigger = CreateTrigger()
  g_OnDayTrigger.registerGameStateEvent(GAME_STATE_TIME_OF_DAY, EQUAL, TIME_OF_DAY)

  g_OnNightTrigger = CreateTrigger()
  g_OnNightTrigger.registerGameStateEvent(GAME_STATE_TIME_OF_DAY, EQUAL, TIME_OF_NIGHT)