package UndeadSpawnManager
import public SpawnManager
import GameConstants
import HumanPlayerComponent
import HashMap
import RegionExtensions
import Spawning
import Range

constant int NUM_RETRIES = 32
constant real TEST_RADIUS = 800.0
group g_testGroup = CreateGroup()

HashMap<int, UndeadSpawnManager> g_playerUndeadSpawnManagers

// ============================================================================
public class UndeadSpawnManager implements ISpawnPointProvider
  private player m_player
  
  // --------------------------------------------------------------------------
  construct(player p)
    m_player = p

  // --------------------------------------------------------------------------
  function getCenter() returns vec2
    return m_player.getHumanPlayerComponent().getCampCenter()

  // --------------------------------------------------------------------------
  override function getRandomSpawnPointInRange(vec2 pos, SpawnRange range) returns spawnPointResult
    var angleDeg = GetRandomReal(0, 360.0)
    let rangeReal = range.toRangeReal()
    for i = 0 to NUM_RETRIES
      angleDeg = angleDeg + GetRandomReal(180.0 * 0.75, 180.0 * 1.25)
      let dist = rangeReal.getRandom()
      let angle = angle(angleDeg*DEGTORAD)
      let testPos = pos.polarOffset(angle, dist)
      if (isSpawnPointValid(testPos))
        return spawnPointResult(true, testPos)

    return spawnPointResult(false, ZERO2)

  // --------------------------------------------------------------------------
  override function getRandomSpawnPointsInRange(vec2 pos, SpawnRange range, int count) returns spawnPointsResult
    let validSpawnPoints = new SpawnPointCollection()
    for i = 0 to count - 1
      let spawnPosResult = getRandomSpawnPointInRange(pos, range)
      if (spawnPosResult.succeeded)
        validSpawnPoints.pushPoint(spawnPosResult.spawnPoint)
    return spawnPointsResult(true, validSpawnPoints)

  // --------------------------------------------------------------------------
  private function isSpawnPointValid(vec2 pos) returns bool
    if (not g_spawnRegion.containsPoint(pos))
      return false
    g_testGroup.enumUnitsInRange(pos, TEST_RADIUS)    
    var isValid = true
    for u from g_testGroup
      if (doesUnitInvalidateUndeadSpawnPoint(u))
        isValid = false
        break
    return isValid

  // --------------------------------------------------------------------------
  private function doesUnitInvalidateUndeadSpawnPoint(unit u) returns bool
    return u.isAlive()
           and u.isEnemyOf(PLAYER_UNDEAD)
           and not u.isInvulnerable()

// ============================================================================
public function getPlayerUndeadSpawnManager(player p) returns UndeadSpawnManager
  UndeadSpawnManager spawnManager = g_playerUndeadSpawnManagers.get(p.getId())
  if (spawnManager == null)
    spawnManager = new UndeadSpawnManager(p)
    g_playerUndeadSpawnManagers.put(p.getId(), spawnManager)
  return spawnManager

// ============================================================================
init
  g_playerUndeadSpawnManagers = new HashMap<int, UndeadSpawnManager>()