package DirectorDebugger
import Frame
import ProjectConstants
import DebuggerDialog
import TabPanel
import Deck
import RealExtensions
import Directors
import LinkedList
import UnitAdmin

LinkedList<Director> g_debuggerDirectors = new LinkedList<Director>()
LinkedList<DirectorDebugger> g_directorDebuggers = new LinkedList<DirectorDebugger>()

// ============================================================================
class DirectorDebugger extends DebuggerRealtimeUpdateFrame
  private TabPanel m_tabPanel

  // --------------------------------------------------------------------------
  construct()
    super(createFrame("FRAME", "DirectorDebugger", GAME_UI, "", 0))

    m_tabPanel = new TabPanel()
    addChild(m_tabPanel)

    g_directorDebuggers.add(this)

    rebuildTabPanel()

  // ----------------------------------------------------------------------------
  ondestroy
    g_directorDebuggers.remove(this)

  // ----------------------------------------------------------------------------
  static function registerDirector(Director director)
    g_debuggerDirectors.add(director)
    for debugger in g_directorDebuggers
      debugger.onDirectorRegistered(director)

  // ----------------------------------------------------------------------------
  static function unregisterDirector(Director director)
    g_debuggerDirectors.remove(director)
    for debugger in g_directorDebuggers
      debugger.onDirectorUnregistered(director)

  // --------------------------------------------------------------------------
  override function realtimeUpdate(real _)
    skip

  // --------------------------------------------------------------------------
  private function rebuildTabPanel()

    let prevSelectedContentId = m_tabPanel.getSelectedContentId()
    m_tabPanel.removeAllContent()

    for director in g_debuggerDirectors
      m_tabPanel.addContent(director.getId().toString(), director.getName(), new DirectorFrame(director))

    if (m_tabPanel.getContent(prevSelectedContentId) != null)
      m_tabPanel.setSelectedContentId(prevSelectedContentId)

  // ----------------------------------------------------------------------------
  private function onDirectorRegistered(Director director)
    if (m_tabPanel != null)
      m_tabPanel.addContent(director.getId().toString(), director.getName(), new DirectorFrame(director))

  // ----------------------------------------------------------------------------
  private function onDirectorUnregistered(Director director)
    if (m_tabPanel != null)
      let tabContentId = director.getId().toString()
      m_tabPanel.removeContent(tabContentId)

// ============================================================================
class DirectorFrame extends Frame
  private Director m_director
  private TabPanel m_tabPanel

  // --------------------------------------------------------------------------
  construct(Director director)
    super(createFrame("FRAME", "DirectorDebugger", GAME_UI, "", 0))
    m_director = director

    m_tabPanel = new TabPanel()
    m_tabPanel.addContent("Info", new DirectorInfoFrame(m_director))
    //m_tabPanel.addContent("Deck", new DirectorDeckFrame(m_director))
    m_tabPanel.addContent("Admin", new AdminInfoFrame(m_director.getAdmin()))
    addChild(m_tabPanel)

// ============================================================================
class DirectorInfoFrame extends DebuggerRealtimeUpdateFrame
  private Director m_director

  // --------------------------------------------------------------------------
  construct(Director director)
    super(createFrame("TEXTAREA", "DIF", GAME_UI, "", 0))
    m_director = director

  // --------------------------------------------------------------------------
  override protected function realtimeUpdate(real _dt)
    let contentFrame = getFrameHandle()
    contentFrame.setText("=== Director ===")
    contentFrame.addText("Enabled: " + m_director.getEnabled().toString())
    contentFrame.addText("Credits: {0} t: {1} m:{2} a: {3} g: {4}".format(
      m_director.getCredits().toString(),
      m_director.getTotalCreditsAccumulated().toString(),
      m_director.getCreditMultiplier().toString(),
      m_director.getCreditsOnActivation().toString(),
      m_director.getCreditsPerGeneration().toString()
    ))

    m_director.writeDebugString() (string text) ->
      contentFrame.setOrAddText(text)

// ============================================================================
class DirectorDeckFrame extends DebuggerRealtimeUpdateFrame
  private Director m_director

  // --------------------------------------------------------------------------
  construct(Director director)
    super(createFrame("TEXTAREA", "DECK", GAME_UI, "", 0))
    m_director = director

  // --------------------------------------------------------------------------
  override protected function realtimeUpdate(real _dt)
    let contentFrame = getFrameHandle()

    let deck = m_director.getDeck()

    if (deck == null)
      contentFrame.setText("No deck")
      return

    let categories = deck.getCategories()

    for category in categories
    //{
      let cards = category.getCards()

      contentFrame.setOrAddText("[{0}] x{1} w:{2} {3}".format(
        category.getId(),
        cards.size().toString(),
        categories.getWeight(category).toString(),
        categories.getChance(category).toPercentageString01()))

      for card in cards
      //{
        contentFrame.setOrAddText(" > {0} {1}c a:{2} w:{3} {4}".format(
          card.getId(),
          card.getCost().toString(),
          card.canActivate().toString(),
          cards.getWeight(card).toString(),
          cards.getChance(card).toPercentageString01()))
      //}
    //}

// ============================================================================
public class AdminInfoFrame extends DebuggerRealtimeUpdateFrame
  private UnitAdmin m_admin

  // --------------------------------------------------------------------------
  construct(UnitAdmin admin)
    super(createFrame("TEXTAREA", "ADMIN", GAME_UI, "", 0))
    m_admin = admin

  // --------------------------------------------------------------------------
  override protected function realtimeUpdate(real _dt)

    let contentFrame = getFrameHandle()

    if (m_admin == null)
      contentFrame.setText("No admin")
      return

    contentFrame.setText("Admin: " + m_admin.getId())

    m_admin.writeDebugString() (string arg) ->
      contentFrame.addText(arg)

// ============================================================================
init
  if (DEV_ENVIRONMENT)
    DebuggerDialog.registerFrame("DRCT", () -> new DirectorDebugger())