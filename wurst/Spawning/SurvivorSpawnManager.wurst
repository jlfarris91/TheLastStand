package SurvivorSpawnManager
import public SpawnManager
import GameConstants
import PlayerExtensions

constant real TEST_RADIUS = 512.0

public SurvivorSpawnManager g_survivorSpawnManager

public class SurvivorSpawnManager extends SpawnManager
  private static group s_testGroup = CreateGroup()

  // --------------------------------------------------------------------------
  override function isSpawnPointValid(vec2 pos) returns bool
    if (not super.isSpawnPointValid(pos))
      return false
    s_testGroup.enumUnitsInRange(pos, TEST_RADIUS)    
    var isValid = true
    for u from s_testGroup
      if (doesUnitInvalidateSpawnPoint(u))
        isValid = false
        break
    return isValid

  // ============================================================================
  private function doesUnitInvalidateSpawnPoint(unit u) returns bool
    return u.isAlive() and
          (u.isEnemyOf(PLAYER_VILLAGERS) or u.getOwner().isHumanPlayer())
          and not u.isInvulnerable()

init
  g_survivorSpawnManager = new SurvivorSpawnManager()