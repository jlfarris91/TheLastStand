package SpawnUndeadUnitJob
import Jobs
import UndeadTargetService
import Spawning
import UnitRecycler
import GroupUtils
import Range
import Events
import ErrorHandling
import UnitMetadata
import Suspend
import RegisterEvents
import HashMap
import GroupExtensions
import Math
import Func
import PlayerMetadata
import RealTime
import ColorUtility
import RefObject

HashMap<unit, SpawnUndeadUnitJob> g_unitToJobMap

// ============================================================================
public enum SpawnJobState
  Spawn // Go ahead and spawn
  Wait  // Waiting for full group
  Done  // Done spawning units for this wave

// ============================================================================
public enum TierSelectionMode
  Once
  Unit
  Group

// ============================================================================
public class SpawnUndeadUnitJobConfig
  private TierSelectionMode m_tierSelectorMode = TierSelectionMode.Group
  private Func1<SpawnContext, int> m_tierSelector = null
  private ITieredUnitTypeProvider m_unitTypeProvider = null
  private UndeadTargetProvider m_targetProvider = null
  private ISpawnPointProvider m_spawnPointProvider = null
  private int m_maxSpawnCount = INT_MAX
  private int m_maxAliveCount = INT_MAX
  private int m_groupSize = 1
  private bool m_waitForFullSpawnGroup = false
  private rangeReal m_spawnRange
  private rangeReal m_facingAngleRange = rangeReal(0, 360)
  private bool m_useRecycler = false
  private Func2<SpawnUndeadUnitJob, unit, bool> m_prepareUnitFunc = null

  use RefObject

  // --------------------------------------------------------------------------
  ondestroy

    if (m_tierSelector != null)
      m_tierSelector.release()
      m_tierSelector = null

    if (m_prepareUnitFunc != null)
      m_prepareUnitFunc.release()
      m_prepareUnitFunc = null

  // --------------------------------------------------------------------------
  function clone() returns SpawnUndeadUnitJobConfig
    let clone = new SpawnUndeadUnitJobConfig()
    ..setTierSelectionMode(getTierSelectionMode())
    ..setTierSelector(getTierSelector())
    ..setUnitTypeProvider(getUnitTypeProvider())
    ..setSpawnRange(getSpawnRange())
    ..setFacingAngleRange(getFacingAngleRange())
    ..setSpawnPointProvider(getSpawnPointProvider())
    ..setTargetProvider(getTargetProvider())
    ..setWaitForFullGroup(getWaitForFullGroup())
    ..setMaxSpawnCount(getMaxSpawnCount())
    ..setMaxAliveCount(getMaxAliveCount())
    ..setGroupSize(getGroupSize())
    ..setPrepareUnitFunc(getPrepareUnitFunc())
    ..setUseRecycler(getUseRecycler())
    return clone

  // --------------------------------------------------------------------------
  function getTierSelectionMode() returns TierSelectionMode
    return m_tierSelectorMode
  
  // --------------------------------------------------------------------------
  function setTierSelectionMode(TierSelectionMode value)
    m_tierSelectorMode = value

  // --------------------------------------------------------------------------
  function getTierSelector() returns Func1<SpawnContext, int>
    return m_tierSelector
  
  // --------------------------------------------------------------------------
  function setTierSelector(Func1<SpawnContext, int> value)
    value.acquire()
    if (m_tierSelector != null)
      m_tierSelector.release()
    m_tierSelector = value

  // --------------------------------------------------------------------------
  function getUnitTypeProvider() returns ITieredUnitTypeProvider
    return m_unitTypeProvider
  
  // --------------------------------------------------------------------------
  function setUnitTypeProvider(ITieredUnitTypeProvider value)
    m_unitTypeProvider = value

  // --------------------------------------------------------------------------
  function getSpawnRange() returns rangeReal
    return m_spawnRange
  
  // --------------------------------------------------------------------------
  function setSpawnRange(rangeReal value)
    m_spawnRange = value

  // --------------------------------------------------------------------------
  function getFacingAngleRange() returns rangeReal
    return m_facingAngleRange
  
  // --------------------------------------------------------------------------
  function setFacingAngleRange(rangeReal value)
    m_facingAngleRange = value

  // --------------------------------------------------------------------------
  function getSpawnPointProvider() returns ISpawnPointProvider
    return m_spawnPointProvider
  
  // --------------------------------------------------------------------------
  function setSpawnPointProvider(ISpawnPointProvider value)
    m_spawnPointProvider = value

  // --------------------------------------------------------------------------
  function getTargetProvider() returns UndeadTargetProvider
    return m_targetProvider
  
  // --------------------------------------------------------------------------
  function setTargetProvider(UndeadTargetProvider value)
    value.acquire()
    if (m_targetProvider != null)
      m_targetProvider.release()
    m_targetProvider = value

  // --------------------------------------------------------------------------
  function getWaitForFullGroup() returns bool
    return m_waitForFullSpawnGroup
  
  // --------------------------------------------------------------------------
  function setWaitForFullGroup(bool value)
    m_waitForFullSpawnGroup = value

  // --------------------------------------------------------------------------
  function getMaxSpawnCount() returns int
    return m_maxSpawnCount
  
  // --------------------------------------------------------------------------
  function setMaxSpawnCount(int value)
    m_maxSpawnCount = value

  // --------------------------------------------------------------------------
  function getMaxAliveCount() returns int
    return m_maxAliveCount
  
  // --------------------------------------------------------------------------
  function setMaxAliveCount(int value)
    m_maxAliveCount = value

  // --------------------------------------------------------------------------
  function getPrepareUnitFunc() returns Func2<SpawnUndeadUnitJob, unit, bool>
    return m_prepareUnitFunc
  
  // --------------------------------------------------------------------------
  function setPrepareUnitFunc(Func2<SpawnUndeadUnitJob, unit, bool> value)
    value.acquire()
    if (m_prepareUnitFunc != null)
      m_prepareUnitFunc.release()
    m_prepareUnitFunc = value

  // --------------------------------------------------------------------------
  function getUseRecycler() returns bool
    return m_useRecycler
  
  // --------------------------------------------------------------------------
  function setUseRecycler(bool value)
    m_useRecycler = value

  // --------------------------------------------------------------------------
  function getGroupSize() returns int
    return m_groupSize

  // --------------------------------------------------------------------------
  function setGroupSize(int value)
    m_groupSize = value

// ============================================================================
public class SpawnUndeadUnitJob extends Job

  private player m_targetPlayer = null
  private player m_owningPlayer = null
  private SpawnContext m_context = null
  private SpawnUndeadUnitJobConfig m_config = null

  private group m_currentGroup = null
  private group m_spawnedUnits = null

  private int m_nextCounter = 0
  private int m_totalSpawned = 0
  private int m_groupId = -1

  private int m_tier = -1
  private bool m_setTierOnce = false

  private UnitTypeCollection m_unitTypes = null
  private int m_unitTypeIndex = -1

  private Event2<SpawnUndeadUnitJob, unit> m_onUnitSpawned = null
  private Event2<SpawnUndeadUnitJob, group> m_onGroupSpawned = null

  // --------------------------------------------------------------------------
  construct(string id, player owningPlayer, player targetPlayer, SpawnUndeadUnitJobConfig config)
    super(id, REAL_MAX)

    m_owningPlayer = owningPlayer
    m_targetPlayer = targetPlayer
    m_config = config.acquire()
    m_currentGroup = getGroup()
    m_spawnedUnits = getGroup()
    m_groupId = -1
    m_context = new SpawnContext()..acquire()
    m_unitTypes = new UnitTypeCollection()
    m_unitTypeIndex = 0

  // --------------------------------------------------------------------------
  ondestroy

    if (m_config != null)
      m_config.release()
      m_config = null

    if (m_context != null)
      m_context.release()
      m_context = null

    if (m_currentGroup != null)
    // {
      // Remove/stock any units that weren't fully spawned in with the current group
      for u in m_currentGroup
        let recycler = u.getRecycler()
        if (recycler != null)
          recycler.stock(u)
        else
          u.remove()

      m_currentGroup.release()
      m_currentGroup = null
    //}

    if (m_spawnedUnits != null)
    //{
      for u in m_spawnedUnits
        g_unitToJobMap.remove(u)

      m_spawnedUnits.release()
      m_spawnedUnits = null
    //}

    if (m_unitTypes != null)
      destroy m_unitTypes
      m_unitTypes = null

    if (m_onUnitSpawned != null)
      destroy m_onUnitSpawned
      m_onUnitSpawned = null

    if (m_onGroupSpawned != null)
      destroy m_onGroupSpawned
      m_onGroupSpawned = null

  // --------------------------------------------------------------------------
  function getContext() returns SpawnContext
    return m_context
  
  // --------------------------------------------------------------------------
  function setContext(SpawnContext value)
    value.acquire()
    if (m_context != null)
      m_context.release()
    m_context = value

  // --------------------------------------------------------------------------
  function getAliveUnitCount() returns int
    m_spawnedUnits.refresh()
    return m_spawnedUnits.size()

  // --------------------------------------------------------------------------
  function onUnitSpawned() returns IEvent2<SpawnUndeadUnitJob, unit>
    if (m_onUnitSpawned == null)
      m_onUnitSpawned = new Event2<SpawnUndeadUnitJob, unit>()
    return m_onUnitSpawned

  // --------------------------------------------------------------------------
  function onGroupSpawned() returns IEvent2<SpawnUndeadUnitJob, group>
    if (m_onGroupSpawned == null)
      m_onGroupSpawned = new Event2<SpawnUndeadUnitJob, group>()
    return m_onGroupSpawned

  // --------------------------------------------------------------------------
  override function next() returns int

    let restriction = canSpawnNextUnit()

    if (restriction == SpawnJobState.Done)
      // Wave is done spawning units
      return 0

    if (restriction == SpawnJobState.Wait)
      // Too many alive currently
      return 0

    if (m_nextCounter == 0)
      nextGroup()

    m_nextCounter++

    let tierSelectionMode = m_config.getTierSelectionMode()
    if (tierSelectionMode == TierSelectionMode.Unit)
      m_tier = m_config.getTierSelector().call(m_context)
      m_setTierOnce = true

    m_context.setGroupId(m_groupId)
    m_context.setTier(m_tier)

    if (m_config.getTierSelectionMode() == TierSelectionMode.Unit)
      populateUnitTypeIds(1)
      m_unitTypeIndex = 0

    if (m_unitTypes.size() == 0)
      // No units selected?
      return 0

    let unitType = getNextUnitTypeId()

    let spawnedUnit = createUnit(unitType)
    if (spawnedUnit == null)
      Log.error("SpawnUndeadUnitJob", "next", "", "Unable to spawn unit " + UnitId2String(unitType))
      spawnedUnit.removeOrStock()
      return 0

    if (not prepareUnit(spawnedUnit))
      Log.error("SpawnUndeadUnitJob", "next", "", "Unable to prepare unit " + UnitId2String(unitType))
      spawnedUnit.removeOrStock()
      return 0

    m_currentGroup.addUnit(spawnedUnit)
    registerSpawnedUnit(spawnedUnit)

    if (m_onUnitSpawned != null)
      m_onUnitSpawned.call(this, spawnedUnit)

    m_context.setGroupId(-1)
    m_context.setTier(-1)

    if (m_currentGroup.size() == m_config.getGroupSize())
      nextGroup()

    return 1

  // --------------------------------------------------------------------------
  override function hasNext() returns bool
    return canSpawnNextUnit() != SpawnJobState.Done

  // --------------------------------------------------------------------------
  override function complete()
    super.complete()
    completeGroup()

  // --------------------------------------------------------------------------
  private function nextGroup()

    completeGroup()

    if (m_totalSpawned == m_config.getMaxSpawnCount())
      return

    m_groupId++
    m_context.setGroupId(m_groupId)

    let tierSelectionMode = m_config.getTierSelectionMode()
    if (tierSelectionMode == TierSelectionMode.Group or 
        (tierSelectionMode == TierSelectionMode.Once and not m_setTierOnce))
    //{
      m_tier = m_config.getTierSelector().call(m_context)
      m_setTierOnce = true
    //}
    
    m_context.setTier(m_tier)

    if (tierSelectionMode != tierSelectionMode.Unit)
      populateUnitTypeIds(m_config.getGroupSize())

    m_unitTypeIndex = 0

  // --------------------------------------------------------------------------
  private function completeGroup()

    if (m_currentGroup.isEmpty())
      return

    let groupTargetUnit = m_config.getTargetProvider().getRandomTargetUnit(false) // TODO: prefer organic targets?
    m_context.setTargetUnit(groupTargetUnit)

    let result = m_config
      .getSpawnPointProvider()
      .getRandomSpawnPointInRange(groupTargetUnit.getPos(), m_config.getSpawnRange())

    if (not result.succeeded)
      Log.warn("[SpawnWave.onActivated] Found no spawn points!")
      cancel()
      return

    for spawnedUnit in m_currentGroup
      spawnedUnit.setPos(result.spawnPoint)
      spawnedUnit.unsuspend()
      spawnedUnit.show()

    if (m_onGroupSpawned != null)
      m_onGroupSpawned.call(this, m_currentGroup)
    
    m_currentGroup.clear()

    m_context.setTargetUnit(null)

  // --------------------------------------------------------------------------
  private function populateUnitTypeIds(int count)
    // Get new unit types for next subgroup
    m_unitTypes.clear()
    m_config.getUnitTypeProvider().getUnitTypes(m_context, count, m_unitTypes)

  // --------------------------------------------------------------------------
  private function getNextUnitTypeId() returns int
    let unitTypeId = m_unitTypes.get(m_unitTypeIndex)
    m_unitTypeIndex = wrap(m_unitTypeIndex + 1, 0, m_unitTypes.size() - 1)
    return unitTypeId
  
  // --------------------------------------------------------------------------
  private function createUnit(int unitTypeId) returns unit

    unit spawnedUnit

    let facingAngle = m_config.getFacingAngleRange().getRandom().asAngleDegrees()

    if (m_config.getUseRecycler())
      spawnedUnit = createUnitRecycled(m_owningPlayer, unitTypeId, ZERO2, facingAngle)
    else
      spawnedUnit = createUnitTLS(m_owningPlayer, unitTypeId, ZERO2, facingAngle)
    
    spawnedUnit.suspend()
    spawnedUnit.hide()

    return spawnedUnit

  // --------------------------------------------------------------------------
  private function prepareUnit(unit u) returns bool
    let prepareUnitFunc = m_config.getPrepareUnitFunc()
    return prepareUnitFunc != null ? prepareUnitFunc.call(this, u) : true

  // --------------------------------------------------------------------------
  private function canSpawnNextUnit() returns SpawnJobState
  
    if (m_totalSpawned == m_config.getMaxSpawnCount())
      return SpawnJobState.Done

    if (getAliveUnitCount() == m_config.getMaxAliveCount())
      return SpawnJobState.Wait

    if (m_targetPlayer == null)
      return SpawnJobState.Done

    if (not m_targetPlayer.isIngame())
      return SpawnJobState.Done

    let playerMetadata = m_targetPlayer.getMetadata()
    if (playerMetadata == null or playerMetadata.getHasLostGame())
      return SpawnJobState.Done

    return SpawnJobState.Spawn

  // --------------------------------------------------------------------------
  override function getDebuggerStateString() returns string

    string state

    switch (canSpawnNextUnit())
      case SpawnJobState.Spawn
        state = "Spwn"
      case SpawnJobState.Wait
        state = "Wait"
      case SpawnJobState.Done
        state = "Done"

    return "{0} {1} ({2}/{3}) G{4} {5}s".format(
      getId(),
      state,
      m_currentGroup.size().toString(),
      m_config.getGroupSize().toString(),
      m_groupId.toString(),
      (getRealTimeSeconds() - m_startTime).toString())
      .colorize(m_targetPlayer.getColor().toColor())

  // --------------------------------------------------------------------------
  private function registerSpawnedUnit(unit spawnedUnit)

    if (g_unitToJobMap.has(spawnedUnit))
      return

    g_unitToJobMap.put(spawnedUnit, this)
    m_spawnedUnits.addUnit(spawnedUnit)
    m_totalSpawned++

  // --------------------------------------------------------------------------
  private function unregisterSpawnedUnit(unit spawnedUnit)
    g_unitToJobMap.remove(spawnedUnit)
    m_currentGroup.removeUnit(spawnedUnit)
    m_spawnedUnits.removeUnit(spawnedUnit)

  // --------------------------------------------------------------------------
  protected function onUnitKilled(unit dyingUnit)
    unregisterSpawnedUnit(dyingUnit)

// ============================================================================
function onUnitDeath()
  let dyingUnit = GetDyingUnit()
  if (not g_unitToJobMap.has(dyingUnit))
    return
  let spawnJob = g_unitToJobMap.get(dyingUnit)
  spawnJob.onUnitKilled(dyingUnit)

// ============================================================================
init

  g_unitToJobMap = new HashMap<unit, SpawnUndeadUnitJob>()
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitDeath)