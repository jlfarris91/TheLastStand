package MarksmanJobComponent
import UnitMetadata
import Events
import SurvivorJobComponent
import RangedTargetXpReceieverComponent

constant int EXP_PER_KILL = 10

// ============================================================================
public class MarksmanJobComponent extends SurvivorJobComponent
  private Action m_onUnitKilledHandler
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()
    listenToUnitKilledEvent()

    getOwner().getRangedTargetXpRecieverComponent().setEnabled(true)

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()
    unlistenToUnitKilledEvent()

    getOwner().getRangedTargetXpRecieverComponent().setEnabled(false)

  // --------------------------------------------------------------------------
  private function listenToUnitKilledEvent()
    unlistenToUnitKilledEvent()
    m_onUnitKilledHandler = PlayerUnitEvents.unitKilled.add() -> 
      let killingUnit = GetKillingUnit()
      let dyingUnit = GetDyingUnit()
      if (killingUnit == getOwnerUnit() and dyingUnit.isEnemyOf(killingUnit))
        onUnitKilledByOwner()

  // --------------------------------------------------------------------------
  private function unlistenToUnitKilledEvent()
    if (m_onUnitKilledHandler != null)
      PlayerUnitEvents.unitKilled.remove(m_onUnitKilledHandler)
      destroy m_onUnitKilledHandler
      m_onUnitKilledHandler = null

  // --------------------------------------------------------------------------
  private function onUnitKilledByOwner()
    getOwnerUnit().addXp(EXP_PER_KILL, true)

// ============================================================================
public function IUnitMetadata.getMarksmanJobComponent() returns MarksmanJobComponent
  return this.getComponent(Type(MarksmanJobComponent.typeId)) castTo MarksmanJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddMarksmanJobComponent() returns MarksmanJobComponent
  var component = this.getMarksmanJobComponent()
  if (component == null)
    component = this.addComponent(new MarksmanJobComponent(this)) castTo MarksmanJobComponent
  return component