package MilitiaJobComponent
import SurvivorJobComponent
import UnitMetadata
import XpReceiverComponent
import RangedTargetXpReceieverComponent
import UnitPropertiesComponent
import TlsAbilityIds
import SurvivorJobs

constant real BONUS_DAMAGE_TO_NORMALS_PERCENTAGE = 10.0
constant real BONUS_DAMAGE_TO_ELITES_PERCENTAGE = -10.0
constant real BONUS_DAMAGE_TO_BOSS_PERCENTAGE = 0.0
constant real BONUS_EVADE_CHANCE_PERCENTAGE = 10.0

// ============================================================================
public class MilitiaJobComponent extends SurvivorJobComponent
  private int m_cleaveLevel = 0
  private int m_tauntLevel = 0
  private int m_defendLevel = 0
  private int m_thornsLevel = 0
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return MilitiaJobComponent.typeId

  // --------------------------------------------------------------------------
  override function getJob() returns SurvivorJobDefinition
    return SurvivorJobs.militia

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()
    getOwner().getOrAddRangedTargetXpRecieverComponent().setEnabled(true)
    getOwner().getOrAddXpReceiverComponent().setEnabled(true)

    let unitPropsComp = getOwner().getOrAddUnitPropertiesComponent()
    if (unitPropsComp != null)
      unitPropsComp.setNormalDamagePercentage(unitPropsComp.getNormalDamagePercentage() + BONUS_DAMAGE_TO_NORMALS_PERCENTAGE)
      unitPropsComp.setEliteDamagePercentage(unitPropsComp.getEliteDamagePercentage() + BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
      unitPropsComp.setBossDamagePercentage(unitPropsComp.getBossDamagePercentage() + BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
      unitPropsComp.setEvadeChancePercentage(unitPropsComp.getEvadeChancePercentage() + BONUS_EVADE_CHANCE_PERCENTAGE)

    updateAbilities()

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()

    let owner = getOwner()
    if (owner == null)
      return
    
    let rangedTargetXpReceieverComponent = owner.getRangedTargetXpRecieverComponent()
    if (rangedTargetXpReceieverComponent != null)
      rangedTargetXpReceieverComponent.setEnabled(false)

    let gainXpPerKillComponent = owner.getOrAddXpReceiverComponent()
    if (gainXpPerKillComponent != null)
      gainXpPerKillComponent.setEnabled(false)

    let unitPropsComp = getOwner().getUnitPropertiesComponent()
    if (unitPropsComp != null)
      unitPropsComp.setNormalDamagePercentage(unitPropsComp.getNormalDamagePercentage() - BONUS_DAMAGE_TO_NORMALS_PERCENTAGE)
      unitPropsComp.setEliteDamagePercentage(unitPropsComp.getEliteDamagePercentage() - BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
      unitPropsComp.setBossDamagePercentage(unitPropsComp.getBossDamagePercentage() - BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
      unitPropsComp.setEvadeChancePercentage(unitPropsComp.getEvadeChancePercentage() - BONUS_EVADE_CHANCE_PERCENTAGE)

    let ownerUnit = getOwnerUnit()
    ownerUnit.removeAbility(TlsAbilityIds.militiaCleave)
    ownerUnit.removeAbility(TlsAbilityIds.militiaTaunt)
    ownerUnit.removeAbility(TlsAbilityIds.militiaDefend)
    ownerUnit.removeAbility(TlsAbilityIds.militiaThorns)

  // --------------------------------------------------------------------------
  override function saveJobData()
    super.saveJobData()
    let ownerUnit = getOwnerUnit()
    m_cleaveLevel = ownerUnit.getAbilityLevel(TlsAbilityIds.militiaCleave)
    m_tauntLevel = ownerUnit.getAbilityLevel(TlsAbilityIds.militiaTaunt)
    m_defendLevel = ownerUnit.getAbilityLevel(TlsAbilityIds.militiaDefend)
    m_thornsLevel = ownerUnit.getAbilityLevel(TlsAbilityIds.militiaThorns)

  // --------------------------------------------------------------------------
  override function onLevelChanged()
    super.onLevelChanged()
    updateAbilities()

  // --------------------------------------------------------------------------
  private function updateAbilities()
    let ownerUnit = getOwnerUnit()
    let ownerUnitLevel = ownerUnit.getLevel()    
    
    if (not ownerUnit.hasAbility(TlsAbilityIds.militiaCleave))
      ownerUnit.addAbility(TlsAbilityIds.militiaCleave)

    if (not ownerUnit.hasAbility(TlsAbilityIds.militiaTaunt))
      ownerUnit.addAbility(TlsAbilityIds.militiaTaunt)

    if (not ownerUnit.hasAbility(TlsAbilityIds.militiaDefend))
      ownerUnit.addAbility(TlsAbilityIds.militiaDefend)

    if (not ownerUnit.hasAbility(TlsAbilityIds.militiaThorns))
      ownerUnit.addAbility(TlsAbilityIds.militiaThorns)

    ownerUnit.setAbilityLevel(TlsAbilityIds.militiaCleave, ownerUnitLevel)

// ============================================================================
public function IUnitMetadata.getMilitiaJobComponent() returns MilitiaJobComponent
  return this.getComponent(typeInfo(MilitiaJobComponent.typeId)) castTo MilitiaJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddMilitiaJobComponent() returns MilitiaJobComponent
  var component = this.getMilitiaJobComponent()
  if (component == null)
    component = this.addComponent(new MilitiaJobComponent(this), false) castTo MilitiaJobComponent
  return component

// ============================================================================
init
  SurvivorJobComponent.registerSurvivorJobComponentGetter(SurvivorJobs.militia, metadata -> metadata.getMilitiaJobComponent())
  SurvivorJobComponent.registerSurvivorJobComponentFactory(SurvivorJobs.militia, metadata -> metadata.getOrAddMilitiaJobComponent())