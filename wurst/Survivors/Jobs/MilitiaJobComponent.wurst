package MilitiaJobComponent
import SurvivorJobComponent
import UnitMetadata
import UnitPropertiesComponent
import SurvivorJobs
import Handle

constant real BONUS_DAMAGE_TO_NORMALS_PERCENTAGE = 0.1
constant real BONUS_DAMAGE_TO_ELITES_PERCENTAGE = -0.1
constant real BONUS_DAMAGE_TO_BOSS_PERCENTAGE = 0.0
constant real BONUS_EVADE_CHANCE_PERCENTAGE = 0.1

// ============================================================================
public class MilitiaJobComponent extends SurvivorJobComponent
  private _handle m_dmgscalarnormal = INVALID_HANDLE
  private _handle m_dmgscalarelite = INVALID_HANDLE
  private _handle m_dmgscalarboss = INVALID_HANDLE
  private _handle m_evadechance = INVALID_HANDLE
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getJobDefinition() returns SurvivorJobDefinition
    return SurvivorJobs.militia

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()

    let unitPropsComp = getOwner().getOrAddUnitPropertiesComponent()
    m_dmgscalarnormal = unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_NORMAL).affectAdd(BONUS_DAMAGE_TO_NORMALS_PERCENTAGE)
    m_dmgscalarelite = unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_ELITE).affectAdd(BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
    m_dmgscalarboss = unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_BOSS).affectAdd(BONUS_DAMAGE_TO_ELITES_PERCENTAGE)
    m_evadechance = unitPropsComp.getProperty(UnitProperty.EVADE_CHANCE_01).affectAdd(BONUS_EVADE_CHANCE_PERCENTAGE)

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()

    let owner = getOwner()
    if (owner == null)
      return

    let unitPropsComp = getOwner().getUnitPropertiesComponent()
    if (unitPropsComp != null)
      unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_NORMAL).removeAffector(m_dmgscalarnormal)
      unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_ELITE).removeAffector(m_dmgscalarelite)
      unitPropsComp.getProperty(UnitProperty.DAMAGE_SCALAR_BOSS).removeAffector(m_dmgscalarboss)
      unitPropsComp.getProperty(UnitProperty.EVADE_CHANCE_01).removeAffector(m_evadechance)

    m_dmgscalarnormal = INVALID_HANDLE
    m_dmgscalarelite = INVALID_HANDLE
    m_dmgscalarboss = INVALID_HANDLE
    m_evadechance = INVALID_HANDLE

// ============================================================================
public function IUnitMetadata.getMilitiaJobComponent() returns MilitiaJobComponent
  return this.getComponent(MilitiaJobComponent.typeId) castTo MilitiaJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddMilitiaJobComponent() returns MilitiaJobComponent
  var component = this.getMilitiaJobComponent()
  if (component == null)
    component = this.addComponent(new MilitiaJobComponent(this), false) castTo MilitiaJobComponent
  return component

// ============================================================================
init
  SurvivorJobComponent.registerSurvivorJobComponentGetter(SurvivorJobType.Militia, MilitiaJobComponent.typeId)
  SurvivorJobComponent.registerSurvivorJobComponentFactory(SurvivorJobType.Militia, metadata -> metadata.getOrAddMilitiaJobComponent())