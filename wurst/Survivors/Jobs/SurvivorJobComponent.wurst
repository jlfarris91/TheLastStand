package SurvivorJobComponent
import UnitComponent
import SurvivorJobs
import Func

int array[g_survivorJobCount] g_survivorJobComponentIds
Func1<IUnitMetadata, SurvivorJobComponent> array[g_survivorJobCount] g_survivorJobComponentFactories

// ============================================================================
public abstract class SurvivorJobComponent extends UnitComponent
  private int m_unitId
  private int m_skillPoints
  private bool m_isAssigned

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)
    m_skillPoints = 1
    m_isAssigned = false
    m_unitId = getJob().getInitialUnitType()

  // --------------------------------------------------------------------------
  abstract function getJob() returns SurvivorJobDefinition

  // --------------------------------------------------------------------------
  function getUnitId() returns int
    return m_unitId

  // --------------------------------------------------------------------------
  function setUnitId(int unitId)
    m_unitId = unitId

  // --------------------------------------------------------------------------
  function getIsAssigned() returns bool
    return m_isAssigned

  // --------------------------------------------------------------------------
  function setIsAssigned(bool value)
    m_isAssigned = value

  // --------------------------------------------------------------------------
  function getSkillPoints() returns int
    return m_skillPoints

  // --------------------------------------------------------------------------
  function setSkillPoints(int value)
    m_skillPoints = value

  // --------------------------------------------------------------------------
  function onLevelChanged()
    saveJobData()

  // --------------------------------------------------------------------------
  function onSkillPointSpent()
    saveJobData()

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    loadJobData()

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()
    saveJobData()

  // --------------------------------------------------------------------------
  protected function loadJobData()
    let ownerUnit = getOwnerUnit()
    ownerUnit.setSkillPoints(getSkillPoints())

  // --------------------------------------------------------------------------
  protected function saveJobData()
    let ownerUnit = getOwnerUnit()
    setUnitId(ownerUnit.getTypeId())
    setSkillPoints(ownerUnit.getSkillPoints())

  // --------------------------------------------------------------------------
  static function registerSurvivorJobComponentGetter(SurvivorJobType job, int componentTypeId)
    g_survivorJobComponentIds[job castTo int] = componentTypeId

  // --------------------------------------------------------------------------
  static function registerSurvivorJobComponentFactory(SurvivorJobType job, Func1<IUnitMetadata, SurvivorJobComponent> getter)
    g_survivorJobComponentFactories[job castTo int] = getter.acquire()

// ============================================================================
public function IUnitMetadata.getSurvivorJobComponent(SurvivorJobType job) returns SurvivorJobComponent
  let componentTypeId = g_survivorJobComponentIds[job castTo int]
  return componentTypeId == 0 ? null : this.getComponent(componentTypeId) castTo SurvivorJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorJobComponent(SurvivorJobType job) returns SurvivorJobComponent
  var comp = this.getSurvivorJobComponent(job)
  if (comp == null)
    let factory = g_survivorJobComponentFactories[job castTo int]
    if (factory != null)
      comp = factory.call(this)
  return comp