package MedicJobComponent
import SurvivorJobComponent
import UnitMetadata
import Events
import Type

constant real EXP_PER_HEAL = 0.1

// ============================================================================
public class MedicJobComponent extends SurvivorJobComponent
  private Action1<UnitHealedEventArgs> m_unitHealedEventHandler
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()
    listenToHealEvent()

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()
    unlistenToHealEvent()

  // --------------------------------------------------------------------------
  private function onHealedUnit(real amountHealed)
    getOwnerUnit().addXp(R2I(amountHealed * EXP_PER_HEAL), true)

  // --------------------------------------------------------------------------
  private function listenToHealEvent()
    unlistenToHealEvent()
    if (m_unitHealedEventHandler == null)
      m_unitHealedEventHandler = PlayerUnitEvents.unitHealed.add() (UnitHealedEventArgs args) ->
        let ownerUnit = getOwnerUnit()
        if (ownerUnit != null and args.getHealingSource() == ownerUnit)
          onHealedUnit(args.getAmountHealed())

  // --------------------------------------------------------------------------
  private function unlistenToHealEvent()
    if (m_unitHealedEventHandler != null)
      PlayerUnitEvents.unitHealed.remove(m_unitHealedEventHandler)
      destroy m_unitHealedEventHandler
      m_unitHealedEventHandler = null

// ============================================================================
public function IUnitMetadata.getMedicJobComponent() returns MedicJobComponent
  return this.getComponent(Type(MedicJobComponent.typeId)) castTo MedicJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddMedicJobComponent() returns MedicJobComponent
  var component = this.getMedicJobComponent()
  if (component == null)
    component = this.addComponent(new MedicJobComponent(this)) castTo MedicJobComponent
  return component