package SurvivorJoblessComponent
import SurvivorJobComponent
import UnitMetadata
import TlsAbilityIds
import Ability_Harvest
import RegisterEvents
import Orders
import TlsUpgradeIds
import TlsUnitIds
import GroupUtils

// ============================================================================
public class SurvivorJoblessComponent extends SurvivorJobComponent
  protected destructable m_targetDestructable
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)
    m_targetDestructable = null

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return SurvivorJoblessComponent.typeId

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    updateAbility()

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()
    m_targetDestructable = null

  // --------------------------------------------------------------------------
  override function onLevelChanged()
    super.onLevelChanged()
    updateAbility()

  // --------------------------------------------------------------------------
  function updateAbility()
    let ownerUnit = getOwnerUnit()
    let ownerUnitLevel = ownerUnit.getLevel()
    let researchLevel = GetPlayerTechCountSimple(TlsUpgradeIds.lumberHarvest, ownerUnit.getOwner())
    let abilityId = getHarvestAbility(researchLevel, ownerUnitLevel)

    // Remove all versions of harvest
    for i = 0 to HARVEST_LEVELS - 1
      if (ownerUnit.hasAbility(TlsAbilityIds.harvest[i]))
        ownerUnit.removeAbility(TlsAbilityIds.harvest[i])
        break

    // Add the version of harvest that matches the level of the survivor
    if (not ownerUnit.hasAbility(abilityId))
      ownerUnit.addAbility(abilityId)

    // Need to reissue the harvest order when the harvest ability is removed and re-added
    if (m_targetDestructable != null)
      getOwnerUnit().issueTargetOrderById(OrderIds.harvest, m_targetDestructable)

// ============================================================================
public function IUnitMetadata.getSurvivorJoblessComponent() returns SurvivorJoblessComponent
  return this.getComponent(typeInfo(SurvivorJoblessComponent.typeId)) castTo SurvivorJoblessComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorJoblessComponent() returns SurvivorJoblessComponent
  var component = this.getSurvivorJoblessComponent()
  if (component == null)
    component = this.addComponent(new SurvivorJoblessComponent(this)) castTo SurvivorJoblessComponent
  return component

// ============================================================================
function onUnitIssuedAnyOrder()
  let orderedUnit = GetOrderedUnit()
  let orderId = GetIssuedOrderId()

  let metadata = orderedUnit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getSurvivorJoblessComponent()
  if (comp == null or not comp.getEnabled())
    return

  if (orderId == OrderIds.harvest)
    comp.m_targetDestructable = GetOrderTargetDestructable()
  else
    comp.m_targetDestructable = null

// ============================================================================
function onResearchFinished()
  let research = GetResearched()
  let researchingUnit = GetResearchingUnit()
  if (research != TlsUpgradeIds.lumberHarvest)
    return
  let owner = researchingUnit.getOwner()
  let temp = getGroup()
  temp.enumUnitsOfPlayer(owner, null)
  for _unit in temp
    if (_unit.getTypeId() == TlsUnitIds.Survivors.survivorMale and _unit.isAlive())
      let metadata = _unit.getMetadata()
      if (metadata != null)
        let comp = metadata.getSurvivorJoblessComponent()
        if (comp != null)
          comp.updateAbility()
  temp.release()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onUnitIssuedAnyOrder)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, function onUnitIssuedAnyOrder)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, function onUnitIssuedAnyOrder)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_RESEARCH_FINISH, function onResearchFinished)