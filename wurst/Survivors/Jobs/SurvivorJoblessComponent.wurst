package SurvivorJoblessComponent
import SurvivorJobComponent
import UnitMetadata
import Orders
import RegisterEvents
import ClosureTimers

// ============================================================================
public class SurvivorJoblessComponent extends SurvivorJobComponent
  private bool m_wasActivelyRepairing
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return SurvivorJoblessComponent.typeId

  // --------------------------------------------------------------------------
  function getWasActivelyRepairing() returns bool
    return m_wasActivelyRepairing

  // --------------------------------------------------------------------------
  function setWasActivelyRepairing(bool value)
    m_wasActivelyRepairing = value

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()

    if (m_wasActivelyRepairing)
      nullTimer(() -> getOwnerUnit().issueImmediateOrder("repairon"))

// ============================================================================
public function IUnitMetadata.getSurvivorJoblessComponent() returns SurvivorJoblessComponent
  return this.getComponent(typeInfo(SurvivorJoblessComponent.typeId)) castTo SurvivorJoblessComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorJoblessComponent() returns SurvivorJoblessComponent
  var component = this.getSurvivorJoblessComponent()
  if (component == null)
    component = this.addComponent(new SurvivorJoblessComponent(this)) castTo SurvivorJoblessComponent
  return component

// ============================================================================
function onUnitIssuedOrder()
  let orderedUnit = GetOrderedUnit()
  let orderId = GetIssuedOrderId()

  if (orderId != OrderIds.repairon and orderId != OrderIds.repairoff)
    return

  let metadata = orderedUnit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getSurvivorJoblessComponent()
  if (comp != null and comp.getEnabled())
    return

  comp.setWasActivelyRepairing(orderId == OrderIds.repairon)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onUnitIssuedOrder)