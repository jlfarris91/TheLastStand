package SurvivorJoblessComponent
import SurvivorJobComponent
import UnitMetadata
import SurvivorJobs
import SurvivorComponent

// ============================================================================
public class SurvivorJoblessComponent extends SurvivorJobComponent
  private effect m_overheadAssignedJobIcon
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getJobDefinition() returns SurvivorJobDefinition
    return SurvivorJobs.none

  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()

    let survivorComponent = getOwner().getSurvivorComponent()
    let assignedJobComp = survivorComponent.getAssignedJobComponent()
    if (assignedJobComp != null)
      let assignedJobDef = assignedJobComp.getJobDefinition()
      let effectPath = assignedJobDef.getOverheadIconPath(assignedJobComp.getUnitTypeId())
      if (effectPath != null)
        m_overheadAssignedJobIcon = addEffect(effectPath, getOwnerUnit(), "overhead")

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()

    if (m_overheadAssignedJobIcon != null)
      m_overheadAssignedJobIcon.destr()
      m_overheadAssignedJobIcon = null

// ============================================================================
public function IUnitMetadata.getSurvivorJoblessComponent() returns SurvivorJoblessComponent
  return this.getComponent(SurvivorJoblessComponent.typeId) castTo SurvivorJoblessComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorJoblessComponent() returns SurvivorJoblessComponent
  var component = this.getSurvivorJoblessComponent()
  if (component == null)
    component = this.addComponent(new SurvivorJoblessComponent(this), false) castTo SurvivorJoblessComponent
  return component

// ============================================================================
init
  SurvivorJobComponent.registerSurvivorJobComponentGetter(SurvivorJobType.None, SurvivorJoblessComponent.typeId)
  SurvivorJobComponent.registerSurvivorJobComponentFactory(SurvivorJobType.None, metadata -> metadata.getOrAddSurvivorJoblessComponent())