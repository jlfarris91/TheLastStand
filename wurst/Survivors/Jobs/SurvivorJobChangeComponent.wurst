package SurvivorJobChangeComponent
import UnitComponent
import UnitExtensions
import FX
import SurvivorJobs
import RegisterEvents
import DisplayTextToPlayer
import MainItemLibrary
import SurvivorComponent
import TlsUnitDefinition
import TlsJobItemIds
import ColorUtility

group g_temp = CreateGroup()

// ============================================================================
function filterUnitIsSurvivorMale() returns bool
  return GetFilterUnit().getTypeId() == TlsUnitIds.Survivors.survivorMale

// ============================================================================
function countNumberOfJoblessSurvivors(player p) returns int
  let filter = Condition(function filterUnitIsSurvivorMale)
  g_temp.clear()
  g_temp.enumUnitsOfPlayer(p, filter)
  let count = g_temp.size()
  filter.destr()
  return count

// ============================================================================
public class SurvivorJobChangeComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return SurvivorJobChangeComponent.typeId

  // --------------------------------------------------------------------------
  function changeJobs(SurvivorJobDefinition job) returns bool
    let ownerUnit = getOwnerUnit()
    let unitType = ownerUnit.getTypeId()

    if (job.hasUnitType(unitType))
      displayMessageToPlayer(ownerUnit.getOwner(), "{0} is already a {1}!".format(ownerUnit.getProperName().colorize(COLOR_GOLD), job.getTitle().colorize(COLOR_GOLD)))
      return false

    let survivorComp = getOwner().getSurvivorComponent()
    if (survivorComp == null)
      return false

    let survivorData = survivorComp.getSurvivorData()
    if (survivorData == null)
      return false

    let jobData = survivorData.getJobData(job)
    if (jobData == null)
      return false

    let replacementUnitId = jobData.getUnitId()
    let replacedUnit = replaceUnit(replacementUnitId)
  
    let oldJob = getJobDefinitionForUnitType(unitType)
    if (oldJob != SurvivorJobs.none)
      let jobItemType = oldJob.getItemType()
      let jobItem = g_MainItemLibrary.createItem(jobItemType, replacedUnit.getPos())
      UnitAddItemSwapped(jobItem, replacedUnit)

    return true

  // --------------------------------------------------------------------------
  function upgradeJob() returns bool
    let currentUnitId = getOwnerUnit().getTypeId()
    let job = getJobDefinitionForUnitType(currentUnitId)
    
    let nextUnitId = job.getNextUpgrade(currentUnitId)
    if (nextUnitId == -1)
      return false

    let newUnit = replaceUnit(nextUnitId)

    // Reset to level 1 when you upgrade
    newUnit.removeLevels(newUnit.getLevel())
    newUnit.setXp(0, false)

    // There is a bug with upgrading non-building units where the player is not
    // charged for the upgrade - so do that now.
    let nextUnitDef = getUnitDefinition(nextUnitId)
    if (nextUnitDef != null)
      let p = getOwnerUnit().getOwner()
      p.subGold(nextUnitDef.getGoldCost())
      p.subLumber(nextUnitDef.getLumberCost())
    else
      Log.debug("No definition registered for unit type {0}".format(nextUnitId.toString()))

    return true

  // --------------------------------------------------------------------------
  private function replaceUnit(int replacementUnitId) returns unit
    let ownerUnit = getOwnerUnit()

    let wasSelected = ownerUnit.isSelectedByOwner()
    let replacedUnit = replaceUnitTLS(ownerUnit, replacementUnitId, bj_UNIT_STATE_METHOD_RELATIVE)
    if (wasSelected)
      SelectUnitAddForPlayer(replacedUnit, replacedUnit.getOwner())

    FX.createJobChangeEffect(replacedUnit.getPos())
    FX.createJobChangeTag(replacedUnit.getName(), replacedUnit.getPos(), replacedUnit.getOwner())

    return replacedUnit

// ============================================================================
public function IUnitMetadata.getSurvivorJobChangeComponent() returns SurvivorJobChangeComponent
  return this.getComponent(typeInfo(SurvivorJobChangeComponent.typeId)) castTo SurvivorJobChangeComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorJobChangeComponent() returns SurvivorJobChangeComponent
  var component = this.getSurvivorJobChangeComponent()
  if (component == null)
    component = this.addComponent(new SurvivorJobChangeComponent(this)) castTo SurvivorJobChangeComponent
  return component

// ============================================================================
function onUnitUsedItem()
  let manipulatingUnit = GetManipulatingUnit()
  let manipulatedItem = GetManipulatedItem()

  if (not manipulatedItem.isJobItem())
    return

  let metadata = manipulatingUnit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getSurvivorJobChangeComponent()
  if (comp == null or not comp.getEnabled())
    displayMessageToPlayer(manipulatingUnit.getOwner(), "This item can only be used by a survivor")
    return

  let jobId = getJobDefinitionForItemType(manipulatedItem.getTypeId())
  if (jobId == SurvivorJobs.none)
    return

  if (comp.changeJobs(jobId))
    manipulatedItem.remove()

// ============================================================================
function onOrderIssued()

  let issuedOrderId = GetIssuedOrderId()
  let orderedUnit = GetOrderedUnit()

  let metadata = orderedUnit.getMetadata()
  if (metadata == null)
    return
  
  let comp = metadata.getSurvivorJobChangeComponent()
  if (comp == null)
    return

  let currentUnitId = orderedUnit.getTypeId()
  let job = getJobDefinitionForUnitType(currentUnitId)
  if (job == null)
    return

  let nextUnitId = job.getNextUpgrade(currentUnitId)
  if (nextUnitId == issuedOrderId)
    comp.upgradeJob()

// ============================================================================
function onUnitKilled()
  let dyingUnit = GetDyingUnit()

  if (not dyingUnit.isSurvivor())
    return

  let job = getJobDefinitionForUnitType(dyingUnit.getTypeId())
  if (job == SurvivorJobs.none)
    return

  let jobItemType = job.getItemType()
  g_MainItemLibrary.createItem(jobItemType, dyingUnit.getPos())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_USE_ITEM, function onUnitUsedItem)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onOrderIssued)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)