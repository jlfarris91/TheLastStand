package SpearmanJobComponent
import SurvivorJobComponent
import UnitMetadata
import RangedTargetXpReceieverComponent
import GainXpPerKillComponent
import TlsUnitIds
import TlsAbilityIds
import RegisterEvents
import Abilities

// ============================================================================
public class SpearmanJobComponent extends SurvivorJobComponent
  private effect m_upgradeAvailableEffect

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return SpearmanJobComponent.typeId

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()
    getOwner().getOrAddRangedTargetXpRecieverComponent().setEnabled(true)
    getOwner().getOrAddGainXpPerKillComponent().setEnabled(true)

    getOwnerUnit().addAbility(TlsAbilityIds.spearmanUpgrade1Icon)

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()

    hideUpgradeAvailableEffect()

    let ownerUnit = getOwner()
    if (ownerUnit == null)
      return
    
    let rangedTargetXpReceieverComponent = ownerUnit.getRangedTargetXpRecieverComponent()
    if (rangedTargetXpReceieverComponent != null)
      rangedTargetXpReceieverComponent.setEnabled(false)

    let gainXpPerKillComponent = ownerUnit.getOrAddGainXpPerKillComponent()
    if (gainXpPerKillComponent != null)
      gainXpPerKillComponent.setEnabled(false)

  // --------------------------------------------------------------------------
  function showUpgradeAvailableEffect()
    if (m_upgradeAvailableEffect != null)
      return
    let ownerUnit = getOwnerUnit()
    m_upgradeAvailableEffect = AddSpecialEffectTarget(Abilities.spiritLinkTarget, ownerUnit, "origin")

  // --------------------------------------------------------------------------
  function hideUpgradeAvailableEffect()
    if (m_upgradeAvailableEffect == null)
      return
    m_upgradeAvailableEffect.destr()
    m_upgradeAvailableEffect = null

// ============================================================================
public function IUnitMetadata.getSpearmanJobComponent() returns SpearmanJobComponent
  return this.getComponent(typeInfo(SpearmanJobComponent.typeId)) castTo SpearmanJobComponent

// ============================================================================
public function IUnitMetadata.getOrAddSpearmanJobComponent() returns SpearmanJobComponent
  var component = this.getSpearmanJobComponent()
  if (component == null)
    component = this.addComponent(new SpearmanJobComponent(this)) castTo SpearmanJobComponent
  return component

// ============================================================================
function onHeroLeveled()
  let hero = GetLevelingUnit()

  let metadata = hero.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getSpearmanJobComponent()
  if (comp == null)
    return

  if (hero.getTypeId() == TlsUnitIds.Survivors.survivorSpearman and hero.getLevel() == 3)
    hero.removeAbility(TlsAbilityIds.spearmanUpgrade1Icon)
    hero.addAbility(TlsAbilityIds.spearmanUpgrade1)
    comp.showUpgradeAvailableEffect()

// ============================================================================
function onSkillLearned()

  let learnedSkill = GetLearnedSkill()
  let hero = GetLearningUnit()

  let metadata = hero.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getSpearmanJobComponent()
  if (comp == null)
    return

  int actualSkillToLearn

  if (learnedSkill == TlsAbilityIds.learnFireAttack)
    actualSkillToLearn = TlsAbilityIds.fireAttack
  else if (learnedSkill == TlsAbilityIds.learnFrostAttack)
    actualSkillToLearn = TlsAbilityIds.frostAttack
  else if (learnedSkill == TlsAbilityIds.learnLightningAttack)
    actualSkillToLearn = TlsAbilityIds.lightningAttack
  else
    return

  hero.removeAbility(TlsAbilityIds.spearmanUpgrade1Icon)
  hero.removeAbility(TlsAbilityIds.spearmanUpgrade1)
  hero.removeAbility(learnedSkill)
  hero.addAbility(actualSkillToLearn)
  comp.hideUpgradeAvailableEffect()

  hero.addAbility(TlsAbilityIds.spearmanUpgrade2Icon)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_HERO_SKILL, function onSkillLearned)
  registerPlayerUnitEvent(EVENT_PLAYER_HERO_LEVEL, function onHeroLeveled)

  // Make the spearman upgrade spellbook glow
  CreateCommandButtonEffect(TlsAbilityIds.spearmanUpgrade1, "spellbook")