package Hero
import Camp
import Skills
import SkillsComponent
import StaminaComponent
import BackpackInventory
import BackpackComponent
import HumanPlayerMetadata
import MainItemLibrary
import TlsItemIds

// ============================================================================
public class Hero extends CampUnit
  private SkillsComponent _skills

  // --------------------------------------------------------------------------
  construct(ICamp camp)
    super(camp)

    addComponent(new StaminaComponent(this))
    addComponent(new BackpackComponent(this, new BackpackInventory()))

    _skills = new SkillsComponent(this)
    _skills.addSkill(Skills.sprint.createInstance(this))
    addComponent(_skills)

  // --------------------------------------------------------------------------
  override function onUnitChanged(unit oldUnit, unit newUnit)
    super.onUnitChanged(oldUnit, newUnit)

    if (oldUnit != null)
      let humanPlayerMetadata = oldUnit.getOwnerHumanMetadata()
      if (humanPlayerMetadata != null)
        humanPlayerMetadata.setHero(null)

    if (newUnit != null)
      let humanPlayerMetadata = newUnit.getOwnerHumanMetadata()
      if (humanPlayerMetadata != null)
        humanPlayerMetadata.setHero(this)

      for i = 0 to 2
        let grenade = g_MainItemLibrary.createItem(TlsItemIds.grenade, newUnit.getPos())
        UnitAddItem(newUnit, grenade)
