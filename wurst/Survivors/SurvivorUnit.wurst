package SurvivorUnit
import SurvivorJobComponent
import TlsUnitIds
import GameConstants
import SurvivorStats
import Statistic
import Icons
import SurvivorComponent
import HashMap
import KillsStat
import DaysSurvivedStat
import NightsSurvivedStat
import SurvivorData
import SurvivorNames
import SurvivorJobs
import RangedTargetXpReceieverComponent
import UnitMetadata
import RegisterEvents
import PersistActiveAbilityComponent
import ClosureTimers

import SurvivorJoblessComponent
import BuilderJobComponent
import MilitiaJobComponent
import SpearmanJobComponent
import MedicJobComponent
import MarksmanJobComponent
import SurvivorJobChangeComponent
import EngineerJobComponent
import AcolyteComponent
import SurvivorJobData

// ============================================================================
public class Survivor extends UnitMetadata
  private IterableMap<SurvivorJobDefinition, SurvivorJobComponent> m_jobComponents
  private SurvivorJobDefinition m_activeJobDef
  private SurvivorJobComponent m_activeJobComponent
  private SurvivorData m_survivorData

  // --------------------------------------------------------------------------
  construct()
    m_jobComponents = new IterableMap<SurvivorJobDefinition, SurvivorJobComponent>()
    initialize()

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_jobComponents
    m_jobComponents = null
    m_activeJobComponent = null

    destroy m_survivorData
    m_survivorData = null

  // --------------------------------------------------------------------------
  function getActiveJobComponent() returns SurvivorJobComponent
    return m_activeJobComponent

  // --------------------------------------------------------------------------
  function getActiveJobDefinition() returns SurvivorJobDefinition
    return m_activeJobDef

  // --------------------------------------------------------------------------
  protected override function onPreUnitChanged(unit oldUnit, unit newUnit)
    super.onPreUnitChanged(oldUnit, newUnit)

    if (oldUnit != null)
      saveJobData(oldUnit)

    if (m_activeJobComponent != null)
      m_activeJobComponent.setEnabled(false)

    m_activeJobComponent = null
    m_activeJobDef = null

  // --------------------------------------------------------------------------
  protected override function onPostUnitChanged(unit oldUnit, unit newUnit)
    super.onPostUnitChanged(oldUnit, newUnit)

    if (newUnit == null)
      return
    
    //Log.info("SurvivorUnit", "onPostUnitChanged", newUnit.getName(), "newUnit 1 xp " + newUnit.getXp().toString())

    let newUnitId = newUnit.getTypeId()
    let job = getJobDefinitionForUnitType(newUnitId)
    
    m_activeJobDef = job
    m_activeJobComponent = getJobComponent(job)
    
    //Log.info("SurvivorUnit", "onPostUnitChanged", newUnit.getName(), "newUnit 2 xp " + newUnit.getXp().toString())

    if (m_activeJobComponent != null)
      m_activeJobComponent.setEnabled(true)
    
    //Log.info("SurvivorUnit", "onPostUnitChanged", newUnit.getName(), "newUnit 3 xp " + newUnit.getXp().toString())

    resetExperienceIfPromoting(newUnit)

    updateUnitProperName(newUnit)

    // Load this data next frame to avoid some weird War 3 behavior.
    // Learning skills seems to be broken if you try to learn the same skill
    // multiple times the same frame that the unit is replaced. The game reports
    // that the skill was successfully learned and multiple times but it is still
    // level 1.
    nullTimer(() -> loadJobData(newUnit))

    //Log.info("SurvivorUnit", "onPostUnitChanged", newUnit.getName(), "newUnit 4 xp " + newUnit.getXp().toString())

  // --------------------------------------------------------------------------
  private function saveJobData(unit theUnit)
    let unitTypeId = theUnit.getTypeId()
    let jobId = getJobDefinitionForUnitType(unitTypeId)
    let survivorDataComp = this.getSurvivorComponent()
    let survivorData = survivorDataComp.getSurvivorData()

    let jobData = survivorData.getJobData(jobId)
    if (jobData != null)
      m_activeJobComponent.saveJobData(jobData)
    //   Log.info("SurvivorUnit", "saveOldJobStats", jobId.getTitle(), "xp " + theUnit.getXp().toString())
    // else
    //   Log.info("SurvivorUnit", "saveOldJobStats", jobId.getTitle(), "Could not find job data")

  // --------------------------------------------------------------------------
  private static function loadJobData(unit theUnit)
    let survivor = theUnit.getMetadata() castTo Survivor
    if (survivor == null)
      Log.error("SurvivorUnit", "loadJobData", theUnit.getHandleId().toString(), "Unit does not have Survivor metadata")
      return

    if (survivor.m_activeJobComponent == null)
      Log.error("SurvivorUnit", "loadJobData", theUnit.getHandleId().toString(), "Active job component is null")
      return

    let unitTypeId = theUnit.getTypeId()
    let jobId = getJobDefinitionForUnitType(unitTypeId)
    let survivorDataComp = survivor.getSurvivorComponent()
    let survivorData = survivorDataComp.getSurvivorData()

    let jobData = survivorData.getJobData(jobId)
    if (jobData != null)
      survivor.m_activeJobComponent.loadJobData(jobData)
    //   Log.info("SurvivorUnit", "loadNewJobStats", jobId.getTitle(), "xp " + theUnit.getXp().toString())
    // else
    //   Log.info("SurvivorUnit", "loadNewJobStats", jobId.getTitle(), "Could not find job data")

  // --------------------------------------------------------------------------
  private static function updateUnitProperName(unit _unit)
    if (_unit == null)
      return
    let survivor = _unit.getMetadata()
    let survivorDataComp = survivor.getSurvivorComponent()
    let survivorData = survivorDataComp.getSurvivorData()
    _unit.setProperName(survivorData.getName())

  // --------------------------------------------------------------------------
  private function resetExperienceIfPromoting(unit newUnit)
    if (newUnit == null)
      return
    let job = getJobDefinitionForUnitType(newUnit.getTypeId())
    let survivorDataComp = this.getSurvivorComponent()
    let survivorData = survivorDataComp.getSurvivorData()
    let jobData = survivorData.getJobData(job)
    if (jobData == null or jobData.getUnitId() == newUnit.getTypeId())
      return
    jobData.setLevel(1)
    jobData.setExperience(0)

  // --------------------------------------------------------------------------
  private function registerJobComponent(SurvivorJobDefinition job, SurvivorJobComponent comp)
    m_jobComponents.put(job, comp)

  // --------------------------------------------------------------------------
  function getJobComponent(SurvivorJobDefinition job) returns SurvivorJobComponent
    return m_jobComponents.get(job)

  // --------------------------------------------------------------------------
  function onLevelChanged()
    if (m_jobComponents == null)
      return
    for key in m_jobComponents
      let comp = m_jobComponents.get(key)
      if (comp != null and comp.getEnabled())
        comp.onLevelChanged()

  // --------------------------------------------------------------------------
  private function initialize()

    let owningUnit = getUnit()
    let owningPlayer = owningUnit.getOwner()

    m_survivorData = new SurvivorData()
    m_survivorData.setName(getRandomSurvivorNameForPlayer(owningPlayer))

    initializeStats(m_survivorData)

    addComponent(new SurvivorJobChangeComponent(this))
    addComponent(new PersistActiveAbilityComponent(this))

    let rangedTargetXpReceiever = new RangedTargetXpRecieverComponent(this)
    ..setXpPerHit(1)
    addComponent(rangedTargetXpReceiever)

    let survivorDataComponent = this.getOrAddSurvivorComponent()
    survivorDataComponent.setSurvivorData(m_survivorData)

    registerJobComponent(SurvivorJobs.none,     this.getOrAddSurvivorJoblessComponent() ..setEnabled(false))
    registerJobComponent(SurvivorJobs.builder,  this.getOrAddBuilderJobComponent()      ..setEnabled(false))
    registerJobComponent(SurvivorJobs.engineer, this.getOrAddEngineerJobComponent()     ..setEnabled(false))
    registerJobComponent(SurvivorJobs.militia,  this.getOrAddMilitiaJobComponent()      ..setEnabled(false))
    registerJobComponent(SurvivorJobs.spearman, this.getOrAddSpearmanJobComponent()     ..setEnabled(false))
    registerJobComponent(SurvivorJobs.medic,    this.getOrAddMedicJobComponent()        ..setEnabled(false))
    registerJobComponent(SurvivorJobs.marksman, this.getOrAddMarksmanJobComponent()     ..setEnabled(false))
    registerJobComponent(SurvivorJobs.acolyte,  this.getOrAddAcolyteJobComponent()      ..setEnabled(false))

  // --------------------------------------------------------------------------
  private function initializeStats(SurvivorData survivorData)

    // Kills
    let killsStat = new IntStatistic("Kills")
    ..setIcon(Icons.bTNBattleStations)
    ..setUnits("kills")
    ..setTooltip("The number of enemies killed by getting the last hit")

    survivorData.addStatistic(SurvivorStats.kills, killsStat)
    addComponent(new KillsStat(this))

    // Days survived
    let daysSurvivedStat = new IntStatistic("Days Survived")
    ..setIcon(Icons.bTNFarSight)
    ..setUnits("days")
    ..setTooltip("The number of days survived")

    survivorData.addStatistic(SurvivorStats.daysSurvived, daysSurvivedStat)
    addComponent(new DaysSurvivedStat(this))

    // Nights survived
    let nightsSurvivedStat = new IntStatistic("Nights Survived")
    ..setIcon(Icons.bTNAnimateDead)
    ..setUnits("nights")
    ..setTooltip("The number of nights survived")

    survivorData.addStatistic(SurvivorStats.nightsSurvived, nightsSurvivedStat)
    addComponent(new NightsSurvivedStat(this))

    // m_stats.createIntStatistic(Stats.assists, "Assists", 0)
    // ..setIcon(Icons.bTNImmolationOff)
    // ..setUnits("assists")
    // ..setTooltip("The number of enemies killed with the help of this Survivor")

    // m_stats.createRealStatistic(Stats.damageDealt, "Damage Dealt", 0.0)
    // ..setIcon(Icons.bTNSteelMelee)
    // ..setUnits("damage")
    // ..setTooltip("The total amount of damage dealt")

    // m_stats.createRealStatistic(Stats.healthHealed, "HP Healed", 0.0)
    // ..setIcon(Icons.bTNStatUp)
    // ..setUnits("hp")
    // ..setTooltip("The amount of health healed")

// ============================================================================
public function unit.isRescuedSurvivor() returns bool
  return this.isSurvivor() and this.getOwner() != PLAYER_VILLAGERS

// ============================================================================
public function unit.isUnrescuedSurvivor() returns bool
  return this.isSurvivor() and this.getOwner() == PLAYER_VILLAGERS

// ============================================================================
public function unit.isRescuableSurvivor() returns bool
  return this.isUnrescuedSurvivor()

// ============================================================================
public function unit.getSurvivorJobData(SurvivorJobDefinition jobDef) returns SurvivorJobData
  let survivor = this.getMetadata() castTo Survivor
  if (survivor == null)
    return null

  let survivorComp = survivor.getSurvivorComponent()
  if (survivorComp == null)
    return null

  let survivorData = survivorComp.getSurvivorData()
  if (survivorData == null)
    return null

  return survivorData.getJobData(jobDef)

// ============================================================================
function onHeroLeveledUp()
  let leveledUnit = GetLevelingUnit()

  let survivor = leveledUnit.getMetadata() castTo Survivor
  if (survivor == null)
    return

  survivor.onLevelChanged()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_HERO_LEVEL, function onHeroLeveledUp)