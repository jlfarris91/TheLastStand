package KillsStatistic
import Statistic
import UnitMetadata
import UnitComponent
import Events
import SurvivorStats
import SurvivorComponent

// ============================================================================
public class KillsStatisticComponent extends UnitComponent
  private SurvivorComponent m_survivorDataComponent
  private Action m_onUnitKilledHandler

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()
    m_survivorDataComponent = getOwner().getOrAddSurvivorComponent()
    listenToUnitKilledEvent()

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    unlistenToUnitKilledEvent()
    m_survivorDataComponent = null

  // --------------------------------------------------------------------------
  private function listenToUnitKilledEvent()
    unlistenToUnitKilledEvent()
    m_onUnitKilledHandler = PlayerUnitEvents.unitKilled.add() -> 
      let killingUnit = GetKillingUnit()
      let dyingUnit = GetDyingUnit()
      if (killingUnit == getOwnerUnit() and dyingUnit.isEnemyOf(killingUnit))
        onUnitKilledByOwner()

  // --------------------------------------------------------------------------
  private function unlistenToUnitKilledEvent()
    if (m_onUnitKilledHandler != null)
      PlayerUnitEvents.unitKilled.remove(m_onUnitKilledHandler)
      destroy m_onUnitKilledHandler
      m_onUnitKilledHandler = null

  // --------------------------------------------------------------------------
  private function onUnitKilledByOwner()
    let stats = m_survivorDataComponent.getSurvivorData()
    if (stats == null)
      return

    let stat = stats.getStatistic(SurvivorStats.kills) castTo IntStatistic
    if (stat == null)
      return

    stat.increment(1)