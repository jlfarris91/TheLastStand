package NightsSurvivedStatisticComponent
import Statistic
import UnitMetadata
import UnitComponent
import Events
import SurvivorStats
import SurvivorDataComponent

// ============================================================================
public class NightsSurvivedStatisticComponent extends UnitComponent
  private SurvivorDataComponent m_survivorDataComponent
  private Action m_onUnitKilledHandler

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()
    m_survivorDataComponent = getOwner().getOrAddSurvivorDataComponent()
    listenToUnitKilledEvent()

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    unlistenToUnitKilledEvent()
    m_survivorDataComponent = null

  // --------------------------------------------------------------------------
  private function listenToUnitKilledEvent()
    unlistenToUnitKilledEvent()
    m_onUnitKilledHandler = GameEvents.dayEndEvent.add() -> 
      onNightSurvived()

  // --------------------------------------------------------------------------
  private function unlistenToUnitKilledEvent()
    if (m_onUnitKilledHandler != null)
      GameEvents.dayEndEvent.remove(m_onUnitKilledHandler)
      destroy m_onUnitKilledHandler
      m_onUnitKilledHandler = null

  // --------------------------------------------------------------------------
  private function onNightSurvived()
    let stats = m_survivorDataComponent.getSurvivorData()
    if (stats == null)
      return

    let stat = stats.getStatistic(SurvivorStats.nightsSurvived) castTo IntStatistic
    if (stat == null)
      return

    stat.increment(1)