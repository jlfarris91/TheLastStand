package DistanceTraveledStatistic
import Statistic
import UnitMetadata
import UnitComponent
import MovementComponent
import Events
import SurvivorStats
import SurvivorDataComponent

// ============================================================================
public class DistanceTraveledStatisticComponent extends UnitComponent
  private SurvivorDataComponent m_survivorDataComponent
  private MovementComponent m_movementComponent
  private Action2<MovementComponent, real> m_onUnitMovedHandler

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()
    m_survivorDataComponent = getOwner().getOrAddSurvivorDataComponent()
    m_movementComponent = getOwner().getOrAddMovementComponent()
    listenToUnitMovedEvent()

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    unlistenToUnitMovedEvent()
    m_movementComponent = null
    m_survivorDataComponent = null

  // --------------------------------------------------------------------------
  private function listenToUnitMovedEvent()
    unlistenToUnitMovedEvent()
    if (m_movementComponent != null)
      m_onUnitMovedHandler = m_movementComponent.getUnitMovedEvent().add() (MovementComponent movementComponent, real distanceTraveled) ->
        onUnitMoved(distanceTraveled)

  // --------------------------------------------------------------------------
  private function unlistenToUnitMovedEvent()
    if (m_onUnitMovedHandler != null)
      if (m_movementComponent != null)
        m_movementComponent.getUnitMovedEvent().remove(m_onUnitMovedHandler)
      destroy m_onUnitMovedHandler
      m_onUnitMovedHandler = null

  // --------------------------------------------------------------------------
  private function onUnitMoved(real distanceTraveled)
    let stats = m_survivorDataComponent.getSurvivorData()
    if (stats == null)
      return

    let distanceTraveledStat = stats.getStatistic(SurvivorStats.distanceTraveled) castTo RealStatistic
    if (distanceTraveledStat == null)
      return

    distanceTraveledStat.increment(distanceTraveled)

