package Survivors
import TlsUnitIds
import HumanPlayerMetadata
import UnitMetadata
import WeightedSet
import DisplayTextToPlayer
import SurvivorComponent
import Orders
import ColorUtility
import ErrorHandling
import SurvivorSpawnManager
import ClosureTimers
import GameConstants
import FX
import Bounds
import BoundsExtensions
import Range
import HumanPlayers
import TentComponent
import FriendlyCircularCachedSpawnManager
import RandomSpawnPointProvider

group g_tempGroup = CreateGroup()

// ============================================================================
public function spawnSurvivorUnitForPlayer(player p) returns unit

  if (p == null)
    error("[spawnSurvivorUnitForPlayer] Argument 'p' cannot be null")
    return null

  let playerMetadata = p.getHumanMetadataRequired()
  if (playerMetadata.getIsDead())
    Log.debug("[spawnSurvivorUnitForPlayer] Player {0} is dead".format(p.getName()))
    return null

  let weightedSet = new WeightedSet<int>()
  ..add(TlsUnitIds.Survivors.survivorMale, 50)
  ..add(TlsUnitIds.Survivors.survivorMilitia1, 1)
  ..add(TlsUnitIds.Survivors.survivorSpearman1, 1)
  ..add(TlsUnitIds.Survivors.survivorMedic, 1)
  ..add(TlsUnitIds.Survivors.survivorMarksman, 1)
  ..add(TlsUnitIds.Survivors.survivorEngineer, 1)

  let unitId = weightedSet.getRandom()

  let spawnPointResult = getSpawnPoint(p)
  if (not spawnPointResult.succeeded)
    Log.debug("[spawnSurvivorUnitForPlayer] Failed to find spawn point for survivor")
    return null

  let survivor = createUnitTLS(p, unitId, spawnPointResult.spawnPoint, GetRandomDirectionDeg())
  let survivorMetadata = survivor.getMetadataRequired()

  let survivorUnit = survivorMetadata.getUnit()
  survivorUnit.issuePointOrderById(OrderIds.move, playerMetadata.getCampCenter())

  let survivorDataComponent = survivorMetadata.getSurvivorComponent()
  let survivorData = survivorDataComponent.getSurvivorData()
  
  let message = "{0} {1} has joined your camp!".format(
    survivorUnit.getName().colorize(Colors.gold),
    survivorData.getName())
  displayMessageToPlayer(p, message)

  AddIndicator(survivorUnit, 0, 255, 0, 255)

  doPeriodicallyTimed(SURVIVOR_FADE_INTERVAL, SURVIVOR_FADE_DURATION) (CallbackCounted cb) ->
    if (survivorUnit != null)
      survivorUnit.setVertexColor(colorA(150, 150, 150, 0).mix(colorA(150, 150, 150, 255), cb.getPercentage()))

  return survivorUnit

// ============================================================================
public function trySpawnSurvivorForPlayer(player p)
  if (not p.isPlayingHumanPlayer())
    return
  let numberOfTents = countNumberOfConstructedTentsOwnedByPlayer(p)
  let numberOfSurvivors = countNumberOfSurvivorsOwnedByPlayer(p)
  Log.debug("[trySpawnSurvivorForPlayer] Player {0} has {1} tent(s) and {2} survivor(s)".format(p.getName(), numberOfTents.toString(), numberOfSurvivors.toString()))
  if (numberOfTents > numberOfSurvivors)
    spawnSurvivorUnitForPlayer(p)

// ============================================================================
public function countNumberOfConstructedTentsOwnedByPlayer(player p) returns int
  g_tempGroup.clear()
  g_tempGroup.enumUnitsOfPlayer(p, null)
  var count = 0
  for u from g_tempGroup
    let metadata = u.getMetadata()
    if (metadata != null and metadata.hasComponent(TentComponent.typeId))
      let tentComponent = metadata.getTentComponent()
      if (tentComponent != null and tentComponent.getIsConstructed())
        if (u.getTypeId() == TlsUnitIds.shelter1)
        count++
  return count

// ============================================================================
public function countNumberOfSurvivorsOwnedByPlayer(player p) returns int
  g_tempGroup.clear()
  g_tempGroup.enumUnitsOfPlayer(p, null)
  var count = 0
  for u from g_tempGroup
    if (u.isAlive() and u.isSurvivor())
      count++
  return count

// ============================================================================
public function owningPlayerBanishSurvivor(unit targetUnit)
  let owningPlayer = targetUnit.getOwner()

  FX.createJobChangeTag("Banished!", targetUnit.getPos(), owningPlayer)

  displayMessageToPlayer(
    owningPlayer,
    "You have banished {0} {1} from your camp.".format(
      targetUnit.getName().colorize(Colors.gold),
      targetUnit.getProperName()))

  AddIndicator(targetUnit, 0, 255, 0, 255)

  // Try to spawn another survivor for the player later
  doAfter(SURVIVORS_RESPAWN_BANISHED_INTERVAL_RANGE.getRandom(), () -> trySpawnSurvivorForPlayer(owningPlayer))

  banishSurvivor(targetUnit)

// ============================================================================
public function banishSurvivor(unit targetUnit)

  // Give to the villagers player so the human player can't control it anymore
  targetUnit.setOwner(PLAYER_VILLAGERS, false)

  // Make the target move to a random point in the map while it is being banished
  targetUnit.issuePointOrder("move", playableBounds.getRandomPoint())

  // Fade out the survivor over time
  doPeriodicallyTimed(SURVIVOR_FADE_INTERVAL, SURVIVOR_FADE_DURATION) (CallbackCounted cb) ->
    if (targetUnit != null)
      targetUnit.setVertexColor(colorA(150, 150, 150, 255).mix(colorA(150, 150, 150, 0), cb.getPercentage()))
  
  // Remove the survivor once it is invisible
  doAfter(SURVIVOR_FADE_DURATION, () -> targetUnit.remove())

// ============================================================================
function getSpawnPoint(player p) returns spawnPointResult
  let currentSpawnManager = getPlayerCachedFriendlySpawnManager(p)
  let fallbackSpawnManager = getPlayerSurvivorSpawnManager(p)
  let spawnPointProvider = new RandomSpawnPointProvider(currentSpawnManager, fallbackSpawnManager, SpawnRange.CLOSE)
  let result = spawnPointProvider.getSpawnPoint()
  destroy spawnPointProvider
  return result