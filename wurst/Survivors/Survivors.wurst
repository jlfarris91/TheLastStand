package Survivors
import TlsUnitIds
import HumanPlayerComponent
import UnitMetadata
import WeightedSet
import DisplayTextToPlayer
import Orders
import ColorUtility
import ErrorHandling
import SurvivorSpawnManager
import ClosureTimers
import GameConstants
import FX
import Bounds
import Range
import HumanPlayers
import RandomSpawnPointProvider
import UnitExtensions
import SurvivorJobs
import SurvivorComponent
import PlayerExtensions
import Math
import GameInstance
import FriendlyGridCachedSpawnManager

group g_tempGroup = CreateGroup()
WeightedSet<SurvivorJobDefinition> g_weightedSurvivorJobSet

// ============================================================================
public class SurvivorUtility
    
  // --------------------------------------------------------------------------
  static function orderSelectedUnitsToActivateJob(player owner, SurvivorJobDefinition job)

    let selectedUnits = owner.getSelectedUnits()

    for u in selectedUnits
    //{
      if (owner.controlsUnit(u))
      //{
        let survivor = u.getSurvivorComponent()
        if (survivor != null and survivor.canActivateJob(job))
        //{
          survivor.activateJob(job)
        //}
      //}
    //}

    destroy selectedUnits

  // --------------------------------------------------------------------------
  static function orderSelectedUnitsToUnlearnJob(player owner, SurvivorJobDefinition job)

    let selectedUnits = owner.getSelectedUnits()

    for u in selectedUnits
    //{
      if (owner.controlsUnit(u))
      //{
        let survivor = u.getSurvivorComponent()
        if (survivor != null)
          survivor.unassignJob(job)
      //}
    //}
  
    destroy selectedUnits
    
  // --------------------------------------------------------------------------
  static function orderSelectedUnitsToToggleActivateJob(player owner)

    let selectedUnits = owner.getSelectedUnits()

    // Only switch to the last active job if everyone is already jobless
    var switchToLastActiveJob = true

    for u in selectedUnits
    //{
      if (owner.controlsUnit(u))
      //{
        let survivor = u.getSurvivorComponent()
        if (survivor != null and survivor.getActiveJob() != SurvivorJobs.none)
          switchToLastActiveJob = false
      //}
    //}

    for u in selectedUnits
    //{
      if (owner.controlsUnit(u))
      //{
        let survivor = u.getSurvivorComponent()
        if (survivor != null)
        //{
          SurvivorJobDefinition job = SurvivorJobs.none 

          if (switchToLastActiveJob)
            let lastActiveJob = survivor.getLastActiveJob()
            if (lastActiveJob != null and survivor.canActivateJob(lastActiveJob))
              job = lastActiveJob

          if (survivor.canActivateJob(job))
            survivor.activateJob(job)
        //}
      //}
    //}

    destroy selectedUnits

  // --------------------------------------------------------------------------
  static function spawnSurvivorWithRandomJob(player forPlayer, vec2 pos) returns unit
    return spawnSurvivorWithRandomJob(forPlayer, pos, g_weightedSurvivorJobSet)

  // --------------------------------------------------------------------------
  static function spawnSurvivorWithRandomJob(player forPlayer, vec2 pos, WeightedSet<SurvivorJobDefinition> jobSet) returns unit

    let survivor = createUnitTLS(forPlayer, jobSet.getRandom().getInitialUnitType(), pos, GetRandomDirectionDeg())

    // Scale survivor level with game progress
    let gameProgress01 = clamp01(g_GameInstance.getGameProgress01())
    let survivorLevel = SURVIVORS_SPAWN_LEVEL_RANGE.lerp(gameProgress01).floor()
    if (survivorLevel > 1)
      survivor.setLevel(survivorLevel, false)

    return survivor

  // --------------------------------------------------------------------------
  static function spawnSurvivorUnitForPlayer(player _player) returns unit

    if (_player == null)
      argumentNullError("_player")
      return null

    let playerMetadata = _player.getMetadata()
    if (playerMetadata == null)
      Log.debug("[spawnSurvivorUnitForPlayer] Player {0} has no metadata")
      return null

    if (playerMetadata.getHasLostGame())
      Log.debug("[spawnSurvivorUnitForPlayer] Player {0} has lost the game")
      return null

    if (playerMetadata.getHasLeftGame())
      Log.debug("[spawnSurvivorUnitForPlayer] Player {0} has left the game")
      return null

    let playerHumanComp = playerMetadata.getHumanPlayerComponent()
    if (playerHumanComp == null)
      Log.debug("[spawnSurvivorUnitForPlayer] Player {0} has no human player component")
      return null

    let spawnPointResult = getSpawnPoint(_player)
    if (not spawnPointResult.succeeded)
      Log.debug("[spawnSurvivorUnitForPlayer] Failed to find spawn point for survivor, trying again")
      // try again
      doAfter(1.0, () -> spawnSurvivorUnitForPlayer(_player))
      return null

    let survivor = spawnSurvivorWithRandomJob(_player, spawnPointResult.spawnPoint, g_weightedSurvivorJobSet)

    survivor.issuePointOrderById(OrderIds.move, playerHumanComp.getCampCenter())
    
    let message = "{0} {1} has joined your camp!".format(
      survivor.getName().colorize(Colors.gold),
      survivor.getProperName())
    displayMessageToPlayer(_player, message)

    AddIndicator(survivor, 0, 255, 0, 255)

    // Fade the survivor in over time
    survivor.fadeIn(color(150, 150, 150))

    return survivor

  // --------------------------------------------------------------------------
  static function trySpawnSurvivorForPlayer(player _player)
    if (not _player.isPlayingHumanPlayer())
      return
    let availableSurvivors = _player.getCurrentSupply()
    let currentSurvivors = countNumberOfSurvivorsOwnedByPlayer(_player)
    Log.debug("[trySpawnSurvivorForPlayer] Player {0} has room for {1} Survivor(s) and has {2} Survivor(s)".format(_player.getName(), availableSurvivors.toString(), currentSurvivors.toString()))
    if (availableSurvivors > currentSurvivors)
      spawnSurvivorUnitForPlayer(_player)

  // --------------------------------------------------------------------------
  static function countNumberOfSurvivorsOwnedByPlayer(player _player) returns int
    g_tempGroup.clear()
    g_tempGroup.enumUnitsOfPlayer(_player, null)
    var count = 0
    for _unit from g_tempGroup
      if (_unit.isAlive() and _unit.isSurvivor())
        count++
    return count

  // --------------------------------------------------------------------------
  static function owningPlayerBanishSurvivor(unit targetUnit)
    let owningPlayer = targetUnit.getOwner()

    FX.createJobChangeTag("Banished!", targetUnit.getPos(), owningPlayer)

    displayMessageToPlayer(
      owningPlayer,
      "You have banished {0} {1} from your camp.".format(
        targetUnit.getName().colorize(Colors.gold),
        targetUnit.getProperName()))

    AddIndicator(targetUnit, 0, 255, 0, 255)

    // Try to spawn another survivor for the player later
    doAfter(SURVIVORS_RESPAWN_BANISHED_INTERVAL_RANGE.getRandom(), () -> trySpawnSurvivorForPlayer(owningPlayer))

    banishSurvivor(targetUnit)

  // --------------------------------------------------------------------------
  static function banishSurvivor(unit targetUnit)

    // Give to the villagers player so the human player can't control it anymore
    targetUnit.setOwner(PLAYER_VILLAGERS, false)

    // Make the target move to a random point in the map while it is being banished
    targetUnit.issuePointOrder("move", playableBounds.getRandomPoint())

    // Fade out the survivor over time
    targetUnit.fadeOut(UNIT_FADE_DURATION, color(150, 150, 150))
    
    // Remove the survivor once it is invisible
    doAfter(UNIT_FADE_DURATION, () -> targetUnit.remove())

// ============================================================================
function getSpawnPoint(player p) returns spawnPointResult

  let playerHumanComp = p.getHumanPlayerComponent()
  if (playerHumanComp == null)
    return spawnPointResult(false, ZERO2)

  let currentSpawnManager = getPlayerCachedFriendlySpawnManager(p)
  let fallbackSpawnManager = getPlayerSurvivorSpawnManager(p)
  let spawnPointProvider = new RandomSpawnPointProvider(currentSpawnManager, fallbackSpawnManager)
  let result = spawnPointProvider.getRandomSpawnPointInRange(playerHumanComp.getCampCenter(), SpawnRange.CLOSE)
  destroy spawnPointProvider
  
  return result

// ============================================================================
public function unit.isRescuedSurvivor() returns bool
  return this.isSurvivor() and this.getOwner() != PLAYER_VILLAGERS

// ============================================================================
public function unit.isUnrescuedSurvivor() returns bool
  return this.isSurvivor() and this.getOwner() == PLAYER_VILLAGERS

// ============================================================================
public function unit.isRescuableSurvivor() returns bool
  return this.isUnrescuedSurvivor()

// ============================================================================
init
  g_weightedSurvivorJobSet = new WeightedSet<SurvivorJobDefinition>()
    ..add(SurvivorJobs.none, 50)
    // ..add(SurvivorJobs.builder, 1)
    // ..add(SurvivorJobs.militia, 1)
    // ..add(SurvivorJobs.spearman, 1)
    // ..add(SurvivorJobs.medic, 1)
    // ..add(SurvivorJobs.marksman, 1)
    // ..add(SurvivorJobs.acolyte, 1)
    // ..add(SurvivorJobs.engineer, 1)
