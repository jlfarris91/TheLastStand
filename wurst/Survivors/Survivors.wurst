package Survivors
import Camp
import TlsUnitIds
import HumanPlayerMetadata
import UnitMetadata
import WeightedSet
import DisplayTextToPlayer
import StringExtensions
import SurvivorComponent
import Orders
import ColorUtility
import ErrorHandling
import SurvivorSpawnManager
import ClosureTimers
import GameConstants

// ============================================================================
public function spawnSurvivorUnitForPlayer(player p) returns unit

  if (p == null)
    error("Argument 'p' cannot be null")
    return null

  let playerMetadata = p.getHumanMetadataRequired()
  if (playerMetadata.getIsDead())
    return null

  let camp = playerMetadata.getCamp()

  let weightedSet = new WeightedSet<int>()
  ..add(TlsUnitIds.Survivors.survivorMale, 50)
  ..add(TlsUnitIds.Survivors.survivorMilitia1, 1)
  ..add(TlsUnitIds.Survivors.survivorSpearman, 1)
  ..add(TlsUnitIds.Survivors.survivorMedic, 1)
  ..add(TlsUnitIds.Survivors.survivorMarksman, 1)

  let unitId = weightedSet.getRandom()

  let spawnPointResult = getSpawnPoint(p)
  if (not spawnPointResult.succeeded)
    Log.debug("Failed to find spawn point for survivor")
    return null

  let survivor = createUnitTLS(p, unitId, spawnPointResult.spawnPoint, GetRandomDirectionDeg())
  let survivorMetadata = survivor.getMetadataRequired()

  let survivorUnit = survivorMetadata.getUnit()
  survivorUnit.issuePointOrderById(OrderIds.move, camp.getCenter())

  let survivorDataComponent = survivorMetadata.getSurvivorComponent()
  let survivorData = survivorDataComponent.getSurvivorData()
  
  let message = "{0} {1} has joined your camp!".format(
    survivorMetadata.getName().colorize(Colors.gold),
    survivorData.getName())
  displayMessageToPlayer(p, message)

  let playerColor = p.getColor().toColor()
  AddIndicator(survivorUnit, playerColor.red, playerColor.green, playerColor.blue, 255)

  doPeriodicallyTimed(SURVIVOR_FADE_INTERVAL, SURVIVOR_FADE_DURATION) (CallbackCounted cb) ->
    if (survivorUnit != null)
      survivorUnit.setVertexColor(colorA(150, 150, 150, 0).mix(colorA(150, 150, 150, 255), cb.getPercentage()))

  return survivorUnit

// ============================================================================
function getSpawnPoint(player p) returns spawnPointResult
  let spawnManager = getPlayerSurvivorSpawnManager(p)
  return spawnManager.getRandomSpawnPointInRange(SpawnRange.CLOSE)