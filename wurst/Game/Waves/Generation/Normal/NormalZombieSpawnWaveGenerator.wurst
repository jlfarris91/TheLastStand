package NormalZombieSpawnWaveGenerator
import SpawnWave
import UndeadSpawnManager
import RandomSpawnPointProvider
import RandomUnitTypeProvider
import TlsUnitIds
import GameConstants
import PeriodicWaveActivator
import TimeOfDayProgress
import Math
import Func
import FunctionalField
import SpawnWaveDefinition
import SetTargetPlayerModifier
import ZombieSetupModifier
import GrantResourcesToKillingPlayerModifier
import Range
import IGameWaveProgress
import ISpawnWaveGenerator

// ============================================================================
public class NormalZombieSpawnWaveGenerator implements ISpawnWaveGenerator

  // --------------------------------------------------------------------------
  override function generateSpawnWave(player p, IGameWaveProgress gameProgress) returns SpawnWave

    // The percentage of the game's progress for the current night
    let currentNightGameProgress = gameProgress.getCurrentGameProgress()

    // The percentage of the game's progress for the next night
    let nextNightGameProgress = gameProgress.getNextGameProgress()

    let unitTypeProvider = new RandomUnitTypeProvider()
    ..addUnitType(TlsUnitIds.Undead.zombieMale, 1)
    ..addUnitType(TlsUnitIds.Undead.zombieFemale, 1)

    let spawnPointProvider = new RandomSpawnPointProvider(g_undeadSpawnManager)
    ..setMinRange(ZOMBIES_SPAWN_RANGE_MIN)
    ..setMaxRange(ZOMBIES_SPAWN_RANGE_MAX)

    let activator = new PeriodicWaveActivator()
    ..setInterval(ZOMBIES_SPAWN_INTERVAL)

    let progress = new NightProgress()

    // This determines the maximum number of alive spawns
    let maxAlive = lerpInt(20, 60, currentNightGameProgress)
    Func1<real, int> getMaxAliveCount = (real _waveProgress) -> 
      return maxAlive

    // This determines the number of spawn points to spawn enemies used each time the spawner is activated.
    // The higher the number the more zombies spawned per activation.
    Func1<real, int> getSpawnsPerActivation = (real waveProgress) ->
      return lerpInt(2, 12, waveProgress)

    // This determines the number of enemies to spawn at each spawn point when the spawner is activated.
    Func1<real, real> getSpawnGroupingFactor = (real waveProgress) ->
      return lerp(0.0, 1.0, waveProgress)

    // These modifiers will run on each unit spawned in the wave
    // The real value passed in is the wave's current progress
    let ufd = new AggregateUnitModifier()

    // These are some common zombie fields we always set
    ufd.add(new ZombieSetupModifier())
    ufd.add(new SetTargetPlayerModifier(p))

    // Each night zombies health will interpolate between the min/max for that night
    let gameStartMaxHp = 120
    let gameEndMaxHp = 1000
    let maxHpMin = lerpInt(gameStartMaxHp, gameEndMaxHp, currentNightGameProgress)
    let maxHpMax = lerpInt(gameStartMaxHp, gameEndMaxHp, nextNightGameProgress)
    let maxHpFunc = new MaxHpUnitField()..setFunc() (real waveProgress) ->
      return lerpInt(maxHpMin, maxHpMax, waveProgress)
    ufd.add(maxHpFunc)

    // Zombies will get faster as the night goes on
    // Speed is reset to 150 every night
    let moveSpeedFunc = new FunctionalUnitRealField(UNIT_RF_SPEED)..setFunc() (real t) ->
      return lerp(150, 200, t)
    ufd.add(moveSpeedFunc)

    let armorGameStart = 0.0
    let armorGameEnd = 5.0
    let armor = lerp(armorGameStart, armorGameEnd, currentNightGameProgress)
    let armorFunc = new ArmorUnitField()..setValue(armor)
    ufd.add(armorFunc)

    let damageBaseGameStart = 10
    let damageBaseGameEnd = 30
    let damageBase = lerpInt(damageBaseGameStart, damageBaseGameEnd, currentNightGameProgress)
    let damageBaseFunc = new FunctionalUnitWeaponIntegerField(UNIT_WEAPON_IF_ATTACK_DAMAGE_BASE, 1)..setValue(damageBase)
    ufd.add(damageBaseFunc)

    let damageNumberOfDiceGameStart = 1
    let damageNumberOfDiceGameEnd = 3
    let damageNumberOfDice = lerpInt(damageNumberOfDiceGameStart, damageNumberOfDiceGameEnd, currentNightGameProgress)
    let damageNumberOfDiceFunc = new FunctionalUnitWeaponIntegerField(UNIT_WEAPON_IF_ATTACK_DAMAGE_NUMBER_OF_DICE, 1)..setValue(damageNumberOfDice)
    ufd.add(damageNumberOfDiceFunc)

    let damageSidesPerDieGameStart = 1
    let damageSidesPerDieGameEnd = 3
    let damageSidesPerDie = lerpInt(damageSidesPerDieGameStart, damageSidesPerDieGameEnd, currentNightGameProgress)
    let damageSidesPerDieFunc = new FunctionalUnitWeaponIntegerField(UNIT_WEAPON_IF_ATTACK_DAMAGE_SIDES_PER_DIE, 1)..setValue(damageSidesPerDie)
    ufd.add(damageSidesPerDieFunc)

    // Configure the unit to give the killing player materials
    let grantMaterialsMod = new GrantResourcesToKillingPlayerModifier()
    grantMaterialsMod.setMaterialsChanceFunc((real t) -> ZOMBIES_MATERIALS_DROP_CHANCE)
    grantMaterialsMod.setMaterialsAmountRangeFunc((real t) -> ZOMBIES_MATERIALS_DROP_AMOUNT.toRange())
    ufd.add(grantMaterialsMod)

    let def = new SpawnWaveDefinition()
    ..setProgress(progress)
    ..setMaxAliveCount(getMaxAliveCount)
    ..setSpawnsPerActivation(getSpawnsPerActivation)
    ..setSpawnGroupingFactor(getSpawnGroupingFactor)
    ..setActivator(activator)
    ..setSpawnPointProvider(spawnPointProvider)
    ..setUnitTypeProvider(unitTypeProvider)
    ..setUnitModifier(ufd)

    return new SpawnWave(def, p)..setName("Zombies")