package NormalNightWaveGenerator
import NightWave
import WaveCollection
import ISpawnWaveGenerator
import IWaveGenerator
import GameWaveProgress

import NormalZombieSpawnWaveGenerator
import NormalAbomSpawnWaveGenerator
import NormalCrowSpawnWaveGenerator

/*
    For each player P
      Spawn no more than N zombies
        Every N seconds
          Select N spawn locations within a range of R
            Spawn N zombies at spawn location
      Once
        Select N spawn locations within a range of R
          Spawn N aboms at spawn location
      Every N seconds
        Select N spawn locations within a range of R
          Spawn N crows at spawn location
*/

// ============================================================================
public class NormalNightWaveGenerator implements IWaveGenerator<NightWave>

  private constant int TOTAL_NUM_NIGHTS = 30

  private ISpawnWaveGenerator m_zombieWaveGenerator
  private ISpawnWaveGenerator m_abomWaveGenerator
  private ISpawnWaveGenerator m_crowWaveGenerator

  // --------------------------------------------------------------------------
  construct()

    m_zombieWaveGenerator = new NormalZombieSpawnWaveGenerator()    
    m_abomWaveGenerator = new NormalAbomSpawnWaveGenerator()
    m_crowWaveGenerator = new NormalCrowSpawnWaveGenerator()

  // --------------------------------------------------------------------------
  override function generateWavesForPlayer(player p, WaveCollection<NightWave> waves)
    
    let gameProgress = new GameWaveProgress()
    ..setTotalWaves(TOTAL_NUM_NIGHTS)

    NightWave currentWave

    // Zombies
    for i = 1 to TOTAL_NUM_NIGHTS
      gameProgress.setCurrentWaveNumber(i)
      currentWave = waves.getOrCreateWave(gameProgress.getCurrentWaveNumber())
      currentWave.addSpawnWave(m_zombieWaveGenerator.generateSpawnWave(p, gameProgress))

    // Crows
    for i = 3 to TOTAL_NUM_NIGHTS
      gameProgress.setCurrentWaveNumber(i)
      currentWave = waves.getOrCreateWave(gameProgress.getCurrentWaveNumber())
      currentWave.addSpawnWave(m_crowWaveGenerator.generateSpawnWave(p, gameProgress))

    // Aboms
    for i = 5 to TOTAL_NUM_NIGHTS
      gameProgress.setCurrentWaveNumber(i)
      currentWave = waves.getOrCreateWave(gameProgress.getCurrentWaveNumber())
      currentWave.addSpawnWave(m_abomWaveGenerator.generateSpawnWave(p, gameProgress))