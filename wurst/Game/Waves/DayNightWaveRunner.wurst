package DayNightWaveRunner
import Runnable
import Wave
import DayWaveCollection
import NightWaveCollection
import DayWave
import NightWave
import DisplayTextToPlayer
import SpawnWave
import LinkedListExtensions
import LinkedList
import StringExtensions

// ============================================================================
public class DayNightWaveRunner
  private player m_targetPlayer
  private NightWaveCollection m_nightWaves
  private DayWaveCollection m_dayWaves
  private Wave m_currentWave
  private bool m_day

  // --------------------------------------------------------------------------
  construct(player targetPlayer, NightWaveCollection nightWaves, DayWaveCollection dayWaves)
    m_targetPlayer = targetPlayer
    m_nightWaves = nightWaves
    m_dayWaves = dayWaves

  // --------------------------------------------------------------------------
  ondestroy
    cancelCurrentWave()

  // --------------------------------------------------------------------------
  function getCurrentWave() returns Wave
    return m_currentWave
  
  // --------------------------------------------------------------------------
  function startNightWave(int night)
    Log.debug("Start night wave " + I2S(night))
    cancelCurrentWave()
    m_currentWave = m_nightWaves.getLatestWave(night)
    if (m_currentWave != null)
      m_currentWave.start()
      m_day = false
      Log.debug("Night wave {0} started.".format(m_currentWave.getId()))
      announceWave(m_currentWave)
    else
      Log.debug("No night wave found for night " + I2S(night))

  // --------------------------------------------------------------------------
  function startDayWave(int day)
    Log.debug("Starting day wave {0}...".format(day.toString()))
    cancelCurrentWave()
    m_currentWave = m_dayWaves.getLatestWave(day)
    if (m_currentWave != null)
      m_day = true
      m_currentWave.start()
      Log.debug("Day wave {0} started.".format(m_currentWave.getId()))
    else
      Log.debug("No day wave found for day " + I2S(day))

  // --------------------------------------------------------------------------
  function cancelCurrentWave()
    if (m_currentWave == null or m_currentWave.isCompleted())
      return

    Log.debug("Wave {0} canceled by runner. State: {1}".format(m_currentWave.getId(), m_currentWave.getStatusString()))

    m_currentWave.cancel()

  // --------------------------------------------------------------------------
  private function announceWave(Wave wave)
    let nightWave = wave castTo NightWave
    if (nightWave == null)
      return

    let spawnWaves = wave.getSpawnWaves()
    if (spawnWaves.isEmpty())
      return

    let spawnWaveNames = spawnWaves.select((SpawnWave spawnWave) -> spawnWave.getDefinition().getName())
    
    // Remove "Zombies" spawn wave that exists for every night
    if (not spawnWaveNames.isEmpty())
      spawnWaveNames.removeAt(0)

    let spawnWaveNamesStr = spawnWaveNames.joinBy(", ")
    destroy spawnWaveNames

    // Night 1 - Skeleton Archers
    displayMessageToPlayer(m_targetPlayer, "Night {0} - ".format(nightWave.getNight().toString(), spawnWaveNamesStr.colorize(COLOR_GOLD)))