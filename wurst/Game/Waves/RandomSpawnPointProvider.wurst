package RandomSpawnPointProvider
import Spawning

constant int RETRY_COUNT = 10

// ============================================================================
public class RandomSpawnPointProvider implements ISpawnPointProvider
  private ISpawnPointProvider m_spawnPointManager
  private ISpawnPointProvider m_fallback

  // --------------------------------------------------------------------------
  construct(ISpawnPointProvider spawnPointManager, ISpawnPointProvider fallback)
    m_spawnPointManager = spawnPointManager
    m_fallback = fallback

  // --------------------------------------------------------------------------
  private function growRange(SpawnRange currentSpawnRange) returns SpawnRange
    // Log.debug("Grew spawn point provider range!")
    switch (currentSpawnRange)
      case SpawnRange.CLOSE
        return SpawnRange.MID
      case SpawnRange.MID
        return SpawnRange.FAR
      case SpawnRange.FAR
        return SpawnRange.FAR
  
  // --------------------------------------------------------------------------
  override function getRandomSpawnPointInRange(vec2 pos, SpawnRange range) returns spawnPointResult

    var currentSpawnRange = range

    for i = 0 to RETRY_COUNT - 1
      let result = m_spawnPointManager.getRandomSpawnPointInRange(pos, currentSpawnRange)
      if (result.succeeded)
        return result
      // Could not find a spawn point within the range, grow the range and try again
      currentSpawnRange = growRange(currentSpawnRange)

    if (m_fallback != null)
      return m_fallback.getRandomSpawnPointInRange(pos, range)

    return spawnPointResult(false, ZERO2)

  // --------------------------------------------------------------------------
  override function getRandomSpawnPointsInRange(vec2 pos, SpawnRange range, int count) returns spawnPointsResult

    var currentSpawnRange = range
    let spawnPoints = new SpawnPointCollection()

    for i = 0 to RETRY_COUNT - 1
      let remainingCount = max(count - spawnPoints.getCount(), 0)
      if (remainingCount == 0)
        break

      let result = m_spawnPointManager.getRandomSpawnPointsInRange(pos, currentSpawnRange, remainingCount)
      if (result.succeeded)
        for sp in result.spawnPoints
          spawnPoints.pushPoint(sp)
        destroy result.spawnPoints

      if (spawnPoints.getCount() == count)
        break

      // Could not find a spawn point within the range, grow the range and try again
      currentSpawnRange = growRange(currentSpawnRange)

    if (spawnPoints.getCount() != count)
    //{
      destroy spawnPoints
      if (m_fallback != null)
        return m_fallback.getRandomSpawnPointsInRange(pos, range, count)
      return spawnPointsResult(false, null)
    //}

    return spawnPointsResult(true, spawnPoints)