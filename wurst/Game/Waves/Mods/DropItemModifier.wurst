package DropItemModifier
import UnitModifier
import Functional
import ErrorHandling
import Math
import ItemSet
import UnitMetadata
import DropItemOnDeathComponent

// ============================================================================
public class DropItemModifier implements IFunctionalUnitModifier
  private FunctionalT<real> m_chance
  private FunctionalT<int> m_itemType
  private ItemSet m_itemSet

  // --------------------------------------------------------------------------
  construct()
    skip

  // --------------------------------------------------------------------------
  construct(real chance, ItemSet itemSet, int itemType)
    m_chance = FunctionalT.fromValue(chance)
    m_itemSet = itemSet
    m_itemType = FunctionalT.fromValue(itemType)

  // --------------------------------------------------------------------------
  construct(FunctionalT<real> chance, ItemSet itemSet, FunctionalT<int> itemType)
    m_chance = chance
    m_itemSet = itemSet
    m_itemType = itemType

  // --------------------------------------------------------------------------
  ondestroy
    if (m_chance != null)
      destroy m_chance
      m_chance = null

    if (m_itemType != null)
      destroy m_itemType
      m_itemType = null

  // --------------------------------------------------------------------------
  function getChance() returns FunctionalT<real>
    return m_chance

  // --------------------------------------------------------------------------
  function setChance(FunctionalT<real> func)
    m_chance = func

  // --------------------------------------------------------------------------
  function getItemSet() returns ItemSet
    return m_itemSet

  // --------------------------------------------------------------------------
  function setItemSet(ItemSet itemSet)
    m_itemSet = itemSet

  // --------------------------------------------------------------------------
  function getItemType() returns FunctionalT<int>
    return m_itemType

  // --------------------------------------------------------------------------
  function setItemType(FunctionalT<int> func)
    m_itemType = func

  // --------------------------------------------------------------------------
  override function apply(unit u, real t)
  
    if (m_chance != null and not passesChanceCheck(m_chance.getValue(t)))
      return

    if (m_itemSet == null)
      error("No item set library assigned for DropItemModifier")
      return

    if (m_itemType == null)
      error("No item type assigned for DropItemModifier")
      return

    let metadata = u.getMetadataRequired()
    let comp = metadata.getOrAddDropItemOnDeathComponent()
    comp.setItemSet(m_itemSet)
    comp.setItemType(m_itemType.getValue(t))