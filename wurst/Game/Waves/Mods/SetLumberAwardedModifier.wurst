package SetLumberAwardedModifier
import UnitModifier
import Functional
import Range
import Unit2
import ErrorHandling
import Math

// ============================================================================
public class SetLumberAwardedModifier implements IFunctionalUnitModifier
  private FunctionalT<real> m_chance
  private FunctionalRangeIntT m_amountRange

  // --------------------------------------------------------------------------
  construct()
    skip

  // --------------------------------------------------------------------------
  construct(real chance, rangeInt amountRange)
    m_chance = FunctionalT.fromValue(chance)
    m_amountRange = FunctionalRangeIntT.fromValue(amountRange)

  // --------------------------------------------------------------------------
  construct(FunctionalT<real> chance, FunctionalRangeIntT amountRange)
    m_chance = chance
    m_amountRange = amountRange

  // --------------------------------------------------------------------------
  ondestroy
    if (m_chance != null)
      destroy m_chance
      m_chance = null

    if (m_amountRange != null)
      destroy m_amountRange
      m_amountRange = null

  // --------------------------------------------------------------------------
  function getChance() returns FunctionalT<real>
    return m_chance

  // --------------------------------------------------------------------------
  function setChance(FunctionalT<real> func)
    m_chance = func

  // --------------------------------------------------------------------------
  function getAmountRange() returns FunctionalRangeIntT
    return m_amountRange

  // --------------------------------------------------------------------------
  function setAmountRange(FunctionalRangeIntT func)
    m_amountRange = func

  // --------------------------------------------------------------------------
  override function apply(unit u, real t)
  
    if (m_chance != null and not passesChanceCheck(m_chance.getValue(t)))
      return

    if (m_amountRange == null)
      error("Amount range was not set for SetLumberAwardedModifier")
      return

    u.setLumberAwarded(m_amountRange.getValue(t))