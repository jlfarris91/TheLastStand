package DropRandomItemModifier
import UnitModifier
import Functional
import ErrorHandling
import Math
import ItemSet
import UnitMetadata
import DropItemOnDeathComponent

// ============================================================================
public class DropRandomItemModifier implements IFunctionalUnitModifier
  private FunctionalT<real> m_chance
  private ItemSet m_itemSet

  // --------------------------------------------------------------------------
  construct()
    skip

  // --------------------------------------------------------------------------
  construct(real chance, ItemSet itemSet)
    m_chance = FunctionalT.fromValue(chance)
    m_itemSet = itemSet

  // --------------------------------------------------------------------------
  construct(FunctionalT<real> chance, ItemSet itemSet)
    m_chance = chance
    m_itemSet = itemSet

  // --------------------------------------------------------------------------
  ondestroy
    if (m_chance != null)
      destroy m_chance
      m_chance = null

  // --------------------------------------------------------------------------
  function getChance() returns FunctionalT<real>
    return m_chance

  // --------------------------------------------------------------------------
  function setChance(FunctionalT<real> func)
    m_chance = func

  // --------------------------------------------------------------------------
  function getItemSet() returns ItemSet
    return m_itemSet

  // --------------------------------------------------------------------------
  function setItemSet(ItemSet itemSet)
    m_itemSet = itemSet

  // --------------------------------------------------------------------------
  override function apply(unit u, real t)
  
    if (m_chance != null and not passesChanceCheck(m_chance.getValue(t)))
      return

    if (m_itemSet == null)
      error("No item set assigned for DropRandomItemModifier")
      return

    let metadata = u.getMetadataRequired()
    let comp = metadata.getOrAddDropItemOnDeathComponent()
    comp.setItemSet(m_itemSet)