package DayWave
import Wave
import ClosureTimers
import WeightedSet
import Environment

WeightedSet<int> g_weatherEffects

// ============================================================================
public class DayWave extends Wave
  private int m_day
  private real m_timeOfDayScalePercent

  // --------------------------------------------------------------------------
  construct(int day)
    m_day = day
    m_timeOfDayScalePercent = 100.0

  // --------------------------------------------------------------------------
  function getDay() returns int
    return m_day

  // --------------------------------------------------------------------------
  function getTimeOfDayScalePercent() returns real
    return m_timeOfDayScalePercent

  // --------------------------------------------------------------------------
  function setTimeOfDayScalePercent(real value)
    m_timeOfDayScalePercent = value

  // --------------------------------------------------------------------------
  override function onStarted()
    super.onStarted()

    // Have to wait for some reason
    doAfter(0.1) ->
      SetTimeOfDayScalePercentBJ(m_timeOfDayScalePercent)
      FogEnableOff()
      FogMaskEnableOff()
      startWeatherEffect()

  // --------------------------------------------------------------------------
  private function startWeatherEffect()

    let weatherEffectIndex = g_weatherEffects.getRandom()

    if (weatherEffectIndex == 1)
      Environment.startRaining()
    else
      Environment.stopRaining()

    if (weatherEffectIndex == 2)
      Environment.startWind()
    else
      Environment.stopWind()

    Environment.stopFog()

// ============================================================================
init
  g_weatherEffects = new WeightedSet<int>()
  g_weatherEffects.add(0, 0.7)
  g_weatherEffects.add(1, 0.15)
  g_weatherEffects.add(2, 0.15)