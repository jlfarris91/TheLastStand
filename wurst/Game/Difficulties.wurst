package Difficulties
import TlsItemLibrary
import Math
import PlayerProperties
import UnitPropertiesComponent

itemChances g_itemChancesStart = itemChances(8.0, 4.0, 2.0, 1.0)
itemChances g_itemChancesEnd = itemChances(1.0, 4.0, 5.0, 3.0)

// ============================================================================
public class DifficultyScaling

  // --------------------------------------------------------------------------
  static function getEliteGeneralScalar01(real difficulty, real unitTier) returns real
    return (difficulty + unitTier - 1) * 0.1 + 1

  // --------------------------------------------------------------------------
  static function getEliteDamageScalar01(real difficulty, real unitTier) returns real
    return (difficulty + unitTier - 1) * 0.25 + 1

  // --------------------------------------------------------------------------
  static function getEliteHealthScalar01(real difficulty, real unitTier) returns real
    return (difficulty + unitTier - 1) * 0.5 + 1

  // --------------------------------------------------------------------------
  static function getItemQualityScalar01(real difficulty) returns real
    return max(difficulty - 1, 0) * 0.1 + 1

  // --------------------------------------------------------------------------
  static function getItemChances(real itemQualityScalar01) returns itemChances
    return lerp(g_itemChancesStart, g_itemChancesEnd, clamp01(itemQualityScalar01))

  // --------------------------------------------------------------------------
  static function getCombinedItemFindQuality01(player p, unit u, real gameProgress01) returns real

    var itemFindQuality = 0.0

    let playerItemFindQualityProp = p.getProperty(PlayerProperty.ITEM_FIND_QUALITY)
    if (playerItemFindQualityProp != null)
      itemFindQuality += playerItemFindQualityProp.getValue() - 1.0

    let unitItemFindQualityProp = u.getProperty(UnitProperty.LOOT_ITEM_FIND_QUALITY)
    if (unitItemFindQualityProp != null)
      itemFindQuality += unitItemFindQualityProp.getValue() - 1.0

    itemFindQuality += gameProgress01 * 0.5

    return itemFindQuality