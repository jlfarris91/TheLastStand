package SurvivorSpawnPoints
import SpawnPoints
import GameConstants
import LinkedListExtensions
import ClosureTimers
import PlayerExtensions

constant real SPAWN_POINT_UPDATE_INTERVAL = 3.0
constant real TEST_RADIUS = 512.0

public SurvivorSpawnPointManager g_SurvivorSpawnPointManager

// ============================================================================
public class SurvivorSpawnPointManager implements ISpawnPointManager
  private static group s_testGroup = CreateGroup()
  private ISpawnPointManager _sourceManager
  private SpawnPointList _spawnPoints
  private CallbackPeriodic _updateCB

  // --------------------------------------------------------------------------
  construct(ISpawnPointManager sourceManager)
    this._sourceManager = sourceManager
    this._spawnPoints = new SpawnPointList()

    startUpdating()

  // --------------------------------------------------------------------------
  ondestroy
    stopUpdating()
    destroy this._spawnPoints
    this._spawnPoints = null
    this._sourceManager = null

  // --------------------------------------------------------------------------
  override function getSpawnPoints() returns SpawnPointList
    return this._spawnPoints

  // --------------------------------------------------------------------------
  function getRandomSpawnPoint() returns ISpawnPoint
    return this._spawnPoints.getRandom()
    
  // --------------------------------------------------------------------------
  function updateSpawnPoints()
    _spawnPoints.clear()
    for sp in _sourceManager.getSpawnPoints()
      if (isSpawnPointValid(sp))
        _spawnPoints.add(sp)
    //Log.info("Survivor spawn points: " + I2S(_spawnPoints.size()))

  // --------------------------------------------------------------------------
  function startUpdating()
    if (_updateCB == null)
      _updateCB = doPeriodically(SPAWN_POINT_UPDATE_INTERVAL) (CallbackPeriodic cb) ->
        updateSpawnPoints()

  // --------------------------------------------------------------------------
  function stopUpdating()
    if (_updateCB != null)
      destroy _updateCB

  // --------------------------------------------------------------------------
  private function isSpawnPointValid(ISpawnPoint sp) returns bool
    
    s_testGroup.enumUnitsInRange(sp.getPos(), TEST_RADIUS)
    
    var isValid = true

    for u from s_testGroup
      if (doesUnitInvalidateSpawnPoint(u))
        isValid = false
        break

    return isValid

  // ============================================================================
  private function doesUnitInvalidateSpawnPoint(unit u) returns bool
    return u.isAlive() and (u.isEnemyOf(PLAYER_VILLAGERS) or u.getOwner().isHumanPlayer())

// ============================================================================
init
  g_SurvivorSpawnPointManager = new SurvivorSpawnPointManager(g_SpawnPointManager)