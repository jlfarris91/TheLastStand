package UndeadSpawnPoints
import SpawnPoints
import GameConstants
import ClosureTimers

constant real SPAWN_POINT_UPDATE_INTERVAL = 3.0
constant real TEST_RADIUS = 800.0

public UndeadSpawnPointManager g_UndeadSpawnPointManager

// ============================================================================
public class UndeadSpawnPointManager implements ISpawnPointManager
  private static group s_testGroup = CreateGroup()
  private ISpawnPointManager _sourceManager
  private SpawnPointList _spawnPoints
  private CallbackPeriodic _updateCB

  // --------------------------------------------------------------------------
  construct(ISpawnPointManager sourceManager)
    this._sourceManager = sourceManager
    this._spawnPoints = new SpawnPointList()

    startUpdating()

  // --------------------------------------------------------------------------
  ondestroy
    stopUpdating()
    destroy _spawnPoints
    this._spawnPoints = null
    this._sourceManager = null

  // --------------------------------------------------------------------------
  override function getSpawnPoints() returns SpawnPointList
    return _spawnPoints

  // --------------------------------------------------------------------------
  function startUpdating()
    if (_updateCB == null)
      _updateCB = doPeriodically(SPAWN_POINT_UPDATE_INTERVAL) (CallbackPeriodic cb) ->
        updateSpawnPoints()

  // --------------------------------------------------------------------------
  function stopUpdating()
    if (_updateCB != null)
      destroy _updateCB

  // --------------------------------------------------------------------------
  protected function updateSpawnPoints()
    this._spawnPoints.clear()
    for sp in _sourceManager.getSpawnPoints()
      if (this.isSpawnPointValid(sp))
        _spawnPoints.add(sp)
    Log.info("undead spawn points: " + I2S(_spawnPoints.size()))

  // --------------------------------------------------------------------------
  private function isSpawnPointValid(ISpawnPoint sp) returns bool

    s_testGroup.enumUnitsInRange(sp.getPos(), TEST_RADIUS)

    var isValid = true

    for u from s_testGroup
      if (doesUnitInvalidateUndeadSpawnPoint(u))
        isValid = false
        break

    return isValid

  // ============================================================================
  private function doesUnitInvalidateUndeadSpawnPoint(unit u) returns bool
    return u.isAlive() and u.isEnemyOf(PLAYER_UNDEAD) and not u.isInvulnerable()

// ============================================================================
init
  g_UndeadSpawnPointManager = new UndeadSpawnPointManager(g_SpawnPointManager)