package SearchLootable
import EventUtility
import Lootables
import ColorUtility
import LootableUnit
import RegisterEvents
import LootableTypes

constant int ABILITY_ID_SEARCH = 'A01R'
constant string ABILITY_SEARCH_ORDERSTRING = "phaseshift"

function onSearchChannel()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()

  if (not target.isLootable())
    return

  let lootable = target.getLootable()
  if (not lootable.canLoot(caster))
    caster.issueImmediateOrder("stop")
    createTTEx(
      lootable.getUnit().getPos().withZ(30),
      "Already looted",
      10.0,
      COLOR_RED,
      caster.getOwner())

  // We don't do anything here except show the error message

function onSearchCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()

  if (not target.isLootable())
    return

  let lootable = target.getLootable()
  if (not lootable.canLoot(caster))
    caster.issueImmediateOrder("stop")
    createTTEx(
      lootable.getUnit().getPos().withZ(30),
      "Already looted",
      10.0,
      COLOR_RED,
      caster.getOwner())
    return

  lootable.startLooting(caster)

function onRightClick()
  let orderedUnit = GetOrderedUnit()
  let targetUnit = GetOrderTargetUnit()
  if (GetIssuedOrderId() == String2OrderIdBJ("smart") and targetUnit.isLootableUnitType())
    orderedUnit.issueTargetOrder(ABILITY_SEARCH_ORDERSTRING, targetUnit)

init
  registerSpellChannelEvent(ABILITY_ID_SEARCH, function onSearchChannel)
  registerSpellEffectEvent(ABILITY_ID_SEARCH, function onSearchCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER, function onRightClick)