package Ability_TinkererRepair
import AbilityObjEditing
import TlsAbilityIds
import RegisterEvents
import HashMap
import TlsUnitIds
import RepairTracking

HashMap<unit, lightning> g_unitToLightningMap = new HashMap<unit, lightning>()

// ============================================================================
@compiletime function createObjectDefinitions()

  new AbilityDefinitionRepairHuman(TlsAbilityIds.tinkererRepair)
    ..presetRepairTimeRatio((int lvl) -> 1.0)
    ..presetCastRange((int lvl) -> 600.0)
    ..presetTooltipNormal((int lvl) -> "Ranged Repair")
    ..presetTooltipNormalExtended((int lvl) -> "Repairs mechanical units and structures from a distance.")
    ..setButtonPositionNormalX(2)
    ..setButtonPositionNormalY(1)
    ..setButtonPositionTurnOffX(2)
    ..setButtonPositionTurnOffY(1)

// ============================================================================
function onRepairAdd()
  let repairAbilityId = getRepairAbilityId()
  if (repairAbilityId != TlsAbilityIds.tinkererRepair)
    return

  let repairingUnit = getRepairingUnit()

  var lightningEffect = g_unitToLightningMap.get(repairingUnit)
  if (lightningEffect != null)
    lightningEffect.destr()

  let startPos = repairingUnit.getPos().withTerrainZ(60.0)
  let endPos = getRepairTargetUnit().getPos().withTerrainZ(60.0)

  lightningEffect = addLightning(LIGHTNING_CHAIN_LIGHTNING_SEECONDARY, true, startPos, endPos)
  g_unitToLightningMap.put(repairingUnit, lightningEffect)

// ============================================================================
function onRepairRemove()
  let repairAbilityId = getRepairAbilityId()
  if (repairAbilityId != TlsAbilityIds.tinkererRepair)
    return

  let repairingUnit = getRepairingUnit()

  let lightningEffect = g_unitToLightningMap.get(repairingUnit)
  if (lightningEffect != null)
    lightningEffect.destr()

  g_unitToLightningMap.remove(repairingUnit)

// ============================================================================
function onUnitDeath()
  let dyingUnit = GetDyingUnit()
  if (dyingUnit.getTypeId() != TlsUnitIds.heroTinker)
    return

  let lightningEffect = g_unitToLightningMap.get(GetSpellAbilityUnit())
  if (lightningEffect != null)
    lightningEffect.destr()

  g_unitToLightningMap.remove(dyingUnit)

// ============================================================================
init

  registerRepairAdd(function onRepairAdd)
  registerRepairRemove(function onRepairRemove)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitDeath)