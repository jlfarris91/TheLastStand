package Ability_TinkererRepair
import AbilityObjEditing
import TlsAbilityIds
import RegisterEvents
import HashMap
import TlsUnitIds

HashMap<unit, lightning> g_unitToLightningMap = new HashMap<unit, lightning>()

// ============================================================================
@compiletime function createObjectDefinitions()

  new AbilityDefinitionRepairHuman(TlsAbilityIds.tinkererRepair)
    ..presetRepairTimeRatio((int lvl) -> 1.0)
    ..presetCastRange((int lvl) -> 600.0)
    ..presetTooltipNormal((int lvl) -> "Ranged Repair")
    ..presetTooltipNormalExtended((int lvl) -> "Repairs mechanical units and structures from a distance.")
    ..setButtonPositionNormalX(2)
    ..setButtonPositionNormalY(1)
    ..setButtonPositionTurnOffX(2)
    ..setButtonPositionTurnOffY(1)

// ============================================================================
function onSpellCast()

  if (GetSpellAbilityId() != TlsAbilityIds.tinkererRepair)
    return

  let castingUnit = GetSpellAbilityUnit()

  var lightningEffect = g_unitToLightningMap.get(castingUnit)
  if (lightningEffect != null)
    lightningEffect.destr()

  let startPos = castingUnit.getPos().withTerrainZ(60.0)
  let endPos = GetSpellTargetUnit().getPos().withTerrainZ(60.0)

  lightningEffect = addLightning(LIGHTNING_CHAIN_LIGHTNING_SEECONDARY, true, startPos, endPos)
  g_unitToLightningMap.put(castingUnit, lightningEffect)

// ============================================================================
function onSpellEndCast()

  if (GetSpellAbilityId() != TlsAbilityIds.tinkererRepair)
    return

  let castingUnit = GetSpellAbilityUnit()

  let lightningEffect = g_unitToLightningMap.get(castingUnit)
  if (lightningEffect != null)
    lightningEffect.destr()

  g_unitToLightningMap.remove(castingUnit)

// ============================================================================
function onUnitDeath()

  let dyingUnit = GetDyingUnit()

  if (dyingUnit.getTypeId() != TlsUnitIds.heroTinker)
    return

  let lightningEffect = g_unitToLightningMap.get(GetSpellAbilityUnit())
  if (lightningEffect != null)
    lightningEffect.destr()

  g_unitToLightningMap.remove(dyingUnit)

// ============================================================================
init

  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST, function onSpellCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_ENDCAST, function onSpellEndCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitDeath)