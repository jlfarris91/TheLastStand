package HeroAbility_Sprint
import ChannelAbilityPreset
import Icons
import RealtimeUpdate
import Meters
import DummyCaster
import Orders
import SmartValue
import BuffObjEditing
import RealExtensions
import Math
import HeroAbilities
import TlsAbilityIds
import RegisterEvents
import TlsUnitIds

constant int EXHAUST_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant int EXHAUST_BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant int LEARN_SPRINTING_MAX_LEVEL = 3
constant real MAX_DIST_THRESHOLD = 300.0

constant buffTuple SPRINT_BUFF = compiletime(createDummyBuffObject("Sprinting", "This unit is sprinting; it has increased movement speed.", Icons.bTNBootsOfSpeed, "abilities\\sprint\\sprint.mdl"))

// ============================================================================
class Sprint
  
  // --------------------------------------------------------------------------
  static function getSprintMovementSpeedFactor(int level) returns real
    return min(lerp(0.2, 0.6, parameterize(1, LEARN_SPRINTING_MAX_LEVEL, level)), 1.0)

  // --------------------------------------------------------------------------
  static function getExhaustDuration(int level) returns real
    return min(lerp(5.0, 10.0, parameterize(1, LEARN_SPRINTING_MAX_LEVEL, level)), 12.0)

  // --------------------------------------------------------------------------
  static function getExhaustAttackSpeedFactor(int level) returns real
    return max(lerp(0.4, 0.2, parameterize(1, LEARN_SPRINTING_MAX_LEVEL, level)), 0.01)

  // --------------------------------------------------------------------------
  static function getExhaustMovementSpeedFactor(int level) returns real
    return max(lerp(0.6, 0.3, parameterize(1, LEARN_SPRINTING_MAX_LEVEL, level)), 0.1)

  // --------------------------------------------------------------------------
  static function getSprintManaCostPerMeter(int _level) returns real
    //return lerp(2.0, 4.0, parameterize(1, LEARN_SPRINTING_MAX_LEVEL, level))
    return 2.0

  // --------------------------------------------------------------------------
  static function getTooltipNormal(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return "Sprint - [{0}]".format("Level {0}".format(
      effectiveAbilityLevel.toString()).colorizeTooltipForHeroAbility(effectiveAbilityLevel, learnedAbilityLevel))

  // --------------------------------------------------------------------------
  static function getTooltipNormalExtended(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return "Increases movement speed by {0} at the cost of mana lost while moving.".format(
      (getSprintMovementSpeedFactor(effectiveAbilityLevel)).toPercentageString01().colorizeTooltipForHeroAbility(effectiveAbilityLevel, learnedAbilityLevel))

// ============================================================================
public class SprintComponent extends HeroAbilityComponent implements IRealtimeListener
  private bool m_isSprinting
  private vec2 m_lastPos

  SmartValue movementSpeedFactor = new SmartValue()
  SmartValue exhaustLevel = new SmartValue()
  SmartValue manaCostPerMeter = new SmartValue()

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner, TlsAbilityIds.Hero.sprint, TlsAbilityIds.Hero.sprint_learn)

  // --------------------------------------------------------------------------
  ondestroy
    destroy exhaustLevel
    destroy manaCostPerMeter

  // --------------------------------------------------------------------------
  function startSprinting()
    if (m_isSprinting)
      return

    m_lastPos = getOwnerUnit().getPos()
    m_isSprinting = true
    updateAbilityState()
    this.registerForRealtimeUpdate(RealtimeUpdatePriority.Fast)

  // --------------------------------------------------------------------------
  function stopSprinting()
    stopSprinting(true)

  // --------------------------------------------------------------------------  
  protected function stopSprinting(bool _issueOrder)
    if (not m_isSprinting)
      return

    m_isSprinting = false
    updateAbilityState()
    this.unregisterForRealtimeUpdate()

  // --------------------------------------------------------------------------
  function exhaust()
    if (m_isSprinting)
      stopSprinting()

    let ownerUnit = getOwnerUnit()
    let exhaustLvl = exhaustLevel.getIntValue().clamp(1, 20)

    let caster = new DummyCaster()..origin(ownerUnit.getPos())..delay(2.0)    
    if (caster.castTarget(EXHAUST_ABIL_ID, exhaustLvl, OrderIds.slow, ownerUnit) == null)
      Log.error("Failed to cast lvl {0} exhaust!".format(exhaustLvl.toString()))

    let exhaustLevelValue = exhaustLevel.getIntValue()
    let exhaustDuration = Sprint.getExhaustDuration(exhaustLevelValue)
    if (exhaustDuration > 0.0)
      BlzStartUnitAbilityCooldown(ownerUnit, TlsAbilityIds.Hero.sprint, exhaustDuration)

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    this.unregisterForRealtimeUpdate()

  // --------------------------------------------------------------------------
  override protected function realtimeUpdate(real _)
    let ownerUnit = getOwnerUnit()
    let unitPos = ownerUnit.getPos()
    let dist = unitPos.distanceTo(m_lastPos)
    m_lastPos = unitPos
    if (dist > MAX_DIST_THRESHOLD)
      return
    let meters = dist * WORLD_TO_METERS
    let manaCost = meters * manaCostPerMeter.getValue()
    if (manaCost > 0.0)
      ownerUnit.subMana(manaCost)
    if (ownerUnit.getMana() <= 0.0)
      stopSprinting()
      exhaust()

  // --------------------------------------------------------------------------
  override function onCast(int abilityId)
    if (abilityId == TlsAbilityIds.Hero.sprint)
      startSprinting()

  // --------------------------------------------------------------------------
  protected function onIssuedOrder(int order)
    if (order == OrderIds.unimmolation)
      stopSprinting()

  // --------------------------------------------------------------------------
  protected override function updateAbilityState()
    let sprintLevelVal = getEffectiveAbilityLevel()
    
    exhaustLevel.setBase(sprintLevelVal)
    manaCostPerMeter.setBase(Sprint.getSprintManaCostPerMeter(sprintLevelVal))
    movementSpeedFactor.setBase(Sprint.getSprintMovementSpeedFactor(sprintLevelVal))
    
    let ownerUnit = getOwnerUnit()

    if (sprintLevelVal == 0)
      ownerUnit.removeAbility(TlsAbilityIds.Hero.sprint)
      if (m_isSprinting)
        stopSprinting()
        ownerUnit.setMoveSpeed(ownerUnit.getDefaultMovespeed())
      return
    
    if (not ownerUnit.hasAbility(TlsAbilityIds.Hero.sprint))
      ownerUnit.addAbility(TlsAbilityIds.Hero.sprint)

    ownerUnit.setAbilityLevel(TlsAbilityIds.Hero.sprint, sprintLevelVal)

    if (m_isSprinting)
    //{
      ownerUnit.addAbility(SPRINT_BUFF.abilId)
      ownerUnit.setMoveSpeed(clamp(ownerUnit.getDefaultMovespeed() * (1.0 + movementSpeedFactor.getValue()), 1, 522))
    //}
    else
    //{
      ownerUnit.removeAbility(SPRINT_BUFF.abilId)
      ownerUnit.removeAbility(SPRINT_BUFF.buffId)
      ownerUnit.setMoveSpeed(ownerUnit.getDefaultMovespeed())
    //}

  // --------------------------------------------------------------------------
  protected override function getTooltipNormal(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return Sprint.getTooltipNormal(effectiveAbilityLevel, learnedAbilityLevel)

  // --------------------------------------------------------------------------
  protected override function getTooltipNormalExtended(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return Sprint.getTooltipNormalExtended(effectiveAbilityLevel, learnedAbilityLevel)

// ============================================================================
public function IUnitMetadata.getSprintComponent() returns SprintComponent
  return this.getComponent(SprintComponent.typeId) castTo SprintComponent

// ============================================================================
public function IUnitMetadata.getOrAddSprintComponent() returns SprintComponent
  var component = this.getSprintComponent()
  if (component == null)
    component = this.addComponent(new SprintComponent(this)) castTo SprintComponent
  return component

// ============================================================================
public function unit.getSprintComponent() returns SprintComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getSprintComponent() : null

// ============================================================================
public function unit.getOrAddSprintComponent() returns SprintComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getOrAddSprintComponent() : null

// ============================================================================
@compiletime function createObjects()

  new HeroLearnAbilityDefinition(TlsAbilityIds.Hero.sprint_learn, 3)
    ..setupHeroAbilityGroup(HeroAbilityGroup.W, 0)
    ..setName("Learn Sprint")
    ..setTooltipLearn("Learn Sprint - [|cffffcc00Level %d|r]")
    ..setTooltipLearnExtended("Increases movement speed at the cost of mana lost while moving.|n|n|cffffcc00Level 1|r - Movement speed is increased by {0}|n|cffffcc00Level 2|r - Movement speed is increased by {1}|n|cffffcc00Level 3|r - Movement speed is increased by {2}".format(
      Sprint.getSprintMovementSpeedFactor(1).toPercentageString01(),
      Sprint.getSprintMovementSpeedFactor(2).toPercentageString01(),
      Sprint.getSprintMovementSpeedFactor(3).toPercentageString01()))
    ..setIconResearch(Icons.bTNBootsOfSpeed)

  new AbilityDefinitionImmolationcreep(TlsAbilityIds.Hero.sprint)
    ..setEditorSuffix("(Sprint)")
    ..setupHeroAbilityGroup(HeroAbilityGroup.W)
    ..setArtCaster("")
    ..setAnimationNames("")
    ..setLevels(20)
    ..setName("Start Sprinting")
    ..presetTooltipNormal(lvl -> Sprint.getTooltipNormal(lvl, LEARN_SPRINTING_MAX_LEVEL))
    ..presetTooltipNormalExtended(lvl -> Sprint.getTooltipNormalExtended(lvl, LEARN_SPRINTING_MAX_LEVEL))
    ..presetTooltipTurnOff(lvl -> "Stop Sprinting")
    ..presetTooltipTurnOffExtended(lvl -> "Stop sprinting.")
    ..setIconNormal(Icons.bTNBootsOfSpeed)
    ..setIconTurnOff(Icons.bTNCancel)
    ..presetBufferManaRequired(lvl -> 0)
    ..presetDamageperInterval(lvl -> 0)
    ..presetManaDrainedperSecond(lvl -> 0)
    ..presetCooldown(lvl -> 0.0)
    ..presetDurationHero(lvl -> 0.1)
    ..presetDurationNormal(lvl -> 0.1)
    ..presetBuffs(lvl -> SPRINT_BUFF.buffId.toRawCode())
    ..presetTargetsAllowed(lvl -> "")
    ..presetManaCost(lvl -> 0)
    ..presetAreaofEffect(lvl -> 0.0)

  new BuffDefinition(EXHAUST_BUFF_ID, 'Bslo')
    ..setEditorSuffix("(Sprint)")
    ..setTooltipNormal(1, "Exhausted")
    ..setTooltipNormalExtended(1, "This unit is exhausted; it's movement speed and attack rate are temporarily reduced.")

  new AbilityDefinitionSlowCreep(EXHAUST_ABIL_ID)
    ..setEditorSuffix("(Sprint)")
    ..setLevels(20)
    ..presetManaCost(lvl -> 0)
    ..presetCooldown(lvl -> 0)
    ..presetCastRange(lvl -> 9999)
    ..presetDurationHero(lvl -> Sprint.getExhaustDuration(lvl))
    ..presetDurationNormal(lvl -> Sprint.getExhaustDuration(lvl))
    ..presetAttackSpeedFactor(lvl -> Sprint.getExhaustAttackSpeedFactor(lvl))
    ..presetMovementSpeedFactor(lvl -> Sprint.getExhaustMovementSpeedFactor(lvl))
    ..presetTargetsAllowed(lvl -> "ground,air,vulnerable,invulnerable")
    ..setArtCaster("")
    ..presetBuffs(lvl -> EXHAUST_BUFF_ID.toRawCode())
    ..presetAlwaysAutocast(lvl -> false)

// ============================================================================
function onIssuedOrder()
  let triggerUnit = GetOrderedUnit()
  if (not triggerUnit.isTlsHero())
    return
  let order = GetIssuedOrderId()
  let sprintComp = triggerUnit.getSprintComponent()
  if (sprintComp != null and sprintComp.getEnabled())
    sprintComp.onIssuedOrder(order)

// ============================================================================
init
  registerHeroAbilityComponentType(SprintComponent.typeId, TlsAbilityIds.Hero.sprint)
  registerHeroAbilityComponentType(SprintComponent.typeId, TlsAbilityIds.Hero.sprint_learn)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onIssuedOrder)