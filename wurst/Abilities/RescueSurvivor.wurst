package RescueSurvivor
import RegisterEvents
import Orders
import TlsAbilityIds
import SurvivorUnit

constant int ABILITY_RESCUE_ORDER = Orders.innerfire

// ============================================================================
function onRescueCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()
  
  caster.issueImmediateOrderById(OrderIds.stop)

  if (target.isRescuableSurvivor())
    target.setOwner(caster.getOwner(), true)
    target.issueTargetOrderById(Orders.move, caster)

// ============================================================================
function onRightClick()
  let order = GetIssuedOrderId()
  let orderedUnit = GetOrderedUnit()
  let targetUnit = GetOrderTargetUnit()

  if (order != Orders.smart or
      not targetUnit.isRescuableSurvivor())
    return

  if (not orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorHero) and
      not orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorUnit))
    return

  orderedUnit.issueTargetOrderById(ABILITY_RESCUE_ORDER, targetUnit)

// ============================================================================
init
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorHero, function onRescueCast)
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorUnit, function onRescueCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER, function onRightClick)