package RescueSurvivor
import RegisterEvents
import Orders
import TlsAbilityIds
import GameConstants
import LootableTypes
import Abilities
import ClosureTimers
import FX
import DisplayTextToPlayer
import ColorUtility
import Ability_RescueSurvivor

// ============================================================================
function onRescueCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()
  
  caster.issueImmediateOrderById(OrderIds.stop)

  // Can only rescue non-lootable units owned by villagers
  if (not target.isRescuableUnit())
    displayMessageToPlayer(caster.getOwner(), "Must target a unit owned by the {0} player".format("Villagers".colorize(PLAYER_VILLAGERS.getColor().toColor())))
    return

  let p = caster.getOwner()

  target.setOwner(p, true)
  target.issueTargetOrderById(Orders.move, caster)

  let fx = FX.createEffect(Abilities.ancestralSpiritCaster, target.getPos())
  fx.setScale(0.7)
  fx.setTimeScale(2.0)
  doAfter(3.0, () -> fx.destr())

// ============================================================================
function onRightClick()
  let issuedOrder = GetIssuedOrderId()
  let orderedUnit = GetOrderedUnit()
  let targetUnit = GetOrderTargetUnit()

  let orderedUnitCanRescue = orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorHero) or
                             orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorUnit)
  if (not orderedUnitCanRescue)
    return

  if (issuedOrder == Orders.smart and targetUnit.isRescuableUnit())
    if (not orderedUnit.issueTargetOrder(ABILITY_RESCUE_ORDER, targetUnit))
      Log.error("Failed to issue rescue order")
    return

  if (issuedOrder == OrderId(ABILITY_RESCUE_ORDER_HEROACTION) and targetUnit.isRescuableUnit())
    if (not orderedUnit.issueTargetOrder(ABILITY_RESCUE_ORDER, targetUnit))
      Log.error("Failed to issue rescue order")
    return  

// ============================================================================
function unit.isRescuableUnit() returns bool
  return not this.isLootable() and this.getOwner() == PLAYER_VILLAGERS

// ============================================================================
init
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorHero, function onRescueCast)
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorUnit, function onRescueCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER, function onRightClick)