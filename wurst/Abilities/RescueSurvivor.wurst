package RescueSurvivor
import RegisterEvents
import Orders
import TlsUnitIds
import TlsAbilityIds
import UnitMetadata
import SurvivorUnit

constant int ABILITY_RESCUE_ORDER = Orders.innerfire

// ============================================================================
function onRescueCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()

  if (target.isRescuableSurvivor())
    let survivor = replaceUnitTLS(target, TlsUnitIds.survivorMale, bj_UNIT_STATE_METHOD_RELATIVE)
    survivor.setOwner(caster.getOwner(), true)
    survivor.issueTargetOrderById(Orders.move, caster)
  else
    caster.issueImmediateOrderById(OrderIds.stop)

// ============================================================================
function onRightClick()
  let order = GetIssuedOrderId()
  let orderedUnit = GetOrderedUnit()
  let targetUnit = GetOrderTargetUnit()

  if (order != Orders.smart or
      not targetUnit.isRescuableSurvivor())
    return

  if (not orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorHero) and
      not orderedUnit.hasAbility(TlsAbilityIds.rescueSurvivorUnit))
    return

  orderedUnit.issueTargetOrderById(ABILITY_RESCUE_ORDER, targetUnit)

// ============================================================================
init
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorHero, function onRescueCast)
  registerSpellEffectEvent(TlsAbilityIds.rescueSurvivorUnit, function onRescueCast)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_UNIT_ORDER, function onRightClick)