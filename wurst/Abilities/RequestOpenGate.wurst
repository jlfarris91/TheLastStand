package RequestOpenGate
import RegisterEvents
import GateComponent
import GameConstants
import DisplayTextToPlayer
import TlsAbilityIds
import UnitMetadata
import Unit_GateCommon

// ============================================================================
function onRequestToOpenGateCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()
  let casterOwner = caster.getOwner()
  let targetOwner = target.getOwner()

  if (not target.isGate())
    return

  let metadata = target.getMetadata()
  if (metadata == null)
    return

  let gate = metadata.getGateComponent()
  if (gate == null)
    return

  // If the player target's their own gate just open/close it
  if (casterOwner == targetOwner)
    sendOpenRequestToHuman(casterOwner, gate)
    //if (gate.getIsOpen())
    //  gate.closeGate()
    //else
    //  gate.openGate()
    return

  // Same with villagers
  if (targetOwner == PLAYER_VILLAGERS)
    sendOpenRequestToVillagers(gate)
    return

  if (targetOwner.isAllyOf(casterOwner))
    sendOpenRequestToHuman(casterOwner, gate)
  else
    displayMessageToPlayer(casterOwner, "{0} does not consider you an ally".format(targetOwner.getNameColored()))

// ============================================================================
function sendOpenRequestToVillagers(GateComponent gate)
  if (gate.getIsOpen())
    gate.closeGate()
  else
    gate.openGate()

// ============================================================================
function sendOpenRequestToHuman(player sender, GateComponent gate)
  let targetOwner = gate.getOwnerPlayer()
  string message
  if (gate.getIsOpen())
    message = "{0} is requesting that you close your gate".format(sender.getNameColored())
  else
    message = "{0} is requesting that you open your gate".format(sender.getNameColored())
  displayMessageToPlayer(targetOwner, message)

// ============================================================================
init
  registerSpellEffectEvent(TlsAbilityIds.requestOpenGate, function onRequestToOpenGateCast)