package RequestOpenGate
import RegisterEvents
import Ability_RequestToOpenGate
import Gate
import CampManager
import ICampStructure
import GameConstants
import DisplayTextToPlayer
import Notifications

function onRequestToOpenGateCast()
  let caster = GetSpellAbilityUnit()
  let target = GetSpellTargetUnit()
  let casterOwner = caster.getOwner()
  let targetOwner = target.getOwner()

  if (not target.isGate())
    return

  let gate = targetOwner.getCamp().getStructure(target) castTo Gate

  // If the player target's their own gate just open/close it
  if (casterOwner == targetOwner)
    sendOpenRequestToHuman(casterOwner, gate)
    //if (gate.getIsOpen())
    //  gate.closeGate()
    //else
    //  gate.openGate()
    return

  // Same with villagers
  if (targetOwner == PLAYER_VILLAGERS)
    sendOpenRequestToVillagers(gate)
    return

  if (targetOwner.isAllyOf(casterOwner))
    sendOpenRequestToHuman(casterOwner, gate)
  else
    displayTextToPlayer(casterOwner, "{0} does not consider you an ally".format(targetOwner.getNameColored()))

function sendOpenRequestToVillagers(Gate gate)
  if (gate.getIsOpen())
    gate.closeGate()
  else
    gate.openGate()

function sendOpenRequestToHuman(player sender, Gate gate)
  let targetOwner = gate.getCamp().getOwner()
  string message
  if (gate.getIsOpen())
    message = "{0} is requesting that you close your gate".format(sender.getNameColored())
  else
    message = "{0} is requesting that you open your gate".format(sender.getNameColored())
  displayTextToPlayer(targetOwner, message)
  showNotification(targetOwner, message) (notification) -> 
    targetOwner.selectSingle(gate.getUnit())

init
  registerSpellEffectEvent(ABILITY_ID_REQUESTTOOPENGATE, function onRequestToOpenGateCast)