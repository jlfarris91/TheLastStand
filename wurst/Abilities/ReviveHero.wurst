package ReviveHero
import RegisterEvents
import TlsAbilityIds
import TlsUnitIds
import DisplayTextToPlayer
import HeroHeart
import TlsItemIds
import FX
import ColorUtility
import UnitExtensions
import TlsJobItemIds
import ItemType
import HumanPlayerComponent
import Heroes

// ============================================================================
function reviveHeroUsingHeartItem(item heroHeart, unit sacrifice)
  let heartOwner = getHeartOwner(heroHeart)
  destroyHeroHeartItem(heroHeart)
  
  if (heartOwner == null)
    Log.debug("Could not find heart owner")
    return
  
  let humanPlayerComp = heartOwner.getHumanPlayerComponent()
  if (humanPlayerComp == null)
    Log.debug("Heart owner player comp is null")
    return
  
  var heroUnit = humanPlayerComp.getHero()
  let revivingHero = heroUnit != null
  let pos = sacrifice.getPos()
  let xp = sacrifice.getXp()

  for itm in sacrifice.inventory()
    sacrifice.dropItemPoint(itm, sacrifice.getPos())

  sacrifice.remove()

  if (heroUnit == null)
    heroUnit = spawnHeroForPlayer(heartOwner, pos, false)
    
  heroUnit.resetLevelAndXP()
  heroUnit.setXp(xp, false)
  
  if (revivingHero)
    heroUnit.revive(pos, true)

  FX.createSpawnEffect(pos)
  FX.createTag(pos, "Revived", Colors.gold)
  PanCameraToTimedForPlayer(heroUnit.getOwner(), pos.x, pos.y, 3.0)

  // Create a dagger and give it to the hero
  ItemType.createItemForUnit(TlsJobItemIds.acolyte1, heroUnit)

// ============================================================================
function onSpellCast()
  let caster = GetSpellAbilityUnit()
  let abilId = GetSpellAbilityId()
  let target = GetSpellTargetUnit()

  if (abilId != TlsAbilityIds.altarRevive)
    return

  if (not target.isAcolyte() or not caster.getOwner().isAllyOf(target.getOwner()))
    displayMessageToPlayer(caster.getOwner(), "You must target a friendly Acolyte")
    return

  let heroHeart = caster.getItemById(TlsItemIds.heart)
  if (heroHeart == null)
    Log.debug("Could not find heart item in altar")
    return

  reviveHeroUsingHeartItem(heroHeart, target)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST, function onSpellCast)