package Banish
import RegisterEvents
import TlsAbilityIds
import DisplayTextToPlayer
import FX
import TlsUnitIds

// ============================================================================
function onSpellEffect()
  let castingUnit = GetSpellAbilityUnit()
  let targetUnit = GetSpellTargetUnit()
  let abilId = GetSpellAbilityId()

  if (abilId != TlsAbilityIds.banish)
    return

  let castingPlayer = castingUnit.getOwner()

  if (not targetUnit.isSurvivor() or targetUnit.getOwner() != castingPlayer)
    displayMessageToPlayer(castingPlayer, "You must target one of your survivors when using Banish")
    return

  FX.createJobChangeEffect(targetUnit.getPos())
  FX.createJobChangeTag("Banished!", targetUnit.getPos(), castingPlayer)

  displayMessageToPlayer(castingPlayer, "You have banished {0} from your camp.".format(targetUnit.getProperName()))

  targetUnit.remove()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, function onSpellEffect)