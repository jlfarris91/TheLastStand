package Banish
import RegisterEvents
import TlsAbilityIds
import DisplayTextToPlayer
import FX
import TlsUnitIds
import GameConstants
import ClosureTimers
import Bounds
import BoundsExtensions
import Range
import RespawnDeadSurvivors

// ============================================================================
function onSpellEffect()
  let castingUnit = GetSpellAbilityUnit()
  let targetUnit = GetSpellTargetUnit()
  let abilId = GetSpellAbilityId()

  if (abilId != TlsAbilityIds.banish)
    return

  let castingPlayer = castingUnit.getOwner()

  if (not targetUnit.isSurvivor() or targetUnit.getOwner() != castingPlayer)
    displayMessageToPlayer(castingPlayer, "You can only banish survivors that belong to your camp.")
    return

  FX.createJobChangeTag("Banished!", targetUnit.getPos(), castingPlayer)

  displayMessageToPlayer(castingPlayer, "You have banished {0} from your camp.".format(targetUnit.getProperName()))

  // Give to the villagers player so the human player can't control it anymore
  targetUnit.setOwner(PLAYER_VILLAGERS, false)

  // Make the target move to a random point in the map while it is being banished
  targetUnit.issuePointOrder("move", playableBounds.getRandomPoint())

  let playerColor = castingPlayer.getColor().toColor()
  AddIndicator(targetUnit, playerColor.red, playerColor.green, playerColor.blue, 255)

  // Fade out the survivor over time
  doPeriodicallyTimed(SURVIVOR_FADE_INTERVAL, SURVIVOR_FADE_DURATION) (CallbackCounted cb) ->
    if (targetUnit != null)
      targetUnit.setVertexColor(colorA(150, 150, 150, 255).mix(colorA(150, 150, 150, 0), cb.getPercentage()))
  
  // Remove the survivor once it is invisible
  doAfter(SURVIVOR_FADE_DURATION, () -> targetUnit.remove())

  // Try to spawn another survivor for the player later
  doAfter(SURVIVORS_RESPAWN_BANISHED_INTERVAL_RANGE.getRandom(), () -> trySpawnSurvivorForPlayer(castingPlayer))

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT, function onSpellEffect)