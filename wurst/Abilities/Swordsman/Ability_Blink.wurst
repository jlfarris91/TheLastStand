package Ability_Blink
import TlsAbilityIds
import RegisterEvents
import ChannelAbilityPreset
import Icons
import ClosureTimers

// ============================================================================
@compiletime function createObjectDefinitions()

  new ChannelAbilityPreset(TlsAbilityIds.swordsmanBlink, 1, true)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..presetTargetTypes(Targettype.UNIT)
    ..setTargetsAllowed(1, "ground,friend,vuln,invu")
    ..setIconNormal(Icons.bTNBlink)
    ..setName("Blink")
    ..setTooltipNormal(1, "Blink")
    ..setTooltipNormalExtended(1, "The Swordsman teleports to a nearby allied unit.")
    ..setButtonPositionNormalX(2)
    ..setButtonPositionNormalY(1)
    ..presetCastRange(lvl -> 600.0)
    ..presetCooldown(lvl -> 30)
    ..setManaCost(1, 60)

// ============================================================================
function onSpellCast()

  if (GetSpellAbilityId() != TlsAbilityIds.swordsmanBlink)
    return

  let castingUnit = GetSpellAbilityUnit()
  let targetUnit = GetSpellTargetUnit()

  flashEffect("Abilities\\Spells\\NightElf\\Blink\\BlinkCaster.mdl", castingUnit.getPos())
  flashEffect("Abilities\\Spells\\NightElf\\Blink\\BlinkTarget.mdl", targetUnit.getPos())

  doAfter(0.33) () ->
    if (castingUnit.isAlive() and targetUnit.isAlive())
      castingUnit.setPos(targetUnit.getPos())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST, function onSpellCast)