package SmokeGrenade
import RealExtensions
import ChannelAbilityPreset
import Icons
import UnitIds
import UnitIndexer
import DummyCaster
import Math
import Orders
import BuffObjEditing
import ClosureTimers
import HeroAbilities
import TlsAbilityIds
import Execute

constant int DUMMY_ID = compiletime(UNIT_ID_GEN.next())
constant int CLOUD_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant int CLOUD_BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant int DUMMY_PROJECTILE_ABIL_ID = compiletime(ABIL_ID_GEN.next())
constant real CLOUD_AOE = 300.0
constant real CLOUD_DURATION = 15.0

group g_castingSmokeGrenades = CreateGroup()

// ============================================================================
class SmokeGrenade

  // --------------------------------------------------------------------------
  static function getChanceToMissPercentage01(int effectiveAbilityLevel) returns real
    return clamp01(lerp(0.3, 1.0, parameterize(1, 3, effectiveAbilityLevel)))

  // --------------------------------------------------------------------------
  static function getAttackSpeedModifier(int effectiveAbilityLevel) returns real
    return lerp(-0.1, -0.3, parameterize(1, 3, effectiveAbilityLevel))

  // --------------------------------------------------------------------------
  static function getMovementSpeedModifier(int effectiveAbilityLevel) returns real
    return lerp(-0.1, -0.3, parameterize(1, 3, effectiveAbilityLevel))

  // --------------------------------------------------------------------------
  static function getManaCost(int effectiveAbilityLevel) returns int
    return lerpInt(150, 350, parameterize(1, 3, effectiveAbilityLevel))

  // --------------------------------------------------------------------------
  static function getCooldown(int effectiveAbilityLevel) returns real
    return lerp(40.0, 25.0, parameterize(1, 3, effectiveAbilityLevel))

  // --------------------------------------------------------------------------
  static function getTooltipNormal(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return "Smoke Grenade - [{0}]".format("Level {0}".format(
      effectiveAbilityLevel.toString()).colorizeTooltipForHeroAbility(effectiveAbilityLevel, learnedAbilityLevel))

  // --------------------------------------------------------------------------
  static function getTooltipNormalExtended(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return "Throws a smoke grenade at a target area causing all enemies within to have a {0} chance to miss attacks and have {1} reduced attack speed for {2} seconds.".format(
      getChanceToMissPercentage01(effectiveAbilityLevel).toPercentageString01().colorizeTooltipForHeroAbility(effectiveAbilityLevel, learnedAbilityLevel),
      (-getAttackSpeedModifier(effectiveAbilityLevel)).toPercentageString01().colorizeTooltipForHeroAbility(effectiveAbilityLevel, learnedAbilityLevel),
      CLOUD_DURATION.toInt().toString())

// ============================================================================
public class SmokeGrenadeComponent extends HeroAbilityComponent
  protected vec2 m_lastTargetPoint
  
  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner, TlsAbilityIds.Hero.smokeGrenade, TlsAbilityIds.Hero.smokeGrenade_learn)

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    g_castingSmokeGrenades.removeUnit(getOwnerUnit())

  // --------------------------------------------------------------------------
  override protected function onCastPoint(int _abilityId, vec2 target)
    m_lastTargetPoint = target
    g_castingSmokeGrenades.addUnit(getOwnerUnit())

    let ownerUnit = getOwnerUnit()

    let dummy = new DummyCaster()
      ..origin(ownerUnit.getPos())
      ..owner(ownerUnit.getOwner())
      ..delay(2.0)
  
    if (dummy.castPoint(DUMMY_PROJECTILE_ABIL_ID, 1, OrderIds.summonfactory, target) == null)
      // Refund since the ability failed to cast
      ownerUnit.resetAbilityCooldown(TlsAbilityIds.Hero.smokeGrenade)
      ownerUnit.addMana(SmokeGrenade.getManaCost(getEffectiveAbilityLevel()).toReal())

  // --------------------------------------------------------------------------
  protected function onMissileLanded()
    let ownerUnit = getOwnerUnit()

    try() () -> 
    //{
      let caster = new DummyCaster()
        ..owner(ownerUnit.getOwner())
        ..origin(m_lastTargetPoint)
        ..delay(CLOUD_DURATION)

      let effectiveAbilityLevel = getEffectiveAbilityLevel()
    
      if (caster.castPoint(CLOUD_ABIL_ID, effectiveAbilityLevel, OrderIds.cloudoffog, m_lastTargetPoint) == null)
        Log.error("Failed to cast cloud ability")
        // Refund since the ability failed to cast
        ownerUnit.resetAbilityCooldown(TlsAbilityIds.Hero.smokeGrenade)
        ownerUnit.addMana(SmokeGrenade.getManaCost(effectiveAbilityLevel).toReal())

      let circlefx = addEffect("circle.mdl", m_lastTargetPoint)
        ..setPos(m_lastTargetPoint)
        ..setScale(CLOUD_AOE / 64.0)
        ..setColor(colorA(150, 150, 150, 100))
      doAfter(CLOUD_DURATION, () -> circlefx.destr())
    //}

    m_lastTargetPoint = ZERO2
    g_castingSmokeGrenades.removeUnit(ownerUnit)

  // --------------------------------------------------------------------------
  protected override function getTooltipNormal(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return SmokeGrenade.getTooltipNormal(effectiveAbilityLevel, learnedAbilityLevel)
    
  // --------------------------------------------------------------------------
  protected override function getTooltipNormalExtended(int effectiveAbilityLevel, int learnedAbilityLevel) returns string
    return SmokeGrenade.getTooltipNormalExtended(effectiveAbilityLevel, learnedAbilityLevel)

// ============================================================================
public function IUnitMetadata.getSmokeGrenadeComponent() returns SmokeGrenadeComponent
  return this.getComponent(SmokeGrenadeComponent.typeId) castTo SmokeGrenadeComponent

// ============================================================================
public function IUnitMetadata.getOrAddSmokeGrenadeComponent() returns SmokeGrenadeComponent
  var component = this.getSmokeGrenadeComponent()
  if (component == null)
    component = this.addComponent(new SmokeGrenadeComponent(this)) castTo SmokeGrenadeComponent
  return component

// ============================================================================
public function unit.getSmokeGrenadeComponent() returns SmokeGrenadeComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getSmokeGrenadeComponent() : null

// ============================================================================
public function unit.getOrAddSmokeGrenadeComponent() returns SmokeGrenadeComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getOrAddSmokeGrenadeComponent() : null

// ============================================================================
function onUnitIndexed()
  let indexingUnit = getIndexingUnit()
  if (indexingUnit.getTypeId() != DUMMY_ID)
    return

  let dummyUnitPos = indexingUnit.getPos()

  real closestDistance = REAL_MAX
  unit closestCaster = null
    
  for castingUnit in g_castingSmokeGrenades
    let comp = castingUnit.getSmokeGrenadeComponent()
    if (comp != null and comp.getEnabled())
      let dd = dummyUnitPos.distanceToSq(comp.m_lastTargetPoint)
      if (dd < closestDistance)
        closestDistance = dd
        closestCaster = castingUnit
  
  if (closestCaster == null)
    Log.error("Not sure who smoke grenade dummy unit belongs to")
    return

  closestCaster.getSmokeGrenadeComponent().onMissileLanded()

// ============================================================================
@compiletime function createObjects()

  new UnitDefinition(DUMMY_ID, UnitIds.wisp)
    ..setScalingValue(0.01)
    ..setNormalAbilities(commaList(AbilityIds.invulnerable, AbilityIds.locust))
    ..setSightRadiusDay(1)
    ..setSightRadiusNight(1)
    ..setArtSpecial("")
    ..setModelFile("dummy.mdl")
    ..setCollisionSize(0.0)
    ..setFoodCost(0)
    ..setFoodProduced(0)
    ..setAttacksEnabled(0)

  new BuffDefinition(CLOUD_BUFF_ID, 'bclf')
    ..setName("Smoke Grenade")
    ..setTooltipNormal(1, "Smoke Grenade")
    ..setTooltipNormalExtended(1, "This unit is inside of a thick cloud of smoke causing it to miss attacks.")
  
  new AbilityDefinitionCloudofFog(CLOUD_ABIL_ID)
    ..setLevels(10)
    ..setItemAbility(false)
    ..presetAreaofEffect(lvl -> CLOUD_AOE)
    ..presetAttackSpeedModifier(lvl -> SmokeGrenade.getAttackSpeedModifier(lvl))
    ..presetAttacksPrevented(lvl -> 0)
    ..presetChanceToMiss(lvl -> SmokeGrenade.getChanceToMissPercentage01(lvl))
    ..presetMovementSpeedModifier(lvl -> SmokeGrenade.getMovementSpeedModifier(lvl))
    ..presetBuffs(lvl -> CLOUD_BUFF_ID.toRawCode())
    ..presetTargetsAllowed(lvl -> "ground,vulnerable,invulnerable")
    ..presetDurationHero(lvl -> CLOUD_DURATION)
    ..presetDurationNormal(lvl -> CLOUD_DURATION)
    ..presetManaCost(lvl -> 0)
    ..presetCooldown(lvl -> 0)
    ..setRequirements("")
  
  new AbilityDefinitionCloudofFog(TlsAbilityIds.Hero.smokeGrenade)
    ..setupHeroAbilityGroup(HeroAbilityGroup.E)
    ..setLevels(10)
    ..setItemAbility(false)
    ..setAnimationNames("spell,attack,throw")
    ..presetTooltipNormal(lvl -> SmokeGrenade.getTooltipNormal(lvl, lvl))
    ..presetTooltipNormalExtended(lvl -> SmokeGrenade.getTooltipNormalExtended(lvl, lvl))
    ..presetCastingTime(lvl -> (5. / 30.) * 1.267)
    ..presetAreaofEffect(lvl -> CLOUD_AOE)
    ..presetAttackSpeedModifier(lvl -> 0.0)
    ..presetAttacksPrevented(lvl -> 0)
    ..presetChanceToMiss(lvl -> 0.0)
    ..presetMovementSpeedModifier(lvl -> 0.0)
    ..presetBuffs(lvl -> "")
    ..presetTargetsAllowed(lvl -> "")
    ..presetDurationHero(lvl -> 0.1)
    ..presetDurationNormal(lvl -> 0.1)
    ..presetManaCost(lvl -> SmokeGrenade.getManaCost(lvl))
    ..presetCooldown(lvl -> SmokeGrenade.getCooldown(lvl))
    ..presetCastRange(lvl -> 900)
    ..setRequirements("")

  new AbilityDefinitionTinkererSummonFactoryLevel0(DUMMY_PROJECTILE_ABIL_ID)
    ..setLevels(1)
    ..setName("Smoke Grenade")
    ..presetCastRange(lvl -> 900)
    ..presetAreaofEffect(lvl -> 1.0)
    ..presetTargetsAllowed(lvl -> "")
    ..setAnimationNames("spell,attack,throw")
    ..setIconNormal(Icons.bTNCloudOfFog)
    ..setMissileArt("Abilities\\Spells\\Other\\AcidBomb\\BottleMissile.mdl")
    ..setMissileSpeed(400)
    ..setMissileArc(0.5)
    ..presetFactoryUnitID(lvl -> DUMMY_ID.toRawCode())
    ..presetLeashRange(lvl -> 0.0)
    ..presetSpawnInterval(lvl -> 0.0)
    ..presetSpawnUnitDuration(lvl -> 0.0)
    ..presetSpawnUnitID(lvl -> "")
    ..presetSpawnUnitOffset(lvl -> 0.0)
    ..presetBuffs(lvl -> "")
    ..presetCastRange(lvl -> 900)
    ..presetManaCost(lvl -> 0)
    ..presetDurationHero(lvl -> 0.1)
    ..presetDurationNormal(lvl -> 0.1)
    ..presetCooldown(lvl -> 0.0)

  new HeroLearnAbilityDefinition(TlsAbilityIds.Hero.smokeGrenade_learn, 3)
    ..setupHeroAbilityGroup(HeroAbilityGroup.E)
    ..setName("Smoke Grenade")
    ..setTooltipLearn("Learn Smoke Grenade - [|cFFFFCC00 Level %d|r]")
    ..setTooltipLearnExtended("Throws a smoke grenade at a target area causing all enemies within to miss attacks for {6} seconds.|n|n|cFFFFCC00Level 1|r - {0} chance to miss and attack speed is reduced by {1}|n|cFFFFCC00Level 2|r - {2} chance to miss and attack speed is reduced by {3}|n|cFFFFCC00Level 3|r - {4} chance to miss and attack speed is reduced by {5}".format(
      SmokeGrenade.getChanceToMissPercentage01(1).toPercentageString01(), (-SmokeGrenade.getAttackSpeedModifier(1)).toPercentageString01(),
      SmokeGrenade.getChanceToMissPercentage01(2).toPercentageString01(), (-SmokeGrenade.getAttackSpeedModifier(2)).toPercentageString01(),
      SmokeGrenade.getChanceToMissPercentage01(3).toPercentageString01(), (-SmokeGrenade.getAttackSpeedModifier(3)).toPercentageString01(),
      CLOUD_DURATION.toInt().toString()))
    ..setIconResearch(Icons.bTNCloudOfFog)
    ..presetManaCost(lvl -> SmokeGrenade.getManaCost(lvl))
    ..presetCooldown(lvl -> SmokeGrenade.getCooldown(lvl))

// ============================================================================
init
  onUnitIndex(() -> onUnitIndexed())