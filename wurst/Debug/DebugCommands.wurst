package DebugCommands
import ChatCommands
import DisplayTextToPlayer
import Host
import ChatCommandHandler
import MemoryTrackingMultiboard
import ClosureTimers
import Godlike

constant string DEBUG_COMMAND_MEMORYTRACKER = "mem"
constant string DEBUG_COMMAND_GODLIKE = "godlike"

DebugChatCommandHandler g_debugChatCommandHandler
MemoryTrackingMultiboard g_memoryTrackingMultiboard

// ============================================================================
public class DebugChatCommandHandler extends ChatCommandHandler

  // --------------------------------------------------------------------------
  override function handleChatCommand(player p, string command) returns bool

    if (p.isHost())
      return super.handleChatCommand(p, command)

    return false

// ============================================================================
function showMemoryTrackerMultiboard()

  if (g_memoryTrackingMultiboard.isShowing())
    displayMessageToPlayer(g_HostPlayer, "Hiding memory tracking multiboard")
    g_memoryTrackingMultiboard.hide()
  else
    displayMessageToPlayer(g_HostPlayer, "Showing memory tracking multiboard")
    g_memoryTrackingMultiboard.updateMultiboardRows()
    g_memoryTrackingMultiboard.show()

// ============================================================================
function godlike()
  if (isGodModeEnabled())
    disableGodMode()
    displayMessageToPlayer(g_HostPlayer, "Disabled God Mode")
  else
    displayMessageToPlayer(g_HostPlayer, "Enabled God Mode")
    enableGodMode()

// ============================================================================
init
  g_debugChatCommandHandler = new DebugChatCommandHandler()
  registerChatCommandHandler(g_debugChatCommandHandler, -1)

  g_debugChatCommandHandler.registerHandlerAction(DEBUG_COMMAND_MEMORYTRACKER, function showMemoryTrackerMultiboard)
  g_debugChatCommandHandler.registerHandlerAction(DEBUG_COMMAND_GODLIKE, function godlike)

  nullTimer() () -> 
    g_memoryTrackingMultiboard = new MemoryTrackingMultiboard()
    ..hide()