package GenericEvent
import LinkedList

// ============================================================================
public abstract class GenericEventHandler<TSender>
  bool _isMarkedForRemoval
  abstract function call(TSender sender)

// ============================================================================
public class GenericEvent<TSender>
  private LinkedList<GenericEventHandler<TSender>> _handlers

  // --------------------------------------------------------------------------
  ondestroy
    if (_handlers != null)
      for handler in _handlers
        destroy handler
      destroy _handlers
      _handlers = null

  // --------------------------------------------------------------------------
  function addListener(GenericEventHandler<TSender> handler) returns GenericEventHandler<TSender>
    if (handler == (0 castTo GenericEventHandler<TSender>))
      return handler
    if (_handlers == null)
      _handlers = new LinkedList<GenericEventHandler<TSender>>()
    _handlers.add(handler)
    return handler

  // --------------------------------------------------------------------------
  function removeListener(GenericEventHandler<TSender> handler) returns GenericEventHandler<TSender>
    if (handler != (0 castTo GenericEventHandler<TSender>) and _handlers != null)
      handler._isMarkedForRemoval = true
    return 0 castTo GenericEventHandler<TSender>

  // --------------------------------------------------------------------------
  function invoke(TSender sender)
    if (_handlers != null)
      for handler in _handlers
        if (handler._isMarkedForRemoval)
          _handlers.remove(handler)
          destroy handler
        else
          handler.call(sender)
    