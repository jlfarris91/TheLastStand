package UnitMetadata
import Composition
import HashMap
import LinkedList
import public UnitMetadataExtensions
import ErrorHandling
import OnUnitEnterLeave
import HashList
import Type
import ErrorIf
//import Reflection

HashMap<unit, IUnitMetadata> g_unitMetadataMap
LinkedList<IUnitMetadataFactory> g_unitMetadataFactories
HashList<int> g_ignoreAdd
HashList<int> g_ignoreRemove

// ============================================================================
public interface IUnitMetadata extends IComposite
  function getUnit() returns unit
  function setUnit(unit u)

// ============================================================================
public interface IUnitMetadataFactory
  function createUnit(unit u) returns IUnitMetadata

// ============================================================================
public abstract class BaseUnitMetadata implements IUnitMetadata
  protected unit _unit

  // --------------------------------------------------------------------------
  construct(unit u)
    setUnit(u)

  // --------------------------------------------------------------------------
  ondestroy
    unregisterUnitMetadata(this)
    _unit = null

  // --------------------------------------------------------------------------
  override function getUnit() returns unit
    return _unit

  // --------------------------------------------------------------------------
  override function setUnit(unit u)
    let oldUnit = _unit
    let newUnit = u
    if (oldUnit != null)
      unregisterUnitMetadata(this)
    _unit = newUnit
    registerUnitMetadata(this)
    onUnitChanged(oldUnit, newUnit)
    //if (oldUnit != null)
    //  Log.info(typeIdToTypeName(this.typeId) + " replaced " + I2S(oldUnit.getTypeId()) + " with " + I2S(newUnit.getTypeId()))

  // --------------------------------------------------------------------------
  protected function onUnitChanged(unit _oldUnit, unit _newUnit)
    skip

// ============================================================================
public class NullUnitMetadata extends BaseUnitMetadata
  
  // --------------------------------------------------------------------------
  construct(unit u)
    super(u)

  // --------------------------------------------------------------------------
  @inline
  override function getUnit() returns unit
    return super.getUnit()
    
  // --------------------------------------------------------------------------
  @inline
  override function addComponent(IComponent component) returns IComponent
    return super.addComponent(component)

  // --------------------------------------------------------------------------
  @inline
  override function getComponent(Type componentTypeId) returns IComponent
    return super.getComponent(componentTypeId)

// ============================================================================
public function unit.getMetadata() returns IUnitMetadata
  //if (g_unitMetadataMap.has(this) == false)
  //  Log.info("No metadata found for unit " + this.getName())
  return g_unitMetadataMap.get(this)

// ============================================================================
public function registerUnitMetadataFactory(IUnitMetadataFactory factory)
  ErrorIf.argumentIsNull(factory, "factory")
  g_unitMetadataFactories.addAt(factory, 0)

// ============================================================================
public function unregisterUnitMetadataFactory(IUnitMetadataFactory factory)
  ErrorIf.argumentIsNull(factory, "factory")
  g_unitMetadataFactories.remove(factory)

// ============================================================================
public function constructUnitMetadata(unit u) returns IUnitMetadata
  ErrorIf.argumentIsNull(u, "u")
  for factory in g_unitMetadataFactories
    var uu = factory.createUnit(u)
    if (uu != null)
      return uu
  return new NullUnitMetadata(u)

// ============================================================================
public function registerUnitMetadata(IUnitMetadata metadata)
  ErrorIf.argumentIsNull(metadata, "metadata")
  let u = metadata.getUnit()
  if (g_unitMetadataMap.has(u))
    error("Unit already has a metadata object assigned")
  g_unitMetadataMap.put(u, metadata)

// ============================================================================
public function unregisterUnitMetadata(IUnitMetadata metadata)
  ErrorIf.argumentIsNull(metadata, "metadata")
  g_unitMetadataMap.remove(metadata.getUnit())
  destroy metadata

// ============================================================================
public function replaceUnitTLS(unit whichUnit, integer newUnitId, integer unitStateMethod) returns unit
  let existingMetadata = whichUnit.getMetadata()
  pauseMetadataRemoval(whichUnit.getTypeId())
  pauseMetadataGeneration(newUnitId)
  let newUnit = ReplaceUnitBJ(whichUnit, newUnitId, unitStateMethod)
  if (existingMetadata != null)
    existingMetadata.setUnit(newUnit)
  //Log.info("replaceUnitTLS: reassigned metadata from unit " + whichUnit.getName() + " to unit " + newUnit.getName())
  return newUnit

// ============================================================================
public function createUnitTLS(player p, integer unitId, real x, real y, real face) returns unit
  pauseMetadataGeneration(unitId)
  let newUnit = CreateUnit(p, unitId, x, y, face)
  constructUnitMetadata(newUnit)
  //Log.info("createUnitTLS: 2 created metadata for unit " + newUnit.getName())
  return newUnit

// ============================================================================
public function createUnitTLS(player p, integer unitId, vec2 pos, real face) returns unit
  return createUnitTLS(p, unitId, pos.x, pos.y, face)

// ============================================================================
public function createUnitTLS(IUnitMetadata metadata, player p, integer unitId, real x, real y, real face) returns unit
  ErrorIf.argumentIsNull(metadata, "metadata")
  ErrorIf.argumentIsNull(p, "p")
  pauseMetadataGeneration(unitId)
  let newUnit = CreateUnit(p, unitId, x, y, face)
  metadata.setUnit(newUnit)
  //Log.info("createUnitTLS: reassigned metadata from unit " + (oldUnit != null ? oldUnit.getName() : "null") + " to unit " + newUnit.getName())
  return newUnit

// ============================================================================
public function removeUnitTLS(unit u)
  pauseMetadataRemoval(u.getTypeId())
  u.remove()

// ============================================================================
public function pauseMetadataGeneration(int unitTypeId)
  g_ignoreAdd.add(unitTypeId)

// ============================================================================
function resumeMetadataGeneration(int unitTypeId)
  g_ignoreAdd.remove(unitTypeId)

// ============================================================================
public function pauseMetadataRemoval(int unitTypeId)
  g_ignoreRemove.add(unitTypeId)

// ============================================================================
function resumeMetadataRemoval(int unitTypeId)
  g_ignoreRemove.remove(unitTypeId)

// ============================================================================
function onUnitEnteredMap()
  let u = getEnterLeaveUnit()
  let shouldGenerateMetadata = g_ignoreAdd.isEmpty() or not g_ignoreAdd.has(u.getTypeId())

  // if (g_ignoreAdd.has(u.getTypeId()))
  //   Log.info("onUnitEnteredMap: ignoring unit " + u.getName())
  // else
  //   Log.info("onUnitEnteredMap: creating metadata for unit " + u.getName())

  if (shouldGenerateMetadata)
    constructUnitMetadata(u)
    //let metadata = constructUnitMetadata(u)
    //if (metadata != null)
    //  Log.info("Registered metadata for unit " + u.getName() + " " + I2S(u.getTypeId()))
  else
    resumeMetadataGeneration(u.getTypeId())

// ============================================================================
function onUnitRemovedFromMap()
  let u = getEnterLeaveUnit()
  let shouldRemoveMetadata = g_ignoreRemove.isEmpty() or not g_ignoreRemove.has(u.getTypeId())

  // if (g_ignoreRemove.has(u.getTypeId()))
  //   Log.info("onUnitRemovedFromMap: ignoring unit " + u.getName())
  // else
  //   Log.info("onUnitRemovedFromMap: removing metadata for unit " + u.getName())

  if (shouldRemoveMetadata)
    let metadata = u.getMetadata()
    if (metadata != null)
      unregisterUnitMetadata(metadata)
      destroy metadata
      //Log.info("Unregistered metadata for unit " + u.getName() + " " + I2S(u.getTypeId()))
  else
    resumeMetadataRemoval(u.getTypeId())

// ============================================================================
init
  g_unitMetadataMap = new HashMap<unit, IUnitMetadata>()
  g_unitMetadataFactories = new LinkedList<IUnitMetadataFactory>()
  g_ignoreAdd = new HashList<int>()
  g_ignoreRemove = new HashList<int>()

  onEnter(function onUnitEnteredMap)
  onLeave(function onUnitRemovedFromMap)