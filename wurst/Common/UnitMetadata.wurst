package UnitMetadata
import Composition
import HashMap
import LinkedList
import public UnitMetadataExtensions
import ErrorHandling
import OnUnitEnterLeave
import HashList
import Reflection

HashMap<unit, IUnitMetadata> g_unitMetadataMap
LinkedList<IUnitMetadataFactory> g_unitMetadataFactories
HashList<unit> g_ignoreAdd
HashList<unit> g_ignoreRemove

// ============================================================================
public interface IUnitMetadata extends IComposite
  function getUnit() returns unit
  function setUnit(unit u)

// ============================================================================
public interface IUnitMetadataFactory
  function createUnit(unit u) returns IUnitMetadata

// ============================================================================
public abstract class BaseUnitMetadata implements IUnitMetadata
  protected unit _unit

  // --------------------------------------------------------------------------
  construct(unit u)
    setUnit(u)

  // --------------------------------------------------------------------------
  ondestroy
    unregisterUnitMetadata(this)
    _unit = null

  // --------------------------------------------------------------------------
  override function getUnit() returns unit
    return _unit

  // --------------------------------------------------------------------------
  override function setUnit(unit u)
    let oldUnit = _unit
    let newUnit = u
    if (oldUnit != null)
      unregisterUnitMetadata(this)
    _unit = newUnit
    registerUnitMetadata(this)
    onUnitChanged(oldUnit, newUnit)
    if (oldUnit != null)
      Log.info(typeIdToTypeName(this.typeId) + " replaced " + I2S(oldUnit.getTypeId()) + " with " + I2S(newUnit.getTypeId()))

  // --------------------------------------------------------------------------
  protected function onUnitChanged(unit _oldUnit, unit _newUnit)
    skip

// ============================================================================
public function unit.getMetadata() returns IUnitMetadata
  if (g_unitMetadataMap.has(this) == false)
    Log.info("No metadata found for unit " + this.getName())
  return g_unitMetadataMap.get(this)

// ============================================================================
public function registerUnitMetadataFactory(IUnitMetadataFactory factory)
  if (factory == null)
    error("Unit metadata factory cannot be null")
  g_unitMetadataFactories.addAt(factory, 0)

// ============================================================================
public function unregisterUnitMetadataFactory(IUnitMetadataFactory factory)
  if (factory == null)
    error("Unit metadata factory cannot be null")
  g_unitMetadataFactories.remove(factory)

// ============================================================================
public function constructUnitMetadata(unit u) returns IUnitMetadata
  if (u == null)
    error("Unit cannot be null")
  for factory in g_unitMetadataFactories
    var uu = factory.createUnit(u)
    if (uu != null)
      return uu
  return null

// ============================================================================
public function registerUnitMetadata(IUnitMetadata u)
  g_unitMetadataMap.put(u.getUnit(), u)

// ============================================================================
public function unregisterUnitMetadata(IUnitMetadata u)
  g_unitMetadataMap.remove(u.getUnit())

// ============================================================================
public function replaceUnitTLS(unit whichUnit, integer newUnitId, integer unitStateMethod) returns unit
  let existingMetadata = whichUnit.getMetadata()
  let newUnit = ReplaceUnitBJ(whichUnit, newUnitId, unitStateMethod)
  if (existingMetadata != null)
    existingMetadata.setUnit(newUnit)
  pauseMetadataGeneration(newUnit)
  pauseMetadataRemoval(whichUnit)
  return newUnit

// ============================================================================
public function createUnitTLS(player p, integer unitId, real x, real y, real face) returns unit
  let newUnit = CreateUnit(p, unitId, x, y, face)
  pauseMetadataGeneration(newUnit)
  constructUnitMetadata(newUnit)
  return newUnit

// ============================================================================
public function createUnitTLS(player p, integer unitId, vec2 pos, real face) returns unit
  return createUnitTLS(p, unitId, pos.x, pos.y, face)

// ============================================================================
public function createUnitTLS(IUnitMetadata metadata, player p, integer unitId, real x, real y, real face) returns unit
  if (metadata == null)
    error("metadata cannot be null")
  let newUnit = CreateUnit(p, unitId, x, y, face)
  pauseMetadataGeneration(newUnit)
  metadata.setUnit(newUnit)
  return newUnit

// ============================================================================
public function removeUnitTLS(unit u)
  pauseMetadataRemoval(u)
  u.remove()

// ============================================================================
public function pauseMetadataGeneration(unit u)
  g_ignoreAdd.add(u)

// ============================================================================
function resumeMetadataGeneration(unit u)
  g_ignoreAdd.remove(u)

// ============================================================================
public function pauseMetadataRemoval(unit u)
  g_ignoreRemove.add(u)

// ============================================================================
function resumeMetadataRemoval(unit u)
  g_ignoreRemove.remove(u)

// ============================================================================
function onUnitEnteredMap()
  let u = getEnterLeaveUnit()
  let shouldGenerateMetadata = g_ignoreAdd.isEmpty() or not g_ignoreAdd.has(u)
  if (shouldGenerateMetadata)
    let metadata = constructUnitMetadata(u)
    if (metadata != null)
      Log.info("Registered metadata for unit " + u.getName() + " " + I2S(u.getTypeId()))
  else
    resumeMetadataGeneration(u)

// ============================================================================
function onUnitRemovedFromMap()
  let u = getEnterLeaveUnit()
  let shouldRemoveMetadata = g_ignoreRemove.isEmpty() or not g_ignoreRemove.has(u)
  if (shouldRemoveMetadata)
    let metadata = u.getMetadata()
    if (metadata != null)
      unregisterUnitMetadata(metadata)
      destroy metadata
      Log.info("Unregistered metadata for unit " + u.getName() + " " + I2S(u.getTypeId()))
  else
    resumeMetadataRemoval(u)

// ============================================================================
init
  g_unitMetadataMap = new HashMap<unit, IUnitMetadata>()
  g_unitMetadataFactories = new LinkedList<IUnitMetadataFactory>()
  g_ignoreAdd = new HashList<unit>()
  g_ignoreRemove = new HashList<unit>()

  onEnter(function onUnitEnteredMap)
  onLeave(function onUnitRemovedFromMap)