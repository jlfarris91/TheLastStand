package SmartUnit
import initlater LinkedList
import initlater UnitMetadata
import Composition

// ============================================================================
public class ComposableUnit extends BaseUnitMetadata
  protected LinkedList<IComponent> _components

  // --------------------------------------------------------------------------
  construct(unit u)
    super(u)

  // --------------------------------------------------------------------------
  ondestroy
    if (_components != null)
      for component in _components
        destroy component
      destroy _components
      _components = null

  // --------------------------------------------------------------------------
  @inline
  override function addComponent(IComponent component) returns IComponent
    if (_components == null)
      _components = new LinkedList<IComponent>()
    _components.add(component)
    component.initialize()
    return component

  // --------------------------------------------------------------------------
  @inline
  override function getComponent(int componentTypeId) returns IComponent
    if (_components != null)
      for component in _components
        if (component.typeId == componentTypeId)
          return component
    return null