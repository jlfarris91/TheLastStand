package Spikes
import Camp
import FX
import ClosureTimers
import AbilityIds

// ============================================================================
public class Spikes extends CampBuilding

  // --------------------------------------------------------------------------
  construct(ICamp camp)
    super(camp)
    listenToUnitDamagedEvent()
    listenToUnitConstructedEvent()

  // --------------------------------------------------------------------------
  protected override function onConstructed()
    super.onConstructed()

    getUnit().addAbility(AbilityIds.invulnerable)

  // --------------------------------------------------------------------------
  protected override function onDamaged()
    super.onDamaged()

    let spikesUnit = getUnit()
    let damagedUnit = GetTriggerUnit()

    spikesUnit.subHP(GetEventDamage())

    let spikesPos = spikesUnit.getPos()
    let damagedPos = damagedUnit.getPos()
    
    let fxPos = (spikesPos + damagedPos) / 2.0

    let fx = FX.createEffect("Abilities\\Weapons\\Rifle\\RifleImpact.mdl", fxPos)
    fx.setAlpha(255)
    fx.setColor(255, 255, 255)
    fx.setScale(0.8)
    fx.setZ(16.0)
    doAfter(2) ->
      DestroyEffect(fx)