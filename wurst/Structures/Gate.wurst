package Gate
import RegisterEvents
import Camp
import Unit_Gate
import UnitMetadata

// ============================================================================
public class Gate extends CampStructure
  private bool _isOpen
  private int _angle

  // --------------------------------------------------------------------------
  construct(ICamp camp)
    super(camp)
    this.updateState()

  // --------------------------------------------------------------------------
  function getIsOpen() returns bool
    return this._isOpen

  // --------------------------------------------------------------------------
  function getIsClosed() returns bool
    return not this._isOpen

  // --------------------------------------------------------------------------
  override function onUnitChanged(unit oldUnit, unit newUnit)
    super.onUnitChanged(oldUnit, newUnit)
    this.updateState()

  // --------------------------------------------------------------------------
  function openGate()
    if (this._isOpen)
      return
    let unitId = this.getUnit().getTypeId()
    let upgradeId = GateUtility.getOpenGateId(_angle)
    if (unitId != upgradeId)
      IssueTrainOrderByIdBJ(this.getUnit(), upgradeId)
    updateState()

  // --------------------------------------------------------------------------
  function closeGate()
    if (not this._isOpen)
      return
    let unitId = this.getUnit().getTypeId()
    let upgradeId = GateUtility.getClosedGateId(_angle)
    if (unitId != upgradeId)
      IssueTrainOrderByIdBJ(this.getUnit(), upgradeId)
    updateState()

  // --------------------------------------------------------------------------
  protected function updateState()
    let u = getUnit()
    _angle = GateUtility.getGateAngle(u.getTypeId())
    if (u.isClosedGate())
      u.setAnimation("stand")
      u.setPathing(true)
      _isOpen = false
    else if (u.isOpenGate())
      u.setAnimation("death alternate")
      u.setPathing(false)
      _isOpen = true

// ============================================================================
function onGateUpgradeFinished()
  let caster = GetTriggerUnit()
  if (not caster.isGate())
    return
  let gate = caster.getMetadata() castTo Gate
  if (gate != null)
    gate.updateState()

// ============================================================================
function onGateUpgradeStart()
  let caster = GetTriggerUnit()
  if (not caster.isGate())
    return
  let gate = caster.getMetadata() castTo Gate
  if (gate != null)
    gate.updateState()

// ============================================================================
function onGateAttacked()
  let caster = GetTriggerUnit()
  if (not caster.isGate())
    return
  let gate = caster.getMetadata() castTo Gate
  if (gate != null and gate.getIsClosed())
    gate.getUnit().setAnimation("hit")
    ResetUnitAnimation(gate.getUnit())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_START, function onGateUpgradeStart)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_UPGRADE_FINISH, function onGateUpgradeFinished)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ATTACKED, function onGateAttacked)
