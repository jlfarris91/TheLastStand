package Chest
import RegisterEvents
import Camp
import TlsUnitIds
import UnitMetadata

// ============================================================================
public class Chest extends CampUnit
  private bool _isOpen

  // --------------------------------------------------------------------------
  construct (ICamp camp)
    super(camp)
    
  // --------------------------------------------------------------------------
  function isOpen() returns bool
    return this._isOpen

  // --------------------------------------------------------------------------
  function isClosed() returns bool
    return not this._isOpen

  // --------------------------------------------------------------------------
  function openChest()
    if (this.isClosed())
      this.getUnit().queueAnimation("death alternate")
      this._isOpen = true

  // --------------------------------------------------------------------------
  function closeChest()
    if (this.isOpen())
      this.getUnit().queueAnimation("birth")
      this._isOpen = false

// ============================================================================
function onChestSelected()
  let selectedUnit = GetTriggerUnit()
  if (selectedUnit.getTypeId() != TlsUnitIds.chest)
    return

  let chest = selectedUnit.getMetadata() castTo Chest
  if (chest != null)
    chest.openChest()

// ============================================================================
function onChestDeselected()
  let deselectedUnit = GetTriggerUnit()
  if (deselectedUnit.getTypeId() != TlsUnitIds.chest)
    return

  let chest = deselectedUnit.getMetadata() castTo Chest
  if (chest != null)
    chest.closeChest()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELECTED, function onChestSelected)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DESELECTED, function onChestDeselected)
  