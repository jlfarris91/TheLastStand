package Behaviors
import LinkedList
import TimerUtils
import ClosureTimers
import Table
import Keys
import ErrorHandling
import UnitMetadata

constant real LOW_PRIORITY_TIMER_INTERVAL = 3.0
constant real HIGH_PRIORITY_TIMER_INTERVAL = 1.0
constant real REALTIME_PRIORITY_TIMER_INTERVAL = 0.4

PeriodicBehaviorTreeRunner g_lowPriorityRunner
PeriodicBehaviorTreeRunner g_highPriorityRunner
PeriodicBehaviorTreeRunner g_realtimePriorityRunner

// ============================================================================
public enum BehaviorState
  RUNNING
  FAILURE
  SUCCESS

// ============================================================================
public enum BehaviorTreeExecutionPriority
  LOW
  HIGH
  REALTIME

// ============================================================================
public class BehaviorCollection extends LinkedList<IBehavior>

// ============================================================================
public interface IBehavior
  function update(IBehaviorTreeContext context) returns BehaviorState
  function clone() returns IBehavior

// ============================================================================
public interface IBehaviorTree
  function setRoot(IBehavior behavior)
  function getRoot() returns IBehavior
  function start(IBehaviorTreeContext context, BehaviorTreeExecutionPriority priority)
  function resume()
  function pause()
  function stop()
  function update()
  function clone() returns IBehaviorTree
  function toString() returns string

// ============================================================================
public interface IBehaviorTreeContext
  function getUnit() returns IUnitMetadata
  function getTable() returns Table

// ============================================================================
public interface IBehaviorTreeRunner
  function register(IBehaviorTree tree)
  function unregister(IBehaviorTree tree)

// ============================================================================
public interface IHasBehaviors
  function getBehavior() returns IBehaviorTree
  function pushBehavior(IBehaviorTree behaviorTree, BehaviorTreeExecutionPriority priority)
  function popBehavior() returns IBehaviorTree
  function cancelAllBehaviors()

// ============================================================================
class PeriodicBehaviorTreeRunner implements IBehaviorTreeRunner
  LinkedList<IBehaviorTree> _trees
  LinkedList<IBehaviorTree> _cleanup
  private timer _updateTimer
  private real _updateRate

  // --------------------------------------------------------------------------
  construct(real updateRate)
    _trees = new LinkedList<IBehaviorTree>()
    _cleanup = new LinkedList<IBehaviorTree>()
    _updateRate = updateRate

    _updateTimer = getTimer()
    _updateTimer.doPeriodically(updateRate) (CallbackPeriodic cb) ->
      this.updateTrees()

  // --------------------------------------------------------------------------
  ondestroy
    destroy _trees
    _trees = null
    destroy _cleanup
    _cleanup = null

  // --------------------------------------------------------------------------
  function getDeltaTime() returns real
    return _updateRate

  // --------------------------------------------------------------------------
  override function register(IBehaviorTree tree)
    _trees.add(tree)

  // --------------------------------------------------------------------------
  override function unregister(IBehaviorTree tree)
    _cleanup.add(tree)

  // --------------------------------------------------------------------------
  private function updateTrees()
    for tree in _trees
      tree.update()
    for tree in _cleanup
      _trees.remove(tree)
      destroy tree
    _cleanup.clear()

// ============================================================================
public class BehaviorTree implements IBehaviorTree
  static constant KEY_DELTA_TIME = key(StringHashBJ("BehaviorTree.DeltaTime"))

  private IBehavior _root
  private IBehaviorTreeRunner _runner
  private IBehaviorTreeContext _context
  private bool _isRunning
  private string _name
  private real _deltaTime

  // --------------------------------------------------------------------------
  construct()
    _name = "BehaviorTree"

  // --------------------------------------------------------------------------
  construct(string name)
    _name = name

  // --------------------------------------------------------------------------
  construct(BehaviorTree other)
    _root = other._root.clone()
    _name = other._name

  // --------------------------------------------------------------------------
  ondestroy
    if (_context != null)
      destroy _context
      _context = null
    if (_root != null)
      destroy _root
      _root = null
    _runner = null
  
  // --------------------------------------------------------------------------  
  override function toString() returns string
    return _name

  // --------------------------------------------------------------------------
  override function clone() returns IBehaviorTree
    return new BehaviorTree(this)

  // --------------------------------------------------------------------------
  override function setRoot(IBehavior behavior)
    _root = behavior

  // --------------------------------------------------------------------------
  override function getRoot() returns IBehavior
    return _root

  // --------------------------------------------------------------------------
  override function start(IBehaviorTreeContext context, BehaviorTreeExecutionPriority priority)
    if (_root == null)
      error("Trying to call start() on a BehaviorTree with no root")
      return

    if (_isRunning or _runner != null)
      error("BehaviorTree is already running!")
      return

    switch (priority)
      case BehaviorTreeExecutionPriority.LOW
        _runner = g_lowPriorityRunner
        _deltaTime = LOW_PRIORITY_TIMER_INTERVAL
      case BehaviorTreeExecutionPriority.HIGH
        _runner = g_highPriorityRunner
        _deltaTime = HIGH_PRIORITY_TIMER_INTERVAL
      case BehaviorTreeExecutionPriority.REALTIME
        _runner = g_realtimePriorityRunner
        _deltaTime = REALTIME_PRIORITY_TIMER_INTERVAL

    _context = context
    _runner.register(this)
    _isRunning = true

  // --------------------------------------------------------------------------
  override function resume()
    if (not _isRunning)
      _isRunning = true
      _runner.register(this)

  // --------------------------------------------------------------------------
  override function pause()
    if (_isRunning)
      _isRunning = false
      _runner.unregister(this)

  // --------------------------------------------------------------------------
  override function stop()
    if (_isRunning)
      _isRunning = false
      _runner.unregister(this)
      _runner = null
      destroy _context
      _context = null
      _deltaTime = 0.0
  
  // --------------------------------------------------------------------------
  override function update()
    if (_root == null)
      error("Trying to call update() on a BehaviorTree with no root")
      return

    if (_isRunning == false)
      error("Must start the BehaviorTree before calling update()")
      return

    _context.getTable().saveReal(KEY_DELTA_TIME.value, _deltaTime)

    let state = _root.update(_context)

    if (not (state == BehaviorState.RUNNING))
      stop()

// ============================================================================
public class BehaviorTreeContext implements IBehaviorTreeContext
  Table _table
  IUnitMetadata _unit

  // --------------------------------------------------------------------------
  construct(IUnitMetadata u)
    _unit = u
    _table = new Table()

  // --------------------------------------------------------------------------
  ondestroy
    destroy _table
    _unit = null

  // --------------------------------------------------------------------------
  override function getUnit() returns IUnitMetadata
    return _unit

  // --------------------------------------------------------------------------
  override function getTable() returns Table
    return _table

// ============================================================================
public abstract class BehaviorBase implements IBehavior
  private bool _isRunning

  // --------------------------------------------------------------------------
  construct()
    skip

  // --------------------------------------------------------------------------
  construct(BehaviorBase _other)
    skip

  // --------------------------------------------------------------------------
  abstract function getName() returns string

  // --------------------------------------------------------------------------
  override function update(IBehaviorTreeContext context) returns BehaviorState
    if (not _isRunning)
      _isRunning = true
      //Log.info(getName() + "::onEnter")
      onEnter(context)

    let state = onUpdate(context)

    if (state == BehaviorState.SUCCESS)
      //Log.info(getName() + "::SUCCESS")
    if (state == BehaviorState.FAILURE)
      //Log.info(getName() + "::FAILURE")

    if (not (state == BehaviorState.RUNNING))
      _isRunning = false
      //Log.info(getName() + "::onExit")
      onExit(context)

    return state

  // --------------------------------------------------------------------------
  override abstract function clone() returns IBehavior

  // --------------------------------------------------------------------------
  abstract protected function onUpdate(IBehaviorTreeContext context) returns BehaviorState

  // --------------------------------------------------------------------------
  protected function onEnter(IBehaviorTreeContext _context)
    skip

  // --------------------------------------------------------------------------
  protected function onExit(IBehaviorTreeContext _context)
    skip

// ============================================================================
init
  g_lowPriorityRunner = new PeriodicBehaviorTreeRunner(LOW_PRIORITY_TIMER_INTERVAL)
  g_highPriorityRunner = new PeriodicBehaviorTreeRunner(HIGH_PRIORITY_TIMER_INTERVAL)
  g_realtimePriorityRunner = new PeriodicBehaviorTreeRunner(REALTIME_PRIORITY_TIMER_INTERVAL)