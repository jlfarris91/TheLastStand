package RealBehaviors
import Behaviors
import Keys
import ErrorHandling
import ComparisonOperator

// ==========================================================================
public class RealComparison extends BehaviorBase
  private key _variableA = INVALID_KEY
  private key _variableB = INVALID_KEY
  private real _value
  private ComparisonOperator _op

  // --------------------------------------------------------------------------
  construct(key variableA, real value, ComparisonOperator op)
    if (variableA == INVALID_KEY)
      error("Argument variableA cannot be INVALID_KEY")
    _variableA = variableA
    _value = value
    _op = op

  // --------------------------------------------------------------------------
  construct(key variableA, key variableB, ComparisonOperator op)
    if (variableA == INVALID_KEY)
      error("Argument variableA cannot be INVALID_KEY")
    if (variableB == INVALID_KEY)
      error("Argument variableB cannot be INVALID_KEY")
    _variableA = variableA
    _variableB = variableB
    _op = op

  // --------------------------------------------------------------------------
  construct(RealComparison other)
    super(other)
    _variableA = other._variableA
    _variableB = other._variableB
    _value = other._value
    _op = other._op

  // --------------------------------------------------------------------------
  override function getName() returns string
    return "RealComparison"

  // --------------------------------------------------------------------------
  override function clone() returns IBehavior
    return new RealComparison(this)
    
  // --------------------------------------------------------------------------
  override function onUpdate(IBehaviorTreeContext context) returns BehaviorState
    let t = context.getTable()

    if (not t.hasReal(_variableA.value))
      return BehaviorState.FAILURE

    var a = t.loadReal(_variableB.value)
    var b = _value
    
    if (_variableB != INVALID_KEY)
      if t.hasReal(_variableB.value)
        b = t.loadReal(_variableB.value)
      else
        return BehaviorState.FAILURE

    return compare(a, b) ? BehaviorState.SUCCESS : BehaviorState.FAILURE

  // --------------------------------------------------------------------------
  private function compare(real a, real b) returns bool
    switch (_op)
      case EQUAL
        return a == b
      case NOT_EQUAL
        return a != b
      case GREATER_THAN
        return a > b
      case GREATER_THAN_OR_EQUAL
        return a >= b
      case LESS_THAN
        return a < b
      case LESS_THAN_OR_EQUAL
        return a <= b

// ==========================================================================
public class IncrementReal extends BehaviorBase
  private key _variable = INVALID_KEY
  private key _variableValue = INVALID_KEY
  private real _value

  // --------------------------------------------------------------------------
  construct(key variable, real value)
    if (variable == INVALID_KEY)
      error("Argument variable cannot be INVALID_KEY")
    _variable = variable
    _value = value

  // --------------------------------------------------------------------------
  construct(key variable, key variableValue)
    if (variable == INVALID_KEY)
      error("Argument variable cannot be INVALID_KEY")
    if (variableValue == INVALID_KEY)
      error("Argument variableValue cannot be INVALID_KEY")
    _variable = variable
    _variableValue = variableValue

  // --------------------------------------------------------------------------
  construct(IncrementReal other)
    super(other)
    _variable = other._variable
    _variableValue = other._variableValue
    _value = other._value

  // --------------------------------------------------------------------------
  override function getName() returns string
    return "IncrementReal"

  // --------------------------------------------------------------------------
  override function clone() returns IBehavior
    return new IncrementReal(this)

  // --------------------------------------------------------------------------
  override function onUpdate(IBehaviorTreeContext context) returns BehaviorState
    let t = context.getTable()

    var a = 0.0

    if (t.hasReal(_variable.value))
      a = t.loadReal(_variableValue.value)

    var b = _value
    
    if (_variableValue != INVALID_KEY)
      if t.hasReal(_variableValue.value)
        b = t.loadReal(_variableValue.value)
      else
        return BehaviorState.FAILURE

    t.saveReal(_variable.value, a + b)

    return BehaviorState.SUCCESS

// ==========================================================================
public class SetReal extends BehaviorBase
  private key _storage = INVALID_KEY
  private key _variableValue = INVALID_KEY
  private real _value

  // --------------------------------------------------------------------------
  construct(key storage, real value)
    if (storage ==  INVALID_KEY)
      error("Argument storage cannot be INVALID_KEY")
    _storage = storage
    _value = value

  // --------------------------------------------------------------------------
  construct(key storage, key value)
    if (storage == INVALID_KEY)
      error("Argument storage cannot be INVALID_KEY")
    if (value == INVALID_KEY)
      error("Argument value cannot be INVALID_KEY")
    _storage = storage
    _variableValue = value

  // --------------------------------------------------------------------------
  construct(SetReal other)
    super(other)
    _storage = other._storage
    _variableValue = other._variableValue
    _value = other._value

  // --------------------------------------------------------------------------
  override function getName() returns string
    return "SetReal"

  // --------------------------------------------------------------------------
  override function clone() returns IBehavior
    return new SetReal(this)
    
  // --------------------------------------------------------------------------
  override function onUpdate(IBehaviorTreeContext context) returns BehaviorState
    let t = context.getTable()

    var value = _value
    
    if (_variableValue != INVALID_KEY)
      if t.hasReal(_variableValue.value)
        value = t.loadReal(_variableValue.value)
      else
        return BehaviorState.FAILURE

    t.saveReal(_storage.value, value)

    return BehaviorState.SUCCESS