package LootItemCard
import Lootables
import ItemSet
import ErrorHandling
import ItemType
import FX
import Notifications
import SoundUtils
import ColorUtility
import UnitMetadata
import LootablesItemLibrary
import Math

SoundDefinition g_lootItemRewardSound

ItemSet array g_itemSets = [
  g_LootableItemLibrary.commonSet,
  g_LootableItemLibrary.rareSet,
  g_LootableItemLibrary.epicSet,
  g_LootableItemLibrary.legendarySet
]

// ============================================================================
public class LootItemCard extends LootCard

  // --------------------------------------------------------------------------
  construct(string id)
    super(id)

  // ----------------------------------------------------------------------------
  override function getTierCount() returns int
    return 4

  // ----------------------------------------------------------------------------
  override function getTierValueMultiplier(int tier) returns real
    return Pow(1 + 1.3 * tier, 2)

  // --------------------------------------------------------------------------
  override function activateCard(int tier) returns bool
    
    let lootable = getLootable()
    let looter = getLooter()

    let ownerPlayer = looter.getOwner()
    let lootableComp = lootable.getMetadata().getLootableComponent()

    let itemSet = g_itemSets[clamp(tier, 0, 3)]
    let itm = itemSet.createRandomItemForUnit(looter)
    if (itm == null or itm.getTypeId() == 0)
      error("No item could be found in loot item library")
      return false

    let itemTypeId = itm.getTypeId()
    
    let itemType = ItemType.getItemType(itemTypeId)
    if (itemType == null)
      error("No ItemType could be found for item with type id " + itemTypeId.toString())
      return false

    FX.createFoundItemTag(looter.getPos(), ownerPlayer)

    let itemRarity = itemType.getRarity()

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      ownerPlayer,
      looter.getPos(),
      g_lootItemRewardSound)
    ..setMessage("Found item " + itm.getName().colorize(itemRarity.getColor()))
    ..setIconPath(itm.getIconPath())

    lootableComp.showLootRewardNotification(ownerPlayer, notification)

    return true

// ============================================================================
init

  g_lootItemRewardSound = new SoundDefinition("sound\\interface\\pickupitem.flac", false, false)
  g_lootItemRewardSound.volume = 126