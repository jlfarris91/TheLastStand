package LootItemCard
import Lootables
import ItemSet
import LootableComponent
import ErrorHandling
import ItemType
import FX
import Notifications
import SoundUtils
import ColorUtility
import UnitMetadata

SoundDefinition g_lootItemRewardSound

// ============================================================================
public class LootItemCard extends LootCard
  private ItemSet m_itemSet

  // --------------------------------------------------------------------------
  construct(string id, ItemSet itemSet)
    super(id)
    m_itemSet = itemSet

  // --------------------------------------------------------------------------
  override function giveReward(unit lootable, unit looter) returns bool

    let ownerPlayer = looter.getOwner()
    let lootableComp = lootable.getMetadata().getLootableComponent()

    let itm = m_itemSet.createRandomItemForUnit(looter)
    if (itm == null or itm.getTypeId() == 0)
      error("No item could be found in loot item library")
      return false

    let itemTypeId = itm.getTypeId()
    
    let itemType = ItemType.getItemType(itemTypeId)
    if (itemType == null)
      error("No ItemType could be found for item with type id " + itemTypeId.toString())
      return false

    FX.createFoundItemTag(looter.getPos(), ownerPlayer)

    let itemRarity = itemType.getRarity()

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      ownerPlayer,
      looter.getPos(),
      g_lootItemRewardSound)
    ..setMessage("Found item " + itm.getName().colorize(itemRarity.getColor()))
    ..setIconPath(itm.getIconPath())

    lootableComp.showLootRewardNotification(ownerPlayer, notification)

    return true

// ============================================================================
init

  g_lootItemRewardSound = new SoundDefinition("sound\\interface\\pickupitem.flac", false, false)
  g_lootItemRewardSound.volume = 126