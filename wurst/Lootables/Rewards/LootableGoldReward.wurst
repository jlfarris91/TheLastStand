package LootableGoldReward
import LootableReward
import HumanPlayerMetadata
import FX
import LootableComponent
import Range
import ColorUtility

// ============================================================================
public class LootableGoldReward implements ILootableReward
  private rangeInt m_range

  // --------------------------------------------------------------------------
  construct (rangeInt range)
    m_range = range

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  function setRange(rangeInt range)
    m_range = range

  // --------------------------------------------------------------------------
  function getRange() returns rangeInt
    return m_range

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter)
    let gold = m_range.getRandom()
    let owner = looter.getOwnerMetadata() castTo HumanPlayerMetadata
    owner.giveGold(gold)
    
    FX.createFoundGoldTag(looter.getPos(), gold, owner.getPlayer())
    FX.createFoundGoldEffect(looter.getPos(), owner.getPlayer())
    
    lootable.displayLootRewardText(
      looter.getOwner(),
      "Found {0} gold!".format(gold.toString().colorize(Colors.gold)))