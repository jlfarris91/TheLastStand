package LootableMaterialsReward
import LootableReward
import HumanPlayerMetadata
import FX
import LootableComponent
import ColorUtility
import Range
import GameInstance

// ============================================================================
public class LootableMaterialsReward implements ILootableReward
  private rangeInt m_materialStart
  private rangeInt m_materialsEnd

  // --------------------------------------------------------------------------
  construct(rangeInt matsStart, rangeInt matsEnd)
    m_materialStart = matsStart
    m_materialsEnd = matsEnd

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter) returns bool
    let mats = lerp(m_materialStart, m_materialsEnd, g_GameInstance.getGameProgress01()).getRandom()
    let owner = looter.getOwnerMetadata() castTo HumanPlayerMetadata
    owner.giveMaterials(mats)
    
    FX.createFoundMaterialsTag(looter.getPos(), mats, owner.getPlayer())
    FX.createFoundMaterialsEffect(looter.getPos(), owner.getPlayer())
    
    lootable.displayLootRewardText(
      looter.getOwner(),
      "Found {0} materials!".format(mats.toString().colorize(Colors.lumber)))

    return true