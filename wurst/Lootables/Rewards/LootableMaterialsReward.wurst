package LootableMaterialsReward
import LootableReward
import FX
import LootableComponent
import ColorUtility
import Range
import GameInstance
import SoundUtils

SoundDefinition array[3] g_lootRewardSound

// ============================================================================
public class LootableMaterialsReward implements ILootableReward
  private rangeInt m_materialStart
  private rangeInt m_materialsEnd

  // --------------------------------------------------------------------------
  construct(rangeInt matsStart, rangeInt matsEnd)
    m_materialStart = matsStart
    m_materialsEnd = matsEnd

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter) returns bool
    let mats = lerp(m_materialStart, m_materialsEnd, g_GameInstance.getGameProgress01()).getRandom()
    let owner = looter.getOwner()
    owner.addLumber(mats)
    
    FX.createFoundMaterialsTag(looter.getPos(), mats, owner)
    FX.createFoundMaterialsEffect(looter.getPos(), owner)
    
    lootable.displayLootRewardText(
      looter.getOwner(),
      "Found {0} materials!".format(mats.toString().colorize(Colors.lumber)),
      g_lootRewardSound[GetRandomInt(0, 2)])

    return true

// ============================================================================
init

  for i = 0 to 2
    g_lootRewardSound[i] = new SoundDefinition("abilities\\spells\\items\\resourceitems\\bundleoflumber.flac", false, false)
    g_lootRewardSound[i].volume = 80
    g_lootRewardSound[i].pitch += (i - 1) * 0.1