package LootableItemReward
import LootableReward
import MainItemLibrary
import FX
import DisplayTextToPlayer
import LootableUnit
import StringExtensions
import ColorUtility

// ============================================================================
public class LootableItemReward implements ILootableReward
  private ItemChances _itemChances

  // --------------------------------------------------------------------------
  function getItemChances() returns ItemChances
    return _itemChances

  // --------------------------------------------------------------------------
  function setItemChances(ItemChances chances)
    _itemChances = chances

  // --------------------------------------------------------------------------
  override function giveReward(LootableUnit lootable, unit looter)
    let itm = g_MainItemLibrary.giveRandomItemToUnit(looter, _itemChances)

    if (itm == null)
      Log.error("No item could be found in main item library")
      return

    let itemSet = g_MainItemLibrary.getOwningSet(itm.getTypeId())
    if (itemSet == null)
      Log.error("Could not find matching item set for item {0}".format(itm.getTypeId().toString()))
      return

    FX.createFoundItemTag(looter.getPos(), looter.getOwner())
    displayMessageToPlayer(looter.getOwner(),
      "Found {0} item {1}!".format(
        itemSet.getName().colorize(itemSet.getColor()),
        itm.getName().colorize(Colors.gold)))