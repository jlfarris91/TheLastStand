package LootableSurvivorReward
import LootableReward
import UnitMetadata
import GameConstants
import TlsUnitIds
import FX
import Orders
import LootableUnit
import StringExtensions
import ColorUtility
import DisplayTextToPlayer
import SurvivorJobs

// ============================================================================
public class LootableSurvivorReward implements ILootableReward

  // --------------------------------------------------------------------------
  override function giveReward(LootableUnit lootable, unit looter)
    spawnSurvivor(lootable, looter)

  // --------------------------------------------------------------------------
  function spawnSurvivor(LootableUnit lootable, unit looter)
    let lootableUnit = lootable.getUnit()
    let survivor = createUnitTLS(
      PLAYER_VILLAGERS,
      TlsUnitIds.Survivors.survivorMale,
      lootableUnit.getX(),
      lootableUnit.getY(),
      GetRandomDirectionDeg())
    survivor.issuePointOrderById(OrderIds.move, looter.getPos())
    FX.createFoundSurvivorTag(lootable.getPos(), lootableUnit.getOwner())
    
    let job = getJobDefinitionForUnitType(survivor.getTypeId())

    displayMessageToPlayer(
      looter.getOwner(),
      "Found {0} {1}!".format(
        job.getTitle().colorize(Colors.gold),
        survivor.getProperName()))