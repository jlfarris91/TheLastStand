package LootableSurvivorReward
import LootableReward
import UnitMetadata
import GameConstants
import TlsUnitIds
import FX
import Orders
import LootableComponent
import StringExtensions
import ColorUtility
import SurvivorJobs

// ============================================================================
public class LootableSurvivorReward implements ILootableReward

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return true

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter)
    spawnSurvivor(lootable, looter)

  // --------------------------------------------------------------------------
  function spawnSurvivor(LootableComponent lootable, unit looter)
    let lootableUnit = lootable.getOwnerUnit()
    let survivor = createUnitTLS(
      PLAYER_VILLAGERS,
      TlsUnitIds.Survivors.survivorMale,
      lootableUnit.getX(),
      lootableUnit.getY(),
      GetRandomDirectionDeg())
    survivor.issuePointOrderById(OrderIds.move, looter.getPos())
    FX.createFoundSurvivorTag(lootableUnit.getPos(), lootableUnit.getOwner())
    
    let job = getJobDefinitionForUnitType(survivor.getTypeId())

    lootable.displayLootRewardText(
      looter.getOwner(),
      "Found {0} {1}!".format(
        job.getTitle().colorize(Colors.gold),
        survivor.getProperName()))