package LootableZombieReward

import LootableReward
import UnitMetadata
import GameConstants
import TlsUnitIds
import ErrorHandling
import MainItemLibrary
import Orders
import LootableUnit
import TlsSounds

// ============================================================================
public class LootableZombieReward implements ILootableReward
  private int _minZombies
  private int _maxZombies
  private ItemChances _itemChances

  // --------------------------------------------------------------------------
  function getMinZombies() returns int
    return _minZombies

  // --------------------------------------------------------------------------
  function setMinZombies(int zombies)
    if (zombies <= 0)
      error("Argument 'zombies' must be greater than 0")
    _minZombies = zombies

  // --------------------------------------------------------------------------
  function getMaxZombies() returns int
    return _maxZombies

  // --------------------------------------------------------------------------
  function setMaxZombies(int zombies)
    if (zombies <= 0)
      error("Argument 'zombies' must be greater than 0")
    _maxZombies = zombies

  // --------------------------------------------------------------------------
  function setItemChances(ItemChances chances)
    _itemChances = chances

  // --------------------------------------------------------------------------
  override function giveReward(LootableUnit lootable, IUnitMetadata looter)
    spawnZombies(lootable, looter)

  // --------------------------------------------------------------------------
  protected function spawnZombies(LootableUnit lootable, IUnitMetadata looter)
    let zombieCount = GetRandomInt(_minZombies, _maxZombies)
    
    unit lastCreatedZombie = null

    for i = 0 to zombieCount - 1
      lastCreatedZombie = createUnitTLS(
        PLAYER_UNDEAD,
        TlsUnitIds.Undead.zombieDay,
        lootable.getX(),
        lootable.getY(),
        GetRandomDirectionDeg())
      lastCreatedZombie.issueTargetOrderById(OrderIds.attack, looter.getUnit())

    // Give a random item to the last created zombie
    g_MainItemLibrary.giveRandomItemToUnit(lastCreatedZombie, _itemChances)

    // Play a sound to alert the player
    PlaySoundOnUnitBJ(TlsSounds.zombieDeath, 30.00, lootable.getUnit())