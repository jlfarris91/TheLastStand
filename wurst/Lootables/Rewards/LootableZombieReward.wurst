package LootableZombieReward

import LootableReward
import UnitMetadata
import GameConstants
import TlsUnitIds
import ErrorHandling
import MainItemLibrary
import Orders
import LootableComponent
import TlsSounds
import ColorUtility
import ClosureTimers
import UnitModifier
import GameInstance

constant real ZOMBIE_SPAWN_INTERVAL = 0.8

// ============================================================================
public class LootableZombieReward implements ILootableReward
  private int _minZombies
  private int _maxZombies
  private ItemChances _itemChances
  private IFunctionalUnitModifier m_zombieModifier

  // --------------------------------------------------------------------------
  ondestroy

    if (m_zombieModifier != null)
      destroy m_zombieModifier
      m_zombieModifier = null

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return true

  // --------------------------------------------------------------------------
  function getUnitMofidier() returns IFunctionalUnitModifier
    return m_zombieModifier

  // --------------------------------------------------------------------------
  function setUnitModifier(IFunctionalUnitModifier value)
    m_zombieModifier = value

  // --------------------------------------------------------------------------
  function getMinZombies() returns int
    return _minZombies

  // --------------------------------------------------------------------------
  function setMinZombies(int zombies)
    if (zombies <= 0)
      error("Argument 'zombies' must be greater than 0")
    _minZombies = zombies

  // --------------------------------------------------------------------------
  function getMaxZombies() returns int
    return _maxZombies

  // --------------------------------------------------------------------------
  function setMaxZombies(int zombies)
    if (zombies <= 0)
      error("Argument 'zombies' must be greater than 0")
    _maxZombies = zombies

  // --------------------------------------------------------------------------
  function setItemChances(ItemChances chances)
    _itemChances = chances

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter)
    spawnZombies(lootable, looter)

  // --------------------------------------------------------------------------
  protected function spawnZombies(LootableComponent lootable, unit looter)
    let lootableUnit = lootable.getOwnerUnit()
    let zombieCount = GetRandomInt(_minZombies, _maxZombies)
    
    // Play a sound to alert the player
    PlaySoundOnUnitBJ(TlsSounds.zombieDeath, 30.00, lootableUnit)

    lootable.displayLootRewardText(
      looter.getOwner(),
      "Found... zombies!".colorize(Colors.red))

    let gameProgressT = g_GameInstance.getPlayerGameProgress01(looter.getOwner())

    doPeriodicallyCounted(ZOMBIE_SPAWN_INTERVAL, zombieCount) (cb) ->
    //{
      let lastCreatedZombie = createUnitTLS(
        PLAYER_UNDEAD,
        TlsUnitIds.Undead.zombieDay,
        lootableUnit.getX(),
        lootableUnit.getY(),
        GetRandomDirectionDeg())

      if (m_zombieModifier != null)
        m_zombieModifier.apply(lastCreatedZombie, gameProgressT)

      lastCreatedZombie.issueTargetOrderById(OrderIds.attack, looter)

      // Give a random item to the last created zombie
      if (cb.isLast())
        g_MainItemLibrary.giveRandomItemToUnit(lastCreatedZombie, _itemChances)
    //}