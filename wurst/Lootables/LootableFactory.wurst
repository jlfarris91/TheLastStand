package LootableFactory
import LootableTypes
import UnitMetadata
import initlater Lootables
import MainItemLibrary
import LootableGoldReward
import Range
import LootableComponent
import HashList

constant ItemChances g_defaultItemChances = ItemChances(83.0, 12.0, 4.0, 1.0)

HashList<int> g_smallLootables = new HashList<int>()
HashList<int> g_mediumBuildingLootables = new HashList<int>()
HashList<int> g_largeBuildingLootables = new HashList<int>()

// ============================================================================
public class LootableFactory implements IUnitMetadataFactory

  // --------------------------------------------------------------------------
  override function createUnit(unit u) returns IUnitMetadata
    let id = u.getTypeId()

    if (g_smallLootables.has(id))
      return createSmallLootable()

    if (g_mediumBuildingLootables.has(id))
      return createMediumBuildingLootable()

    if (g_largeBuildingLootables.has(id))
      return createLargeBuildingLootable()

    return null    

  // --------------------------------------------------------------------------
  private function createSmallLootable() returns UnitMetadata
    let metadata = new UnitMetadata()
    let lootable = metadata.getOrAddLootableComponent()
    ..setSearchDuration(5.0)

    let goldReward = new LootableGoldReward(rangeInt(1,1))

    let materialsReward = new LootableMaterialsReward()
    ..setMinMaterials(30)
    ..setMaxMaterials(60)

    let itemReward = new LootableItemReward()
    ..setItemChances(g_defaultItemChances)
    
    lootable.getRewardsSet()
    ..add(goldReward, 10.0)
    ..add(materialsReward, 50.0)
    ..add(itemReward, 20.0)

    return metadata

  // --------------------------------------------------------------------------
  private function createMediumBuildingLootable() returns UnitMetadata
    let metadata = new UnitMetadata()
  
    let lootable = metadata.getOrAddLootableComponent()
    ..setSearchDuration(20.0)

    let goldReward = new LootableGoldReward(rangeInt(2,4))

    let materialsReward = new LootableMaterialsReward()
    ..setMinMaterials(60)
    ..setMaxMaterials(120)

    let itemReward = new LootableItemReward()
    ..setItemChances(g_defaultItemChances)

    let survivorReward = new LootableSurvivorReward()

    let zombieReward = new LootableZombieReward()
    ..setMinZombies(2)
    ..setMaxZombies(4)
    
    lootable.getRewardsSet()
    ..add(goldReward, 10.0)
    ..add(materialsReward, 50.0)
    ..add(itemReward, 25.0)
    ..add(survivorReward, 5.0)
    ..add(zombieReward, 10.0)

    return metadata

  // --------------------------------------------------------------------------
  private function createLargeBuildingLootable() returns UnitMetadata
    let metadata = new UnitMetadata()
    
    let lootable = metadata.getOrAddLootableComponent()
    ..setSearchDuration(30.0)

    let goldReward = new LootableGoldReward(rangeInt(3,5))

    let materialsReward = new LootableMaterialsReward()
    ..setMinMaterials(80)
    ..setMaxMaterials(160)

    let itemReward = new LootableItemReward()
    ..setItemChances(g_defaultItemChances)

    let survivorReward = new LootableSurvivorReward()

    let zombieReward = new LootableZombieReward()
    ..setMinZombies(5)
    ..setMaxZombies(10)
    
    lootable.getRewardsSet()
    ..add(goldReward, 10)
    ..add(materialsReward, 50.0)
    ..add(itemReward, 25.0)
    ..add(survivorReward, 5.0)
    ..add(zombieReward, 10.0)

    return metadata

// ============================================================================
init
  g_smallLootables.add(
    UNIT_ID_LOOTABLE_BARREL,
    UNIT_ID_LOOTABLE_CART,
    UNIT_ID_LOOTABLE_CRATES,
    UNIT_ID_LOOTABLE_HAY,
    UNIT_ID_LOOTABLE_JUNK_1,
    UNIT_ID_LOOTABLE_JUNK_2,
    UNIT_ID_LOOTABLE_MARKET_BAUBLES,
    UNIT_ID_LOOTABLE_MARKET_MINECART,
    UNIT_ID_LOOTABLE_MARKET_STALL,
    UNIT_ID_LOOTABLE_MARKET_TABLE,
    UNIT_ID_LOOTABLE_TENT
  )

  g_mediumBuildingLootables.add(
    UNIT_ID_LOOTABLE_BARN,
    UNIT_ID_LOOTABLE_BUILDING_HORIZONTAL_BLUE,
    UNIT_ID_LOOTABLE_BUILDING_HORIZONTAL_GREEN,
    UNIT_ID_LOOTABLE_BUILDING_HORIZONTAL_RED,
    UNIT_ID_LOOTABLE_BUILDING_GRANARY,
    UNIT_ID_LOOTABLE_HOUSE_LARGE_BLUE,
    UNIT_ID_LOOTABLE_HOUSE_LARGE_RED,
    UNIT_ID_LOOTABLE_HOUSE_SMALL_GREEN,
    UNIT_ID_LOOTABLE_MARKET,
    UNIT_ID_LOOTABLE_TOWER,
    UNIT_ID_LOOTABLE_WINDMILL
  )

  g_largeBuildingLootables.add(
    UNIT_ID_LOOTABLE_BREWERY,
    UNIT_ID_LOOTABLE_BUILDING_CHAPEL,
    UNIT_ID_LOOTABLE_BUILDING_LARGE_HORIZONTAL_GREEN,
    UNIT_ID_LOOTABLE_BUILDING_LARGE_VERTICAL_PURPLE,
    UNIT_ID_LOOTABLE_CATHEDRAL,
    UNIT_ID_LOOTABLE_INN,
    UNIT_ID_LOOTABLE_MINE,
    UNIT_ID_LOOTABLE_SHIPYARD,
    UNIT_ID_LOOTABLE_TAVERN
  )