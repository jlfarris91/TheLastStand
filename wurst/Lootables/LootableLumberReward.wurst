package LootableLumberReward
import LootableReward
import FX
import LootableComponent
import ColorUtility
import Range
import GameInstance
import SoundUtils
import Notifications
import Icons
import TlsSounds

// ============================================================================
public class LootableLumberReward implements ILootableReward
  private rangeInt m_lumberStart
  private rangeInt m_lumbersEnd

  // --------------------------------------------------------------------------
  construct(rangeInt matsStart, rangeInt matsEnd)
    m_lumberStart = matsStart
    m_lumbersEnd = matsEnd

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter) returns bool
    let mats = lerp(m_lumberStart, m_lumbersEnd, g_GameInstance.getGameProgress()).getRandom()
    let owner = looter.getOwner()
    owner.addLumber(mats)
    
    FX.createGainedLumberTag(looter.getPos(), mats, owner)
    FX.createGainedLumberEffect(looter.getPos(), owner)

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      owner,
      looter.getPos(),
      TlsSounds.getRandomFoundLumber())
    ..setMessage("Found " + ("+" + mats.toString() + " lumber").colorize(Colors.lumber))
    ..setIconPath(Icons.bTNBundleOfLumber)

    lootable.showLootRewardNotification(owner, notification)

    return true