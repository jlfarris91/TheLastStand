package LootableGoldReward
import LootableReward
import FX
import LootableComponent
import Range
import ColorUtility
import GameInstance
import SoundUtils
import Notifications
import Icons
import TlsSounds

// ============================================================================
public class LootableGoldReward implements ILootableReward
  private rangeInt m_rangeStart
  private rangeInt m_rangeEnd

  // --------------------------------------------------------------------------
  construct (rangeInt rangeStart, rangeInt rangeEnd)
    m_rangeStart = rangeStart
    m_rangeEnd = rangeEnd

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter) returns bool
    let gold = lerp(m_rangeStart, m_rangeEnd, g_GameInstance.getGameProgress01()).getRandom()
    let owner = looter.getOwner()
    owner.addGold(gold)
    
    FX.createGainedGoldTag(looter.getPos(), gold, owner)
    FX.createGainedGoldEffect(looter.getPos(), owner)

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      owner,
      looter.getPos(),
      TlsSounds.getRandomFoundGold())
    ..setMessage("Found " + ("+" + gold.toString() + " gold").colorize(Colors.gold))
    ..setIconPath(Icons.bTNChestOfGold)

    lootable.showLootRewardNotification(owner, notification)

    return true