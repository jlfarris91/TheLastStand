package LootGoldCard
import Lootables
import LootableComponent
import GameInstance
import FX
import Notifications
import TlsSounds
import Icons
import ColorUtility
import UnitMetadata

constant real array TIER_VALUE_MULTIPLIER = [1.0, 2.0, 3.0]

// ============================================================================
public class LootGoldCard extends LootCard

  // --------------------------------------------------------------------------
  construct(string id)
    super(id)

  // ----------------------------------------------------------------------------
  override function getTierCount() returns int
    return 3

  // ----------------------------------------------------------------------------
  override function getTierValueMultiplier(int tier) returns real
    return (tier + 1).toReal()

  // --------------------------------------------------------------------------
  override function activateCard(int tier) returns bool
    
    let lootable = getLootable()
    let looter = getLooter()
    
    let owner = looter.getOwner()
    let lootableComp = lootable.getMetadata().getLootableComponent()

    let diff = g_GameInstance.getPlayerDifficulty(owner)
    let diffCoeff = g_GameInstance.getPlayerDifficultyCoeff(diff)
    let baseAmount = tier
    let gold = baseAmount + (Pow(diffCoeff, 0.65) + max(diff - 2, 0)).floor()

    owner.addGold(gold)
    
    FX.createGainedGoldTag(looter.getPos(), gold, owner)
    FX.createGainedGoldEffect(looter.getPos(), owner)

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      owner,
      looter.getPos(),
      TlsSounds.getRandomFoundGold())
    ..setMessage("Found " + ("+" + gold.toString() + " gold").gold())
    ..setIconPath(Icons.bTNChestOfGold)

    lootableComp.showLootRewardNotification(owner, notification)

    return true