package LootableMaterialsReward
import LootableReward
import FX
import LootableComponent
import ColorUtility
import Range
import GameInstance
import SoundUtils
import Notifications
import Icons

SoundDefinition array[3] g_lootRewardSound

// ============================================================================
public class LootableMaterialsReward implements ILootableReward
  private rangeInt m_materialStart
  private rangeInt m_materialsEnd

  // --------------------------------------------------------------------------
  construct(rangeInt matsStart, rangeInt matsEnd)
    m_materialStart = matsStart
    m_materialsEnd = matsEnd

  // --------------------------------------------------------------------------
  override function getStopsRetrigger() returns bool
    return false

  // --------------------------------------------------------------------------
  override function giveReward(LootableComponent lootable, unit looter) returns bool
    let mats = lerp(m_materialStart, m_materialsEnd, g_GameInstance.getGameProgress01()).getRandom()
    let owner = looter.getOwner()
    owner.addLumber(mats)
    
    FX.createFoundMaterialsTag(looter.getPos(), mats, owner)
    FX.createFoundMaterialsEffect(looter.getPos(), owner)

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      owner,
      looter.getPos(),
      g_lootRewardSound[GetRandomInt(0, 2)])
    ..setMessage("Found " + ("+" + mats.toString() + " lumber").colorize(Colors.lumber))
    ..setIconPath(Icons.bTNBundleOfLumber)

    lootable.showLootRewardNotification(owner, notification)

    return true

// ============================================================================
init

  for i = 0 to 2
    g_lootRewardSound[i] = new SoundDefinition("abilities\\spells\\items\\resourceitems\\bundleoflumber.flac", false, false)
    g_lootRewardSound[i].volume = 80
    g_lootRewardSound[i].pitch += (i - 1) * 0.1