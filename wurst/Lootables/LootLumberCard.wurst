package LootLumberCard
import Lootables
import FX
import Notifications
import TlsSounds
import Icons
import ColorUtility
import UnitMetadata

// ============================================================================
public class LootLumberCard extends LootCard
  private int m_baseAmount

  // --------------------------------------------------------------------------
  construct(string id, int baseAmount)
    super(id)
    m_baseAmount = baseAmount

  // ----------------------------------------------------------------------------
  function getBaseAmount() returns int
    return m_baseAmount
  
  // ----------------------------------------------------------------------------
  function setBaseAmount(int value)
    m_baseAmount = value

  // ----------------------------------------------------------------------------
  override function getTierCount() returns int
    return 10

  // ----------------------------------------------------------------------------
  override function getTierValueMultiplier(int tier) returns real
    return Pow(4, tier.toReal())

  // --------------------------------------------------------------------------
  override function activateCard(int tier) returns bool
    
    let lootable = getLootable()
    let looter = getLooter()
    
    let owner = looter.getOwner()
    let lootableComp = lootable.getMetadata().getLootableComponent()

    let lumber = (m_baseAmount * Pow(1.5, tier.toReal())).floor()

    owner.addLumber(lumber)
    
    FX.createGainedLumberTag(looter.getPos(), lumber, owner)
    FX.createGainedLumberEffect(looter.getPos(), owner)

    let notification = new LootRewardNotification(
      NotificationManager.nextNotificationId(),
      owner,
      looter.getPos(),
      TlsSounds.getRandomFoundLumber())
    ..setMessage("Found " + ("+" + lumber.toString() + " lumber").colorize(Colors.lumber))
    ..setIconPath(Icons.bTNBundleOfLumber)

    lootableComp.showLootRewardNotification(owner, notification)

    return true