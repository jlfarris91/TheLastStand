package SurvivorMilitiaSkill
import Skills
import UnitMetadata
import Events
import SkillsComponent
import Type
import Icons
import SurvivorJobSkill
import SurvivorStats
import Statistic
import PropertyEvents
import StatsComponent

constant real EXP_PER_KILL = 10.0

// ============================================================================
public class SurvivorMilitiaSkillDefinition extends SkillDefinition

  // --------------------------------------------------------------------------
  override function createInstance(IUnitMetadata u) returns ISkill
    return new SurvivorMilitiaSkill(this, u)

// ============================================================================
public class SurvivorMilitiaSkill extends SurvivorJobSkill
  private StatsComponent m_statsComponent
  private Action3<string, int, int> m_onStatChangingHandler

  // --------------------------------------------------------------------------
  construct(SurvivorMilitiaSkillDefinition skill, IUnitMetadata u)
    super(skill, u)

  // --------------------------------------------------------------------------
  override function onLevelUp()
    super.onLevelUp()

  // --------------------------------------------------------------------------
  override function onReset()
    super.onReset()

  // --------------------------------------------------------------------------
  protected override function onEnabled()
    super.onEnabled()
    m_statsComponent = getUnit().getStatsComponent()
    listenToKillsStatChanged()

  // --------------------------------------------------------------------------
  protected override function onDisabled()
    super.onDisabled()
    unlistenToUnitKilledEvent()
    m_statsComponent = null

  // --------------------------------------------------------------------------
  private function listenToKillsStatChanged()
    unlistenToUnitKilledEvent()

    let stat = getKillsStatistic()
    if (stat != null)
      m_onStatChangingHandler = stat.getValueChangingEvent().add() (string propertyName, integer oldValue, integer newValue) ->
        if (propertyName == "Value")
          let numberOfKills = newValue - oldValue
          incrementExperience(numberOfKills * EXP_PER_KILL)

  // --------------------------------------------------------------------------
  private function unlistenToUnitKilledEvent()
    if (m_onStatChangingHandler != null)
      let stat = getKillsStatistic()
      if (stat != null)
        stat.getValueChangingEvent().remove(m_onStatChangingHandler)
      destroy m_onStatChangingHandler
      m_onStatChangingHandler = null

  // --------------------------------------------------------------------------
  private function getKillsStatistic() returns IntStatistic
    return m_statsComponent.getStatistic(SurvivorStats.kills) castTo IntStatistic

// ============================================================================
public function SkillsComponent.getOrAddSurvivorMilitiaSkill() returns SurvivorMilitiaSkill
  var skill = this.getSkill(Type(SurvivorMilitiaSkill.typeId)) castTo SurvivorMilitiaSkill
  if (skill == null)
    skill = this.addSkill(Skills.militia.createInstance(this.getOwner() castTo IUnitMetadata)) castTo SurvivorMilitiaSkill
  return skill
  
// ============================================================================
init
  let skillDefinition = new SurvivorMilitiaSkillDefinition()
  ..setName("Militia")
  ..setIconPath(Icons.bTNSteelMelee)
  ..setUnits("exp")
  ..setMaxLevel(4)
  ..setLevelRequirement(1, 0)
  ..setLevelRequirement(2, 10.0)
  ..setLevelRequirement(3, 100.0)
  ..setLevelRequirement(4, 1000.0)

  Skills.militia = skillDefinition