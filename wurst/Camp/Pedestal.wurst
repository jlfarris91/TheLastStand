package Pedestal
import TlsUnitDefinition
import AbilityIds
import GameInstance
import UnitIndexer
import GroupUtils
import RegisterEvents
import TlsHeroDefinition
import Icons

// ============================================================================
@compiletime function createDefinitions()
  new TlsHeroDefinition(TlsUnitIds.pedestal, UnitIds.wisp)
    ..setName("Archives Pedestal")
    ..setProperNames("Camp")
    ..setNormalAbilities(commaList(AbilityIds.invulnerable, AbilityIds.ghostVisible))
    ..setCollisionSize(0)
    ..setStructuresBuilt("")
    ..setUpgradesUsed("")
    ..setUnitClassification("")
    ..setFoodCost(0)
    ..setMovementType(MovementType.None)
    ..setSpeedBase(0)
    ..setIsaBuilding(true)
    ..setModelFile("buildings\\other\\BookOfSummoning\\BookOfSummoning.mdl")
    ..setIconGameInterface(Icons.bTNBookOfSummoning)
    ..setManaMaximum(10)
    ..setManaInitialAmount(10)
    ..setManaRegeneration(0)

// ============================================================================
function onConstructFinished()
  let structure = GetTriggerUnit()
  if (not structure.isHeadquarters() or not g_GameInstance.getSpawnPedestals())
    return

  let pos = structure.getPos() + vec3(75, -75, 0)

  let owningPlayer = structure.getOwner()
  createUnit(owningPlayer, TlsUnitIds.pedestal, pos, 270.0.asAngleDegrees())

// ============================================================================
function onUnitDeindexed()
  let deindexingUnit = getIndexingUnit()
  if (not deindexingUnit.isHeadquarters())
    return

  let owningPlayer = deindexingUnit.getOwner()
  let temp = getGroup()
  temp.enumUnitsOfType(TlsUnitIds.pedestal, null)
  for u in temp
    if (u.getOwner() == owningPlayer)
      u.remove()
  temp.release()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_CONSTRUCT_FINISH, function onConstructFinished)
  onUnitDeindex(function onUnitDeindexed)