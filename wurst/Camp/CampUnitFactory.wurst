package CampUnitFactory
import TlsUnitIds
import UnitMetadata
import SkillsComponent
import Skills
import HumanPlayerMetadata
import Unit_Gate2

import initlater CampBase
import initlater Gate
import initlater Wall
import initlater AidStation
import initlater GunLab
import initlater Chest
import initlater Spikes
import initlater Workshop
import initlater Camp
import initlater Hero

// ============================================================================
public class CampUnitFactory implements IUnitMetadataFactory

  // --------------------------------------------------------------------------
  override function createUnit(unit u) returns IUnitMetadata
    let playerMetadata = u.getOwnerHumanMetadata()
    if (playerMetadata == null)
      return null

    let camp = playerMetadata.getCamp()
    if (camp == null)
      return null

    let unitId = u.getTypeId()
    ICampUnitMetadata metadata = null

    switch (unitId)
      case TlsUnitIds.camp | TlsUnitIds.campHouse | TlsUnitIds.campHall
        metadata = new CampBase(camp, u)
      case TlsUnitIds.aidStation
        metadata = new AidStation(camp, u)
      case TlsUnitIds.chest
        metadata = new Chest(camp, u)
      case TlsUnitIds.armory
        metadata = new GunLab(camp, u)
      case TlsUnitIds.spikes
        metadata = new Spikes(camp, u)
      case TlsUnitIds.workshop
        metadata = new Workshop(camp, u)
      case TlsUnitIds.survivorMale
        metadata = new Survivor(camp, u)
      case TlsUnitIds.survivorBuilder
        metadata = new Survivor(camp, u)
      case TlsUnitIds.survivorMarksman
        metadata = new Survivor(camp, u)
      case TlsUnitIds.survivorMedic
        metadata = new Survivor(camp, u)

    if (u.isGate())
      metadata = new Gate(camp, u)

    if (unitId == TlsUnitIds.wallCorner or
        unitId == TlsUnitIds.wallHorizontal)
      metadata = new Wall(camp, u)

    if (unitId == TlsUnitIds.hero)
      metadata = createHero(camp, u)

    if (metadata != null)
      camp.addUnit(metadata)

    return metadata

  // --------------------------------------------------------------------------
  private function createHero(ICamp camp, unit u) returns ICampUnitMetadata
    let hero = new Hero(camp, u)

    let skillsComponent = new SkillsComponent(hero)
    
    skillsComponent.addSkill(Skills.sprint.createInstance(hero))

    hero.addComponent(skillsComponent)

    u.getOwnerHumanMetadata().setHero(hero)

    return hero