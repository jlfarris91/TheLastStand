package CampUnitFactory
import TlsUnitIds
import UnitMetadata
import HumanPlayerMetadata
import Unit_Gate

import initlater CampBase
import initlater Gate
import initlater AidStation
import initlater Armory
import initlater Chest
import initlater Workshop
import initlater Camp
import initlater Hero
import initlater SurvivorUnit
import Unit_WoodFence

import SpikesComponent
import TentComponent
import RangedTargetComponent

// ============================================================================
public class CampUnitFactory implements IUnitMetadataFactory

  // --------------------------------------------------------------------------
  override function createUnit(unit u) returns IUnitMetadata

    let playerMetadata = u.getOwnerHumanMetadata()
    if (playerMetadata == null)
      return null

    let camp = playerMetadata.getCamp()
    if (camp == null)
      return null

    return createMetadataForUnit(u, camp)

  // --------------------------------------------------------------------------
  private function createMetadataForUnit(unit u, ICamp camp) returns CampUnit
    let unitTypeId = u.getTypeId()

    switch (unitTypeId)

      case TlsUnitIds.campTent | TlsUnitIds.campHouse | TlsUnitIds.campHall
        return new CampBase(camp)

      case TlsUnitIds.aidStation
        return new AidStation(camp)

      case TlsUnitIds.workshop
        return new Workshop(camp)

      case TlsUnitIds.armory
        return new Armory(camp)

      case TlsUnitIds.chest
        return new Chest(camp)

      case TlsUnitIds.spikes[0] | TlsUnitIds.spikes[1] | TlsUnitIds.spikes[2] | TlsUnitIds.spikes[3] | TlsUnitIds.spikes[4]
        let spikes = new CampUnit(camp)
        ..getOrAddSpikesComponent()
        return spikes

      case TlsUnitIds.tent
        let tent = new CampUnit(camp)
        ..getOrAddTentComponent()
        return tent

      case TlsUnitIds.rangedTarget
        let range = new CampUnit(camp)
        ..getOrAddRangedTargetComponent()
        return range

    if (u.isSurvivor())
      return new Survivor(camp)

    if (u.isGate())
      return new Gate(camp)

    if (unitTypeId == TlsUnitIds.hero)
      return new Hero(camp)

    if (unitTypeId == TlsUnitIds.barricade or
        unitTypeId == TlsUnitIds.campFire or
        unitTypeId == TlsUnitIds.scoutTower or
        unitTypeId == TlsUnitIds.turretPortable or
        unitTypeId == TlsUnitIds.turretPowered or
        unitTypeId == TlsUnitIds.turretUnpowered or
        unitTypeId == TlsUnitIds.lampPowered or
        unitTypeId == TlsUnitIds.lampUnpowered or
        unitTypeId == TlsUnitIds.generator or
        u.isWoodFence())
      return new CampUnit(camp)

    return null