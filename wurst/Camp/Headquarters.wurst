package Headquarters
import GroupUtils
import TlsUnitIds
import UnitIndexer
import DisplayTextToPlayer
import TlsItemIds
import MainItemLibrary
import ColorUtility
import RegisterEvents
import Preload

// ============================================================================
function onUnitIndexedDeindexed()

  if (Preload.isPreloading())
    return

  let _unit = getIndexingUnit()
  if (_unit.isCampHeadquarters())
    destroyHeadquartersIfPlayerAlreadyHasOne(_unit)

// ============================================================================
function isEnumUnitHeadquarters() returns bool
  return GetEnumUnit().isCampHeadquarters()

// ============================================================================
function countNumberOfHeadquarters(player _player) returns int
  var count = 0
  let temp = getGroup()
  temp.enumUnitsOfPlayer(_player, null)
  for _unit in temp
    if (_unit.isCampHeadquarters() and _unit.isAlive())
      count++
  temp.release()
  return count

// ============================================================================
function destroyHeadquartersIfPlayerAlreadyHasOne(unit headquartersUnit) returns bool
  if (not headquartersUnit.isCampHeadquarters())
    return false

  // This is the one and only alive headquarters unit
  if (countNumberOfHeadquarters(headquartersUnit.getOwner()) <= 1)
    return false

  // This unit shouldn't have been created
  headquartersUnit.remove()
  
  // Warn the player and create an item on the ground
  displayMessageToPlayer(headquartersUnit.getOwner(), "You cannot build a second Headquarters Tent".colorize(Colors.red))

  // Convert the unit to it's item form
  spawnHeadquartersTentItem(headquartersUnit.getTypeId(), headquartersUnit.getPos())

  return true

// ============================================================================
function spawnHeadquartersTentItem(int unitTypeId, vec2 pos)
  var buildItemId = 0
  switch (unitTypeId)
    case TlsUnitIds.headquarters1
      buildItemId = TlsItemIds.headquarters1
    case TlsUnitIds.headquarters2
      buildItemId = TlsItemIds.headquarters2
    case TlsUnitIds.headquarters3
      buildItemId = TlsItemIds.headquarters3
  g_MainItemLibrary.createItem(buildItemId, pos)

// ============================================================================
function onConstructCanceled()
  let structure = GetTriggerUnit()
  if (structure.isCampHeadquarters())
    spawnHeadquartersTentItem(structure.getTypeId(), structure.getPos())

// ============================================================================
init
  onUnitIndex(function onUnitIndexedDeindexed)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_CONSTRUCT_CANCEL, function onConstructCanceled)