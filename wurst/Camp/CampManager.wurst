package CampManager
import HashMap
import ICamp
import UnitExtensions
import Camp
import ICampUnitFactory
import PlayerExtensions
import RegisterEvents
import HumanPlayers
import EventHelper
import CampUnitFactory
import TlsUnitIds

ICampUnitFactory g_CampUnitFactory
HashMap<player, ICamp> g_PlayerCamps
trigger g_UnitEnteredMapTrigger

public function player.getCamp() returns ICamp
  return getPlayerCamp(this)

public function unit.getCamp() returns ICamp
  return this.getOwner().getCamp()

public function unit.hasCamp() returns bool
  return this.getCamp() != null

function getPlayerCamp(player p) returns ICamp
  if (not g_PlayerCamps.has(p))
    return null
  return g_PlayerCamps.get(p)

function createCamp(player p)
  let camp = new Camp(p, g_CampUnitFactory)
  g_PlayerCamps.put(p, camp)

function isCampStructure(unit u) returns bool
  let unitTypeId = u.getTypeId()
  if (not u.isStructure())
    return false
  if (unitTypeId == TlsUnitIds.campFlag)
    return false
  return true

function isCampSurvivor(unit u) returns bool
  let unitTypeId = u.getTypeId()
  if (u.isStructure())
    return false
  if (unitTypeId == TlsUnitIds.camp or
      unitTypeId == TlsUnitIds.hero or
      unitTypeId == TlsUnitIds.admin or
      unitTypeId == TlsUnitIds.skills)
    return false
  return true

function onPlayerUnitDeath()
  let dyingUnit = GetDyingUnit()
  let owner = dyingUnit.getOwner()
  let camp = owner.getCamp()
  if (camp == null)
    return
  let dyingCampUnit = camp.getUnit(dyingUnit)
  camp.removeUnit(dyingCampUnit)
  destroy dyingCampUnit

function onPlayerUnitConstructFinished()
  let structure = GetConstructedStructure()
  if (not isCampStructure(structure))
    return
  let owner = structure.getOwner()
  let camp = owner.getCamp()
  if (camp != null and structure.getTypeId() != TlsUnitIds.campFlag)
    camp.addUnit(structure)

function onUnitEnteredMap()
  let u = GetEnteringUnit()
  if (not isCampSurvivor(u))
    return
  let owner = u.getOwner()
  if (not owner.isHumanPlayer())
    return
  let camp = owner.getCamp()
  if (camp != null)
    camp.addUnit(u)

function initializePlayerCamp(player p)
  createCamp(p)
  registerPlayerUnitEventForPlayer(EVENT_PLAYER_UNIT_CONSTRUCT_FINISH, function onPlayerUnitConstructFinished, p)
  registerPlayerUnitEventForPlayer(EVENT_PLAYER_UNIT_DEATH, function onPlayerUnitDeath, p)

function initializePlayerCamps()
  for p in g_PlayingHumanPlayers
    initializePlayerCamp(p)

init
  g_PlayerCamps = new HashMap<player, ICamp>()
  g_CampUnitFactory = new CampUnitFactory()
  initializePlayerCamps()

  g_UnitEnteredMapTrigger = CreateTrigger()
  g_UnitEnteredMapTrigger.registerRectEnterEventSource(GetEntireMapRect())
  g_UnitEnteredMapTrigger.addAction(function onUnitEnteredMap)