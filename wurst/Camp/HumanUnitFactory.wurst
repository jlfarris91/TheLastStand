package HumanUnitFactory
import TlsUnitIds
import UnitMetadata
import Unit_GateCommon

import initlater GateComponent
import initlater ChestComponent
import HeroComponent
import Unit_WoodFence

import SpikesComponent
import RangedTargetComponent
import CampCenterComponent
import XpReceiverComponent
import CampFireComponent
import PersistActiveAbilityComponent
import AltarComponent
import SurvivorComponent
import BuildingUndeadTarget
import BuildingUndeadTargetComponent

// ============================================================================
public class HumanUnitFactory implements IUnitMetadataFactory

  // --------------------------------------------------------------------------
  override function createUnit(IUnitMetadata metadata) returns bool
    let u = metadata.getUnit()
    let unitTypeId = u.getTypeId()

    if (u.isBuildingUndeadTarget())
      metadata.addComponent(new BuildingUndeadTargetComponent(metadata))

    if (u.isCampHeadquarters())
      metadata.addComponent(new CampCenterComponent(metadata))
      metadata.getBuildingUndeadTargetComponent().setAffectUTCTargetOrganic(true)
      return true

    switch (unitTypeId)
    //{
      case TlsUnitIds.chest
        metadata.addComponent(new ChestComponent(metadata))
        return true

      case TlsUnitIds.rangedTarget
        metadata.addComponent(new RangedTargetComponent(metadata))
        return true

      case TlsUnitIds.altar
        metadata.addComponent(new AltarComponent(metadata))
        return true
    //}

    if (u.isSpikes())
      metadata.addComponent(new SpikesComponent(metadata))
      metadata.addComponent(new XpReceiverComponent(metadata)..setGiveXpAreaPercent01(1.0))
      return true

    if (u.isSurvivor())
      metadata.addComponent(new SurvivorComponent(metadata))
      return true

    if (u.isWoodFence() or u.isGate())
      metadata.addComponent(new GateComponent(metadata))
      return true

    if (u.isCampFire())
      metadata.addComponent(new CampFireComponent(metadata), false)
      metadata.addComponent(new PersistActiveAbilityComponent(metadata))
      return true

    if (unitTypeId == TlsUnitIds.hero)
      metadata.addComponent(new HeroComponent(metadata))
      return true

    return false