package HostCommands
import Commands
import GameConstants
import DisplayTextToPlayer
import Host
import GroupUtils
import Ids
import ErrorHandling

constant string COMMAND_DAY = "day"
constant string COMMAND_NIGHT = "night"
constant string COMMAND_SEE = "see"
constant string COMMAND_KILL = "kill"
constant string COMMAND_MOVE = "move"
constant string COMMAND_CREATE_ITEM = "item"

function startNextDay()
  SetTimeOfDay(TIME_OF_DAY)

function startNextNight()
  SetTimeOfDay(TIME_OF_NIGHT)

function turnOnMapVisibility()
  if (IsFogEnabled())
    displayTextToPlayer(g_HostPlayer, "Disabled fog of war")
    FogEnableOff()
    FogMaskEnableOff()
  else
    displayTextToPlayer(g_HostPlayer, "Enabled fog of war")
    FogEnableOn()
    FogMaskEnableOn()

function killSelectedUnits()
  var ug = getGroup()
  ug.enumUnitsSelected(g_HostPlayer, null)
  for u from ug
    u.kill()
  ug.release()

function moveSelectedUnits()
  let cameraLoc = GetCameraTargetPositionLoc()
  let cameraPos = vec2(GetLocationX(cameraLoc), GetLocationY(cameraLoc))
  let ug = getGroup()
  ug.enumUnitsSelected(GetTriggerPlayer(), null)
  for u in ug
    u.setPos(cameraPos)
  ug.release()
  RemoveLocation(cameraLoc)

function createItemAtCameraPos()
  let cameraLoc = GetCameraTargetPositionLoc()
  let cameraPos = vec2(GetLocationX(cameraLoc), GetLocationY(cameraLoc))

  var args = getCommandArgs()
  let itemId = args.get(0)
  let id = itemId.toId()
  if (id == -1)
    error("Couldn't parse item id from " + itemId)
    return
  let itm = CreateItem(id, cameraPos.x, cameraPos.y)
  if (itm != null)
    displayTextToPlayer(g_HostPlayer, "Created " + itm.getName())

  RemoveLocation(cameraLoc)

init
  registerHostCommand(COMMAND_DAY, function startNextDay)
  registerHostCommand(COMMAND_NIGHT, function startNextNight)
  registerHostCommand(COMMAND_SEE, function turnOnMapVisibility)
  registerHostCommand(COMMAND_KILL, function killSelectedUnits)
  registerHostCommand(COMMAND_MOVE, function moveSelectedUnits)
  registerHostCommand(COMMAND_CREATE_ITEM, function createItemAtCameraPos)