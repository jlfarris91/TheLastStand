package HostCommands
import DisplayTextToPlayer
import Host
import GroupUtils
import Ids
import ErrorHandling
import HumanPlayerMetadata
import UnitMetadata
import GameInstance
import ChatCommands
import ChatCommandHandler
import MainItemLibrary

constant string COMMAND_DAY = "day"
constant string COMMAND_NIGHT = "night"
constant string COMMAND_SKIP_TO_DAY = "skipToDay"
constant string COMMAND_SKIP_TO_NIGHT = "skipToNight"
constant string COMMAND_SEE = "see"
constant string COMMAND_KILL_SELECTED_UNITS = "kill"
constant string COMMAND_REMOVE_SELECTED_UNITS = "remove"
constant string COMMAND_MOVE_SELECTED_UNITS = "move"
constant string COMMAND_CREATE_ITEM = "item"
constant string COMMAND_CREATE_UNIT = "unit"
constant string COMMAND_ADD_MATS = "mats"
constant string COMMAND_SHOW_WAVES = "waves"
constant string COMMAND_LOG_LEVEL = "loglvl"
constant string COMMAND_HELP = "help"

HostCommandsHandler g_hostCommandHandler

// ============================================================================
public class HostCommandsHandler extends ChatCommandHandler

  // --------------------------------------------------------------------------
  override function handleChatCommand(player p, string command) returns bool

    if (p.isHost())
      return super.handleChatCommand(p, command)

    return false

// ============================================================================
function startNextDay()
  g_GameInstance.startNextDay()

// ============================================================================
function startNextNight()
  g_GameInstance.startNextNight()

// ============================================================================
function turnOnMapVisibility()
  if (IsFogEnabled())
    displayMessageToPlayer(g_HostPlayer, "Disabled fog of war")
    FogEnableOff()
    FogMaskEnableOff()
  else
    displayMessageToPlayer(g_HostPlayer, "Enabled fog of war")
    FogEnableOn()
    FogMaskEnableOn()

// ============================================================================
function killSelectedUnits()
  let ug = getGroup()
  ug.enumUnitsSelected(g_HostPlayer, null)
  for u from ug
    u.kill()
  ug.release()

// ============================================================================
function removeSelectedUnits()
  let ug = getGroup()
  ug.enumUnitsSelected(g_HostPlayer, null)
  for u from ug
    u.remove()
  ug.release()

// ============================================================================
function moveSelectedUnits()
  let cameraLoc = GetCameraTargetPositionLoc()
  let cameraPos = vec2(GetLocationX(cameraLoc), GetLocationY(cameraLoc))
  let ug = getGroup()
  ug.enumUnitsSelected(GetTriggerPlayer(), null)
  for u in ug
    u.setPos(cameraPos)
  ug.release()
  RemoveLocation(cameraLoc)

// ============================================================================
function createItemForSelectedUnit()

  let g = getGroup()
  g.enumUnitsSelected(g_HostPlayer, null)
  if (g.isEmpty())
    displayMessageToPlayer(g_HostPlayer, "Must selected a unit to give the items to")
    g.release()
    return

  let selectedUnit = g.getUnitAt(0)
  g.release()

  let args = getChatCommandArgs()

  let itemIdArg = args.get(0)
  let id = itemIdArg.toId()
  if (id == -1)
    error("Couldn't parse item id from arg 0 \"{0}\"".format(itemIdArg))
    return

  var count = 1
  if (args.size() > 1)
    let itemCountArg = args.get(1)
    count = itemCountArg.toInt()
    if (count == -1)
      count = 1

  let pos = selectedUnit.getPos()

  for i = 1 to count
    let itm = g_MainItemLibrary.createItem(id, pos)
    if (itm != null)
      displayMessageToPlayer(g_HostPlayer, "Created " + count.toString() + " item(s) of type " + itm.getName())

// ============================================================================
function createUnitAtCameraPos()
  let cameraLoc = GetCameraTargetPositionLoc()
  let cameraPos = vec2(GetLocationX(cameraLoc), GetLocationY(cameraLoc))

  let args = getChatCommandArgs()
  let playerIdArg = args.get(0)
  let unitIdArg = args.get(1)
  let countArg = args.get(2)
  
  let p = Player(playerIdArg.toInt())
  if (p == null)
    error("Couldn't parse player id from argument 0 \"{0}\"".format(playerIdArg))
    return

  let id = unitIdArg.toId()
  if (id == -1)
    error("Couldn't parse unit id from argument 1 \"{0}\"".format(unitIdArg))
    return

  let count = countArg.toInt()
  if (count == -1)
    error("Couldn't parse count from argument 2 \"{0}\"".format(countArg))
    return

  string unitName = null
  for i = 1 to count
    let u = createUnitTLS(p, id, cameraPos.x, cameraPos.y, GetRandomDirectionDeg())
    if (u != null and unitName == null)
      unitName = u.getName()
  
  if (unitName != null)
    displayMessageToPlayer(g_HostPlayer, "Created " + I2S(count) + " " + unitName)

  RemoveLocation(cameraLoc)

// ============================================================================
function addMats()
  let args = getChatCommandArgs()
  let playerIdArg = args.get(0)
  let matsArg = args.get(1)

  let p = Player(playerIdArg.toInt())
  if (p == null)
    error("Couldn't parse player id from argument 0 \"{0}\"".format(playerIdArg))
    return

  let mats = matsArg.toInt()
  if (mats == -1)
    error("Couldn't parse material count from argument 1 \"{0}\"".format(matsArg))
    return

  let playerMetadata = p.getMetadata() castTo HumanPlayerMetadata
  if (playerMetadata == null)
    error("Can't give materials to player \"{0}\"".format(p.getName()))

  playerMetadata.giveMaterials(mats)
  displayMessageToPlayer(g_HostPlayer, "Gave {0} materials to {1}".format(I2S(mats), p.getNameColored()))

// ============================================================================
function skipToDay()
  let args = getChatCommandArgs()
  let dayArg = args.get(0)
  
  let day = dayArg.toInt()
  if (day == -1)
    error("Couldn't parse day integer from argument 0 \"{0}\"".format(dayArg))
    return

  displayMessageToPlayer(g_HostPlayer, "Skipping to day {0}...".format(I2S(day)))

  g_GameInstance.skipToDay(day)
    
// ============================================================================
function skipToNight()
  let args = getChatCommandArgs()
  let nightArg = args.get(0)

  let night = nightArg.toInt()
  if (night == -1)
    error("Couldn't parse day integer from argument 0 \"{0}\"".format(nightArg))
    return

  displayMessageToPlayer(g_HostPlayer, "Skipping to night {0}...".format(I2S(night)))

  g_GameInstance.skipToNight(night)  

// ============================================================================
function showWavesMultiboard()
  let args = getChatCommandArgs()

  let waveMultiboard = g_GameInstance.getWaveMultiboard()

  if (args.size() == 0)
    waveMultiboard.setPlayer(null)
    waveMultiboard.setDisplayTarget(null)
    waveMultiboard.hide()
    displayMessageToPlayer(g_HostPlayer, "Hiding wave multiboard")
    return

  let playerIdArg = args.get(0)

  let playerId = playerIdArg.toInt()
  if (playerId == -1)
    error("Couldn't parse player id integer from argument 0 \"{0}\"".format(playerIdArg))
    return

  let p = Player(playerId)

  waveMultiboard.setPlayer(p)
  waveMultiboard.setDisplayTarget(g_HostPlayer)
  waveMultiboard.show()
  displayMessageToPlayer(g_HostPlayer, "Showing wave multiboard")

// ============================================================================
function setLogLevel()
  let args = getChatCommandArgs()
  let levelArg = args.get(0).toLowerCase()

  switch (levelArg)
    case "trace"
      DEBUG_LEVEL = Loglevel.TRACE
    case "debug"
      DEBUG_LEVEL = Loglevel.DEBUG
    case "info"
      DEBUG_LEVEL = Loglevel.INFO
    case "warning"
      DEBUG_LEVEL = Loglevel.WARNING
    case "error"
      DEBUG_LEVEL = Loglevel.ERROR
    default
      error("Couldn't parse log level string from argument 0 \"{0}\"".format(levelArg)) 

  displayMessageToPlayer(g_HostPlayer, "Set log level to " + levelArg)

// ============================================================================
function help()
  let commands = g_hostCommandHandler.getCommands()
  for command in commands
    displayMessageToPlayer(g_HostPlayer, command)

// ============================================================================
init
  g_hostCommandHandler = new HostCommandsHandler()
  registerChatCommandHandler(g_hostCommandHandler, 0)

  g_hostCommandHandler.registerHandlerAction(COMMAND_DAY, function startNextDay)
  g_hostCommandHandler.registerHandlerAction(COMMAND_NIGHT, function startNextNight)
  g_hostCommandHandler.registerHandlerAction(COMMAND_SEE, function turnOnMapVisibility)
  g_hostCommandHandler.registerHandlerAction(COMMAND_KILL_SELECTED_UNITS, function killSelectedUnits)
  g_hostCommandHandler.registerHandlerAction(COMMAND_REMOVE_SELECTED_UNITS, function removeSelectedUnits)
  g_hostCommandHandler.registerHandlerAction(COMMAND_MOVE_SELECTED_UNITS, function moveSelectedUnits)
  g_hostCommandHandler.registerHandlerAction(COMMAND_CREATE_ITEM, function createItemForSelectedUnit)
  g_hostCommandHandler.registerHandlerAction(COMMAND_CREATE_UNIT, function createUnitAtCameraPos)
  g_hostCommandHandler.registerHandlerAction(COMMAND_ADD_MATS, function addMats)
  g_hostCommandHandler.registerHandlerAction(COMMAND_SKIP_TO_DAY, function skipToDay)
  g_hostCommandHandler.registerHandlerAction(COMMAND_SKIP_TO_NIGHT, function skipToNight)
  g_hostCommandHandler.registerHandlerAction(COMMAND_SHOW_WAVES, function showWavesMultiboard)
  g_hostCommandHandler.registerHandlerAction(COMMAND_LOG_LEVEL, function setLogLevel)
  g_hostCommandHandler.registerHandlerAction(COMMAND_HELP, function help)