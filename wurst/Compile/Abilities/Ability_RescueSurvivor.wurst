package Ability_RescueSurvivor
import TlsAbilityIds
import StringBuilder
import ColorUtility
import GameConstants
import ChannelAbilityPreset

public constant string ABILITY_RESCUE_ORDER = "innerfire"
public constant string ABILITY_RESCUE_ORDER_HEROACTION = "dispel"

// ============================================================================
function createRescueAbility(int newId, string baseOrderId, string tooltipExtended) returns ChannelAbilityPreset
  return new ChannelAbilityPreset(newId, 1, true)
  ..setHeroAbility(false)
  ..setItemAbility(false)
  ..presetTargetTypes(Targettype.UNIT)
  ..removeChannelProperties(true, true)
  ..presetBaseOrderID((lvl) -> baseOrderId)
  ..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNRescueSurvivor.blp")
  ..setName("Search")
  ..setHotkeyNormal("E")
  ..setTooltipNormal(1, "Rescue Survivor / Claim Building")
  ..setTooltipNormalExtended(1, tooltipExtended)
  ..presetCastRange((int lvl) -> 150.0)
  ..presetManaCost((int lvl) -> 0)
  ..setTargetsAllowed(1, "allies,friend,ground,air,neutral,structure,vulnerable,invulnerable")
  ..setButtonPositionNormalX(1)
  ..setButtonPositionNormalY(0)

  ..presetArtDuration((int lvl) -> 1.0)
  ..presetFollowThroughTime((int lvl) -> 1.0)
  ..presetDurationNormal((int lvl) -> 1.0)
  ..presetDurationHero((int lvl) -> 1.0)
  ..presetCooldown((int lvl) -> 1.0)
  ..presetDisableOtherAbilities((int lvl) -> false)

// ============================================================================
@compiletime function createAbilities()

  let tooltipExtendedSB = new StringBuilder()
  ..appendLine("Target a survivor to invite them to your camp or target an abandoned camp building to claim it as your own.")
  ..appendLine()
  ..appendLine("You can quickly cast this ability by right-clicking on the target unit.".colorize(Colors.gold))
  ..appendLine()
  ..appendLine("Note:".colorize(Colors.gold))
  ..appendLine("Only works on {0} and non-lootable buildings belonging to the {1} player."
    .format(
      "Survivors".colorize(Colors.gold),
      "Villagers".colorize(PLAYER_VILLAGERS.getColor().toColor())))

  let tooltipExtended = tooltipExtendedSB.toStringAndDestroy()

  createRescueAbility(TlsAbilityIds.rescueSurvivorHero, ABILITY_RESCUE_ORDER, tooltipExtended)
    ..setButtonPositionNormalX(0)
    ..setButtonPositionNormalY(-11)

  createRescueAbility(TlsAbilityIds.rescueSurvivorHero_heroAction, ABILITY_RESCUE_ORDER_HEROACTION, tooltipExtended)
  createRescueAbility(TlsAbilityIds.rescueSurvivorUnit, ABILITY_RESCUE_ORDER, tooltipExtended)