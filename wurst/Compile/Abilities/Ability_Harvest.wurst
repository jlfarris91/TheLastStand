package Ability_Harvest
import AbilityObjEditing
import TlsAbilityIds
import UnitAnimations
import ColorUtility
import StringBuilder

constant real TREE_HARVEST_INCOME_INTERVAL = UnitAnimations.VillagerMan.attack.duration
constant int LUMBER_CAPACITY_BASE = 10
constant int LUMBER_PER_LEVEL = 2
constant real LUMBER_PER_HIT_PER_LEVEL = 1.0 / 2.0
public constant int HARVEST_LEVELS = 10

// ============================================================================
@compiletime function createAbility()
  for i = 1 to HARVEST_LEVELS
    createHarvestAbility(i)

// ============================================================================
function createHarvestAbility(int level)

  let lumberCapacity = LUMBER_CAPACITY_BASE + level * LUMBER_PER_LEVEL
  let damageToTree = 1 + (level * LUMBER_PER_HIT_PER_LEVEL).floor()

  let sb = new StringBuilder()
  sb.appendLine("Max lumber carried (before upgrades): {0}".format(lumberCapacity.toString().colorize(Colors.gold)))
  sb.appendLine("Lumber per hit: {0}".format(damageToTree.toString().colorize(Colors.gold)))
  sb.appendLine()
  sb.appendLine("Each level of the Survivor increases the amount of lumber carried by {0} and lumber per hit by {1}%".format(
    LUMBER_PER_LEVEL.toString().colorize(Colors.gold),
    (LUMBER_PER_HIT_PER_LEVEL * 100.0).floor().toString().colorize(Colors.gold)))

  new AbilityDefinitionHarvestLumber(TlsAbilityIds.harvest[level - 1])
  ..setLevels(1)
  ..setName("Harvest")
  ..setEditorSuffix("(Level {0})".format(level.toString()))
  ..setTooltipNormal(1, "Gather [Level {0}]".format(level.toString()))
  ..setTooltipNormalExtended(1, "Harvest lumber from trees without killing the tree. Somehow.|n|n" + sb.toString())
  ..setTooltipTurnOff(1, "Return Resources [Level {0}]".format(level.toString()))
  ..setTooltipTurnOffExtended(1, "Return the carried lumber to the nearest HQ Tent or Shelter.|n|n" + sb.toString())
  ..setLumberCapacity(1, lumberCapacity)
  ..setDamagetoTree(1, damageToTree)
  ..setButtonPositionNormalX(2)
  ..setButtonPositionNormalY(1)
  ..setButtonPositionTurnOffX(2)
  ..setButtonPositionTurnOffY(1)
  ..setTargetsAllowed(1, "tree,alive,dead")
  ..setDurationNormal(1, 1.1)
  ..setCastRange(1, 116.0)
  ..setAreaofEffect(1, 900.0)

  destroy sb