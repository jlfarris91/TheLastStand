package Ability_SpikeAura
import AbilityObjEditing
import TlsAbilityIds
import BuffObjEditing
import TlsBuffIds
import ObjectIds

public constant real array SPIKEAURA_ATTACK_SPEED_DECREASE = [0.1, 0.2, 0.3, 0.4, 0.5]
public constant real array SPIKEAURA_MOVE_SPEED_DECREASE = [0.1, 0.2, 0.3, 0.4, 0.5]

// ============================================================================
@compiletime function createAbility()

  new AbilityDefinitionAuraSlow(TlsAbilityIds.spikeAttackSlowBonus)
  ..setName("Spikes")
  ..setEditorSuffix("")
  ..setLevels(5)
  ..setRace(Race.Human)
  ..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNImprovedSpikedBarricades.blp")
  ..setButtonPositionNormalX(0)
  ..setButtonPositionNormalY(0)
  ..setAnimationNames("work")
  ..setMissileArt("")

  ..presetBuffs((int lvl) -> commaList(TlsBuffIds.spikeSlow))
  ..presetAttackSpeedFactor((int lvl) -> -SPIKEAURA_ATTACK_SPEED_DECREASE[lvl - 1])
  ..presetMovementSpeedFactor((int lvl) -> -SPIKEAURA_MOVE_SPEED_DECREASE[lvl - 1])
  ..presetAreaofEffect((int lvl) -> 128.0)
  ..presetTargetsAllowed((int lvl) -> commaList(TargetsAllowed.ground, TargetsAllowed.enemies, TargetsAllowed.vulnerable, TargetsAllowed.invulnerable))

  ..presetTooltipNormal((int lvl) -> "Spikes [Level {0}]".format(I2S(lvl)))
  ..presetTooltipNormalExtended() (int lvl) ->
    return "Spikes slow nearby enemy movement speed by {0}% and attack rate by {1}%".format(
      I2S(R2I(SPIKEAURA_MOVE_SPEED_DECREASE[lvl - 1] * 100.0)),
      I2S(R2I(SPIKEAURA_ATTACK_SPEED_DECREASE[lvl - 1] * 100.0)))

// ============================================================================
@compiletime function createBuff()
  new BuffDefinition(TlsBuffIds.spikeSlow, 'Basl')
  ..setRace(1, "human")
  ..setTooltipNormal(1, "Spikes")
  ..setTooltipNormalExtended(1, "Nearby Spikes have decreased the movement speed and attack rate of this unit.")
  ..setArtTarget(1, "Abilities\\Spells\\Human\\slowz\\slowtarget.mdl")
  ..setIcon("ReplaceableTextures\\CommandButtons\\BTNImprovedSpikedBarricades.blp")
  ..setIconNormal(1, "ReplaceableTextures\\CommandButtons\\BTNImprovedSpikedBarricades.blp")