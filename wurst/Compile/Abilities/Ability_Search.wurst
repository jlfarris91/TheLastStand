package Ability_Search
import TlsAbilityIds
import ChannelAbilityPreset
import Icons
import StringBuilder
import ColorUtility

public constant string ABILITY_SEARCH_ORDER = "hex"
public constant string ABILITY_SEARCH_ORDER_HEROACTION = "spellsteal"
constant int SEARCH_BASE_MANA_COST = 10

// ============================================================================
function createSearchAbility(int newId, string baseOrderId, string tooltipExtended) returns ChannelAbilityPreset
  return new ChannelAbilityPreset(newId, 1, true)
  ..setHeroAbility(false)
  ..setItemAbility(false)
  ..presetTargetTypes(Targettype.UNIT)
  ..removeChannelProperties(true, true)
  ..presetBaseOrderID((lvl) -> baseOrderId)
  ..setIconNormal(Icons.bTNTelescope)
  ..setName("Search")
  ..setHotkeyNormal("E")
  ..setTooltipNormal(1, "Search")
  ..setTooltipNormalExtended(1, tooltipExtended)
  ..presetCastRange((int lvl) -> 150.0)
  ..presetManaCost((int lvl) -> SEARCH_BASE_MANA_COST)
  ..presetTargetsAllowed((int lvl) -> "ancient,friend,invulnerable,neutral")
  ..setButtonPositionNormalX(0)
  ..setButtonPositionNormalY(0)

  ..presetArtDuration((int lvl) -> 1.0)
  ..presetFollowThroughTime((int lvl) -> 1.0)
  ..presetDurationNormal((int lvl) -> 1.0)
  ..presetDurationHero((int lvl) -> 1.0)
  ..presetCooldown((int lvl) -> 1.0)
  ..presetDisableOtherAbilities((int lvl) -> false)

// ============================================================================
@compiletime function createAbility()
  
  let tooltipExtendedSB = new StringBuilder()
  ..appendLine("Search a lootable structure for supplies. Costs {0} mana per second.".format(SEARCH_BASE_MANA_COST.toString()))
  ..appendLine()
  ..appendLine("You can quickly cast this ability by right-clicking on the target unit.".colorize(Colors.gold))
  ..appendLine()
  ..appendLine("Note:".colorize(Colors.gold))
  ..appendLine("The duration of the Search ability depends on the unit being looted; the larger the unit being looted the longer the duration but the greater the reward. ")
  ..appendLine("Additionally, some items can affect the duration of Search.")

  let tooltipExtended = tooltipExtendedSB.toStringAndDestroy()
  
  createSearchAbility(TlsAbilityIds.search, ABILITY_SEARCH_ORDER, tooltipExtended)
    ..setButtonPositionNormalX(0)
    ..setButtonPositionNormalY(-11)
  
  createSearchAbility(TlsAbilityIds.search_heroAction, ABILITY_SEARCH_ORDER_HEROACTION, tooltipExtended)