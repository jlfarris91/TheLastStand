package Classification
import ColorUtility
import ErrorHandling
import LinkedList
import ItemObjEditing

constant colorA array UNIT_CLASSIFICATION_COLORS = [
  colorA(189, 39, 39, 255), // MELEE
  colorA(36, 192, 36, 255), // RANGED
  colorA(163, 51, 255, 255), // MAGIC
  colorA(66, 211, 255, 255), // HUMAN
  colorA(66, 66, 255, 255), // BUILDING
  colorA(156, 156, 156, 255)  // WORKSTATION
]

constant string array UNIT_CLASSIFICATION_SYMBOLS = [
  "¢", // MELEE
  "£", // RANGED
  "¤", // MAGIC
  "§", // HUMAN
  "¥", // BUILDING
  "¦"  // WORKSTATION
]

constant string array UNIT_CLASSIFICATION_SYMBOLS_COLORED = [
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.MELEE castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.MELEE castTo int]),             // MELEE
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.RANGED castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.RANGED castTo int]),           // RANGED
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.MAGIC castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.MAGIC castTo int]),             // MAGIC
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.HUMAN castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.HUMAN castTo int]),             // HUMAN
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.BUILDING castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.BUILDING castTo int]),       // BUILDING
  UNIT_CLASSIFICATION_SYMBOLS[TlsUnitClassification.WORKSTATION castTo int].colorize(UNIT_CLASSIFICATION_COLORS[TlsUnitClassification.WORKSTATION castTo int])  // WORKSTATION
]

constant string ITEM_DESC_FORMAT_STRING = "|cffffcc00Requires|r {0}|n|n{1}"
constant string ITEM_TOOLTIP_EX_FORMAT_STRING = "|cffffcc00Requires|r {0}|n|n{1}"

public enum TlsUnitClassification
  MELEE
  RANGED
  MAGIC
  HUMAN
  BUILDING
  WORKSTATION
  __COUNT

// ============================================================================
public function TlsUnitClassification.toString() returns string
  switch (this)
    case MELEE
      return "Melee"
    case RANGED
      return "Ranged"
    case MAGIC
      return "Magic"
    case HUMAN
      return "Human"
    case BUILDING
      return "Building"
    case WORKSTATION
      return "Workstation"
    default
      error("Invalid argument")
  return "<Unknown Classification>"

// ============================================================================
public function TlsUnitClassification.getSymbolString() returns string
  return UNIT_CLASSIFICATION_SYMBOLS[this castTo int]

// ============================================================================
public function TlsUnitClassification.getSymbolStringColored() returns string
  return UNIT_CLASSIFICATION_SYMBOLS_COLORED[this castTo int]

// ============================================================================
public function TlsUnitClassification.getSymbolColor() returns colorA
  return UNIT_CLASSIFICATION_COLORS[this castTo int]

// ============================================================================
public function LinkedList<TlsUnitClassification>.getUnitClassSymbolsColored() returns string
  var symbolString = ""
  for c in this
    symbolString += c.getSymbolStringColored()
  return symbolString

// ============================================================================
public class TLSItemDefinitionBuilder
  private LinkedList<TlsUnitClassification> m_unitClassifications
  private string m_description
  private string m_tooltipExtended

  // --------------------------------------------------------------------------
  ondestroy
    if (m_unitClassifications != null)
      destroy m_unitClassifications
      m_unitClassifications = null

  // --------------------------------------------------------------------------
  function addUnitClassification(TlsUnitClassification value)
    if (m_unitClassifications == null)
      m_unitClassifications = new LinkedList<TlsUnitClassification>()
    m_unitClassifications.add(value)

  // --------------------------------------------------------------------------
  function setDescription(string value)
    m_description = value

  // --------------------------------------------------------------------------
  function setTooltipExtended(string value)
    m_tooltipExtended = value

  // --------------------------------------------------------------------------
  function apply(ItemDefinition definition)

    if (m_unitClassifications.isEmpty())
      definition.setDescription(m_description)
      definition.setTooltipExtended(m_tooltipExtended)
      return

    let symbolString = m_unitClassifications.getUnitClassSymbolsColored()
    definition.setDescription(ITEM_DESC_FORMAT_STRING.format(symbolString, m_description))
    definition.setTooltipExtended(ITEM_TOOLTIP_EX_FORMAT_STRING.format(symbolString, m_tooltipExtended))