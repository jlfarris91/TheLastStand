package Hero_Swordsman
import Hero_Base
import TlsUnitDefinition
import AbilityIds
import HeroAbilities
import TlsUnitDefinitionBuilder
import TlsUpgradeIds

public HeroBaseDefinition g_heroSoldierDefinition

// ============================================================================
public class HeroSwordsmanDefinition extends HeroBaseDefinition
  construct(int newId)
    super(newId, UnitIds.paladin)

  // --------------------------------------------------------------------------
  protected override function init_abilities()
    super.init_abilities()

    if (compiletime)
      setupHeroAbilities()

    setHeroAbilities(HeroAbilities.swordsman.getHeroAbilitiesDefinitionString())

    setNormalAbilities(commaList(
      TlsAbilityIds.heroActions,
      TlsAbilityIds.search,
      TlsAbilityIds.rescueSurvivorHero,
      AbilityIds.inventory))

  // --------------------------------------------------------------------------
  protected override function init_art()
    super.init_art()

    setModelFile("units\\human\\ArthaswithSword\\ArthaswithSword")

    setUnitSoundSet("Arthas")

  // --------------------------------------------------------------------------
  protected override function init_combat()
    super.init_combat()

    setAttack1DamageBase(100)
    setAttack1DamageNumberofDice(2)
    setAttack1DamageSidesperDie(16)
    setAttack1CooldownTime(2.5)
    setDefenseBase(5)
    setAttack1AttackType(AttackType.Hero)

  // --------------------------------------------------------------------------
  protected override function init_movement()
    super.init_movement()

    setSpeedBase(300)

  // --------------------------------------------------------------------------
  protected override function init_stats()
    super.init_stats()

    setPrimaryAttribute("str")

    setHitPointsMaximumBase(600)
    setHitPointsRegenerationRate(1.5)
    setManaRegeneration(1)
    
    setStartingStrength(22)
    setStrengthPerLevel(12)

    setStartingAgility(12)
    setAgilityPerLevel(6)

    setStartingIntelligence(16)
    setIntelligencePerLevel(3)

  // --------------------------------------------------------------------------
  protected override function init_techtree()
    super.init_techtree()

    setUpgradesUsed(commaList(TlsUpgradeIds.survivorHpBonus))

  // --------------------------------------------------------------------------
  protected override function init_text()
    super.init_text()

    setName("Swordsman")

// ============================================================================
@compiletime function createHero()
  g_heroSoldierDefinition = new HeroSwordsmanDefinition(TlsUnitIds.heroSwordsman)
    ..addUnitClass(TlsUnitClassification.HUMAN)
    ..addUnitClass(TlsUnitClassification.MELEE)
    ..build("", null)

// ============================================================================
init
  if (g_heroSoldierDefinition == null)
    createHero()