package Unit_ScoutTower
import TlsBuildingDefinition
import BuildingUndeadTarget
import GameConstants
import TlsUpgradeIds
import TlsUnitDefinitionBuilder
import WorkstationComponent
import ColorUtility

public TlsBuildingDefinition g_scoutTowerDefinition

// ============================================================================
public class ScoutTowerDefinition extends TlsBuildingDefinition
  construct(int newId)
    super(newId, UnitIds.scouttower)
    this.makeBuildingUndeadTarget()
    setName("Scout Tower")
    setButtonPositionX(0)
    setButtonPositionY(0)
    setDeathTimeseconds(1.0)
    //setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNWorkbench.dds")
    //setModelFile("Units\\Workbench\\Workbench.mdx")
    setScalingValue(1.0)
    setSelectionScale(3.0)
    setTintingColorRed(200)
    setTintingColorBlue(200)
    setTintingColorGreen(200)
    setPlacementRequires("unbuildable,unwalkable")
    setHideMinimapDisplay(true)
    setBuildTime(BUILD_TIME_BUILDING_MEDIUM)
    setRepairTime(REPAIR_TIME_BUILDING_MEDIUM)
    setSightRadiusDay(STRUCTURE_LARGE_SIGHT_RANGE_DAY)
    setSightRadiusNight(STRUCTURE_LARGE_SIGHT_RANGE_NIGHT)
    setRequirements("")
    setUpgradesUsed(commaList(TlsUpgradeIds.campDefense))
    setNormalAbilities(commaList(
      TlsAbilityIds.recycle,
      TlsAbilityIds.blightDispelSmall,
      TlsAbilityIds.workstationLoadUnit,
      TlsAbilityIds.workstationUnloadUnit,
      TlsAbilityIds.inventory2Slots))
    setUnitsTrained("")
    setUpgradesTo("")
    
    setHitPointsMaximumBase(2000)
    setArmorType(ArmorType.Fortified)
    setDefenseBase(0)

    // To see over trees and terrain
    setMovementType(MovementType.Fly)

    setGoldCost(2)
    setLumberCost(600)
    this.setCommonBuildingProperties()

    setHotkey("T")
    setTooltipBasic("Build Scout Tower")

// ============================================================================
@compiletime function createUnit()
  g_scoutTowerDefinition = new ScoutTowerDefinition(TlsUnitIds.scoutTower)
    ..addUnitClass(TlsUnitClassification.BUILDING)
    ..addUnitClass(TlsUnitClassification.WORKSTATION)
    ..build("A tall tower fitted with a scope that reveals the nearby area when operated by a survivor. Can be upgraded to mark nearby high priority enemies to take extra damage.|n|n{0}".format("Who knows what you might spot during the day...".colorize(Colors.gold)),
      tooltipBuilder -> begin
        let workstationItem = new WorkstationOperatorBonusesTooltipItem()
        ..addValue("Increased sight range")
        ..addValue("Increased bonus damage to marked targets")
        tooltipBuilder.addItem(workstationItem)
      end)

// ============================================================================
init
  createUnit()