package BuildingUndeadTarget
import TlsUnitDefinition
import HashList
import DamageEvent
import Damage
import Dice

HashList<int> g_undeadTargetBuildings = new HashList<int>()
HashList<int> g_ignoreDamage = new HashList<int>()

// ============================================================================
public function BuildingDefinition.makeBuildingUndeadTarget()
  this
  ..setAttacksEnabled(1)
  ..setAcquisitionRange(128)
  ..setAttack1AttackType(AttackType.Unknown)
  ..setAttack1CooldownTime(1.0)
  ..setAttack1DamageDice(dice(0, 1, 1))
  ..setAttack1MaximumNumberofTargets(6)
  ..setAttack1Range(128)
  ..setAttack1RangeMotionBuffer(250.0)
  ..setAttack1ShowUI(false)
  ..setAttack1TargetsAllowed("ground,enemy,vulnerable")
  ..setAttack1WeaponSound(WeaponSound.Nothing)
  ..setAttack1WeaponType(WeaponType.Normal)

  // Debugging
  // ..setAttack1ShowUI(true)
  // ..setAttack1ProjectileArt("Abilities\\Weapons\\ZigguratMissile\\ZigguratMissile.mdl")
  // ..setAttack1ProjectileHomingEnabled(true)
  // ..setAttack1ProjectileSpeed(900)
  // ..setAttack1WeaponType(WeaponType.Missile)

  addUndeadTargetBuildingDefinition(this.getNewId(), true)

// ============================================================================
public function addUndeadTargetBuildingDefinition(int unitTypeId, bool ignoreDamage)
  g_undeadTargetBuildings.add(unitTypeId)
  if (ignoreDamage)
    g_ignoreDamage.add(unitTypeId)

// ============================================================================
public function ITlsUnitDefinition.isBuildingUndeadTarget() returns bool
  return g_undeadTargetBuildings != null and g_undeadTargetBuildings.has(this.getNewId())

// ============================================================================
public function unit.isBuildingUndeadTarget() returns bool
  return g_undeadTargetBuildings != null and g_undeadTargetBuildings.has(this.getTypeId())

// ============================================================================
function onUnitDamageDealt()
  let source = DamageEvent.getSource()
  if (g_ignoreDamage.has(source.getTypeId()))
    DamageEvent.setAmount(0)

// ============================================================================
init
  DamageEvent.addListener(DAMAGE_EVENT_PRIO_ABSOLUTE, () -> onUnitDamageDealt())