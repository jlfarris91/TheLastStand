package Unit_Spikes
import TlsBuildingDefinition
import GameConstants
import StringBuilder
import StringExtensions
import Ability_SpikeAura
import Icons
import Range
import TlsUpgradeIds
import TlsUnitDefinition
import AbilityIds
import ColorUtility

constant int ATTACK_RANGE = 100
constant real ATTACK_COOLDOWN = 0.6

public TlsBuildingDefinition array g_spikesDefinition

constant string array SPIKE_ICON = [
  "ReplaceableTextures\\CommandButtons\\BTNSpikes.tga",
  "ReplaceableTextures\\CommandButtons\\BTNImprovedSpikes.tga",
  "ReplaceableTextures\\CommandButtons\\BTNReinforcedSpikes.tga",
  "ReplaceableTextures\\CommandButtons\\BTNEngineeredSpikes.tga",
  Icons.bTNSlaughterHouse
]

constant string array SPIKE_MODELPATH = [
  "Doodads\\Outland\\Props\\Obstacle\\Obstacle0.mdl",
  "Doodads\\Outland\\Props\\Obstacle\\Obstacle2.mdl",
  "Doodads\\Outland\\Props\\Obstacle\\Obstacle1.mdl",
  "Units\\Spikes3\\Spikes3.mdl",
  "Units\\Spikes4\\Spikes4.mdl"
]

// ============================================================================
public class SpikesDefinition extends TlsBuildingDefinition
  construct(int newId)
    super(newId, UnitIds.scouttower)

  // --------------------------------------------------------------------------
  protected override function init_abilities()
    super.init_abilities()

    setNormalAbilities(commaList(
        AbilityIds.invulnerable,
        TlsAbilityIds.recycle,
        TlsAbilityIds.spikeAttackSlowBonus,
        TlsAbilityIds.spikeBarrage))

  // --------------------------------------------------------------------------
  protected override function init_art()
    super.init_art()

    setButtonPositionX(3)
    setButtonPositionY(0)
    setDeathTimeseconds(0.1)
    setGroundTexture("")
    setShadowImageUnit("")
    setTintingColorRed(150)
    setTintingColorBlue(150)
    setTintingColorGreen(150)

  // --------------------------------------------------------------------------
  protected override function init_combat()
    super.init_combat()
    
    setAcquisitionRange(256.0)
    setArmorSoundType(ArmorSoundType.Metal)
    setAttack1AnimationBackswingPoint(0.3)
    setAttack1AnimationDamagePoint(0.3)
    setAttack1AttackType(AttackType.Normal)
    setAttack1CooldownTime(ATTACK_COOLDOWN)
    setAttack1Range(ATTACK_RANGE)
    setAttack1RangeMotionBuffer(250.0)
    setAttack1ProjectileArt("Objects\\Spawnmodels\\Undead\\UndeadBlood\\UndeadBloodGhoul.mdl")
    setAttack1ProjectileSpeed(9999)
    setAttack1TargetsAllowed(commaList(TargetsAllowed.ground, TargetsAllowed.organic))
    setAttack1WeaponSound(WeaponSound.MetalMediumSlice)
    setAttack1WeaponType(WeaponType.Missile)
    setAttack1MaximumNumberofTargets(100)
    setAttacksEnabled(1)
    setTargetedAs("invulnerable,structure")
    setDeathType(DeathType.CannotRaiseDoesNotDecay)

    setArmorType(ArmorType.Fortified)

  // --------------------------------------------------------------------------
  protected override function init_movement()
    super.init_movement()
    setCollisionSize(0.0)

  // --------------------------------------------------------------------------
  protected override function init_pathing()
    super.init_pathing()

    setPathingMap("PathTextures\\4x4Unbuildable.tga")
    setPlacementRequires("unbuildable,unwalkable")

  // --------------------------------------------------------------------------
  protected override function init_stats()
    super.init_stats()

    setBuildTime(5)
    setHideMinimapDisplay(true)
    setHitPointsMaximumBase(500)
    setRepairTime(10)
    setSightRadiusDay(STRUCTURE_MEDIUM_SIGHT_RANGE_DAY)
    setSightRadiusNight(STRUCTURE_MEDIUM_SIGHT_RANGE_NIGHT)
    setUpgradesTo("")
    setUpgradesUsed(commaList(TlsUpgradeIds.campDefense))

    setGoldCost(0)
    setMaterialCost(10)
    setRepairGoldCost(R2I(getGoldCost() * BUILDING_REPAIR_GOLD_SCALAR))
    setRepairLumberCost(R2I(getLumberCost() * BUILDING_REPAIR_LUMBER_SCALAR))
    setRecycleGoldRewarded(getGoldCost())
    setRecycleLumberRewarded(getLumberCost())

  // --------------------------------------------------------------------------
  protected override function init_text()
    super.init_text()
    setHotkey("S")

// ============================================================================
@compiletime function createSpikes()

  let tooltipBuilder = new StringBuilder()
  tooltipBuilder.appendLine("{0}")
  tooltipBuilder.appendLine()
  tooltipBuilder.appendLine("Attacks ground only!".colorize(Colors.red))
  tooltipBuilder.appendLine()
  tooltipBuilder.appendLine("Damage: {1} ({2} dps)")
  tooltipBuilder.appendLine("Slows enemy movement by {3}%")
  tooltipBuilder.appendLine("Slows enemy attack speed by {4}%")
  let tooltipExtended = tooltipBuilder.toStringAndDestroy()

  // Spikes Level 1
  g_spikesDefinition[0] = new SpikesDefinition(TlsUnitIds.spikes[0])
    ..setName("Spikes")
    ..setTooltipBasic("Build Spikes")
    ..setIconGameInterface(SPIKE_ICON[0])
    ..setModelFile(SPIKE_MODELPATH[0])
    ..setUpgradesTo(commaList(TlsUnitIds.spikes[1]))
    ..setAttack1DamageRange(rangeInt(8, 12))
    ..setGoldCost(0)
    ..setMaterialCost(40)
    ..setHitPointsMaximumBase(500)
    ..setRequirements(commaList(TlsUnitIds.headquarters1))
    ..setTooltipExtended(0, tooltipExtended, "A flimsy contraption of spikes that hurt and slow enemies that move through them.")

  // Spikes Level 2
  g_spikesDefinition[1] = new SpikesDefinition(TlsUnitIds.spikes[1])
    ..setName("Reinforced Spikes")
    ..setTooltipBasic("Upgrade to Reinforced Spikes")
    ..setButtonPositionX(0)
    ..setButtonPositionY(2)
    ..setIconGameInterface(SPIKE_ICON[1])
    ..setModelFile(SPIKE_MODELPATH[1])
    ..setScalingValue(0.9)
    ..setUpgradesTo(commaList(TlsUnitIds.spikes[2]))
    ..setAttack1DamageRange(rangeInt(32, 42))
    ..setGoldCost(0)
    ..setMaterialCost(135)
    ..setHitPointsMaximumBase(1000)
    ..setRequirements(commaList(TlsUnitIds.headquarters1, TlsUnitIds.workshop))
    ..setTooltipExtended(1, tooltipExtended, "Reinforce the spikes with additional materials to make them sturdier and deal more damage.")

  // Spikes Level 3
  g_spikesDefinition[2] = new SpikesDefinition(TlsUnitIds.spikes[2])
    ..setName("Improved Reinforced Spikes")
    ..setTooltipBasic("Upgrade to Improved Reinforced Spikes")
    ..setButtonPositionX(0)
    ..setButtonPositionY(2)
    ..setIconGameInterface(SPIKE_ICON[2])
    ..setModelFile(SPIKE_MODELPATH[2])
    ..setScalingValue(0.8)
    ..setUpgradesTo(commaList(TlsUnitIds.spikes[3]))
    ..setAttack1DamageRange(rangeInt(60, 84))
    ..setGoldCost(0)
    ..setMaterialCost(635)
    ..setHitPointsMaximumBase(1500)
    ..setTooltipExtended(2, tooltipExtended, "Reinforce the spikes with additional materials to make them sturdier and deal more damage.")

  // Spikes Level 4
  g_spikesDefinition[3] = new SpikesDefinition(TlsUnitIds.spikes[3])
    ..setName("Engineered Spikes")
    ..setTooltipBasic("Upgrade to Engineered Spikes")
    ..setButtonPositionX(0)
    ..setButtonPositionY(2)
    ..setIconGameInterface(SPIKE_ICON[3])
    ..setModelFile(SPIKE_MODELPATH[3])
    ..setScalingValue(1.1)
    ..setTintingColorRed(255)
    ..setTintingColorGreen(255)
    ..setTintingColorBlue(255)
    ..setUpgradesTo(commaList(TlsUnitIds.spikes[4]))
    ..setAttack1DamageRange(rangeInt(120, 168))
    ..setGoldCost(5)
    ..setMaterialCost(1635)
    ..setHitPointsMaximumBase(2000)
    ..setTooltipExtended(3, tooltipExtended, "Reinforce the spikes with additional materials to make them sturdier and deal more damage.")

  // Spikes Level 5
  g_spikesDefinition[4] = new SpikesDefinition(TlsUnitIds.spikes[4])
    ..setName("Advanced Engineered Spikes")
    ..setTooltipBasic("Upgrade to Advanced Engineered Spikes")
    ..setButtonPositionX(0)
    ..setButtonPositionY(2)
    ..setProjectileImpactZ(10)
    ..setIconGameInterface(SPIKE_ICON[4])
    ..setModelFile(SPIKE_MODELPATH[4])
    ..setRequiredAnimationNames("stand,work")
    ..setScalingValue(0.6)
    ..setTintingColorRed(150)
    ..setTintingColorGreen(150)
    ..setTintingColorBlue(150)
    ..setAttack1DamageRange(rangeInt(400, 560))
    ..setGoldCost(50)
    ..setMaterialCost(5635)
    ..setHitPointsMaximumBase(4000)
    ..setTooltipExtended(4, tooltipExtended, "Reinforce the spikes with additional materials to make them sturdier and deal more damage.")

// ============================================================================
function TlsBuildingDefinition.setTooltipExtended(int lvl, string tooltipExtendedUnformatted, string description)
  
  this.setTooltipExtended(tooltipExtendedUnformatted.format(
    description,
    this.getAttack1DamageRangeString(),
    this.getApproxAttack1DPSString(),
    I2S(R2I(SPIKEAURA_MOVE_SPEED_DECREASE[lvl] * 100.0)),
    I2S(R2I(SPIKEAURA_ATTACK_SPEED_DECREASE[lvl] * 100.0))))


// ============================================================================
init
  createSpikes()