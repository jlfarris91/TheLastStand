package Item_Materials
import RegisterEvents
import GameConstants
import FX
import HumanPlayerMetadata
import TlsItemDefinition
import Range

constant int GOLD_COST = 0

// ============================================================================
@compiletime function createItem()
  new TlsItemDefinition(TlsItemIds.materials, 'lmbr')
  ..setName("Materials")
  ..setDescription("Resources used to build camp structures.")
  ..setTooltipBasic("Materials")
  ..setTooltipExtended("Resources used to build camp structures.")
  ..setAbilities("")
  ..setInterfaceIcon("ReplaceableTextures\\CommandButtons\\BTNBundleOfLumber.blp")
  ..setModelUsed("Doodads\\LordaeronSummer\\Terrain\\Crates\\Crates1.mdl")
  ..setGoldCost(GOLD_COST)
  ..setScalingValue(0.8)
  ..setNumberofCharges(1)
  ..setCanBeDropped(false)
  ..setCanBeSoldToMerchants(false)
  ..setCanBeSoldByMerchants(false)

// ============================================================================
function onItemAcquired()
  let u = GetManipulatingUnit()
  let i = GetManipulatedItem()

  let ownerMetadata = u.getOwnerHumanMetadata()
  if (ownerMetadata == null)
    return

  if (i.getTypeId() != TlsItemIds.materials)
    return

  i.remove()
  
  let reward = MATERIALS_ITEM_REWARD.getRandom()
  ownerMetadata.giveMaterials(reward)

  let pos = u.getPos()

  FX.createFoundMaterialsEffect(pos, ownerMetadata.getPlayer())
  FX.createFoundMaterialsTag(pos, reward, ownerMetadata.getPlayer())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM, function onItemAcquired)
  
  g_MainItemLibrary.commonSet.add(TlsItemIds.materials, 50.0)