package Items_BundleOfWood
import RegisterEvents
import GameConstants
import FX
import HumanPlayerMetadata
import TlsItemDefinition
import Objects
import Icons
import Range

constant int GOLD_COST = 0

// ============================================================================
@compiletime function createItem()
  new TlsItemDefinition(TlsItemIds.bundleOfWood, 'lmbr')
  ..setName("Bundle of Wood")
  ..setDescription("Resources used to build camp structures.")
  ..setTooltipBasic("Bundle of Wood")
  ..setTooltipExtended("Resources used to build camp structures.")
  ..setAbilities("")
  ..setInterfaceIcon(Icons.bTNBundleOfLumber)
  ..setModelUsed(Objects.bundleofLumber)
  ..setGoldCost(GOLD_COST)
  ..setScalingValue(0.8)
  ..setNumberofCharges(1)
  ..setCanBeDropped(false)
  ..setCanBeSoldToMerchants(false)
  ..setCanBeSoldByMerchants(false)

// ============================================================================
function onItemAcquired()
  let u = GetManipulatingUnit()
  let i = GetManipulatedItem()

  let ownerMetadata = u.getOwnerHumanMetadata()
  if (ownerMetadata == null)
    return

  if (i.getTypeId() != TlsItemIds.bundleOfWood)
    return

  i.remove()
  
  let reward = BUNDLEWOOD_ITEM_REWARD.getRandom()
  ownerMetadata.giveMaterials(reward)

  let pos = u.getPos()

  FX.createFoundMaterialsEffect(pos, ownerMetadata.getPlayer())
  FX.createFoundMaterialsTag(pos, reward, ownerMetadata.getPlayer())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM, function onItemAcquired)
  
  g_MainItemLibrary.commonSet.add(TlsItemIds.bundleOfWood, 50.0)