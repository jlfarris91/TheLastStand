package Item_LightningRod
import TlsItemDefinition
import Icons
import AbilityObjEditing
import StringBuilder
import ColorUtility

// ============================================================================
@compiletime function createCompiletimeObjects()

  let sb = new StringBuilder()
  sb.appendLine("Attacks have a 10% chance of casting chain lightning dealing 100 damage to up to 3 nearby enemies. Damage is reduced by 25% for each target hit.")
  sb.appendLine()
  sb.append("Each additional stack increases the chance by 5% and damage by 100.".colorize(Colors.gold))
  let tooltipExtended = sb.toStringAndDestroy()

  new TlsItemDefinition(TlsItemIds.lightningRod, 'wtlg')
  ..setName("Lightning Rod")
  ..setDescription("")
  ..setTooltipBasic("Purchase Lightning Rod")
  ..setTooltipExtended(tooltipExtended)
  ..setAbilities("")
  ..setInterfaceIcon(Icons.bTNManaFlare)
  ..setGoldCost(6)
  ..setNumberofCharges(1)
  ..addTlsUnitClassifications(TlsUnitClassification.HUMAN)
  ..build()

  new AbilityDefinitionChainLightningcreep(TlsAbilityIds.Items.lightningRodChainLighting)
    ..setName("Chain Lightning")
    ..setEditorSuffix("(Lightning Rod)")
    ..setLevels(5)
    ..presetNumberofTargetsHit((int lvl) -> 3)
    ..presetDamageReductionperTarget((int lvl) -> 0.25)
    ..presetDamageperTarget((int lvl) -> lvl * 100.0)
    ..presetAreaofEffect((int lvl) -> 512.0)
    ..presetCastRange((int lvl) -> 9999.0)
    ..presetManaCost((int lvl) -> 0)
    ..presetCooldown((int lvl) -> 9999.0)

// ============================================================================
init
  if (not compiletime)
    createCompiletimeObjects()