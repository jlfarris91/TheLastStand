package JobItemTooltipBuilder
import public TooltipBuilder
import TlsUnitDefinition
import StringBuilder
import ColorUtility

// ============================================================================
/*
  Sturdy Spears

  A quiver of sturdy spears. Good for killing undead at medium range.

  [Survivor Job Change]
  Use this item on a Survivor to give them new abilities.

  Survivor Male -> Spearman

  Does not attack air!

  Spearman
  Damage:       18-32
  Range:        350
  Abilities:    Ability 1
                Ability 2
                Ability 3
*/
public class JobItemTooltipBuilder extends TooltipBuilder
  private ITlsUnitDefinition m_fromUnit
  private ITlsUnitDefinition m_toUnit
    
  // --------------------------------------------------------------------------
  function setUnits(ITlsUnitDefinition fromUnit, ITlsUnitDefinition toUnit) returns JobItemTooltipBuilder
    m_fromUnit = fromUnit
    m_toUnit = toUnit
    return this

  // --------------------------------------------------------------------------
  override function clear()
    super.clear()
    m_fromUnit = null
    m_toUnit = null

  // --------------------------------------------------------------------------
  override function getDescription() returns string
    return super.getDescription()

  // --------------------------------------------------------------------------
  override function getDescription(int level) returns string
    let sb = new StringBuilder()

    let desc = super.getDescription(level)
    if (desc != null)
      sb.appendLine(desc)
      sb.appendLine()

    sb.appendLine("[Learn Survivor Job]".colorize(COLOR_GOLD))
    sb.appendLine("Give this item to a {0} to give them new abilities.".format(m_fromUnit.getName()))
    sb.appendLine()

    return sb.toStringAndDestroy()

  // --------------------------------------------------------------------------
  override function getTooltipExtended() returns string
    return super.getTooltipExtended()

  // --------------------------------------------------------------------------
  override function getTooltipExtended(int level) returns string
    let sb = new StringBuilder()
    
    sb.appendLine("Teaches a {0} the {1} job".format(m_fromUnit.getName().colorize(COLOR_GOLD), m_toUnit.getName().colorize(COLOR_GOLD)))
    sb.appendLine()

    let tooltipExtended = super.getTooltipExtended(level)
    if (tooltipExtended != "")
      sb.appendLine(tooltipExtended)
      sb.appendLine()

    sb.appendLine(m_toUnit.getName().colorize(COLOR_GOLD))
    sb.append(m_toUnit.getTooltipExtended())

    return sb.toStringAndDestroy()

  // --------------------------------------------------------------------------
  static function createDefault(ITlsUnitDefinition fromUnit, ITlsUnitDefinition toUnit, string tip) returns JobItemTooltipBuilder
    let builder = new JobItemTooltipBuilder()
    builder.setUnits(fromUnit, toUnit)
    builder.setDescription(tip)
    return builder