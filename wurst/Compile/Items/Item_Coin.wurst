package Item_Coin
import TlsItemDefinition
import HumanPlayerMetadata
import GameConstants
import FX
import RegisterEvents

// ============================================================================
class CoinItemDefinition extends TlsItemDefinition

  // --------------------------------------------------------------------------
  construct(int newId)
    super(newId, 'gold')
    setName("Coins")
    setTooltipBasic("Coins")
    setTooltipExtended("Resources used to advanced build camp structures and purchase rare items from the trader.")
    setAbilities("")
    setGoldCost(0)
    setNumberofCharges(1)
    setCanBeDropped(false)
    setCanBeSoldToMerchants(false)
    setCanBeSoldByMerchants(false)

// ============================================================================
@compiletime function createItems()
  new CoinItemDefinition(TlsItemIds.coinSmall)
    ..setName("Coin")
    ..setTooltipBasic("Coin")
    ..setDescription("Worth {0} gold!".format(COIN_REWARD_SMALL.toString()))
    ..setScalingValue(0.6)

  new CoinItemDefinition(TlsItemIds.coinMedium)
    ..setDescription("Worth {0} gold!".format(COIN_REWARD_MEDIUM.toString()))
    ..setScalingValue(0.8)
    ..setTintingColor1Red(255)
    ..setTintingColor2Green(200)
    ..setTintingColor3Blue(200)

  new CoinItemDefinition(TlsItemIds.coinLarge)
    ..setDescription("Worth {0} gold!".format(COIN_REWARD_LARGE.toString()))
    ..setScalingValue(1.0)
    ..setTintingColor1Red(255)
    ..setTintingColor2Green(125)
    ..setTintingColor3Blue(125)

  new CoinItemDefinition(TlsItemIds.coinEpic)
    ..setDescription("Worth {0} gold!".format(COIN_REWARD_EPIC.toString()))
    ..setScalingValue(1.2)
    ..setTintingColor1Red(255)
    ..setTintingColor2Green(0)
    ..setTintingColor3Blue(125)

// ============================================================================
function onItemAcquired()
  let itm = GetManipulatedItem()

  if (not itm.isCoinItem())
    return

  let manipulatingUnit = GetManipulatingUnit()
  let itemTypeId = itm.getTypeId()

  let ownerMetadata = manipulatingUnit.getOwnerHumanMetadata()
  if (ownerMetadata == null)
    return

  itm.remove()
  
  var reward = 0

  switch (itemTypeId)
    case TlsItemIds.coinSmall
      reward = COIN_REWARD_SMALL
    case TlsItemIds.coinMedium
      reward = COIN_REWARD_MEDIUM
    case TlsItemIds.coinLarge
      reward = COIN_REWARD_LARGE
    case TlsItemIds.coinEpic
      reward = COIN_REWARD_EPIC

  ownerMetadata.giveGold(reward)

  let pos = manipulatingUnit.getPos()

  FX.createFoundGoldEffect(pos, ownerMetadata.getPlayer())
  FX.createFoundGoldTag(pos, reward, ownerMetadata.getPlayer())

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM, function onItemAcquired)
  
  g_MainItemLibrary.commonSet.add(TlsItemIds.coinSmall, 0.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.coinMedium, 0.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.coinLarge, 0.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.coinEpic, 0.0)