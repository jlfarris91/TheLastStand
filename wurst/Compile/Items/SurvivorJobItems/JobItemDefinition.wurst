package JobItemDefinition
import public TlsItemDefinition
import StringExtensions
import StringBuilder
import ErrorHandling
import TlsUnitDefinition
import DataStringUtility
import LinkedList
import ColorUtility

constant int JOB_ITEM_BASE_ID = 'ratc'
constant int MAX_JOB_DESC_ITEM_COUNT = 32
constant colorA ITEM_NAME_COLOR = fromHexString("FF3B97D3")
constant colorA ITEM_VALUE_COLOR = COLOR_GOLD
constant colorA ARROW_COLOR = fromHexString("FF6cf542")

// ============================================================================
public class JobItemDefinition extends TlsItemDefinition
  construct(int newId, int abilId)
    super(newId, JOB_ITEM_BASE_ID)
    setActivelyUsed(true)
    setPerishable(true)
    setAbilities(commaList(abilId))

// ============================================================================
public interface IJobDescriptionItem
  function getName() returns string
  function getValue(int index) returns string
  function getValueCount() returns int

// ============================================================================
public class JobDescriptionItem implements IJobDescriptionItem
  private string m_name
  private LinkedList<string> m_values

  // --------------------------------------------------------------------------
  construct(string name)
    m_values = new LinkedList<string>()
    m_name = name

  // --------------------------------------------------------------------------
  construct(string name, string value)
    m_values = new LinkedList<string>()
    m_name = name
    addValue(value)

  // --------------------------------------------------------------------------
  ondestroy
    clear()
    destroy m_values
    m_values = null

  // --------------------------------------------------------------------------
  function clear()
    m_name = null
    m_values.clear()

  // --------------------------------------------------------------------------
  override function getName() returns string
    return m_name

  // --------------------------------------------------------------------------
  override function getValue(int index) returns string
    return m_values.get(index)
  
  // --------------------------------------------------------------------------
  override function getValueCount() returns int
    return m_values.size()

  // --------------------------------------------------------------------------
  function addValue(string value) returns JobDescriptionItem
    if (m_values.size() == MAX_JOB_DESC_ITEM_COUNT)
      error("Reached limit of number of values " + I2S(MAX_JOB_DESC_ITEM_COUNT))
    m_values.add(value)
    return this

// ============================================================================
/*
  Sturdy Spears

  A quiver of sturdy spears. Good for killing undead at medium range.

  [Survivor Job Change]
  Use this item on a Survivor to give them new abilities.
  Job assignment is permanent!

  Survivor Male -> Spearman

  Spearman
  Damage:       18-32
  Range:        350
  Abilities:    Ability 1
                Ability 2
                Ability 3
*/
public class JobItemTooltipBuilder
  private string m_fromUnitName
  private string m_toUnitName
  private string m_desc
  private LinkedList<IJobDescriptionItem> m_items

  // --------------------------------------------------------------------------
  construct()
    m_items = new LinkedList<IJobDescriptionItem>()

  // --------------------------------------------------------------------------
  ondestroy 
    clear()

    destroy m_items
    m_items = null

  // --------------------------------------------------------------------------
  function setUnits(string fromUnit, string toUnit) returns JobItemTooltipBuilder
    m_fromUnitName = fromUnit
    m_toUnitName = toUnit
    return this

  // --------------------------------------------------------------------------
  function getItemDescription() returns string
    return m_desc

  // --------------------------------------------------------------------------
  function setItemDescription(string desc) returns JobItemTooltipBuilder
    m_desc = desc
    return this

  // --------------------------------------------------------------------------
  function addItem(string name, string value) returns JobItemTooltipBuilder
    return addItem(new JobDescriptionItem(name, value))

  // --------------------------------------------------------------------------
  function addItem(IJobDescriptionItem itm) returns JobItemTooltipBuilder
    if (m_items.size() == MAX_JOB_DESC_ITEM_COUNT)
      error("Reached limit of job description items " + I2S(MAX_JOB_DESC_ITEM_COUNT))
    m_items.add(itm)
    return this

  // --------------------------------------------------------------------------
  function clear()
    m_fromUnitName = null
    m_toUnitName = null
    m_desc = null
    for itm in m_items
      destroy itm
    m_items.clear()

  // --------------------------------------------------------------------------
  function getDescriptionString() returns string
    let sb = new StringBuilder()

    if (m_desc != null)
      sb.appendLine(m_desc)
      sb.appendLine()

    sb.appendLine("[Survivor Job Change]".colorize(COLOR_GOLD))
    sb.appendLine("Use this item on a {0} to give them new abilities.".format(m_fromUnitName))
    sb.appendLine("Job assignment is permanent!".colorize(Colors.red))
    sb.appendLine()
    
    sb.append("Changes a ")
    sb.append(m_fromUnitName.colorize(COLOR_GOLD))
    sb.append(" to a ")
    sb.appendLine(m_toUnitName.colorize(COLOR_GOLD))

    return sb.toStringAndDestroy()

  // --------------------------------------------------------------------------
  function getTooltipExtendedString() returns string
    let sb = new StringBuilder()

    if (m_desc != null)
      sb.appendLine(m_desc)
      sb.appendLine()

    sb.appendLine("[Survivor Job Change]".colorize(COLOR_GOLD))
    sb.appendLine("Give this item to a {0} to give them new abilities.".format(m_fromUnitName))
    sb.appendLine("Job assignment is permanent!".colorize(Colors.red))
    sb.appendLine()

    sb.append("Changes a ")
    sb.append(m_fromUnitName.colorize(COLOR_GOLD))
    sb.append(" to a ")
    sb.appendLine(m_toUnitName.colorize(COLOR_GOLD))
    sb.appendLine()

    let padStr = " "
    for itm in m_items      
      sb.append(itm.getName().colorize(ITEM_NAME_COLOR))

      let itemValueCount = itm.getValueCount()
      if (itemValueCount == 1)
        sb.appendLine(itm.getValue(0).colorize(ITEM_VALUE_COLOR))
      else
        sb.appendLine()
        for j = 0 to itemValueCount - 1
          sb.append("- ".padLeft(10, padStr))
          sb.appendLine(itm.getValue(j).colorize(ITEM_VALUE_COLOR))

    return sb.toStringAndDestroy()

  // --------------------------------------------------------------------------
  static function createDefault(ITlsUnitDefinition fromUnit, ITlsUnitDefinition toUnit, string tip) returns JobItemTooltipBuilder
    let builder = new JobItemTooltipBuilder()

    builder.setUnits(fromUnit.getName(), toUnit.getName())
    builder.setItemDescription(tip)

    let toUnitId = toUnit.getNewId()
    let toUnitBaseId = toUnit.getBaseId()

    builder.addItem("Health: ", toUnit.getHitPointsMaximumBase().toString())

    if (toUnit.getAttacksEnabled() > 0)
      builder.addItem("Range: ", toUnit.getAttack1Range().toString())
      let minDmgStr = getDataString(toUnitId, toUnitBaseId, "mindmg1")
      let maxDmgStr = getDataString(toUnitId, toUnitBaseId, "maxdmg1")
      builder.addItem("Damage: ", "{0}-{1}".format(minDmgStr, maxDmgStr))
      builder.addItem("Attack: ", toUnit.getAttack1AttackType().toString())
    else
      builder.addItem("Attack: ", "Does not attack")
    
    builder.addItem("Armor: ", toUnit.getArmorType().toString())

    return builder