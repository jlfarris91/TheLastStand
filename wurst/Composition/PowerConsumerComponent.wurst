package PowerConsumerComponent
import UnitComponent
import initlater PowerSourceComponent
import Func
import Action

// ============================================================================
public class PowerConsumerComponent extends UnitComponent
  private PowerSourceComponent m_source
  private real m_powerRequired

  Func<bool> powerReadyCallback
  Action1<real> powerConsumedCallback

  // --------------------------------------------------------------------------
  construct (IUnitMetadata metadata)
    super(metadata)
    m_source = null

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return PowerConsumerComponent.typeId

  // --------------------------------------------------------------------------
  function setPowerRequired(real power)
    m_powerRequired = power

  // --------------------------------------------------------------------------
  function getPowerRequired() returns real
    return m_powerRequired

  // --------------------------------------------------------------------------
  function getPowerSource() returns PowerSourceComponent
    return m_source

  // --------------------------------------------------------------------------
  function link(PowerSourceComponent source)
    m_source = source

  // --------------------------------------------------------------------------
  function unlink()
    m_source = null

  // --------------------------------------------------------------------------
  function powerReady(PowerSourceComponent source)
    if (powerReadyCallback != null)
      if (powerReadyCallback.call())
        let powerRequired = getPowerRequired()
        let powerConsumed = source.consumePower(powerRequired)
        getOwnerUnit().addMana(powerConsumed)
        if (powerConsumedCallback != null)
          powerConsumedCallback.call(powerConsumed)

// ============================================================================
public function IUnitMetadata.getPowerConsumerComponent() returns PowerConsumerComponent
  return this.getComponent(typeInfo(PowerConsumerComponent.typeId)) castTo PowerConsumerComponent

// ============================================================================
public function IUnitMetadata.getOrAddPowerConsumerComponent() returns PowerConsumerComponent
  var component = this.getPowerConsumerComponent()
  if (component == null)
    component = this.addComponent(new PowerConsumerComponent(this)) castTo PowerConsumerComponent
  return component