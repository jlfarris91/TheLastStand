package GainXpPerKillComponent
import UnitComponent
import RegisterEvents
import Elites
import GrantXpOnDeathComponent
import HashMap

HashMap<unit, unit> m_xpSourceToTargetMap = new HashMap<unit, unit>()

// ============================================================================
public class GainXpPerKillComponent extends UnitComponent
  private int m_expPerKill = 1
  private int m_expPerEliteKill = 10

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return GainXpPerKillComponent.typeId

  // --------------------------------------------------------------------------
  function getExpPerKill() returns int
    return m_expPerKill

  // --------------------------------------------------------------------------
  function setExpPerKill(int exp)
    m_expPerKill = exp

  // --------------------------------------------------------------------------
  function getExpPerEliteKill() returns int
    return m_expPerEliteKill

  // --------------------------------------------------------------------------
  function setExpPerEliteKill(int exp)
    m_expPerEliteKill = exp

  // --------------------------------------------------------------------------
  function getExpValueForDyingUnit(unit dyingUnit) returns int

    let metadata = dyingUnit.getMetadata()
    if (metadata != null)
      let comp = metadata.getGrantXpOnDeathComponent()
      if (comp != null)
        return comp.getExpValue()

    if (dyingUnit.isElite())
      return getExpPerEliteKill()
    else
      return getExpPerKill()

// ============================================================================
public function setXpSource(unit source, unit target)
  m_xpSourceToTargetMap.put(source, target)

// ============================================================================
public function clearXpSource(unit source)
  m_xpSourceToTargetMap.remove(source)

// ============================================================================
public function getXpTarget(unit source) returns unit
  return m_xpSourceToTargetMap.get(source)

// ============================================================================
public function IUnitMetadata.getGainXpPerKillComponent() returns GainXpPerKillComponent
  return this.getComponent(typeInfo(GainXpPerKillComponent.typeId)) castTo GainXpPerKillComponent

// ============================================================================
public function IUnitMetadata.getOrAddGainXpPerKillComponent() returns GainXpPerKillComponent
  var component = this.getGainXpPerKillComponent()
  if (component == null)
    component = this.addComponent(new GainXpPerKillComponent(this)) castTo GainXpPerKillComponent
  return component

// ============================================================================
function onUnitKilled()

  let killingUnit = GetKillingUnit()
  let dyingUnit = GetDyingUnit()

  if (not dyingUnit.isEnemyOf(killingUnit))
    return

  var xpRecipient = getXpTarget(killingUnit)
  if (xpRecipient == null)
    xpRecipient = killingUnit

  let metadata = xpRecipient.getMetadata()
  if (metadata == null)
    return

  let gainXpComp = metadata.getGainXpPerKillComponent()
  if (gainXpComp == null or not gainXpComp.getEnabled())
    return

  xpRecipient.addXp(gainXpComp.getExpValueForDyingUnit(dyingUnit), true)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)