package CampCenterComponent
import HumanPlayerComponent
import UnitComponent
import RegisterEvents

// ============================================================================
public class CampCenterComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata metadata)
    super(metadata)

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()

    let ownerUnit = getOwnerUnit()
    let newPlayerMetadata = ownerUnit.getOwnerHumanPlayerComponent()
    newPlayerMetadata.addCampCenter(ownerUnit)

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()

    let ownerUnit = getOwnerUnit()
    let oldPlayerMetadata = ownerUnit.getOwnerHumanPlayerComponent()
    if (oldPlayerMetadata != null)
      oldPlayerMetadata.removeCampCenter(ownerUnit)

// ============================================================================
public function IUnitMetadata.getCampCenterComponent() returns CampCenterComponent
  return this.getComponent(CampCenterComponent.typeId) castTo CampCenterComponent

// ============================================================================
public function IUnitMetadata.getOrAddCampCenterComponent() returns CampCenterComponent
  var component = this.getCampCenterComponent()
  if (component == null)
    component = this.addComponent(new CampCenterComponent(this)) castTo CampCenterComponent
  return component

// ============================================================================
function onUnitKilled()
  let dyingUnit = GetDyingUnit()

  let metadata = dyingUnit.getMetadata()
  if (metadata == null)
    return

  let campCenterComp = metadata.getCampCenterComponent()
  if (campCenterComp == null)
    return

  let ownerHumanPlayerComp = dyingUnit.getOwner().getHumanPlayerComponent()
  if (ownerHumanPlayerComp == null)
    return

  ownerHumanPlayerComp.removeCampCenter(dyingUnit)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)