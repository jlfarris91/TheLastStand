package CampCenterComponent
import HumanPlayerMetadata
import UnitComponent
import RegisterEvents

// ============================================================================
public class CampCenterComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata metadata)
    super(metadata)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return CampCenterComponent.typeId

  // --------------------------------------------------------------------------
  override function onUnitChanged(unit oldUnit, unit newUnit)
    super.onUnitChanged(oldUnit, newUnit)

    if (oldUnit != null)
      let oldPlayerMetadata = oldUnit.getOwnerHumanMetadata()
      oldPlayerMetadata.removeCampCenter(oldUnit)

    if (newUnit != null)
      let newPlayerMetadata = newUnit.getOwnerHumanMetadata()
      newPlayerMetadata.addCampCenter(newUnit)

// ============================================================================
public function IUnitMetadata.getCampCenterComponent() returns CampCenterComponent
  return this.getComponent(typeInfo(CampCenterComponent.typeId)) castTo CampCenterComponent

// ============================================================================
public function IUnitMetadata.getOrAddCampCenterComponent() returns CampCenterComponent
  var component = this.getCampCenterComponent()
  if (component == null)
    component = this.addComponent(new CampCenterComponent(this)) castTo CampCenterComponent
  return component

// ============================================================================
function onUnitKilled()
  let dyingUnit = GetDyingUnit()

  let metadata = dyingUnit.getMetadata()
  if (metadata == null)
    return

  let campCenterComp = metadata.getCampCenterComponent()
  if (campCenterComp == null)
    return

  let ownerMetadata = dyingUnit.getOwner().getHumanMetadata()
  if (ownerMetadata == null)
    return

  ownerMetadata.removeCampCenter(dyingUnit)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)