package HeroComponent
import UnitComponent
import BackpackComponent
import ExperienceComponent
import RangedTargetXpReceieverComponent
import LooterComponent
import HumanPlayerComponent

// ============================================================================
public class HeroComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)
    setDisableOnDeath(false)

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()

    let ownerMetadata = getOwnerUnit().getOwner().getHumanPlayerComponent()
    ownerMetadata.setHero(getOwnerUnit())

    getOwner().getOrAddBackpackComponent()
    getOwner().getOrAddExperienceComponent()
    getOwner().getOrAddRangedTargetXpRecieverComponent()..setXpPerHit(1)
    getOwner().getOrAddLooterComponent()

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()

    let ownerUnit = getOwnerUnit()
    let ownerMetadata = ownerUnit.getOwner().getHumanPlayerComponent()
    if (ownerMetadata != null)
      ownerMetadata.setHero(null)

// ============================================================================
public function IUnitMetadata.getHeroComponent() returns HeroComponent
  return this.getComponent(HeroComponent.typeId) castTo HeroComponent

// ============================================================================
public function IUnitMetadata.getOrAddHeroComponent() returns HeroComponent
  var component = this.getHeroComponent()
  if (component == null)
    component = this.addComponent(new HeroComponent(this)) castTo HeroComponent
  return component

// ============================================================================
public function unit.getHeroComponent() returns HeroComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getHeroComponent() : null

// ============================================================================
public function unit.getOrAddHeroComponent() returns HeroComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getOrAddHeroComponent() : null
