package RangedTargetComponent
import UnitComponent
import DamageEvent
import UnitMetadata
import Type
import FX
import GameConstants

// ============================================================================
public class RangedTargetComponent extends UnitComponent
  private DamageListener m_onDamagedHandler

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()
    listenToDamageEvent()

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    unlistenToDamageEvent()
  
  // --------------------------------------------------------------------------
  private function onDamaged()
    let source = DamageEvent.getSource()
    let target = DamageEvent.getTarget()

    if (target != getOwnerUnit())
      return

    if (source.getOwner() == PLAYER_UNDEAD)
      return
    
    DamageEvent.setAmount(0)
    FX.createFoundMaterialsTag(source.getPos(), 1, source.getOwner())

  // --------------------------------------------------------------------------
  private function listenToDamageEvent()
    unlistenToDamageEvent()
    m_onDamagedHandler = DamageEvent.addUnreducedListener() -> 
      onDamaged()

  // --------------------------------------------------------------------------
  private function unlistenToDamageEvent()
    if (m_onDamagedHandler != null)
      destroy m_onDamagedHandler
      m_onDamagedHandler = null
      
// ============================================================================
public function IUnitMetadata.getOrAddRangedTargetComponent() returns RangedTargetComponent
  var component = this.getComponent(Type(RangedTargetComponent.typeId)) castTo RangedTargetComponent
  if (component == null)
    component = this.addComponent(new RangedTargetComponent(this)) castTo RangedTargetComponent
  return component