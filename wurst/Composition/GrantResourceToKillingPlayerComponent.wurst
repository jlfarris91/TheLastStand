package GrantResourceToKillingPlayerComponent
import UnitComponent
import FX
import Range
import HumanPlayerMetadata
import RegisterEvents
import Math

// ============================================================================
public class GrantResourceToKillingPlayerComponent extends UnitComponent
  private real m_matsChance = 0
  private rangeInt m_matsAmountRange = INT_RANGE_ZERO
  private real m_goldChance = 0
  private rangeInt m_goldAmountRange = INT_RANGE_ZERO

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  function getMaterialsChance() returns real
    return m_matsChance

  // --------------------------------------------------------------------------
  function setMaterialsChance(real value)
    m_matsChance = value

  // --------------------------------------------------------------------------
  function getMaterialsAmountRange() returns rangeInt
    return m_matsAmountRange

  // --------------------------------------------------------------------------
  function setMaterialsAmountRange(rangeInt range)
    m_matsAmountRange = range

  // --------------------------------------------------------------------------
  function getGoldChance() returns real
    return m_goldChance

  // --------------------------------------------------------------------------
  function setGoldChance(real value)
    m_goldChance = value

  // --------------------------------------------------------------------------
  function getGoldAmountRange() returns rangeInt
    return m_goldAmountRange

  // --------------------------------------------------------------------------
  function setGoldAmountRange(rangeInt range)
    m_goldAmountRange = range

// ============================================================================
public function IUnitMetadata.getGrantResourceToKillingPlayerComponent() returns GrantResourceToKillingPlayerComponent
  return this.getComponent(typeInfo(GrantResourceToKillingPlayerComponent.typeId)) castTo GrantResourceToKillingPlayerComponent

// ============================================================================
public function IUnitMetadata.getOrAddGrantResourceToKillingPlayerComponent() returns GrantResourceToKillingPlayerComponent
  var component = this.getGrantResourceToKillingPlayerComponent()
  if (component == null)
    component = this.addComponent(new GrantResourceToKillingPlayerComponent(this)) castTo GrantResourceToKillingPlayerComponent
  return component

// ============================================================================
function onUnitKilled()

  let dyingUnit = GetDyingUnit()
  let metadata = dyingUnit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getGrantResourceToKillingPlayerComponent()
  if (comp == null or not comp.getEnabled())
    return

  let killingUnit = GetKillingUnit()
  if (killingUnit == null)
    return

  let killingPlayer = killingUnit.getOwner()
  let killingPlayerMetadata = killingPlayer.getHumanMetadata()
  if (killingPlayerMetadata == null)
    return

  let pos = dyingUnit.getPos()

  if (passesChanceCheck(comp.getMaterialsChance()))
    let matsGranted = comp.getMaterialsAmountRange().getRandom()
    killingPlayerMetadata.giveMaterials(matsGranted)
    FX.createFoundMaterialsEffect(pos, killingPlayer)
    FX.createFoundMaterialsTag(pos, matsGranted, killingPlayer)

  if (passesChanceCheck(comp.getGoldChance()))
    let goldGranted = comp.getGoldAmountRange().getRandom()
    killingPlayerMetadata.giveGold(goldGranted)
    FX.createFoundGoldEffect(pos, killingPlayer)
    FX.createFoundGoldTag(pos, goldGranted, killingPlayer)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onUnitKilled)