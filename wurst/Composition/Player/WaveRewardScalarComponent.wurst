package WaveRewardScalarComponent
import PlayerComponent
import PlayerMetadata
import Func

group g_tempGroup

// ============================================================================
public class WaveRewardScalarComponent extends PlayerComponent
  private Func1<int, int> m_xpFunc
  private Func1<int, int> m_matsFunc
  private Func1<int, int> m_goldFunc

  // --------------------------------------------------------------------------
  construct (IPlayerMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  ondestroy
    if (m_xpFunc != null)
      m_xpFunc.release()
      m_xpFunc = null
    if (m_matsFunc != null)
      m_matsFunc.release()
      m_matsFunc = null
    if (m_goldFunc != null)
      m_goldFunc.release()
      m_goldFunc = null

  // --------------------------------------------------------------------------
  function scaleXpReward(int xp) returns int
    return m_xpFunc != null ? m_xpFunc.call(xp) : xp

  // --------------------------------------------------------------------------
  function scaleMatsReward(int mats) returns int
    return m_matsFunc != null ? m_matsFunc.call(mats) : mats

  // --------------------------------------------------------------------------
  function scaleGoldReward(int gold) returns int
    return m_goldFunc != null ? m_goldFunc.call(gold) : gold

  // --------------------------------------------------------------------------
  function setXpFunc(Func1<int, int> func)
    func.acquire()
    if (m_xpFunc != null)
      m_xpFunc.release()
    m_xpFunc = func

  // --------------------------------------------------------------------------
  function getXpFunc() returns Func1<int, int>
    return m_xpFunc

  // --------------------------------------------------------------------------
  function setMatsFunc(Func1<int, int> func)
    func.acquire()
    if (m_matsFunc != null)
      m_matsFunc.release()
    m_matsFunc = func

  // --------------------------------------------------------------------------
  function getMatsFunc() returns Func1<int, int>
    return m_matsFunc

  // --------------------------------------------------------------------------
  function setGoldFunc(Func1<int, int> func)
    func.acquire()
    if (m_goldFunc != null)
      m_goldFunc.release()
    m_goldFunc = func

  // --------------------------------------------------------------------------
  function getGoldFunc() returns Func1<int, int>
    return m_goldFunc

// ============================================================================
public function IPlayerMetadata.getWaveRewardScalarComponent() returns WaveRewardScalarComponent
  return this.getComponent(WaveRewardScalarComponent.typeId) castTo WaveRewardScalarComponent

// ============================================================================
public function IPlayerMetadata.getOrAddWaveRewardScalarComponent() returns WaveRewardScalarComponent
  var component = this.getWaveRewardScalarComponent()
  if (component == null)
    component = this.addComponent(new WaveRewardScalarComponent(this)) castTo WaveRewardScalarComponent
  return component

// ============================================================================
init
  g_tempGroup = CreateGroup()