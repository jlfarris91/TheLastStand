package ScoutTowerComponent
import UnitComponent
import Events
import WorkstationComponent
import TlsUnitIds
import ObjectIdGenerator
import AbilityObjEditing

constant int SCOUT_TOWER_SIGHT_ABILITY_ID = compiletime(ABIL_ID_GEN.next())
constant int GUARD_TOWER_SIGHT_ABILITY_ID = compiletime(ABIL_ID_GEN.next())

// ============================================================================
@compiletime function createAbility()
  new AbilityDefinitionSightBonus(SCOUT_TOWER_SIGHT_ABILITY_ID)
  ..setSightRangeBonus(1, 1800)
  new AbilityDefinitionSightBonus(GUARD_TOWER_SIGHT_ABILITY_ID)
  ..setSightRangeBonus(1, 900)

// ============================================================================
public class ScoutTowerComponent extends UnitComponent
  private _handle m_workedEnteredCallback = HANDLE_INVALID
  private _handle m_workedExitedCallback = HANDLE_INVALID
  private bool m_activated = false

  // --------------------------------------------------------------------------
  construct (IUnitMetadata metadata)
    super(metadata)

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    
    let workstationComponent = getOwner().getOrAddWorkstationComponent()
    ..setWorkerUnitType(TlsUnitIds.Survivors.survivorMale)
    ..setMaxWorkers(1)

    m_workedEnteredCallback = workstationComponent.workerEntered.registerDispatched(this, () -> onWorkerEntered())
    m_workedExitedCallback = workstationComponent.workerExited.registerDispatched(this, () -> onWorkedExited())

    if (getNumberOfWorkers() > 0)
      activate()

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()

    let workstationComponent = getOwner().getWorkstationComponent()
    if (workstationComponent != null)
      workstationComponent.workerEntered.unregister(m_workedEnteredCallback)
      workstationComponent.workerExited.unregister(m_workedExitedCallback)

    deactivate()
    
  // --------------------------------------------------------------------------
  private function onWorkerEntered()
    if (getNumberOfWorkers() == 1)
      activate()

  // --------------------------------------------------------------------------
  private function onWorkedExited()
    if (getNumberOfWorkers() == 0)
      deactivate()

  // --------------------------------------------------------------------------
  private function activate()

    if (m_activated)
      return

    m_activated = true

    let ownerUnit = getOwnerUnit()

    if (ownerUnit.getTypeId() == TlsUnitIds.scoutTower)
      ownerUnit.removeAbility(GUARD_TOWER_SIGHT_ABILITY_ID)
      ownerUnit.addAbility(SCOUT_TOWER_SIGHT_ABILITY_ID)

    else if (ownerUnit.getTypeId() == TlsUnitIds.guardTower)
      ownerUnit.removeAbility(SCOUT_TOWER_SIGHT_ABILITY_ID)
      ownerUnit.addAbility(GUARD_TOWER_SIGHT_ABILITY_ID)
      getOwnerUnit().setFieldWeapon(UNIT_WEAPON_BF_ATTACKS_ENABLED, 0, true)

  // --------------------------------------------------------------------------
  private function deactivate()

    if (m_activated == false)
      return

    m_activated = false

    let ownerUnit = getOwnerUnit()

    ownerUnit.removeAbility(SCOUT_TOWER_SIGHT_ABILITY_ID)
    ownerUnit.removeAbility(GUARD_TOWER_SIGHT_ABILITY_ID)

    if (ownerUnit.getTypeId() == TlsUnitIds.guardTower)
      getOwnerUnit().setFieldWeapon(UNIT_WEAPON_BF_ATTACKS_ENABLED, 0, false)

  // --------------------------------------------------------------------------
  private function getNumberOfWorkers() returns int
    let workstationComponent = getOwner().getWorkstationComponent()
    return workstationComponent != null ? workstationComponent.getWorkerUnitCount() : 0

// ============================================================================
public function IUnitMetadata.getScoutTowerComponent() returns ScoutTowerComponent
  return this.getComponent(ScoutTowerComponent.typeId) castTo ScoutTowerComponent

// ============================================================================
public function IUnitMetadata.getOrAddScoutTowerComponent() returns ScoutTowerComponent
  var component = this.getScoutTowerComponent()
  if (component == null)
    component = this.addComponent(new ScoutTowerComponent(this)) castTo ScoutTowerComponent
  return component