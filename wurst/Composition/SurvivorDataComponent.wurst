package SurvivorDataComponent
import UnitComponent
import UnitMetadata
import Type
import Composition
import Statistic
import ClosureTimers
import RegisterEvents
import initlater StatsBoard
import SurvivorData

// ============================================================================
public class SurvivorDataComponent extends UnitComponent
  private SurvivorData m_survivorData
  private StatsBoard m_statsBoard

  // --------------------------------------------------------------------------
  construct(IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  ondestroy
    destroy m_survivorData

  // --------------------------------------------------------------------------
  override function initialize()
    super.initialize()
    m_statsBoard = new StatsBoard(this, "Stats")
    m_statsBoard.updateMultiboardRows()
    
  // --------------------------------------------------------------------------
  function show()
    if (not m_statsBoard.isShowing())
      m_statsBoard.updateMultiboardRows()
      m_statsBoard.update()
      m_statsBoard.show()
    
  // --------------------------------------------------------------------------
  function hide()
    if (m_statsBoard.isShowing())
      m_statsBoard.hide()

  // --------------------------------------------------------------------------
  function getSurvivorData() returns SurvivorData
    return m_survivorData

  // --------------------------------------------------------------------------
  function setSurvivorData(SurvivorData stats)
    m_survivorData = stats

  // --------------------------------------------------------------------------
  function getStatistic(string key) returns IStatistic
    return m_survivorData.getStatistic(key)

  // --------------------------------------------------------------------------
  function addStatistic(string key, IStatistic statistic)
    m_survivorData.addStatistic(key, statistic)

// ============================================================================
public function IUnitMetadata.getSurvivorDataComponent() returns SurvivorDataComponent
  return this.getComponent(Type(SurvivorDataComponent.typeId)) castTo SurvivorDataComponent

// ============================================================================
public function IUnitMetadata.getOrAddSurvivorDataComponent() returns SurvivorDataComponent
  var component = this.getSurvivorDataComponent()
  if (component == null)
    component = this.addComponent(new SurvivorDataComponent(this)) castTo SurvivorDataComponent
  return component

// ============================================================================
function onUnitSelected()
  let u = GetTriggerUnit()
  let metadata = u.getMetadata()
  if (metadata == null)
    return
  let statsComponent = metadata.getSurvivorDataComponent()
  if (statsComponent == null)
    return
  statsComponent.show()

// ============================================================================
function onUnitDeselected()
  let u = GetTriggerUnit()
  let metadata = u.getMetadata()
  if (metadata == null)
    return
  let statsComponent = metadata.getSurvivorDataComponent()
  if (statsComponent == null)
    return
  statsComponent.hide()

// ============================================================================
function delayedInit()
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELECTED, function onUnitSelected)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DESELECTED, function onUnitDeselected)

// ============================================================================
init
  doAfter(1.0) () ->
    delayedInit()