package MinimapIconComponent
import UnitComponent
import MinimapIcon
import Visibility

// ============================================================================
public class MinimapIconComponent extends UnitComponent
  protected minimapicon m_icon
  protected color m_color = color(255, 255, 255)
  protected string m_iconPath = null

  use VisibilityModule

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  function update(string iconPath, color color)
    m_iconPath = iconPath
    m_color = color
    rebuildIcon()
  
  // --------------------------------------------------------------------------
  override protected function onEnabled()
    super.onEnabled()
    rebuildIcon()

  // --------------------------------------------------------------------------
  override protected function onDisabled()
    super.onDisabled()
    destroyIcon()

  // --------------------------------------------------------------------------
  override protected function onVisibilityChanged()
    if (m_icon == null)
      if (isVisibleForAnyPlayers())
        rebuildIcon()
      return
    updateIconVisibility()
  
  // --------------------------------------------------------------------------
  private function rebuildIcon()

    destroyIcon()

    if (m_iconPath == null)
      return

    m_icon = createMinimapIcon(getOwnerUnit(), m_color, m_iconPath, FOG_OF_WAR_MASKED)

    updateIconVisibility()

  // --------------------------------------------------------------------------
  private function updateIconVisibility()

    if (m_icon == null)
      return

    if (isHidden() or isVisibleForAllPlayers())
      m_icon.setVisible(isVisibleForAllPlayers())
      return

    for i = 0 to bj_MAX_PLAYERS-1
      if (isVisibleFor(players[i]))
        m_icon.showFor(players[i])

  // --------------------------------------------------------------------------
  private function destroyIcon()
    if (m_icon != null)
      m_icon.destr()
      m_icon = null

// ============================================================================
public function IUnitMetadata.getMinimapIconComponent() returns MinimapIconComponent
  return this.getComponent(MinimapIconComponent.typeId) castTo MinimapIconComponent

// ============================================================================
public function IUnitMetadata.getOrAddMinimapIconComponent() returns MinimapIconComponent
  var component = this.getMinimapIconComponent()
  if (component == null)
    component = this.addComponent(new MinimapIconComponent(this)) castTo MinimapIconComponent
  return component

// ============================================================================
public function unit.getMinimapIconComponent() returns MinimapIconComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getMinimapIconComponent() : null

// ============================================================================
public function unit.getOrAddMinimapIconComponent() returns MinimapIconComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getOrAddMinimapIconComponent() : null