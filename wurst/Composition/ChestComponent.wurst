package ChestComponent
import RegisterEvents
import TlsUnitIds
import UnitMetadata

// ============================================================================
public class ChestComponent extends UnitComponent
  private bool _isOpen

  // --------------------------------------------------------------------------
  construct (IUnitMetadata metadata)
    super(metadata)

  // --------------------------------------------------------------------------
  function isOpen() returns bool
    return this._isOpen

  // --------------------------------------------------------------------------
  function isClosed() returns bool
    return not this._isOpen

  // --------------------------------------------------------------------------
  function openChest()
    if (this.isClosed())
      this.getOwnerUnit().queueAnimation("death alternate")
      this._isOpen = true

  // --------------------------------------------------------------------------
  function closeChest()
    if (this.isOpen())
      this.getOwnerUnit().queueAnimation("birth")
      this._isOpen = false

// ============================================================================
public function IUnitMetadata.getChestComponent() returns ChestComponent
  return this.getComponent(ChestComponent.typeId) castTo ChestComponent

// ============================================================================
public function IUnitMetadata.getOrAddChestComponent() returns ChestComponent
  var component = this.getChestComponent()
  if (component == null)
    component = this.addComponent(new ChestComponent(this)) castTo ChestComponent
  return component

// ============================================================================
function onChestSelected()
  let selectedUnit = GetTriggerUnit()
  if (selectedUnit.getTypeId() != TlsUnitIds.chest)
    return

  let metadata = selectedUnit.getMetadata()
  if (metadata == null)
    return

  let chest = metadata.getChestComponent()
  if (chest != null)
    chest.openChest()

// ============================================================================
function onChestDeselected()
  let deselectedUnit = GetTriggerUnit()
  if (deselectedUnit.getTypeId() != TlsUnitIds.chest)
    return

  let metadata = deselectedUnit.getMetadata()
  if (metadata == null)
    return

  let chest = metadata.getChestComponent()
  if (chest != null)
    chest.closeChest()

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELECTED, function onChestSelected)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DESELECTED, function onChestDeselected)
  