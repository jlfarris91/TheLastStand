package TLSMenuBar
import LoadToc
import ClosureTimers
import HumanPlayers
import HashMap

import TLSMenuButton
import AlliancesButton
import DiscordLinkButton
import CameraButton
import HiveLinkButton

public constant vec2 MENU_POS = vec2(0.09, 0.55)

public constant int MENU_ITEM_HIVE = 0
public constant int MENU_ITEM_DISCORD = 1
public constant int MENU_ITEM_ALLIANCES = 2
public constant int MENU_ITEM_CAMERA = 3
public constant int MENU_ITEM_COUNT = 4

HashMap<player, TLSMenuBar> g_playerToMenuMap = new HashMap<player, TLSMenuBar>()

// ============================================================================
public class TLSMenuBar
  player m_player
  TLSMenuButton array[MENU_ITEM_COUNT] m_items

  // --------------------------------------------------------------------------
  function invalidateLayout()
    for i = 0 to MENU_ITEM_COUNT - 1
      m_items[i].setPos(vec2(MENU_POS.x + (i * 0.035), MENU_POS.y))

// ============================================================================
function createMenusForPlayers()
  for _player in g_PlayingHumanPlayers
    let menu = new TLSMenuBar()
    menu.m_player = _player
    menu.m_items[MENU_ITEM_HIVE] = new HiveToggleButton()..setPlayer(_player)
    menu.m_items[MENU_ITEM_DISCORD] = new DiscordToggleButton()..setPlayer(_player)
    menu.m_items[MENU_ITEM_ALLIANCES] = new AlliancesToggleButton()..setPlayer(_player)
    menu.m_items[MENU_ITEM_CAMERA] = new CameraToggleButton()..setPlayer(_player)
    menu.invalidateLayout()
    g_playerToMenuMap.put(_player, menu)

// ============================================================================
public function getTLSMenu(player p) returns TLSMenuBar
  return g_playerToMenuMap.get(p)

// ============================================================================
init
  ensureTocIsLoaded()
  nullTimer(() -> createMenusForPlayers())