package SurvivorJobFrame
import Frame
import ClosureFrames
import SurvivorJobs
import StandardTooltip
import ColorUtility
import Survivors
import ClosureKeyPresses

public enum SurvivorJobFrameEnabledState
  DISABLED
  ENABLED
  ACTIVE

public function SurvivorJobFrameEnabledState.toString() returns string
  switch (this)
    case DISABLED 
      return "DISABLED"
    case ENABLED
      return "ENABLED"
    case ACTIVE
      return "ACTIVE"

public constant string UNKNOWN_JOB_ICON_PATH = "ui\\widgets\\escmenu\\human\\quest-unknown.dds"

// ============================================================================
public class SurvivorJobFrame extends Frame
  private framehandle m_mainButtonIcon
  private framehandle m_activeIcon
  private framehandle m_hoverActionText
  private SurvivorJobType m_job
  private string m_iconPath
  private string m_iconPathDisabled
  private int m_level
  private SurvivorJobFrameEnabledState m_enabledState
  private int m_isHovered
  private string m_jobTitle
  private string m_jobTooltip
  private string m_itemName
  private player m_player
  private bool m_isCtrlHeld
  private bool m_showHoverActionText

  // --------------------------------------------------------------------------
  construct(player _player)
    super(createFrame("SurvivorJobFrame", GAME_UI, 0, 0))

    m_player = _player
    m_iconPath = UNKNOWN_JOB_ICON_PATH
    m_iconPathDisabled = UNKNOWN_JOB_ICON_PATH
    m_level = 1
    m_enabledState = SurvivorJobFrameEnabledState.DISABLED
    
    let frameHandle = getFrameHandle()

    let mainButton = getFrame("MainButton", 0)..setLevel(3)
    m_mainButtonIcon = getFrame("MainButtonIcon", 0)
    m_activeIcon = getFrame("ActiveIcon", 0)..setLevel(4)
    getFrame("LevelBackdrop", 0)..setLevel(5)

    m_hoverActionText = getFrame("UnassignJobText", 0)
    ..setParent(frameHandle)
    ..setSize(0.035, 0.008)
    ..setLevel(10)
    ..setPoint(FRAMEPOINT_BOTTOMLEFT, frameHandle, FRAMEPOINT_TOPLEFT, 0.0, 0.001)
    ..setText("Unlearn")
    ..hide()

    mainButton.onMouseEnter() -> 
      m_isHovered++
      updateActiveFrame()
      updateUnassignButton()
      showTooltip()

    mainButton.onMouseLeave() -> 
      m_isHovered--
      updateActiveFrame()
      updateUnassignButton()
      hideTooltip()

    mainButton.onClick() ->
      if (m_isCtrlHeld)
        SurvivorUtility.orderSelectedUnitsToUnlearnJob(m_player, m_job)
      else
        SurvivorUtility.orderSelectedUnitsToActivateJob(m_player, m_job)
      mainButton.clearFocus()

    updateIconFrame()
    updateActiveFrame()
    updateVisibility()
    updateUnassignButton()

    let framePlayer = m_player
    onKeyPress(framePlayer, OSKEY_LCONTROL, OSKEY_META.CTRL) () ->
      m_isCtrlHeld = true
      updateUnassignButton()

    onKeyRelease(framePlayer, OSKEY_LCONTROL) () ->
      m_isCtrlHeld = false
      updateUnassignButton()

  // --------------------------------------------------------------------------
  function setJobDef(SurvivorJobType value)
    m_job = value

  // --------------------------------------------------------------------------
  function getJobDef() returns SurvivorJobType
    return m_job

  // --------------------------------------------------------------------------
  function getIconPath() returns string
    return m_iconPath
  
  // --------------------------------------------------------------------------
  function setIconPath(string value)
    if (m_iconPath != value)
      m_iconPath = value
      updateIconFrame()

  // --------------------------------------------------------------------------
  function getIconPathDisabled() returns string
    return m_iconPathDisabled
  
  // --------------------------------------------------------------------------
  function setIconPathDisabled(string value)
    if (m_iconPathDisabled != value)
      m_iconPathDisabled = value
      updateIconFrame()

  // --------------------------------------------------------------------------
  function setEnabledState(SurvivorJobFrameEnabledState value)
    if (m_enabledState != value)
      m_enabledState = value
      updateActiveFrame()
      updateIconFrame()
      updateVisibility()
  
  // --------------------------------------------------------------------------
  function getEnabledState() returns SurvivorJobFrameEnabledState
    return m_enabledState

  // --------------------------------------------------------------------------
  function setJobTitle(string value)
    m_jobTitle = value

  // --------------------------------------------------------------------------
  function getJobTitle() returns string
    return m_jobTitle

  // --------------------------------------------------------------------------
  function setJobTooltip(string value)
    m_jobTooltip = value

  // --------------------------------------------------------------------------
  function getJobTooltip() returns string
    return m_jobTooltip

  // --------------------------------------------------------------------------
  function setItemName(string value)
    m_itemName = value

  // --------------------------------------------------------------------------
  function getItemName() returns string
    return m_itemName

  // --------------------------------------------------------------------------
  private function updateUnassignButton()
    let showHoverActionText = m_isHovered > 0 and m_isCtrlHeld and m_job != SurvivorJobType.None
    if (showHoverActionText != m_showHoverActionText)
      m_showHoverActionText = showHoverActionText
      m_hoverActionText.setVisible(m_showHoverActionText)
      showTooltip()

  // --------------------------------------------------------------------------
  private function showTooltip()
    if (m_showHoverActionText)
      showUnassignTooltip()
    else
      showJobTooltip()

  // --------------------------------------------------------------------------
  private function hideTooltip()
    StandardTooltip.hide(m_player) 

  // --------------------------------------------------------------------------
  private function showJobTooltip()
    var title = m_jobTitle

    if (m_enabledState == DISABLED and m_job != SurvivorJobType.None)
      title = "{0} - {1}".format(m_jobTitle, "Requires {0}".format(m_itemName).colorize(Colors.gold))

    if (m_job == SurvivorJobType.None)
      title += " (|cffffcc00Z|r)"
        
    if (m_enabledState == ENABLED)
      if (m_job != SurvivorJobType.None)
        title += "|n{0}".format("Click to assign job".colorize(Colors.gold))
      else
        title += "|n{0}".format("Click to unassign current job".colorize(Colors.gold))
    
    if (m_enabledState != DISABLED and m_job != SurvivorJobType.None)
      title += "|n{0}".format("Ctrl + click to unlearn job".colorize(Colors.gold))

    StandardTooltip.show(title, m_jobTooltip, m_player)

  // --------------------------------------------------------------------------
  private function showUnassignTooltip()
    let title = "Ctrl + click to unlearn the {0} job".format(m_jobTitle).colorize(Colors.gold)
    let tooltip = "Unlearn this job and place the {0}|nitem back into the Survivor's inventory.".format(m_itemName)
    StandardTooltip.show(title, tooltip, m_player)

  // --------------------------------------------------------------------------
  private function updateIconFrame()
    if (m_mainButtonIcon != null)
      m_mainButtonIcon.setTexture(m_enabledState == DISABLED ? m_iconPathDisabled : m_iconPath, 0, true)

  // --------------------------------------------------------------------------
  private function updateActiveFrame()
    if (m_activeIcon != null)
      m_activeIcon.setVisible(
        m_enabledState == SurvivorJobFrameEnabledState.ACTIVE or 
        (m_enabledState == SurvivorJobFrameEnabledState.ENABLED and m_isHovered > 0))
  
  // --------------------------------------------------------------------------
  private function updateVisibility()
    let visible = m_enabledState != SurvivorJobFrameEnabledState.DISABLED
    if (visible)
      show()
    else
      hide()

  // --------------------------------------------------------------------------
  override function show()
    super.show()
    m_isCtrlHeld = false
    m_isHovered = 0
    updateUnassignButton()
    
  // --------------------------------------------------------------------------
  override function hide()
    super.hide()
    m_isCtrlHeld = false
    m_isHovered = 0