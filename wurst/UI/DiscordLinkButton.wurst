package DiscordLinkButton
import ClosureFrames
import LoadToc
import ClosureTimers
import HumanPlayers
import HashMap
import ProjectConstants

HashMap<player, DiscordToggleButton> g_playerToDiscordButtonMap = new HashMap<player, DiscordToggleButton>()

// ============================================================================
public class DiscordToggleButton
  private player m_player
  private framehandle m_buttonFrame
  private framehandle m_pushedBackdropFrame
  private framehandle m_highlightFrame
  private framehandle m_editBoxFrame
  private bool m_checked

  // --------------------------------------------------------------------------
  construct(player p)
    m_player = p
    createFrame()

  // --------------------------------------------------------------------------
  ondestroy
    if (m_buttonFrame != null)
      m_buttonFrame.remove()
    if (m_pushedBackdropFrame != null)
      m_pushedBackdropFrame.remove()
    if (m_highlightFrame != null)
      m_highlightFrame.remove()
    if (m_editBoxFrame != null)
      m_editBoxFrame.remove()      

  // --------------------------------------------------------------------------
  function createFrame()
    m_buttonFrame = BlzCreateFrame("DiscordButtonTemplate", BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0), 0, 0)

    BlzGetFrameByName("ButtonIcon", 0).setTexture("Discord.tga", 0, true)
    BlzGetFrameByName("ButtonIconDisabled", 0).setTexture("Discord.tga", 0, true)

    m_pushedBackdropFrame = BlzGetFrameByName("ButtonPushedBackdrop", 0)
    m_highlightFrame = BlzGetFrameByName("ButtonMouseOverHighlight", 0)

    m_buttonFrame.setAbsPoint(FRAMEPOINT_CENTER, 0.22, 0.16)

    m_buttonFrame.onClick(() -> onClicked())
    m_buttonFrame.onMouseEnter(() -> onMouseEnter())
    m_buttonFrame.onMouseLeave(() -> onMouseLeave())

    m_editBoxFrame = BlzCreateFrameByType(
      "GLUEEDITBOX",
      "DiscordEditBox",
      BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0),
      "EscMenuEditBoxTemplate", 0)
    m_editBoxFrame.hide()

    m_editBoxFrame.setSize(0.15, 0.03)
    m_editBoxFrame.setPoint(FRAMEPOINT_BOTTOMLEFT, m_buttonFrame, FRAMEPOINT_TOPLEFT, -0.0002, 0.0)
    m_editBoxFrame.setText(DISCORD_LINK)

    m_editBoxFrame.onEditboxChange() -> 
      if (m_editBoxFrame.getText() != DISCORD_LINK)
        m_editBoxFrame.setText(DISCORD_LINK)

    m_buttonFrame.hide()
    m_buttonFrame.show(m_player)

  // --------------------------------------------------------------------------
  function getIsChecked() returns bool
    return m_checked

  // --------------------------------------------------------------------------
  function setIsChecked(bool checked)
    if (m_checked != checked)
      m_checked = checked
      if (m_checked)
        onChecked()
      else
        onUnchecked()

  // --------------------------------------------------------------------------
  function onClicked()
    setIsChecked(not getIsChecked())

  // --------------------------------------------------------------------------
  function onMouseEnter()
    m_highlightFrame.show(m_player)

  // --------------------------------------------------------------------------
  function onMouseLeave()
    if (not getIsChecked())
      m_highlightFrame.hide()

  // --------------------------------------------------------------------------
  private function onChecked()
    m_pushedBackdropFrame.show(m_player)
    m_highlightFrame.show(m_player)
    m_editBoxFrame.show(m_player)

  // --------------------------------------------------------------------------
  private function onUnchecked()
    m_pushedBackdropFrame.hide()
    m_highlightFrame.hide()
    m_editBoxFrame.hide()

// ============================================================================
function createButtonsForPlayers()
  for p in g_PlayingHumanPlayers
    g_playerToDiscordButtonMap.put(p, new DiscordToggleButton(p))

// ============================================================================
function getDiscordToggleButton(player p) returns DiscordToggleButton
  return g_playerToDiscordButtonMap.get(p)

// ============================================================================
init
  ensureTocIsLoaded()
  nullTimer(() -> createButtonsForPlayers())