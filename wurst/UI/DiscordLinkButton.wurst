package DiscordLinkButton
import ClosureFrames
import ProjectConstants
import TLSMenuToggleButton
import initlater TLSMenuBar

// ============================================================================
public class DiscordToggleButton extends TLSMenuToggleButton
  private framehandle m_editBoxFrame

  // --------------------------------------------------------------------------
  construct(TLSMenuBar menuBar)
    super(menuBar, "DiscordLinkMenuButtonTemplate")
    setIconPath("Discord.tga")
    setIconDisabledPath("Discord.tga")
    setTooltipNormal("Discord Link")
    setTooltipExtended("Display the link to the Last Stand Discord server")

  // --------------------------------------------------------------------------
  protected override function initialize()
    super.initialize()

    m_editBoxFrame = BlzCreateFrameByType(
      "GLUEEDITBOX",
      "DiscordEditBox",
      BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0),
      "EscMenuEditBoxTemplate", 0)
    m_editBoxFrame.hide()

    m_editBoxFrame.setSize(0.15, 0.03)
    m_editBoxFrame.setPoint(FRAMEPOINT_TOPLEFT, getFrameHandle(), FRAMEPOINT_BOTTOMLEFT, -0.0002, 0.0)
    m_editBoxFrame.setText(DISCORD_LINK)

    m_editBoxFrame.onEditboxChange() -> 
      if (m_editBoxFrame.getText() != DISCORD_LINK)
        m_editBoxFrame.setText(DISCORD_LINK)

  // --------------------------------------------------------------------------
  protected override function destroyFrames()
    super.destroyFrames()
    
    if (m_editBoxFrame != null)
      m_editBoxFrame.remove()
      m_editBoxFrame = null

  // --------------------------------------------------------------------------
  protected override function onChecked()
    super.onChecked()
    m_editBoxFrame.show(getPlayer())

  // --------------------------------------------------------------------------
  protected override function onUnchecked()
    super.onUnchecked()
    m_editBoxFrame.hide()