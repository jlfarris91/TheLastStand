package DebuggerDialog
import ClosureKeyPresses
import Frame
import Layout
import TabPanel

public DebuggerDialogFrame g_debuggerDialogFrame

// ============================================================================
public class DebuggerDialog
  // @configurable static constant rectr DRAW_AREA = rectr(0.81, 0.54, 0.35, 0.3)
  static constant size = vec2(0.35, 0.3)
  @configurable static constant rectr DRAW_AREA = rectr(SCREEN_CENTER - size * 0.5, size)
  
  // --------------------------------------------------------------------------
  static function initialize()
    if (g_debuggerDialogFrame == null)
      g_debuggerDialogFrame = new DebuggerDialogFrame()
      ..setAbsArea(DebuggerDialog.DRAW_AREA)

  // --------------------------------------------------------------------------
  static function registerFrame(string id, Frame contentFrame)
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.addContent(id, id, contentFrame)

  // --------------------------------------------------------------------------
  static function registerFrame(string id, string buttonText, Frame contentFrame)
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.addContent(id, buttonText, contentFrame)

  // --------------------------------------------------------------------------
  static function registerFrame(string id, Frame buttonFrame, Frame contentFrame)
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.addContent(id, buttonFrame, contentFrame)

  // --------------------------------------------------------------------------
  static function setSelectedFrame(string id)
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.setSelectedContentId(id)

  // --------------------------------------------------------------------------
  static function getSelectedFrame() returns string
    return g_debuggerDialogFrame != null ? g_debuggerDialogFrame.getSelectedContentId() : ""

  // --------------------------------------------------------------------------
  static function getIsVisible() returns bool
    return g_debuggerDialogFrame != null and g_debuggerDialogFrame.isVisible()

  // --------------------------------------------------------------------------
  static function show()
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.show()

  // --------------------------------------------------------------------------
  static function hide()
    if (g_debuggerDialogFrame != null)
      g_debuggerDialogFrame.hide()

// ============================================================================
class DebuggerDialogFrame extends TabPanel

  // --------------------------------------------------------------------------
  construct()
    super("DD")

    onKeyRelease(localPlayer, OSKEY_TAB) () ->
      if (isVisible())
        hide()
      else
        show()

    onKeyRelease(localPlayer, OSKEY_ESCAPE) () ->
      if (isVisible())
        hide()