package HiveLinkButton
import ClosureFrames
import ProjectConstants
import TLSMenuToggleButton
import initlater TLSMenuBar

// ============================================================================
public class HiveToggleButton extends TLSMenuToggleButton
  private framehandle m_editBoxFrame

  // --------------------------------------------------------------------------
  construct(TLSMenuBar menuBar)
    super(menuBar, "HiveLinkMenuButtonTemplate")
    setIconPath("HiveLogoIcon.tga")
    setIconDisabledPath("HiveLogoIcon.tga")
    setTooltipNormal("Hive Workshop")
    setTooltipExtended("Display the link to the official map page on Hive Workshop")

  // --------------------------------------------------------------------------
  protected override function initialize()
    super.initialize()

    m_editBoxFrame = BlzCreateFrameByType(
      "GLUEEDITBOX",
      "DiscordEditBox",
      BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0),
      "EscMenuEditBoxTemplate", 0)
    m_editBoxFrame.hide()

    m_editBoxFrame.setSize(0.2, 0.03)
    m_editBoxFrame.setPoint(FRAMEPOINT_TOPLEFT, m_handle, FRAMEPOINT_BOTTOMLEFT, -0.0002, 0.0)
    m_editBoxFrame.setText(HIVE_LINK)

    m_editBoxFrame.onEditboxChange() -> 
      if (m_editBoxFrame.getText() != HIVE_LINK)
        m_editBoxFrame.setText(HIVE_LINK)

  // --------------------------------------------------------------------------
  protected override function destroyFrames()
    super.destroyFrames()
    
    if (m_editBoxFrame != null)
      m_editBoxFrame.remove()
      m_editBoxFrame = null

  // --------------------------------------------------------------------------
  protected override function onChecked()
    super.onChecked()
    m_editBoxFrame.show(getPlayer())

  // --------------------------------------------------------------------------
  protected override function onUnchecked()
    super.onUnchecked()
    m_editBoxFrame.hide()