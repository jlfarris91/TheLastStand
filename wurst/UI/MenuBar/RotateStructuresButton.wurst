package RotateStructuresButton
import TLSMenuButton
import initlater TLSMenuBar
import Icons
import TechAvailabilityGroup
import GateIds
import Unit_WoodFence
import HashMap
import Math
import FramehandleExtensions
import ClosureKeyPresses
import ColorUtility

TechAvailabilityGroup array[4] g_rotatedStructuresAvailability
HashMap<player, int> g_rotatedStructuresIndex = new HashMap<player, int>()
group g_tempGroup = CreateGroup()

// ============================================================================
public class RotateStructuresButton extends TLSMenuButton
  private TLSMenuBar m_menuBar
  private int m_index = 0
  private framehandle m_button

  // --------------------------------------------------------------------------
  construct(TLSMenuBar menuBar, player _player)
    super("TLSMenuButtonTemplate")
    m_menuBar = menuBar
    
    setPlayer(_player)

    setIconPath(Icons.bTNSelectUnit)
    setIconDisabledPath(Icons.dISBTNSelectUnit)
    setTooltipNormal("Rotate Structures ({0})".format("Ctrl+R".colorize(Colors.gold)))
    setTooltipExtended("Some structures can be built facing different directions. Use this button to rotate the buildings in the build menu before building them.|n|n{0}".format("+Fences|n+Gates".colorize(Colors.gold)))

    onKeyPress(OSKEY_R, OSKEY_META.CTRL) () ->
      rotateStructures()
      if (getPlayer() == localPlayer)
        ForceUIKey("B")

    updateTechAvailability()

  // --------------------------------------------------------------------------
  private function rotateStructures()
    m_index = wrap(m_index + 1, 0, 3)
    updateTechAvailability()

  // --------------------------------------------------------------------------
  private function updateTechAvailability()
    let p = getPlayer()
    for i = 0 to 3
      g_rotatedStructuresAvailability[i].setEnabled(p, i == m_index)

  // --------------------------------------------------------------------------
  protected override function onClicked()
    super.onClicked()
    m_menuBar.uncheckOthers(this)
    m_button.clearFocus()
    rotateStructures()
    if (getPlayer() == localPlayer)
      ForceUIKey("B")    

// ============================================================================
init
  g_rotatedStructuresAvailability[0] = new TechAvailabilityGroup()
  ..add(WoodFenceIds.ns)
  ..add(Gate1Ids.ns_build)

  g_rotatedStructuresAvailability[1] = new TechAvailabilityGroup()
  ..add(WoodFenceIds.nesw)
  ..add(Gate1Ids.nesw_build)

  g_rotatedStructuresAvailability[2] = new TechAvailabilityGroup()
  ..add(WoodFenceIds.ew)
  ..add(Gate1Ids.ew_build)

  g_rotatedStructuresAvailability[3] = new TechAvailabilityGroup()
  ..add(WoodFenceIds.nwse)
  ..add(Gate1Ids.nwse_build)