package StandardTooltip
import Layout

framehandle g_tooltipFrame
framehandle g_tooltipTitle
framehandle g_tooltipExtendedText
framehandle g_separator

// ============================================================================
public class StandardTooltip
  @configurable static constant rectr DRAW_AREA = rectr(0.512, 0.16, 0.288, 0.0)
  static constant real TOTAL_ADDITIONAL_SPACING = 0.018 // in fdf 0.005 + 0.0035 + 0.0005 + 0.004 + 0.005

  // --------------------------------------------------------------------------
  static function show(string tooltip, string tooltipExtended, player _player)
    if (g_tooltipFrame == null)
      createFrames()

    if (localPlayer == _player)
      g_tooltipTitle.setText(tooltip)
      g_tooltipExtendedText.setText(tooltipExtended)
    
    updateFrameHeight()

    g_tooltipFrame.show(_player)

  // --------------------------------------------------------------------------
  static function hide(player _player)
    if (g_tooltipFrame != null)
      g_tooltipFrame.hide(_player)

  // --------------------------------------------------------------------------
  private static function updateFrameHeight()
    let totalHeight = TOTAL_ADDITIONAL_SPACING +
                      g_tooltipExtendedText.getHeight() +
                      g_tooltipTitle.getHeight() +
                      g_separator.getHeight()
    g_tooltipFrame.setHeight(totalHeight)

  // ============================================================================
  protected static function createFrames()

    g_tooltipFrame = createFrame("BoxedText", GAME_UI, 0, 0)
      ..setAbsPoint(FRAMEPOINT_BOTTOMLEFT, DRAW_AREA.bottomLeft())
      ..setAbsPoint(FRAMEPOINT_BOTTOMRIGHT, DRAW_AREA.bottomRight())

    g_tooltipExtendedText = getFrame("BoxedTextValue", 0)
    g_separator = getFrame("Separator", 0)
    g_tooltipTitle = getFrame("BoxedTextTitle", 0)

    g_tooltipFrame.hide()