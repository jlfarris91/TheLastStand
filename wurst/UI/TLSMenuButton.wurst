package TLSMenuButton
import ClosureFrames

// ============================================================================
public class TLSMenuButton
  protected player m_player
  protected framehandle m_buttonFrame
  protected framehandle m_buttonFrameIcon
  protected framehandle m_buttonFrameIconDisabled
  protected framehandle m_pushedBackdropFrame
  protected framehandle m_highlightFrame
  protected framehandle m_tooltipFrame
  protected framehandle m_tooltipTextNormal
  protected framehandle m_tooltipTextExtended
  protected FrameHandleListener m_onClickListener
  protected FrameHandleListener m_onMouseEnterListener
  protected FrameHandleListener m_onMouseLeaveListener
  protected string m_tooltipNormal
  protected string m_tooltipExtended
  protected string m_iconPath
  protected string m_iconDisabledPath
  protected vec2 m_pos
  protected string m_framePath

  // --------------------------------------------------------------------------
  construct()
    createFrames()

  // --------------------------------------------------------------------------
  ondestroy
    destroyFrames()

  // --------------------------------------------------------------------------
  function getPlayer() returns player
    return m_player

  // --------------------------------------------------------------------------
  function setPlayer(player value)
    if (m_player != value)
      m_player = value
      if (m_buttonFrame != null)
        m_buttonFrame.show(m_player)

  // --------------------------------------------------------------------------
  function getPos() returns vec2
    return m_pos

  // --------------------------------------------------------------------------
  function setPos(vec2 pos)
    if (m_pos != pos)
      m_pos = pos
      if (m_buttonFrame != null)
        m_buttonFrame.setAbsPoint(FRAMEPOINT_CENTER, m_pos.x, m_pos.y)

  // --------------------------------------------------------------------------
  function getTooltipNormal() returns string
    return m_tooltipNormal

  // --------------------------------------------------------------------------
  function setTooltipNormal(string value)
    if (m_tooltipNormal != value)
      m_tooltipNormal = value
      if (m_tooltipTextNormal != null)
        m_tooltipTextNormal.setText(value)

  // --------------------------------------------------------------------------
  function getTooltipExtended() returns string
    return m_tooltipExtended

  // --------------------------------------------------------------------------
  function setTooltipExtended(string value)
    if (m_tooltipExtended != value)
      m_tooltipExtended = value
      if (m_tooltipTextExtended != null)
        m_tooltipTextExtended.setText(value)

  // --------------------------------------------------------------------------
  function getIconPath() returns string
    return m_iconPath

  // --------------------------------------------------------------------------
  function setIconPath(string value)
    if (m_iconPath != value)
      m_iconPath = value
      if (m_buttonFrameIcon != null)
        m_buttonFrameIcon.setTexture(value, 0, true)

  // --------------------------------------------------------------------------
  function getIconDisabledPath() returns string
    return m_iconDisabledPath

  // --------------------------------------------------------------------------
  function setIconDisabledPath(string value)
    if (m_iconDisabledPath != value)
      m_iconDisabledPath = value
      if (m_buttonFrameIconDisabled != null)
        m_buttonFrameIconDisabled.setTexture(value, 0, true)

  // --------------------------------------------------------------------------
  function getFramePath() returns string
    return m_framePath

  // --------------------------------------------------------------------------
  function setFramePath(string value)
    if (m_framePath != value)
      m_framePath = value
      invalidateFrames()

  // --------------------------------------------------------------------------
  function invalidateFrames()
    destroyFrames()
    createFrames()

  // --------------------------------------------------------------------------
  protected function createFrames()
    if (m_framePath == null)
      return

    m_buttonFrame = BlzCreateFrame(m_framePath, BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0), 0, 0)

    m_buttonFrameIcon = BlzGetFrameByName("ButtonIcon", 0)..setTexture(m_iconPath, 0, true)
    m_buttonFrameIconDisabled = BlzGetFrameByName("ButtonIconDisabled", 0)..setTexture(m_iconDisabledPath, 0, true)

    m_pushedBackdropFrame = BlzGetFrameByName("ButtonPushedBackdrop", 0)
    m_highlightFrame = BlzGetFrameByName("ButtonMouseOverHighlight", 0)

    m_tooltipFrame = BlzCreateFrame("BoxedText", m_buttonFrame, 0, 0)
    m_tooltipFrame.setAbsPoint(FRAMEPOINT_BOTTOMLEFT, 0.2025, 0.18)
    m_tooltipFrame.setSize(0.2, 0.06)

    m_tooltipTextNormal = BlzGetFrameByName("BoxedTextTitle",0)..setText(m_tooltipNormal)
    m_tooltipTextExtended = BlzGetFrameByName("BoxedTextValue",0)..setText(m_tooltipExtended)

    m_buttonFrame.setAbsPoint(FRAMEPOINT_CENTER, m_pos.x, m_pos.y)
    m_buttonFrame.setTooltip(m_tooltipFrame)

    m_onClickListener = m_buttonFrame.onClick(() -> onClicked())
    m_onMouseEnterListener = m_buttonFrame.onMouseEnter(() -> onMouseEnter())
    m_onMouseLeaveListener = m_buttonFrame.onMouseLeave(() -> onMouseLeave())

    m_buttonFrame.hide()

    if (m_player != null)
      m_buttonFrame.show(m_player)
  
  // --------------------------------------------------------------------------
  protected function destroyFrames()

    if (m_onClickListener != null)
      destroy m_onClickListener
      m_onClickListener = null

    if (m_onMouseEnterListener != null)
      destroy m_onMouseEnterListener
      m_onMouseEnterListener = null

    if (m_onMouseLeaveListener != null)
      destroy m_onMouseLeaveListener
      m_onMouseLeaveListener = null

    if (m_buttonFrame != null)
      m_buttonFrame.remove()
      m_buttonFrame = null

    if (m_pushedBackdropFrame != null)
      m_pushedBackdropFrame.remove()
      m_pushedBackdropFrame = null

    if (m_highlightFrame != null)
      m_highlightFrame.remove()
      m_highlightFrame = null

  // --------------------------------------------------------------------------
  protected function onClicked()
    skip

  // --------------------------------------------------------------------------
  protected function onMouseEnter()
    m_highlightFrame.show(m_player)

  // --------------------------------------------------------------------------
  protected function onMouseLeave()
    m_highlightFrame.hide()