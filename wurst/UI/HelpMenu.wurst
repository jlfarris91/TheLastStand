package HelpMenu
import Window
import Frame
import FullscreenFrame
import Layout
import Checkbox
import PlayerSaveData
import ErrorHandling

HelpMenuWindow array g_helpMenuWindows

// ============================================================================
public class HelpMenu

  // --------------------------------------------------------------------------
  static function show(player forPlayer)
    if (g_helpMenuWindows[forPlayer.getId()] == null)
      g_helpMenuWindows[forPlayer.getId()] = new HelpMenuWindow(forPlayer)..hide()
    g_helpMenuWindows[forPlayer.getId()].show(forPlayer)

  // --------------------------------------------------------------------------
  static function hide(player forPlayer)
    let window = g_helpMenuWindows[forPlayer.getId()]
    if (window != null)
      window.hide(forPlayer)

  // --------------------------------------------------------------------------
  static function isVisible(player forPlayer) returns bool
    let window = g_helpMenuWindows[forPlayer.getId()]
    return window != null and window.isVisible(forPlayer)

// ============================================================================
class HelpMenuWindow extends Window
  private bool m_showTipsOnStartup
  private Checkbox m_showTipsOnStartupCheckbox
  private player m_player

  // --------------------------------------------------------------------------
  construct(player forPlayer)
    super(createFrame("WindowTemplate", CONSOLE_UI_BACKDROP, 0, 0))

    m_player = forPlayer

    setTitle("New Player Tips")
    setIcon("ReplaceableTextures\\WorldEditUI\\Editor-Random-Item.blp")
    canClose(true)
    canMaximize(false)

    let frame = getFrameHandle()
    ..setPoint(FRAMEPOINT_TOPRIGHT, FULLSCREEN_FRAME, FRAMEPOINT_TOPRIGHT, vec2(-0.01, -0.075))
    
    setSize(vec2(0.4, 0.35))

    let contentFrame = getContentAreaFrameHandle()

    let newPlayersPage = createFrame("TEXTAREA", "", CONSOLE_UI_BACKDROP, "BattleNetTextAreaTemplate", 0)
    ..setParent(contentFrame)
    ..setPoint(FRAMEPOINT_TOPLEFT, contentFrame, FRAMEPOINT_TOPLEFT, 0, 0)
    ..setPoint(FRAMEPOINT_BOTTOMRIGHT, contentFrame, FRAMEPOINT_BOTTOMRIGHT, 0, 0.02)

    let newPlayersPageTextArea = newPlayersPage
    newPlayersPageTextArea.setText("|cFFFFCC00Overview|r")
    newPlayersPageTextArea.addText("|nThe Last Stand is a game about surviving in a post apocalyptic world where undead rule the night. In order to survive you will need to build strong defenses and loot the abandoned civilization around you. Your base will only be as strong as the survivors defending it.")
    newPlayersPageTextArea.addText("|n|cFFFFCC00The First Day|r")
    newPlayersPageTextArea.addText("|n· Find a green base using the \"-bases\" chat command and build your HQ Tent (item in hero inventory)")
    newPlayersPageTextArea.addText("|n· Once your HQ Tent is finished being constructed a Survivor will join your camp. Have him build 3 Shelters to make room for 3 more Survivors (they will spawn automatically). Once you have 4 Survivors order them to harvest lumber.")
    newPlayersPageTextArea.addText("|n· Barricade the entrance to your camp with a gate and as many walls as necessary. All entrances have been designed to fit 1 or 2 walls/gates neatly. You must rotate walls and gates BEFORE building them using Ctrl+R.")
    newPlayersPageTextArea.addText("|n· Spikes are effective at dealing with zombies but have a very short range. Build them next to your walls on the outside of your camp. They are invulnerable but with each attack they deal damage to themselves. Once they reach 1 hp they are disabled until they are repaired again.")
    newPlayersPageTextArea.addText("|n· Build a Camp Fire to restore health and mana to nearby organic units.")
    newPlayersPageTextArea.addText("|n· While your camp is being established, send your hero out to loot nearby structures for resources and a chance at finding items and even rescuing more Survivors. Lootable buildings are not affected by fog of war, so take a look around your camp for the nearest buildings to loot.")
    newPlayersPageTextArea.addText("|n|cFFFFCC00The First Night|r")
    newPlayersPageTextArea.addText("|n· Your camp will be mostly defenseless so make sure your hero is back before nightfall.")
    newPlayersPageTextArea.addText("|n· Order at least one Survivor to auto-repair and stand near your defenses. Having them patrol near your walls will cause them to react more quickly when repairing.")
    newPlayersPageTextArea.addText("|n· Hold fast! Zombies will continuously spawn and attack your base during the night. Every 20 Zombies will drop gold when killed, so try to kill them quickly.")
    newPlayersPageTextArea.addText("|n· Once daybreak arrives you will have survived your first night and will be awarded gold and experience for your Survivors.")
    newPlayersPageTextArea.addText("|n|cFFFFCC00Day Two and Beyond|r")
    newPlayersPageTextArea.addText("|n· Repair your walls and spikes but make sure to have as many Survivors harvesting lumber as possible.")
    newPlayersPageTextArea.addText("|n· Send your hero out during the day to loot nearby buildings for resources.")
    newPlayersPageTextArea.addText("|n· Build a Workbench, upgrade your HQ Tent and build more Shelters (or upgrade your existing ones). Now you should have 6 Survivors.")
    newPlayersPageTextArea.addText("|n· In the Workbench, build the Builder's Tools item and give it to one of your Survivors. Activating the item gives him the Builder job and access to avanced structures. The Builder is capable of repairing structures twice as fast as the regular, jobless Survivor, so have them repair during the night. It's good practice to have at least 1 builder per entrance.")
    newPlayersPageTextArea.addText("|n· Choose a path: either build defensive structures starting with the Workshop or build military structures to unlock soldier jobs for your Survivors, starting with the Blacksmith.")
    newPlayersPageTextArea.addText("|n|cFFFFCC00Good Luck!|r")

    m_showTipsOnStartupCheckbox = new Checkbox(createFrame("CheckboxTemplate", CONSOLE_UI_BACKDROP, 0, 0))
    ..setSize(vec2(0.015, 0.015))
    ..setHorizontalAlignment(HorizontalLayoutMode.RIGHT)
    ..setVerticalAlignment(VerticalLayoutMode.BOTTOM)
    ..setMargin(thickness(0, 0.005, 0.005, 0))
    addChild(m_showTipsOnStartupCheckbox)

    m_showTipsOnStartupCheckbox.onValueChanged().register() -> 
      onShowTipsOnStartupCheckboxChecked()

    createFrame("TEXT", "", CONSOLE_UI_BACKDROP, "", 0)
    ..setParent(frame)
    ..setSize(0.1, 0.01)
    ..setPoint(FRAMEPOINT_RIGHT, m_showTipsOnStartupCheckbox.getFrameHandle(), FRAMEPOINT_LEFT, vec2(0.0, 0.0))
    ..setText("Show tips on startup")

    // Load state for show tips on startup checkbox
    let playerSaveData = PlayerSaveData.getPlayerSaveData(m_player) castTo PlayerSaveData_v801
    if (playerSaveData == null)
      error("Player save data is null")

    m_showTipsOnStartup = playerSaveData.showTipsOnStartup

    m_showTipsOnStartupCheckbox.setChecked(m_showTipsOnStartup)

  // --------------------------------------------------------------------------
  function getShowTipsOnStartup() returns bool
    return m_showTipsOnStartup

  // --------------------------------------------------------------------------
  function setShowTipsOnStartup(bool value)
    if (m_showTipsOnStartup != value)
      setShowTipsOnStartupInternal(value)
      if (m_showTipsOnStartupCheckbox != null)
        m_showTipsOnStartupCheckbox.setChecked(value)

  // --------------------------------------------------------------------------
  private function onShowTipsOnStartupCheckboxChecked()
    if (m_showTipsOnStartupCheckbox != null)
      let isChecked = m_showTipsOnStartupCheckbox.isChecked()
      if (m_showTipsOnStartup != isChecked)
        setShowTipsOnStartupInternal(isChecked)

  // --------------------------------------------------------------------------
  private function setShowTipsOnStartupInternal(bool value)
    m_showTipsOnStartup = value

    let playerSaveData = PlayerSaveData.getPlayerSaveData(m_player) castTo PlayerSaveData_v801
    if (playerSaveData == null)
      error("Player save data is null")
    playerSaveData.showTipsOnStartup = m_showTipsOnStartup
    PlayerSaveData.save(m_player, false)