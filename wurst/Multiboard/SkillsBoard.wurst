package SkillsBoard
import ComposableUnit
import SkillsComponent
import Skills
import TextProgressBar
import Math
import StringExtensions
import ClosureTimers
import TimerUtils

TextProgressBar g_progressBar
string array[10] g_levelText
string array[101] g_percentageText
string g_MaxLevelText

constant int PROGRESS_BAR_LENGTH = 40
constant colorA PROGRESS_BAR_FOREGROUND = colorA(0, 170, 0, 255)
constant colorA PROGRESS_BAR_BACKGROUND = colorA(0, 64, 0, 255)
constant colorA LEVEL_TEXT_COLOR = COLOR_GOLD
constant colorA MAX_LEVEL_TEXT_COLOR = COLOR_GOLD

// ============================================================================
public class SkillsBoard
  private constant int NUMBER_OF_COLUMNS = 5
  private constant int ROW_TITLE = 0
  private constant int ROW_LEVEL = 1
  private constant int ROW_EXPERIENCE_BAR = 2
  private constant int ROW_EXPERIENCE_PERCENTAGE = 3
  private constant int ROW_EXPERIENCE_UNITS = 4

  private ComposableUnit _unit
  private multiboard _multiboard
  private SkillsComponent _skillsComponent  
  private timer _timer

  // --------------------------------------------------------------------------
  construct(SkillsComponent skillsComponent, string title)
    _skillsComponent = skillsComponent
    _unit = _skillsComponent.getOwner() castTo ComposableUnit
    _multiboard = CreateMultiboardBJ(NUMBER_OF_COLUMNS, 0, title)
    hide()

  // --------------------------------------------------------------------------
  ondestroy
    DestroyMultiboard(_multiboard)

  // --------------------------------------------------------------------------
  function show()
    _multiboard.show(_unit.getUnit().getOwner())

    if (_timer == null)
      _timer = getTimer()
      _timer.doPeriodically(1.0) (CallbackPeriodic cb) ->
        update()

  // --------------------------------------------------------------------------
  function hide()
    _multiboard.hide()

    if (_timer != null)
      _timer.release()
      _timer = null

  // --------------------------------------------------------------------------
  function update()
    let skillInstances = _skillsComponent.getSkillInstances()
    var row = 0
    for skillInstance in skillInstances
      updateRow(row, skillInstance)
      row++

  // --------------------------------------------------------------------------
  function updateMultiboardRows()

    let skillInstances = _skillsComponent.getSkillInstances()

    _multiboard.setRowCount(skillInstances.size())

    _multiboard.setColumnWidth(ROW_TITLE, 0.15)
    _multiboard.setColumnWidth(ROW_LEVEL, 0.04)
    _multiboard.setColumnWidth(ROW_EXPERIENCE_BAR, 0.14)
    _multiboard.setColumnWidth(ROW_EXPERIENCE_PERCENTAGE, 0.03)
    _multiboard.setColumnWidth(ROW_EXPERIENCE_UNITS, 0.08)

    MultiboardSetItemStyleBJ(_multiboard, ROW_TITLE + 1, 0, true, true)
    MultiboardSetItemStyleBJ(_multiboard, ROW_LEVEL + 1, 0, true, false)
    MultiboardSetItemStyleBJ(_multiboard, ROW_EXPERIENCE_BAR + 1, 0, true, false)
    MultiboardSetItemStyleBJ(_multiboard, ROW_EXPERIENCE_PERCENTAGE + 1, 0, true, false)
    MultiboardSetItemStyleBJ(_multiboard, ROW_EXPERIENCE_UNITS + 1, 0, true, false)

    var row = 0
    for skillInstance in skillInstances
      let skill = skillInstance.getSkill()
      _multiboard.setItemIcon(row, ROW_TITLE, skill.getIconPath())
      _multiboard.setItemValue(row, ROW_TITLE, skill.getName())
      row++
      
  // --------------------------------------------------------------------------
  private function updateRow(int row, ISkill skillInstance)
    let skill = skillInstance.getSkill()
    
    let level = skillInstance.getLevel()
    let nextLevel = level + 1
    let exp = skillInstance.getExperience()

    let levelText = g_levelText[level]
    _multiboard.setItemValue(row, ROW_LEVEL, levelText)

    if (level < skill.getMaxLevel())

      let minExp = skill.getLevelRequirement(level)
      let maxExp = skill.getLevelRequirement(nextLevel)
      let t = clamp((exp - minExp) / (maxExp - minExp), 0.0, 1.0)

      let percentageBarText = g_progressBar.sample(t)
      _multiboard.setItemValue(row, ROW_EXPERIENCE_BAR, percentageBarText)

      let percentageTextIndex = R2I(t * 100.0)
      let percentageText = g_percentageText[percentageTextIndex]
      _multiboard.setItemValue(row, ROW_EXPERIENCE_PERCENTAGE, percentageText)

    else

      let percentageBarText = g_progressBar.sample(1.0)
      _multiboard.setItemValue(row, ROW_EXPERIENCE_BAR, percentageBarText)
      _multiboard.setItemValue(row, ROW_EXPERIENCE_PERCENTAGE, g_MaxLevelText)

    let unitsText = skill.getUnits()
    _multiboard.setItemValue(row, ROW_EXPERIENCE_UNITS, I2S(R2I(exp)) + unitsText)

// ============================================================================
init
  g_progressBar = new TextProgressBar(PROGRESS_BAR_LENGTH, PROGRESS_BAR_FOREGROUND, PROGRESS_BAR_BACKGROUND)

  for i = 0 to 10
    g_levelText[i] = ("Level " + I2S(i)).colorize(LEVEL_TEXT_COLOR)

  for i = 0 to 100
    g_percentageText[i] = I2S(i) + "%"

  g_MaxLevelText = "Max".colorize(MAX_LEVEL_TEXT_COLOR)