package GameStatsMultiboard
import Time
import GameInstance
import HumanPlayers
import HumanPlayerComponent
import ClosureTimers
import ColorUtility
import ProjectConstants
import GameConstants

constant real INTERVAL_PERIOD = 1.0
constant int COLUMN_DIFFICULTY = 0
constant int COLUMN_PLAYER_NAME = 1
constant int COLUMN_KILLS = 2
constant int COLUMN_SCORE = 3
constant int COLUMN_COUNT = 4
constant int ROW_HEADER = 0

multiboard g_gameStatsMultiboard

// ============================================================================
function createGameStatsMultiboard()
    
  // Header + 1 row per player
  let rowCount = 1 + g_HumanPlayers.count()

  g_gameStatsMultiboard = CreateMultiboardBJ(COLUMN_COUNT, rowCount, "UNTITLED")

  MultiboardSetItemsWidth(g_gameStatsMultiboard, 0.05)
  g_gameStatsMultiboard.setColumnWidth(COLUMN_DIFFICULTY, 0.0125)
  g_gameStatsMultiboard.setColumnWidth(COLUMN_PLAYER_NAME, 0.14)
  g_gameStatsMultiboard.setColumnWidth(COLUMN_KILLS, 0.04)
  g_gameStatsMultiboard.setColumnWidth(COLUMN_SCORE, 0.04)
  MultiboardSetItemStyleBJ(g_gameStatsMultiboard, COLUMN_DIFFICULTY + 1, 0, false, true)
  MultiboardSetItemStyleBJ(g_gameStatsMultiboard, COLUMN_DIFFICULTY + 1, 1, false, false)
  MultiboardSetItemStyleBJ(g_gameStatsMultiboard, COLUMN_PLAYER_NAME + 1, 0, true, false)
  MultiboardSetItemStyleBJ(g_gameStatsMultiboard, COLUMN_KILLS + 1, 0, true, false)
  MultiboardSetItemStyleBJ(g_gameStatsMultiboard, COLUMN_SCORE + 1, 0, true, false)

  g_gameStatsMultiboard.setItemValue(ROW_HEADER, COLUMN_PLAYER_NAME, "Player")
  g_gameStatsMultiboard.setItemValue(ROW_HEADER, COLUMN_KILLS, "Kills")
  g_gameStatsMultiboard.setItemValue(ROW_HEADER, COLUMN_SCORE, "Score")

  updateGameStatsMultiboardTitle()
  updateGameStatsMultiboardContent()

  g_gameStatsMultiboard.hide()

// ============================================================================
public function showGameStatsMultiboard(player p)
  g_gameStatsMultiboard.show(p)

// ============================================================================
function updateGameStatsMultiboardTitle()
  let multiboardTitle = "{0} - {1} {2}".format(
    GAME_VERSION.colorize(Colors.white),
    isDay() ? "Day" : "Night",
    (isDay() ? g_GameInstance.getDay() : g_GameInstance.getNight()).toString())
  g_gameStatsMultiboard.setTitle(multiboardTitle)

// ============================================================================
function updateGameStatsMultiboardContent()

  var row = 1
  for p in g_HumanPlayers
    let playerMetadata = p.getHumanPlayerComponent()
    if (playerMetadata != null)
      let difficulty = playerMetadata.getDifficulty().floor()
      let displayName = p.getDisplayNameColored()
      let kills = playerMetadata.getKills()
      let score = playerMetadata.getScore()
      g_gameStatsMultiboard.setItemIcon(row, COLUMN_DIFFICULTY, DIFFICULTY_ICONS[difficulty])
      g_gameStatsMultiboard.setItemValue(row, COLUMN_PLAYER_NAME, displayName)
      g_gameStatsMultiboard.setItemValue(row, COLUMN_KILLS, kills.toString())
      g_gameStatsMultiboard.setItemValue(row, COLUMN_SCORE, score.toString())
    row++

// ============================================================================
function onDayNight()
  updateGameStatsMultiboardTitle()

// ============================================================================
init
  nullTimer(() -> createGameStatsMultiboard())
  doPeriodically(INTERVAL_PERIOD, cb -> updateGameStatsMultiboardContent())
  registerDayEvent(function onDayNight)
  registerNightEvent(function onDayNight)
