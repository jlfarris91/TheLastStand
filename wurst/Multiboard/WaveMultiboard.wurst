package WaveMultiboard
import BaseMultiboard
import initlater GameInstance
import SpawnWave
import Wave
import TimerUtils
import ClosureTimers
import Math
import Time

constant real INTERVAL_PERIOD = 0.1

constant int COLUMN_NAME = 0
constant int COLUMN_VALUE = 1
constant int COLUMN_COUNT = 2

constant int SPAWN_WAVE_ROW_TITLE = 0
constant int SPAWN_WAVE_ROW_PROGRESS = 1
constant int SPAWN_WAVE_ROW_SPAWNS = 2
constant int SPAWN_WAVE_ROW_ALIVE = 3
constant int SPAWN_WAVE_ROW_SPAWNS_PER_ACTIVATION = 4
constant int SPAWN_WAVE_ROW_SPAWN_GROUP_FACTOR = 5
constant int SPAWN_WAVE_ROW_GROUP_COUNT = 6
constant int SPAWN_WAVE_ROW_GROUP_SIZE = 7
constant int SPAWN_WAVE_ROW_GOLD = 8
constant int SPAWN_WAVE_ROW_COUNT = 10

// ============================================================================
public class WaveMultiboard extends BaseMultiboard
  private timer m_timer
  private player m_player
  private Wave m_latestWave

  // --------------------------------------------------------------------------
  override function show()

    update()

    super.show()
    
    if (m_timer == null)
      m_timer = getTimer()
      m_timer.doPeriodically(INTERVAL_PERIOD) (CallbackPeriodic cb) ->
        update()
    
  // --------------------------------------------------------------------------
  override function hide()
    super.hide()    

    if (m_timer != null)
      m_timer.release()
      m_timer = null

  // --------------------------------------------------------------------------
  function getPlayer() returns player
    return m_player

  // --------------------------------------------------------------------------
  function setPlayer(player p)
    m_player = p

  // --------------------------------------------------------------------------
  function updateLayout()

    hide()

    destroyMultiboard()

    let spawnWaveCount = m_latestWave != null ? m_latestWave.getSpawnWaves().getCount() : 0
    let rowCount = max(spawnWaveCount * SPAWN_WAVE_ROW_COUNT, 1)
    
    m_multiboard = CreateMultiboardBJ(COLUMN_COUNT, rowCount, "UNTITLED")

    MultiboardSetItemsWidth(m_multiboard, 0.15)
    MultiboardSetItemStyleBJ(m_multiboard, COLUMN_NAME + 1, 0, true, false)
    MultiboardSetItemStyleBJ(m_multiboard, COLUMN_VALUE + 1, 0, true, false)

    show()

  // --------------------------------------------------------------------------
  function update()

    let currentWave = getCurrentWave()
    if (currentWave != m_latestWave)
      m_latestWave = currentWave
      updateLayout()

    let targetPlayer = getPlayer()
    if (m_latestWave == null or targetPlayer == null)
      return

    let multiboardTitle = "[{0}] {1} Wave {2} {3}%".format(
        getPlayer().getName(),
        isDay() ? "Day" : "Night",
        (isDay() ? g_GameInstance.getDay() : g_GameInstance.getNight()).toString(),
        R2I(m_latestWave.getProgress() * 100.0).toString())

    m_multiboard.setTitle(multiboardTitle)

    updateRows(m_latestWave)

  // --------------------------------------------------------------------------
  private function updateRows(Wave wave)

    let spawnWaves = wave.getSpawnWaves()

    if (spawnWaves.isEmpty())
      setItemValues(0, "No spawn waves", "")
      return

    var row = 0
    for spawnWave in spawnWaves
      updateSpawnWaveRows(spawnWave, row)
      row += SPAWN_WAVE_ROW_COUNT

  // --------------------------------------------------------------------------
  private function updateSpawnWaveRows(SpawnWave spawnWave, int row)
    let spawnWaveContext = spawnWave.getContext()
    let numSpawned = spawnWave.getSpawnedCount()

    let spawnWaveDefinition = spawnWave.getDefinition()
    let spawnCountMax = spawnWaveDefinition.getMaxSpawnCount(spawnWaveContext)
    let aliveCountMax = spawnWaveDefinition.getMaxAliveCount(spawnWaveContext)
    let numAlive = spawnWave.getAliveCount()
    let spawnsPerActivation = spawnWaveDefinition.getSpawnsPerActivation(spawnWaveContext)
    let spawnGroupingFactor = spawnWaveDefinition.getSpawnGroupingFactor(spawnWaveContext)
    let numSpawnGroups = max(R2I(I2R(spawnsPerActivation) * (1.0 - spawnGroupingFactor)), 1)
    let spawnGroupSize = max(R2I(I2R(spawnsPerActivation) * spawnGroupingFactor), 1)
    let goldReward = spawnWaveDefinition.getGoldReward(spawnWaveContext)
    let goldAwarded = spawnWave.getGoldAwarded()
    
    setItemValues(row + SPAWN_WAVE_ROW_TITLE, "=== " + spawnWaveDefinition.getName() + " ===", "")
    setItemValues(row + SPAWN_WAVE_ROW_PROGRESS, "Progress", I2S(R2I(spawnWaveContext.getWaveProgress01() * 100.0)) + "%")
    setItemValues(row + SPAWN_WAVE_ROW_SPAWNS, "Spawns", I2S(numSpawned) + "/" + (spawnCountMax == UNLIMITED ? "Unlimited" : I2S(spawnCountMax)))
    setItemValues(row + SPAWN_WAVE_ROW_ALIVE, "Alive", I2S(numAlive) + "/" + (aliveCountMax == UNLIMITED ? "Unlimited" : I2S(aliveCountMax)))
    setItemValues(row + SPAWN_WAVE_ROW_SPAWNS_PER_ACTIVATION, "Spawns Per Activation", I2S(spawnsPerActivation))
    setItemValues(row + SPAWN_WAVE_ROW_SPAWN_GROUP_FACTOR, "Spawn Group Factor", R2S(spawnGroupingFactor))
    setItemValues(row + SPAWN_WAVE_ROW_GROUP_COUNT, "Groups", I2S(numSpawnGroups))
    setItemValues(row + SPAWN_WAVE_ROW_GROUP_SIZE, "Group Size", I2S(spawnGroupSize))
    setItemValues(row + SPAWN_WAVE_ROW_GOLD, "Gold", goldAwarded.toString() + "/" + goldReward.toString())

  // --------------------------------------------------------------------------
  private function setItemValues(int row, string name, string value)
    m_multiboard.setItemValue(row, COLUMN_NAME, name)
    m_multiboard.setItemValue(row, COLUMN_VALUE, value)

  // --------------------------------------------------------------------------
  private function getCurrentWave() returns Wave

    let wavesOwner = getPlayer()
    if (wavesOwner == null)
      return null
    
    let waves = g_GameInstance.getWaves()
    if (waves == null)
      return null

    let playerWaves = waves.getOrCreatePlayerWaves(wavesOwner)

    if (isDay())
      let dayWaves = playerWaves.getDayWaves()
      return dayWaves.getLatestWave(g_GameInstance.getDay())
    
    if (isNight())
      let nightWaves = playerWaves.getNightWaves()
      return nightWaves.getLatestWave(g_GameInstance.getNight())

    return null