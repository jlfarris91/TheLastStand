package WaveMultiboard
import BaseMultiboard
import initlater GameInstance
import SpawnWave
import Wave
import TimerUtils
import ClosureTimers

constant real INTERVAL_PERIOD = 0.1

constant int COLUMN_NAME = 0
constant int COLUMN_VALUE = 1
constant int COLUMN_COUNT = 2

constant int SPAWN_WAVE_ROW_TITLE = 0
constant int SPAWN_WAVE_ROW_PROGRESS = 1
constant int SPAWN_WAVE_ROW_SPAWNS = 2
constant int SPAWN_WAVE_ROW_ALIVE = 3
constant int SPAWN_WAVE_ROW_SPAWNS_PER_ACTIVATION = 4
constant int SPAWN_WAVE_ROW_SPAWN_GROUP_FACTOR = 5
constant int SPAWN_WAVE_ROW_GROUP_COUNT = 6
constant int SPAWN_WAVE_ROW_GROUP_SIZE = 7
constant int SPAWN_WAVE_ROW_COUNT = 9

// ============================================================================
public class WaveMultiboard extends BaseMultiboard
  private timer m_timer
  private player m_player
  private Wave m_latestWave

  // --------------------------------------------------------------------------
  override function show()

    update()

    super.show()
    
    if (m_timer == null)
      m_timer = getTimer()
      m_timer.doPeriodically(INTERVAL_PERIOD) (CallbackPeriodic cb) ->
        update()
    
  // --------------------------------------------------------------------------
  override function hide()
    super.hide()    

    if (m_timer != null)
      m_timer.release()
      m_timer = null

  // --------------------------------------------------------------------------
  function getPlayer() returns player
    return m_player

  // --------------------------------------------------------------------------
  function setPlayer(player p)
    m_player = p

  // --------------------------------------------------------------------------
  function updateLayout()

    hide()

    destroyMultiboard()

    let spawnWaveCount = m_latestWave != null ? m_latestWave.getSpawnWaves().size() : 0
    let rowCount = max(spawnWaveCount * SPAWN_WAVE_ROW_COUNT, 1)
    
    m_multiboard = CreateMultiboardBJ(COLUMN_COUNT, rowCount, "UNTITLED")

    MultiboardSetItemsWidth(m_multiboard, 0.15)
    MultiboardSetItemStyleBJ(m_multiboard, COLUMN_NAME + 1, 0, true, false)
    MultiboardSetItemStyleBJ(m_multiboard, COLUMN_VALUE + 1, 0, true, false)

    show()

  // --------------------------------------------------------------------------
  function update()

    let currentWave = getCurrentWave()
    if (currentWave != m_latestWave)
      m_latestWave = currentWave
      updateLayout()

    if (m_latestWave == null or getPlayer() == null)
      return

    if (g_GameInstance.getIsDay())
      m_multiboard.setTitle("[" + getPlayer().getName() + "] Day Wave " + I2S(g_GameInstance.getDay()) + " (" + m_latestWave.getStatusString() +  ")")
    
    if (g_GameInstance.getIsNight())
      m_multiboard.setTitle("[" + getPlayer().getName() + "] Night Wave " + I2S(g_GameInstance.getNight()) + " (" + m_latestWave.getStatusString() +  ")")

    updateRows(m_latestWave)

  // --------------------------------------------------------------------------
  private function updateRows(Wave wave)

    let spawnWaves = wave.getSpawnWaves()

    if (spawnWaves.isEmpty())
      setItemValues(0, "No spawn waves", "")
      return

    var row = 0
    for spawnWave in spawnWaves
      updateSpawnWaveRows(spawnWave, row)
      row += SPAWN_WAVE_ROW_COUNT

  // --------------------------------------------------------------------------
  private function updateSpawnWaveRows(SpawnWave spawnWave, int row)
    let waveProgress = spawnWave.getWaveProgress()
    let numSpawned = spawnWave.getSpawnedCount()
    let spawnCountMax = spawnWave.getSpawnCountMax(waveProgress)
    let aliveCountMax = spawnWave.getAliveCountMax(waveProgress)
    let numAlive = spawnWave.getAliveCount()
    let spawnsPerActivation = spawnWave.getSpawnsPerActivation(waveProgress)
    let spawnGroupingFactor = spawnWave.getSpawnGroupFactor(waveProgress)
    let numSpawnGroups = max(R2I(I2R(spawnsPerActivation) * (1.0 - spawnGroupingFactor)), 1)
    let spawnGroupSize = max(R2I(I2R(spawnsPerActivation) * spawnGroupingFactor), 1)
    
    setItemValues(row + SPAWN_WAVE_ROW_TITLE, "=== " + spawnWave.getName() + " ===", spawnWave.getStatusString())
    setItemValues(row + SPAWN_WAVE_ROW_PROGRESS, "Progress", I2S(R2I(waveProgress * 100.0)) + "%")
    setItemValues(row + SPAWN_WAVE_ROW_SPAWNS, "Spawns", I2S(numSpawned) + "/" + (spawnCountMax == UNLIMITED ? "Unlimited" : I2S(spawnCountMax)))
    setItemValues(row + SPAWN_WAVE_ROW_ALIVE, "Alive", I2S(numAlive) + "/" + (aliveCountMax == UNLIMITED ? "Unlimited" : I2S(aliveCountMax)))
    setItemValues(row + SPAWN_WAVE_ROW_SPAWNS_PER_ACTIVATION, "Spawns Per Activation", I2S(spawnsPerActivation))
    setItemValues(row + SPAWN_WAVE_ROW_SPAWN_GROUP_FACTOR, "Spawn Group Factor", R2S(spawnGroupingFactor))
    setItemValues(row + SPAWN_WAVE_ROW_GROUP_COUNT, "Groups", I2S(numSpawnGroups))
    setItemValues(row + SPAWN_WAVE_ROW_GROUP_SIZE, "Group Size", I2S(spawnGroupSize))

  // --------------------------------------------------------------------------
  private function setItemValues(int row, string name, string value)
    m_multiboard.setItemValue(row, COLUMN_NAME, name)
    m_multiboard.setItemValue(row, COLUMN_VALUE, value)

  // --------------------------------------------------------------------------
  private function getCurrentWave() returns Wave

    let wavesOwner = getPlayer()
    if (wavesOwner == null)
      return null
    
    let waves = g_GameInstance.getWaves()
    if (waves == null)
      return null

    let playerWaves = waves.getOrCreatePlayerWaves(wavesOwner)

    if (g_GameInstance.getIsDay())
      let dayWaves = playerWaves.getDayWaves()
      return dayWaves.getLatestWave(g_GameInstance.getDay())
    
    if (g_GameInstance.getIsNight())
      let nightWaves = playerWaves.getNightWaves()
      return nightWaves.getLatestWave(g_GameInstance.getNight())

    return null