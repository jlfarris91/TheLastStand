package TreasureHuntersPendant
import TlsItemIds
import LooterComponent
import UnitMetadata
import MainItemLibrary
import ItemEffectComponent

ItemChances g_betterItemChances = ItemChances(32.0, 32.0, 18.0, 8.0)

// ============================================================================
class TreasureHuntersPendantComponent extends ItemEffectComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

    setMode(ItemEffectApplication.ACQUIRED)
    setItemTypeId(TlsItemIds.treasureHuntersPendant)
    setDestroyOnDrop(true)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return TreasureHuntersPendantComponent.typeId
  
  // --------------------------------------------------------------------------
  override function onApplied(int stacks)
    super.onApplied(stacks)
    let comp = getOwner().getLooterComponent()
    if (comp != null)
      comp.setOverrideItemChances(true)
      comp.setItemChances(g_betterItemChances)

  // --------------------------------------------------------------------------
  override function onReverted(int stacks)
    super.onReverted(stacks)
    let comp = getOwner().getLooterComponent()
    if (comp != null)
      comp.setOverrideItemChances(false)

// ============================================================================
function IUnitMetadata.getTreasureHuntersPendantComponent() returns TreasureHuntersPendantComponent
  return this.getComponent(typeInfo(TreasureHuntersPendantComponent.typeId)) castTo TreasureHuntersPendantComponent

// ============================================================================
function IUnitMetadata.getOrAddTreasureHuntersPendantComponent() returns TreasureHuntersPendantComponent
  var component = this.getTreasureHuntersPendantComponent()
  if (component == null)
    component = this.addComponent(new TreasureHuntersPendantComponent(this)) castTo TreasureHuntersPendantComponent
  return component

// ============================================================================
init
  g_itemIdToComponentFactoryMap.put(TlsItemIds.treasureHuntersPendant) (_unit) ->
  //{
    let metadata = _unit.getMetadata()
    ItemEffectComponent comp = null
    if (metadata != null)
      comp = metadata.getOrAddTreasureHuntersPendantComponent()
    return comp
  //}