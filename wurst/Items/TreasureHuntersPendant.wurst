package TreasureHuntersPendant
import RegisterEvents
import TlsItemIds
import LooterComponent
import UnitMetadata
import MainItemLibrary

ItemChances g_betterItemChances = ItemChances(32.0, 48.0, 16.0, 4.0)

// ============================================================================
function onUnitPickupItem()
  let _item = GetManipulatedItem()
  if (_item.getTypeId() != TlsItemIds.treasureHuntersPendant)
    return

  let _unit = GetManipulatingUnit()

  let metadata = _unit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getLooterComponent()
  if (comp == null)
    return

  comp.setOverrideItemChances(true)
  comp.setItemChances(g_betterItemChances)

// ============================================================================
function onUnitDropItem()
  let _item = GetManipulatedItem()
  if (_item.getTypeId() != TlsItemIds.treasureHuntersPendant)
    return
    
  let _unit = GetManipulatingUnit()

  let metadata = _unit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getLooterComponent()
  if (comp == null)
    return

  // Unit is still holding a pendant
  if (_unit.itemCount(TlsItemIds.treasureHuntersPendant) > 1)
    return

  comp.setOverrideItemChances(false)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM, function onUnitPickupItem)
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DROP_ITEM, function onUnitDropItem)