package MainItemLibrary
import ItemLibrary
import ItemSetLibrary

public MainItemSetLibrary g_MainItemLibrary

public tuple ItemChances(real common, real rare, real epic, real legendary)

constant real ITEM_SET_CHANCE_COMMON = 8.0
constant real ITEM_SET_CHANCE_RARE = 4.0
constant real ITEM_SET_CHANCE_EPIC = 2.0
constant real ITEM_SET_CHANCE_LEGENDARY = 1.0

constant color ITEM_SET_COLOR_COMMON = fromHexString("FFC7C7C7").withoutAlpha()
constant color ITEM_SET_COLOR_RARE = fromHexString("FF4293F5").withoutAlpha()
constant color ITEM_SET_COLOR_EPIC = fromHexString("FF7842F5").withoutAlpha()
constant color ITEM_SET_COLOR_LEGENDARY = fromHexString("FFF2ED5E").withoutAlpha()

// ============================================================================
public class MainItemSetLibrary extends ItemSetLibrary
  ItemSet commonSet
  ItemSet rareSet
  ItemSet epicSet
  ItemSet legendarySet

  // --------------------------------------------------------------------------
  construct()
    this.commonSet = new ItemSet("Common", ITEM_SET_COLOR_COMMON)
    this.rareSet = new ItemSet("Rare", ITEM_SET_COLOR_RARE)
    this.epicSet = new ItemSet("Epic", ITEM_SET_COLOR_EPIC)
    this.legendarySet = new ItemSet("Legendary", ITEM_SET_COLOR_LEGENDARY)

    add(commonSet, ITEM_SET_CHANCE_COMMON)
    add(rareSet, ITEM_SET_CHANCE_RARE)
    add(epicSet, ITEM_SET_CHANCE_EPIC)
    add(legendarySet, ITEM_SET_CHANCE_LEGENDARY)

  // --------------------------------------------------------------------------
  function getRandomItemSet(ItemChances chances) returns ItemSet
    let tempSet = new WeightedItemSetSet()

    if (not commonSet.isEmpty())
      tempSet.add(commonSet, chances.common)
    if (not rareSet.isEmpty())
      tempSet.add(rareSet, chances.rare)
    if (not epicSet.isEmpty())
      tempSet.add(epicSet, chances.epic)
    if (not legendarySet.isEmpty())
      tempSet.add(legendarySet, chances.legendary)

    ItemSet selectedItemSet = null
    if (not tempSet.isEmpty())
      selectedItemSet = this.itemSets.getRandom()

    destroy tempSet
    return selectedItemSet

  // --------------------------------------------------------------------------
  function getRandomItemType(ItemChances chances) returns ItemType
    let itemSet = this.getRandomItemSet(chances)
    return itemSet != null ? itemSet.getRandomItemType() : null

  // --------------------------------------------------------------------------
  function createRandomItem(vec2 pos, ItemChances chances) returns item
    let itemSet = this.getRandomItemSet(chances)
    return itemSet != null ? itemSet.createRandomItem(pos) : null

  // --------------------------------------------------------------------------
  function giveRandomItemToUnit(unit u, ItemChances chances) returns item
    let itm = this.createRandomItem(u.getPos(), chances)
    if (itm != null)
      UnitAddItemSwapped(itm, u)
    return itm

// ============================================================================
init
  g_MainItemLibrary = new MainItemSetLibrary()