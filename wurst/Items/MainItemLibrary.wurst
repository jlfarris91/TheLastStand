package MainItemLibrary
import TlsItemIds
import TlsJobItemIds
import Math
import ItemSet
import Items
import WeightedItemSetSet
import ItemSetLibrary
import ItemType

public MainItemSetLibrary g_MainItemLibrary

public tuple ItemChances(real common, real rare, real epic, real legendary)

// ============================================================================
public function lerp(ItemChances start, ItemChances _end, real t) returns ItemChances
  return ItemChances(
    lerp(start.common, _end.common, t),
    lerp(start.rare, _end.rare, t),
    lerp(start.epic, _end.epic, t),
    lerp(start.legendary, _end.legendary, t))

constant real ITEM_SET_CHANCE_COMMON = 8.0
constant real ITEM_SET_CHANCE_RARE = 4.0
constant real ITEM_SET_CHANCE_EPIC = 2.0
constant real ITEM_SET_CHANCE_LEGENDARY = 1.0

// ============================================================================
public class MainItemSetLibrary extends ItemSetLibrary
  ItemSet commonSet
  ItemSet rareSet
  ItemSet epicSet
  ItemSet legendarySet
  ItemSet questSet

  // --------------------------------------------------------------------------
  construct()
    commonSet = new ItemSet()
    rareSet = new ItemSet()
    epicSet = new ItemSet()
    legendarySet = new ItemSet()
    questSet = new ItemSet()

    add(commonSet, ITEM_SET_CHANCE_COMMON)
    add(rareSet, ITEM_SET_CHANCE_RARE)
    add(epicSet, ITEM_SET_CHANCE_EPIC)
    add(legendarySet, ITEM_SET_CHANCE_LEGENDARY)

  // --------------------------------------------------------------------------
  function getRandomItemSet(ItemChances chances) returns ItemSet
    let tempSet = new WeightedItemSetSet()

    if (not commonSet.isEmpty())
      tempSet.add(commonSet, chances.common)
    if (not rareSet.isEmpty())
      tempSet.add(rareSet, chances.rare)
    if (not epicSet.isEmpty())
      tempSet.add(epicSet, chances.epic)
    if (not legendarySet.isEmpty())
      tempSet.add(legendarySet, chances.legendary)

    ItemSet selectedItemSet = null
    if (not tempSet.isEmpty())
      selectedItemSet = tempSet.getRandom()

    destroy tempSet
    return selectedItemSet

  // --------------------------------------------------------------------------
  function getRandomItemType(ItemChances chances) returns ItemType
    let itemSet = this.getRandomItemSet(chances)
    return ItemType.getItemType(itemSet.getRandom())

  // --------------------------------------------------------------------------
  function createRandomItem(vec2 pos, ItemChances chances) returns item
    let itemSet = this.getRandomItemSet(chances)
    if (itemSet == null)
      Log.error("MainItemLibrary", "createRandomItem", "", "No item set found")
    return itemSet.createRandomItem(pos)

  // --------------------------------------------------------------------------
  function giveRandomItemToUnit(unit u, ItemChances chances) returns item
    let itm = this.createRandomItem(u.getPos(), chances)
    if (itm == null)
      Log.error("MainItemLibrary", "giveRandomItemToUnit", "", "No item was created")
    UnitAddItemSwapped(itm, u)
    return itm

  // --------------------------------------------------------------------------
  function getOwningSet(int itemTypeId) returns ItemSet
    if (commonSet.has(itemTypeId))
      return commonSet
    if (rareSet.has(itemTypeId))
      return rareSet
    if (epicSet.has(itemTypeId))
      return epicSet
    if (legendarySet.has(itemTypeId))
      return legendarySet
    if (questSet.has(itemTypeId))
      return questSet
    return null

// ============================================================================
init

  defineItems()

  g_MainItemLibrary = new MainItemSetLibrary()

  // Common Set
  g_MainItemLibrary.commonSet.add(TlsItemIds.bandages, 30.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.portableBarricade, 10.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.shelter, 5.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.cheese, 20.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.armor1, 20.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.attackSpeed1, 20.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.attackDamage1, 20.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.scrollOfCampPortal, 5.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.staminaPotion1, 20.0)
  g_MainItemLibrary.commonSet.add(TlsItemIds.operationsManual1, 10.0)

  g_MainItemLibrary.commonSet.add(TlsJobItemIds.militia1, 1.0)
  g_MainItemLibrary.commonSet.add(TlsJobItemIds.builder1, 1.0)

  // Rare Set
  g_MainItemLibrary.rareSet.add(TlsItemIds.boots1, 10.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.armor2, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.attackSpeed2, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.attackDamage2, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.nightScope, 10.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.staminaPotion2, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.wirtsPipe, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.wirtsLeg, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.monsterTooth, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.wisdomKnuckle, 20.0)
  g_MainItemLibrary.rareSet.add(TlsItemIds.operationsManual2, 10.0)

  g_MainItemLibrary.rareSet.add(TlsJobItemIds.priest1, 1.0)
  g_MainItemLibrary.rareSet.add(TlsJobItemIds.spearman1, 1.0)

  // Epic Set
  g_MainItemLibrary.epicSet.add(TlsItemIds.grenade, 10.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.armor3, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.attackSpeed3, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.attackDamage3, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.staminaPotion3, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.treasureHuntersPendant, 5.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.bloodRedCape, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.brassContraption, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.lightningRod, 20.0)
  g_MainItemLibrary.epicSet.add(TlsItemIds.operationsManual3, 10.0)

  g_MainItemLibrary.epicSet.add(TlsJobItemIds.acolyte1, 3.0)
  g_MainItemLibrary.epicSet.add(TlsJobItemIds.marksman1, 1.0)

  // Legendary Set
  g_MainItemLibrary.legendarySet.add(TlsItemIds.portableTurret, 10.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.thunderlizardDiamond, 10.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.sacredRelic, 10.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.luckyHorseshoe, 10.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.attackDamage4, 10.0)
  g_MainItemLibrary.legendarySet.add(TlsItemIds.shapeshiftingKey, 5.0)

  // Quest set
  g_MainItemLibrary.questSet.add(TlsItemIds.rawMeat, 0)
  g_MainItemLibrary.questSet.add(TlsItemIds.headquarters1, 0)
  g_MainItemLibrary.questSet.add(TlsItemIds.headquarters2, 0)
  g_MainItemLibrary.questSet.add(TlsItemIds.headquarters3, 0)