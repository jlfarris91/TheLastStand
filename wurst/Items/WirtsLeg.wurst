package WirtsLeg
import UnitMetadata
import UnitPropertiesComponent
import ItemEffectComponent
import SmartValue
import ItemAcquiredPropertyAffectComponent
import StringBuilder
import ColorUtility
import TlsItemDefinition
import Icons

constant real EVADE_CHANCE = 0.1

// ============================================================================
class WirtsLegItemEffectComponent extends ItemAcquiredPropertyAffectComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner, TlsItemIds.wirtsLeg, UnitProperty.EVADE_CHANCE, AffectorType.ABSOLUTE)
  
  // --------------------------------------------------------------------------
  override function getUnitPropertyAffectorValue(int stacks) returns real
    return stacks * EVADE_CHANCE

// ============================================================================
@compiletime function createItem()

  let sb = new StringBuilder()
  sb.appendLine("Increases chance to evade incoming attacks by 10%.")
  sb.appendLine()
  sb.append("Each additional stack increases chance to evade incoming attacks by 10%.".colorize(Colors.gold))
  let tooltipExtended = sb.toStringAndDestroy()

  new TlsItemDefinition(TlsItemIds.wirtsLeg, 'wtlg')
  ..setName("Wirt's Leg")
  ..setDescription("At first glance it appears simply to be the abandoned wooden leg of some unfortunate uniped.")
  ..setTooltipBasic("Purchase Wirt's Leg")
  ..setTooltipExtended(tooltipExtended)
  ..setAbilities("")
  ..setInterfaceIcon(Icons.bTNWirtsLeg)
  ..setGoldCost(6)
  ..setNumberofCharges(1)
  ..addTlsUnitClassifications(TlsUnitClassification.HUMAN)
  ..build()

// ============================================================================
init
  if (not compiletime)
    createItem()

  ItemEffectComponent.registerComponentFactory(TlsItemIds.wirtsLeg) _unit ->
  //{
    let metadata = _unit.getMetadata()
    ItemEffectComponent comp = null
    if (metadata != null)
      comp = metadata.getComponent(WirtsLegItemEffectComponent.typeId) castTo WirtsLegItemEffectComponent
      if (comp == null)
        comp = metadata.addComponent(new WirtsLegItemEffectComponent(metadata)) castTo WirtsLegItemEffectComponent
    return comp
  //}