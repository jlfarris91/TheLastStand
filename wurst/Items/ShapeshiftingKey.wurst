package ShapeshiftingKey
import TlsItemIds
import LooterComponent
import UnitMetadata
import ItemEffectComponent

// ============================================================================
class ShapeshiftingKeyComponent extends ItemEffectComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

    setMode(ItemEffectApplication.ACQUIRED)
    setItemTypeId(TlsItemIds.shapeshiftingKey)
    setDestroyOnDrop(true)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return ShapeshiftingKeyComponent.typeId
  
  // --------------------------------------------------------------------------
  override function onApplied(int stacks)
    super.onApplied(stacks)
    let comp = getOwner().getOrAddLooterComponent()
    if (comp != null)
      comp.setDurationScale(0.5)

  // --------------------------------------------------------------------------
  override function onReverted(int stacks)
    super.onReverted(stacks)
    let comp = getOwner().getOrAddLooterComponent()
    if (comp != null)
      comp.setDurationScale(1.0)

// ============================================================================
function IUnitMetadata.getShapeshiftingKeyComponent() returns ShapeshiftingKeyComponent
  return this.getComponent(typeInfo(ShapeshiftingKeyComponent.typeId)) castTo ShapeshiftingKeyComponent

// ============================================================================
function IUnitMetadata.getOrAddShapeshiftingKeyComponent() returns ShapeshiftingKeyComponent
  var component = this.getShapeshiftingKeyComponent()
  if (component == null)
    component = this.addComponent(new ShapeshiftingKeyComponent(this)) castTo ShapeshiftingKeyComponent
  return component

// ============================================================================
init
  g_itemIdToComponentFactoryMap.put(TlsItemIds.shapeshiftingKey) (_unit) ->
  //{
    let metadata = _unit.getMetadata()
    ItemEffectComponent comp = null
    if (metadata != null)
      comp = metadata.getOrAddShapeshiftingKeyComponent()
    return comp
  //}