package ItemSet
import ItemExtensions
import initlater ItemLibrary
import WeightedSet
import ColorUtility

// ============================================================================
public class ItemSet
  private WeightedItemTypeSet itemTypes
  private string name
  private color color

  // --------------------------------------------------------------------------
  construct(string name, color color)
    this.name = name
    this.color = color
    this.itemTypes = new WeightedItemTypeSet()

  // --------------------------------------------------------------------------
  ondestroy
    destroy this.itemTypes

  // --------------------------------------------------------------------------
  function isEmpty() returns bool
    return itemTypes.isEmpty()

  // --------------------------------------------------------------------------
  function getName() returns string
    return this.name

  // --------------------------------------------------------------------------
  function getColor() returns color
    return color

  // --------------------------------------------------------------------------
  function add(int itemId, real weight) returns ItemType
    let itemType = new ItemType(itemId)
    this.add(itemType, weight)
    return itemType

  // --------------------------------------------------------------------------
  function add(int itemId, string itemTypeName, real weight) returns ItemType
    let itemType = new ItemType(itemId)
    itemType.itemTypeName = itemTypeName
    this.add(itemType, weight)
    return itemType

  // --------------------------------------------------------------------------
  function add(ItemType itemType, real weight)
    this.itemTypes.add(itemType, weight)
    if (not g_AllItemTypes.has(itemType.id))
      g_AllItemTypes.put(itemType.id, itemType)

  // --------------------------------------------------------------------------
  function getItemType(int id) returns ItemType
    for d in itemTypes
      if (d.id == id)
        return d
    return null

  // --------------------------------------------------------------------------
  function createItem(int itemId, vec2 pos) returns item
    let itemType = getItemType(itemId)
    return createItemInternal(itemType, pos)

  // --------------------------------------------------------------------------
  function createRandomItem(vec2 pos) returns item
    let itemType = getRandomItemType()
    return createItemInternal(itemType, pos)

  // --------------------------------------------------------------------------
  function getRandomItemType() returns ItemType
    return itemTypes.getRandom()

  // --------------------------------------------------------------------------
  function getRandomItemTypeId() returns int
    let itm = itemTypes.getRandom()
    return itm != null ? itm.id : -1
  
  // --------------------------------------------------------------------------
  function getItemTypeChance(int id) returns real
    let itemDef = getItemType(id)
    if (itemDef == null)
      return 0.0
    return this.itemTypes.getChance(itemDef)

  // --------------------------------------------------------------------------
  function iterator() returns WeightedSetIterator<ItemType>
    return this.itemTypes.iterator()

  // --------------------------------------------------------------------------
  private function decorateItem(item i, ItemType itemType)
    var _name = itemType.name
    var _description = itemType.description
    var _extended = itemType.extendedTooltip

    if (_name == "")
      _name = i.getName()

    if (_description == "")
      _description = i.getDescription()
    
    if (_extended == "")
      _extended = i.getExtendedTooltip()

    let itemRarityString = (name + " " + itemType.itemTypeName).colorize(color) + "|n|n"
  
    _name = _name.colorize(this.color)
    _description = itemRarityString + _description
    _extended = itemRarityString + _extended

    i.setName(_name)
    i.setDescription(_description)
    i.setExtendedTooltip(_extended)

    if (itemType.tooltip != "")
      i.setTooltip(itemType.tooltip)

    i.setPawnable(itemType.pawnable)
    i.setDroppable(itemType.droppable)
    i.setDropOnDeath(itemType.dropOnDeath)
    i.setTintColor(this.color)

  // --------------------------------------------------------------------------
  private function createItemInternal(ItemType itemType, vec2 pos) returns item
    if (itemType == null)
      return null
    let itm = CreateItem(itemType.id, pos.x, pos.y)
    if (itm != null)
      this.decorateItem(itm, itemType)
    return itm