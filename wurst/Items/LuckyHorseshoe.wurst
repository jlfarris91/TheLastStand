package LuckyHorseshoe
import UnitMetadata
import ItemEffectComponent
import UnitPropertiesComponent
import SmartValue
import ItemAcquiredPropertyAffectComponent
import TlsItemDefinition
import Icons
import RealExtensions

constant real LOOT_TWICE_CHANCE = 0.1

// ============================================================================
class LuckyHorseshoeComponent extends ItemAcquiredPropertyAffectComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner, TlsItemIds.luckyHorseshoe, UnitProperty.LOOT_TWICE_CHANCE, AffectorType.BASE)
  
  // --------------------------------------------------------------------------
  override function getUnitPropertyAffectorValue(int stacks) returns real
    return LOOT_TWICE_CHANCE

// ============================================================================
@compiletime function createItem()

  new TlsItemDefinition(TlsItemIds.luckyHorseshoe, 'skrt')
  ..setName("Lucky Horseshoe")
  ..setDescription("Belonged to a horse once. The horse wasn't so lucky.")
  ..setTooltipBasic("Purchase Lucky Horseshoe")
  ..setTooltipExtended("Grants a {0} chance of immediately looting a structure a second time. Does not stack.".format(LOOT_TWICE_CHANCE.toPercentageString01()))
  ..setInterfaceIcon(Icons.bTNAnimalWarTraining)
  ..setGoldCost(25)
  ..setCanBeDropped(true)
  ..setCanBeSoldByMerchants(true)
  ..setCanBeSoldToMerchants(false)
  ..addTlsUnitClassifications(TlsUnitClassification.HUMAN)
  ..build()

// ============================================================================
init
  if (not compiletime)
    createItem()

  ItemEffectComponent.registerComponentFactory(TlsItemIds.luckyHorseshoe) (_unit) ->
  //{
    let metadata = _unit.getMetadata()
    ItemEffectComponent comp = null
    if (metadata != null)
      comp = metadata.getComponent(LuckyHorseshoeComponent.typeId) castTo LuckyHorseshoeComponent
      if (comp == null)
        comp = metadata.addComponent(new LuckyHorseshoeComponent(metadata)) castTo LuckyHorseshoeComponent
    return comp
  //}