package LuckyHorseshoe
import TlsItemIds
import UnitMetadata
import LooterComponent
import ItemEffectComponent

constant real CHANCE = 0.1

// ============================================================================
class LuckyHorseshoeComponent extends ItemEffectComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

    setMode(ItemEffectApplication.ACQUIRED)
    setItemTypeId(TlsItemIds.luckyHorseshoe)
    setDestroyOnDrop(true)
  
  // --------------------------------------------------------------------------
  override function onApplied(int stacks)
    super.onApplied(stacks)
    let comp = getOwner().getLooterComponent()
    if (comp != null)
      comp.setChanceToLootTwice(comp.getChanceToLootTwice() + CHANCE)

  // --------------------------------------------------------------------------
  override function onReverted(int stacks)
    super.onReverted(stacks)
    let comp = getOwner().getLooterComponent()
    if (comp != null)
      comp.setChanceToLootTwice(comp.getChanceToLootTwice() - CHANCE)

// ============================================================================
function IUnitMetadata.getLuckyHorseshoeComponent() returns LuckyHorseshoeComponent
  return this.getComponent(LuckyHorseshoeComponent.typeId) castTo LuckyHorseshoeComponent

// ============================================================================
function IUnitMetadata.getOrAddLuckyHorseshoeComponent() returns LuckyHorseshoeComponent
  var component = this.getLuckyHorseshoeComponent()
  if (component == null)
    component = this.addComponent(new LuckyHorseshoeComponent(this)) castTo LuckyHorseshoeComponent
  return component

// ============================================================================
init
  g_itemIdToComponentFactoryMap.put(TlsItemIds.luckyHorseshoe) (_unit) ->
  //{
    let metadata = _unit.getMetadata()
    ItemEffectComponent comp = null
    if (metadata != null)
      comp = metadata.getOrAddLuckyHorseshoeComponent()
    return comp
  //}