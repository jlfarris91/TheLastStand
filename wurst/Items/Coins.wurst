package Coins
import RegisterEvents
import FX
import TlsItemIds
import GameConstants
import HumanPlayerComponent

// ============================================================================
function onItemAcquired()
  let itm = GetManipulatedItem()

  if (not itm.isCoinItem())
    return

  let manipulatingUnit = GetManipulatingUnit()
  let itemTypeId = itm.getTypeId()

  let ownerMetadata = manipulatingUnit.getOwnerHumanPlayerComponent()
  if (ownerMetadata == null)
    return

  itm.remove()
  
  let reward = getGoldRewardFromCoinItemType(itemTypeId)
  manipulatingUnit.getOwner().addGold(reward)

  let pos = manipulatingUnit.getPos()

  FX.createFoundGoldEffect(pos, manipulatingUnit.getOwner())
  FX.createFoundGoldTag(pos, reward, manipulatingUnit.getOwner())

// ============================================================================
public function getCoinItemTypeFromGoldReward(int gold) returns int
  if (gold >= COIN_REWARD_SMALL and gold < COIN_REWARD_MEDIUM)
    return TlsItemIds.coinSmall
  if (gold >= COIN_REWARD_MEDIUM and gold < COIN_REWARD_LARGE)
    return TlsItemIds.coinMedium
  if (gold >= COIN_REWARD_LARGE and gold < COIN_REWARD_EPIC)
    return TlsItemIds.coinLarge
  if (gold >= COIN_REWARD_EPIC)
    return TlsItemIds.coinEpic
  return 0

// ============================================================================
public function getGoldRewardFromCoinItemType(int itemTypeId) returns int
  switch (itemTypeId)
    case TlsItemIds.coinSmall
      return COIN_REWARD_SMALL
    case TlsItemIds.coinMedium
      return COIN_REWARD_MEDIUM
    case TlsItemIds.coinLarge
      return COIN_REWARD_LARGE
    case TlsItemIds.coinEpic
      return COIN_REWARD_EPIC
  return 0

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM, function onItemAcquired)