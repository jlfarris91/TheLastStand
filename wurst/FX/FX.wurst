package FX

constant string MODEL_PATH_GOLD_CREDIT = "UI\\Feedback\\GoldCredit\\GoldCredit.mdl"
constant colorA TAG_COLOR_MATERIALS = COLOR_GOLD
constant colorA TAG_COLOR_SURVIVOR = COLOR_WHITE
constant colorA TAG_COLOR_ITEM = COLOR_WHITE

public class FX

  static function createEffect(string modelPath, real x, real y)
    DestroyEffect(AddSpecialEffect(modelPath, x, y))

  static function createEffect(string modelPath, real x ,real y, player p)
    var _modelPath = ""
    if (localPlayer == p)
      _modelPath = modelPath
    createEffect(_modelPath, x, y)

  static function createGoldCreditEffect(real x, real y, player p)
    createEffect(MODEL_PATH_GOLD_CREDIT, x, y, p)

  static function createFoundMaterialsTag(real x, real y, int mats, player p)
    createTag(x, y, "+" + I2S(mats), TAG_COLOR_MATERIALS, p)

  static function createFoundItemTag(real x, real y, player p)
    createTag(x, y, "+Item", TAG_COLOR_ITEM, p)

  static function createFoundSurvivorTag(real x, real y, player p)
    createTag(x, y, "+Survivor", TAG_COLOR_SURVIVOR, p)

  static function createTag(real x, real y, string msg, colorA color, player p)
    var vel = vec2(0.0, TextTagSpeed2Velocity(64.0))
    createTTEx(vec3(x, y, 32.0), vel,  msg, 10.0, 3.0, color, p)

  static function createTag(real x, real y, string msg, colorA color)
    var vel = vec2(0.0, TextTagSpeed2Velocity(64.0))
    createTTEx(vec3(x, y, 32.0), vel,  msg, 10.0, 3.0, color)
    