package TombstoneComponent
import UnitComponent
import ClosureTimers
import Orders
import Trace

constant int SUMMON_ORDER_ID = OrderIds.waterelemental

// ============================================================================
public class TombstoneComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    trySummon()

  // --------------------------------------------------------------------------
  private function trySummon()
    let ownerUnit = getOwnerUnit()
    let cooldown = GetRandomReal(1.0, 3.0)
    doAfter(cooldown) () ->
      if (ownerUnit != null and ownerUnit.isAlive())
        if (ownerUnit.issueImmediateOrderById(SUMMON_ORDER_ID))
          Trace.trace("Tombstone.summon")
        trySummon()

// ============================================================================
public function IUnitMetadata.getTombstoneComponent() returns TombstoneComponent
  return this.getComponent(TombstoneComponent.typeId) castTo TombstoneComponent

// ============================================================================
public function IUnitMetadata.getOrAddTombstoneComponent() returns TombstoneComponent
  var component = this.getTombstoneComponent()
  if (component == null)
    component = this.addComponent(new TombstoneComponent(this)) castTo TombstoneComponent
  return component

// ============================================================================
public function unit.getTombstoneComponent() returns TombstoneComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getTombstoneComponent() : null

// ============================================================================
public function unit.getOrAddTombstoneComponent() returns TombstoneComponent
  let metadata = this.getMetadata()
  return metadata != null ? metadata.getOrAddTombstoneComponent() : null