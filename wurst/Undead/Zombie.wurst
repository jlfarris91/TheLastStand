package Zombie
import UnitMetadata
import HumanPlayerMetadata
import GameConstants
import Math
import FX
import UndeadUnit
import InfectedComponent
import TlsUnitIds

// ============================================================================
public class Zombie extends UndeadUnit
  private real m_matsDropChance

  // --------------------------------------------------------------------------
  construct()
    super()
    m_matsDropChance = ZOMBIES_MATERIALS_DROP_CHANCE

    if (passesChanceCheck(ZOMBIES_INFECTED_CHANCE))
      this.getOrAddInfectedComponent()

  // --------------------------------------------------------------------------
  function getMaterialsDropChance() returns real
    return m_matsDropChance

  // --------------------------------------------------------------------------
  function setMaterialsDropChance(real value)
    m_matsDropChance = value

  // --------------------------------------------------------------------------
  override function onKilled()
    super.onKilled()

    let killingUnit = GetKillingUnit()
    if (killingUnit == null)
      return

    let killingPlayer = killingUnit.getOwner()

    if (passesChanceCheck(ZOMBIES_MATERIALS_DROP_CHANCE))
      let killingPlayerMetadata = killingPlayer.getHumanMetadata()
      if (killingPlayerMetadata != null)
        killingPlayerMetadata.giveMaterials(ZOMBIES_MATERIALS_DROP_AMOUNT)

        let pos = this.getPos()
        FX.createFoundMaterialsEffect(pos, killingPlayer)
        FX.createFoundMaterialsTag(pos, ZOMBIES_MATERIALS_DROP_AMOUNT, killingPlayer)

// ============================================================================
public function isFilterUnitAZombie() returns bool
  let u = GetFilterUnit()
  return u.isZombie() and u.isAlive()

// ============================================================================
public function unit.isZombie() returns bool
  return this.getTypeId().isZombie()

// ============================================================================
public function int.isZombie() returns bool
  return this == TlsUnitIds.zombieMale or
         this == TlsUnitIds.zombieFemale

// ============================================================================
public function unit.isMaleZombie() returns bool
  return this.getTypeId() == TlsUnitIds.zombieMale

// ============================================================================
public function unit.isFemaleZombie() returns bool
  return this.getTypeId() == TlsUnitIds.zombieFemale