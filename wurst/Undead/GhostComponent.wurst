package GhostComponent
import UnitComponent
import AbilityObjEditing
import ObjectIdGenerator
import ClosureTimers
import RegisterEvents
import TlsUnitIds
import Orders
import UnitExtensions
import UndeadTargetingComponent
import Suspend
import FX

constant real STATE_TOGGLE_INTERVAL = 10.0
constant real ETHEREAL_MOVE_RANGE = 256.0
constant real FADE_IN_DURATION = 2.0
constant real FADE_OUT_DURATION = 2.0
constant colorA COLOR_VISIBLE = colorA(255, 255, 255, 255)
constant colorA COLOR_INVISIBLE = colorA(255, 255, 255, 0)
constant int DOT_ABILITY_ID = compiletime(ABIL_ID_GEN.next())

// ============================================================================
public class GhostComponent extends UnitComponent
  private unit m_lastAttackedUnit
  private CallbackPeriodic m_toggleStateTimer = null

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)
    m_lastAttackedUnit = null

  // --------------------------------------------------------------------------
  ondestroy

    if (m_toggleStateTimer != null)
      destroy m_toggleStateTimer
      m_toggleStateTimer = null

  // --------------------------------------------------------------------------
  function setLastAttackedUnit(unit attackedUnit)
    m_lastAttackedUnit = attackedUnit

  // --------------------------------------------------------------------------
  function getLastAttackedUnit() returns unit
    return m_lastAttackedUnit

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    m_lastAttackedUnit = null
    makeVisible()

    m_toggleStateTimer = doPeriodically(STATE_TOGGLE_INTERVAL, cb -> toggleState())

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()

    if (m_toggleStateTimer != null)
      destroy m_toggleStateTimer
      m_toggleStateTimer = null

    let ownerUnit = getOwnerUnit()
    m_lastAttackedUnit = null
    makeVisible()
    ownerUnit.unsuspend()

  // --------------------------------------------------------------------------
  function fade(colorA startColor, colorA endColor, real duration)
    getOwnerUnit().fade(0.1, duration, startColor, endColor)

  // --------------------------------------------------------------------------
  function fadeIn()
    let ownerUnit = getOwnerUnit()
    makeVisible()
    ownerUnit.setVertexColor(COLOR_INVISIBLE)
    fade(COLOR_INVISIBLE, COLOR_VISIBLE, FADE_IN_DURATION)

  // --------------------------------------------------------------------------
  function fadeOut()
    let ownerUnit = getOwnerUnit()
    fade(COLOR_VISIBLE, COLOR_INVISIBLE, FADE_OUT_DURATION)
    ownerUnit.setFieldWeapon(UNIT_WEAPON_BF_ATTACKS_ENABLED, 0, false)
    doAfter(FADE_OUT_DURATION) ->
    //{
      let metadata = ownerUnit.getMetadata()
      if (metadata != null)
        let comp = metadata.getGhostComponent()
        if (comp != null and comp.getEnabled())
          makeInvisible()
    //}

  // --------------------------------------------------------------------------
  function makeInvisible()
    let ownerUnit = getOwnerUnit()
    ownerUnit.hide()
    ownerUnit.addAbility(AbilityIds.invulnerable)
    ownerUnit.setVertexColor(COLOR_INVISIBLE)
    ownerUnit.suspend()
    ownerUnit.setFieldWeapon(UNIT_WEAPON_BF_ATTACKS_ENABLED, 0, false)

  // --------------------------------------------------------------------------
  function makeVisible()
    let ownerUnit = getOwnerUnit()
    ownerUnit.show()
    ownerUnit.removeAbility(AbilityIds.invulnerable)
    ownerUnit.setVertexColor(COLOR_VISIBLE)
    ownerUnit.unsuspend()
    ownerUnit.setFieldWeapon(UNIT_WEAPON_BF_ATTACKS_ENABLED, 0, true)

  // --------------------------------------------------------------------------
  function toggleState()
    let ownerUnit = getOwnerUnit()
    if (ownerUnit.isHidden())
      unsuspendAndFadeIn()
    else
      fadeOutAndSuspend()
    
  // --------------------------------------------------------------------------
  function unsuspendAndFadeIn()
    let ownerUnit = getOwnerUnit()
    let targetingComponent = getOwner().getUndeadTargetingComponent()
    if (targetingComponent == null)
      return
    targetingComponent.tryTargetClosestUnit()
    let spawnDistance = ownerUnit.getMoveSpeed() * FADE_IN_DURATION
    vec2 spawnPos
    let targetUnit = targetingComponent.getTargetUnit()
    if (targetUnit != null)
      spawnPos = targetUnit.getPos().polarOffset(GetRandomDirectionDeg().asAngleDegrees(), spawnDistance)
    else
      spawnPos = ownerUnit.getPos().polarOffset(ownerUnit.getFacingAngle(), spawnDistance)
    ownerUnit.setXY(spawnPos)
    fadeIn()

  // --------------------------------------------------------------------------
  function fadeOutAndSuspend()
    let ownerUnit = getOwnerUnit()
    let moveDistance = ownerUnit.getMoveSpeed() * FADE_OUT_DURATION
    vec2 movePos
    if (m_lastAttackedUnit != null)
      let v = (m_lastAttackedUnit.getPos() - ownerUnit.getPos()).norm()
      movePos = ownerUnit.getPos() + v * moveDistance
    else
      movePos = ownerUnit.getPos().polarOffset(ownerUnit.getFacingAngle(), moveDistance)
    ownerUnit.issuePointOrderById(OrderIds.move, movePos)
    fadeOut()

// ============================================================================
public function IUnitMetadata.getGhostComponent() returns GhostComponent
  return this.getComponent(GhostComponent.typeId) castTo GhostComponent

// ============================================================================
public function IUnitMetadata.getOrAddGhostComponent() returns GhostComponent
  var component = this.getGhostComponent()
  if (component == null)
    component = this.addComponent(new GhostComponent(this)) castTo GhostComponent
  return component

// ============================================================================
function onUnitAttacked()
  let attackedUnit = GetAttackedUnitBJ()
  let attackingUnit = GetAttacker()

  if (not attackingUnit.isTypeOfGhost())
    return

  let metadata = attackingUnit.getMetadata()
  if (metadata == null)
    return

  let comp = metadata.getGhostComponent()
  if (comp == null)
    return

  comp.setLastAttackedUnit(attackedUnit)

// ============================================================================
init
  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ATTACKED, function onUnitAttacked)