package GhostComponent
import UnitComponent
import HashList
import AbilityObjEditing
import ObjectIdGenerator
import ClosureTimers

constant real ETHEREAL_TOGGLE_INTERVAL = 10.0
constant int DOT_ABILITY_ID = compiletime(ABIL_ID_GEN.next())
HashList<unit> g_ghosts = new HashList<unit>()
CallbackPeriodic g_toggleEtherealTimer

// ============================================================================
public class GhostComponent extends UnitComponent

  // --------------------------------------------------------------------------
  construct (IUnitMetadata owner)
    super(owner)

  // --------------------------------------------------------------------------
  override function getTypeId() returns int
    return GhostComponent.typeId

  // --------------------------------------------------------------------------
  override function onEnabled()
    super.onEnabled()
    let ownerUnit = getOwnerUnit()
    registerGhost(ownerUnit)
    ownerUnit.addAbility(AbilityIds.ethereal)

  // --------------------------------------------------------------------------
  override function onDisabled()
    super.onDisabled()
    let ownerUnit = getOwnerUnit()
    unregisterGhost(ownerUnit)
    ownerUnit.removeAbility(AbilityIds.ethereal)

// ============================================================================
public function IUnitMetadata.getGhostComponent() returns GhostComponent
  return this.getComponent(typeInfo(GhostComponent.typeId)) castTo GhostComponent

// ============================================================================
public function IUnitMetadata.getOrAddGhostComponent() returns GhostComponent
  var component = this.getGhostComponent()
  if (component == null)
    component = this.addComponent(new GhostComponent(this)) castTo GhostComponent
  return component

// ============================================================================
function registerGhost(unit ghost)
  g_ghosts.add(ghost)
  
  if (g_ghosts.size() == 1 and g_toggleEtherealTimer == null)
    g_toggleEtherealTimer = doPeriodically(ETHEREAL_TOGGLE_INTERVAL, (CallbackPeriodic cb) -> toggleGhostEthereal())

// ============================================================================
function toggleGhostEthereal()
  for g in g_ghosts
    if (g.hasAbility(AbilityIds.ethereal))
      g.removeAbility(AbilityIds.ethereal)
    else
      g.addAbility(AbilityIds.ethereal)

// ============================================================================
function unregisterGhost(unit ghost)
  g_ghosts.remove(ghost)
  
  if (g_ghosts.size() == 0 and g_toggleEtherealTimer != null)
    destroy g_toggleEtherealTimer
    g_toggleEtherealTimer = null