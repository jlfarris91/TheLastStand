package Zombies
import GroupUtils
import GameConstants
import HumanPlayers
import UndeadSpawnPoints
import GameSettings
import HashMap
import HashList
import UnitExtensions
import RegisterEvents
import FX
import ErrorHandling
import Math
import MainItemLibrary
import TlsUnitIds
import Camp
import HumanPlayerMetadata
import UnitMetadata

timer g_SpawnTimer
HashMap<player, PlayerZombieMetadata> g_PlayerMetadata
HashMap<unit, Zombie> g_Zombies

public class Zombies

  static function stopSpawning()
    g_SpawnTimer.pause()

  static function startSpawning()
    g_SpawnTimer.startPeriodic(ZOMBIES_SPAWN_INTERVAL, function spawnZombiesNearPlayers)
  
  static function killAllUndead()
    var ug = getGroup()
    ug.enumUnitsOfPlayer(PLAYER_UNDEAD, null)
    for u in ug
      u.kill()
    ug.release()

  static function spawnDayZombiesNearPlayers()
    g_PlayingHumanPlayers.forEach() (player p) ->
      spawnDayZombiesNearPlayer(p)

  static function spawnDayZombiesNearPlayer(player p)
    let playerMetadata = p.getHumanMetadataRequired()

    if (playerMetadata.getIsDead())
      return
    
    var camp = playerMetadata.getCamp()
    var center = camp.getCenter()

    // Clamp the spawn area to the world bounds
    center = clampToWorldBounds(center, ZOMBIES_DAY_SPAWN_RANGE)

    var spawnPoints = g_UndeadSpawnPointManager.getRandomSpawnPointsInRange(
      center,
      ZOMBIES_DAY_SPAWN_RANGE,
      GameSettings.zombies_DaySpawnCountPerPlayer)

    var groupSize = GetRandomInt(ZOMBIES_DAY_GROUP_SIZE_MIN, ZOMBIES_DAY_GROUP_SIZE_MAX)

    for sp in spawnPoints
      // TODO: unit recycler
      for i = 0 to groupSize - 1
        createUnitTLS(
          PLAYER_UNDEAD,
          TlsUnitIds.zombieDay,
          sp.getPos().x,
          sp.getPos().y,
          GetRandomDirectionDeg())

    var chance = ZOMBIES_DAY_GROUP_ITEM_CHANCE + (groupSize * ZOMBIES_DAY_GROUP_ITEM_CHANCE)

    if (passesChanceCheck(chance))
      g_MainItemLibrary.giveRandomItemToUnit(bj_lastCreatedUnit, ZOMBIES_DAY_GROUP_ITEM_CHANCES)

    destroy spawnPoints

function getClosestZombieTarget(player p, vec2 pos) returns unit
  var distance = 100000000.0
  var camp = p.getHumanMetadataRequired().getCamp()
  var isSecure = camp.getIsSecure()
  unit target = null

  var ug = getGroup()
  ug.enumUnitsOfPlayer(p, null)

  for u in ug
    if (((isSecure and u.isStructure()) or (not isSecure and not u.isStructure())) and not u.isInvulnerable())
      var d = u.getPos().distanceToSq(pos)
      if (d < distance)
        target = u
        distance = d    

  if (target == null)
    let playerMetadata = p.getHumanMetadata()
    if (playerMetadata != null and not playerMetadata.getIsDead())
      target = playerMetadata.getHero().getUnit()

  return target

function spawnZombieNearPlayer(player p, vec2 pos) returns Zombie
  var zombie = createZombie(pos.x, pos.y)
  zombie.targetPlayer = p

  var target = getClosestZombieTarget(p, zombie.getPos())

  if (target != null)
    zombie.zombieUnit.issueTargetOrder("attack", target)
  else
    BlzSetUnitRealField(zombie.zombieUnit, UNIT_RF_ACQUISITION_RANGE, 99999.0)

  return zombie

function spawnZombiesNearPlayer(player p)
  let playerMetadata = p.getHumanMetadataRequired()

  if (playerMetadata.getIsDead())
    return

  var camp = playerMetadata.getCamp()
  var center = camp.getCenter()
  let playerZombieMetadata = g_PlayerMetadata.get(p)

  // Clamp the spawn area to the world bounds
  center = clampToWorldBounds(center, ZOMBIES_SPAWN_RANGE)

  int zombieCount = playerZombieMetadata.zombies.size()
  int max = R2I(GameSettings.zombies_SpawnsMaxPerPlayer)

  if (zombieCount == max)
    return

  var numberOfSpawnPoints = R2I(GameSettings.zombies_SpawnsPerTick)
  numberOfSpawnPoints = clamp(max - zombieCount, 1, numberOfSpawnPoints)

  if (numberOfSpawnPoints == 1)
    var sp = g_UndeadSpawnPointManager.getRandomSpawnPointInRange(center, ZOMBIES_SPAWN_RANGE)  
    var zombie = spawnZombieNearPlayer(p, sp.getPos())
    playerZombieMetadata.zombies.add(zombie)
  else
    var spawnPoints = g_UndeadSpawnPointManager.getRandomSpawnPointsInRangeWeighted(
      center,
      ZOMBIES_SPAWN_RANGE,
      numberOfSpawnPoints)
    for sp in spawnPoints
      if (zombieCount < max)
        var zombie = spawnZombieNearPlayer(p, sp.getPos())
        playerZombieMetadata.zombies.add(zombie)
        zombieCount++
    destroy spawnPoints

function spawnZombiesNearPlayers()
  for p in g_PlayingHumanPlayers
    spawnZombiesNearPlayer(p)

function createZombie(real x, real y) returns Zombie
  var u = createUnitTLS(PLAYER_UNDEAD, TlsUnitIds.zombieNight, x, y, GetRandomDirectionDeg())
  var scale = GetRandomReal(-0.2, 0.2)

  u.setScale(1.0 + scale)
  u.setTimeScale(1.0 - scale)
  u.setMoveSpeedPercChange(-scale)
  u.setAnimation("birth")

  var w = u.getField(UNIT_RF_SHADOW_IMAGE_WIDTH) * (1.0 + scale)
  var h = u.getField(UNIT_RF_SHADOW_IMAGE_HEIGHT) * (1.0 + scale)
  u.setField(UNIT_RF_SHADOW_IMAGE_WIDTH, w)
  u.setField(UNIT_RF_SHADOW_IMAGE_HEIGHT, h)

  var zombie = new Zombie(u)
  g_Zombies.put(u, zombie)
  return zombie

function destroyZombieMetadata(unit u)
  var zombie = g_Zombies.get(u)
  
  var playerMetadata = g_PlayerMetadata.get(zombie.targetPlayer)
  if (playerMetadata == null)
    error("Couldn't find player metadata for zombie")
  
  playerMetadata.zombies.remove(zombie)

  destroy zombie

  g_Zombies.remove(u)

function onZombieKilled()
  var dyingUnit = GetDyingUnit()
  var killingUnit = GetKillingUnit()

  if (dyingUnit.getTypeId() != TlsUnitIds.zombieNight)
    return

  var killingPlayer = killingUnit.getOwner()

  destroyZombieMetadata(dyingUnit)
  var x = dyingUnit.getX()
  var y = dyingUnit.getY()

  if (passesChanceCheck(ZOMBIES_MATERIALS_DROP_CHANCE))
    let killingPlayerMetadata = killingPlayer.getHumanMetadata()
    if (killingPlayerMetadata != null)
      killingPlayerMetadata.giveMaterials(ZOMBIES_MATERIALS_DROP_AMOUNT)
      FX.createGoldCreditEffect(x, y, killingPlayer)
      FX.createFoundMaterialsTag(x, y, ZOMBIES_MATERIALS_DROP_AMOUNT, killingPlayer)

class PlayerZombieMetadata
  player p
  HashList<Zombie> zombies

  construct(player p)
    this.p = p
    this.zombies = new HashList<Zombie>()

class Zombie
  unit zombieUnit
  player targetPlayer

  construct(unit zombie)
    this.zombieUnit = zombie

  function getPos() returns vec2
    return zombieUnit.getPos()

init
  g_SpawnTimer = CreateTimer()
  g_PlayerMetadata = new HashMap<player, PlayerZombieMetadata>()
  g_Zombies = new HashMap<unit, Zombie>()

  for p in g_HumanPlayers
    g_PlayerMetadata.put(p, new PlayerZombieMetadata(p))

  registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onZombieKilled)