package Elites
import TlsUnitIds
import HashList
import GameConstants

public constant string MINIMAP_ICON_PATH_NORMAL = null
public constant string MINIMAP_ICON_PATH_ELITE = "ui\\minimap\\MinimapEnemyEliteIcon.mdl"
public constant string MINIMAP_ICON_PATH_BOSS = "ui\\minimap\\minimap-boss.mdx"

public constant color MINIMAP_ICON_COLOR_NORMAL = color(150, 150, 150)
public constant color MINIMAP_ICON_COLOR_ELITE = color(255, 0, 0)
public constant color MINIMAP_ICON_COLOR_BOSS = color(255, 0, 0)

HashList<int> g_eliteUnitTypes = new HashList<int>()
HashList<int> g_bossUnitTypes = new HashList<int>()

// ============================================================================
public function unit.isElite() returns bool
  return this.getTypeId().isEliteUnitTypeId()

// ============================================================================
public function int.isEliteUnitTypeId() returns bool
  return g_eliteUnitTypes.has(this)

// ============================================================================
public function unit.isBoss() returns bool
  return this.getTypeId().isBossUnitTypeId()

// ============================================================================
public function int.isBossUnitTypeId() returns bool
  return g_bossUnitTypes.has(this)

// ============================================================================
public function getMinimapIconPath(int unitTypeId) returns string
  if (unitTypeId.isEliteUnitTypeId())
    return MINIMAP_ICON_PATH_ELITE
  if (unitTypeId.isBossUnitTypeId())
    return MINIMAP_ICON_PATH_BOSS
  return MINIMAP_ICON_PATH_NORMAL  

// ============================================================================
public function getMinimapIconColor(int unitTypeId) returns color
  if (unitTypeId.isEliteUnitTypeId())
    return MINIMAP_ICON_COLOR_ELITE
  if (unitTypeId.isBossUnitTypeId())
    return MINIMAP_ICON_COLOR_BOSS
  return MINIMAP_ICON_COLOR_NORMAL

// ============================================================================
public function registerEliteType(int unitTypeId)
  g_eliteUnitTypes.add(unitTypeId)

// ============================================================================
public function registerBossType(int unitTypeId)
  g_eliteUnitTypes.add(unitTypeId)
  g_bossUnitTypes.add(unitTypeId)

// ============================================================================
init

  for i = 0 to MAX_ELITE_TIER-1
  //{
    // Elites
    registerEliteType(TlsUnitIds.Undead.skeletonArchers[i])
    registerEliteType(TlsUnitIds.Undead.crows[i])
    registerEliteType(TlsUnitIds.Undead.necromancers[i])
    registerEliteType(TlsUnitIds.Undead.tombstones[i])
    registerEliteType(TlsUnitIds.Undead.darkMinions[i])
    registerEliteType(TlsUnitIds.Undead.skeletonOrcs[i])
    registerEliteType(TlsUnitIds.Undead.ghosts[i])
    registerEliteType(TlsUnitIds.Undead.cultists[i])
    registerEliteType(TlsUnitIds.Undead.wagons[i])
    registerEliteType(TlsUnitIds.Undead.wyrms[i])
    registerEliteType(TlsUnitIds.Undead.abominations[i])
    registerEliteType(TlsUnitIds.Undead.tentacles[i])

    // Bosses
    registerBossType(TlsUnitIds.Undead.abominations[i])
    registerBossType(TlsUnitIds.Undead.fiends[i])
    registerBossType(TlsUnitIds.Undead.forgottenOnes[i])
  //}

  registerEliteType(TlsUnitIds.Undead.skeletonWarrior)
  registerEliteType(TlsUnitIds.Undead.skeletonMage)
  registerEliteType(TlsUnitIds.Undead.skeletonChampion)

  registerEliteType(TlsUnitIds.Undead.infernal)
  registerBossType(TlsUnitIds.Undead.archimonde)